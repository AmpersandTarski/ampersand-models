CONTEXT ShowTermTexts IN ENGLISH
PURPOSE CONTEXT ShowTermTexts
{+This pattern demonstrates the capability of managing variables that are referred to in a Term (specification),
and maintaining an associated so-called 'ShowText' in which the references of variables that have been provided with a value,
are replaced by that value. The purpose of this pattern is to maintain the actuality/validity of such ShowTexts, 
in case Term specifications are being created, updated or deleted, as well as in cases where variables are assigned (updated) values, or such values are deleted.-}
--! Note: this file needs the PHP-function `ParseTermText` to be included as an ExecEngine extension !--

REPRESENT TermText TYPE BIGALPHANUMERIC
REPRESENT PropertyName TYPE ALPHANUMERIC
REPRESENT VarValue TYPE ALPHANUMERIC

--[Terms]--
critText     :: Term * TermText     [UNI,TOT] -- Text that users specify as Term
critShowText :: Term * TermText     [UNI]     -- as `critText`, with substituted variables

--[Variables]--
varName      :: Variable  * PropertyName [UNI,TOT] -- Label by which a Variable can be identified.
varTerm      :: Variable  * Term              -- Variable is mentioned in Term
varValue     :: Variable  * VarValue     [UNI]     -- Variable has been assigned a VarValue

--[Variable name extraction from Terms-texts]--
{- The idea here is that whenever an Term has a description (TermText), it is parsed to see if it contains names of variables. Such names are surrounded by square brackets (`[` and `]`), and hence can be filtered out. The scope of variables is a Transaction, which means that for every detected variable name in a Term, a variable must exist in the same transaction.
This service ensures that `critVarNames` always contains all names of all Variables that are mentioned in a Term, and does not contain any name of a Variable that is not mentioned.
This service assumes that `critText` is functional, i.e. for every Term, there exists a TermText.
-}

critParsedText :: Term * TermText [UNI] -- The text as it has been parsed - needed to check if original text is edited.
ROLE ExecEngine MAINTAINS "Term text parsing - extract VarNames from TermText"
RULE "Term text parsing - extract VarNames from TermText": (I-(critVarNames;critVarNames~));critText |- critParsedText
MEANING "Terms must have their TermText parsed"
VIOLATION (TXT "{EX} ParseTermText;critVarNames;Term;", SRC I, TXT ";PropertyName;", TGT I
                  -- ParseTermText populates critVarNames with all variable names found in critText
          ,TXT "{EX} InsPair;critParsedText;Term;", SRC I, TXT ";TermText;", TGT I
          ,TXT "{EX} DelPair;critShowText;Term;", SRC I, TXT ";TermText;", SRC critShowText
          )

ROLE ExecEngine MAINTAINS "Term text parsing - delete parsed TermText"
RULE "Term text parsing - delete parsed TermText": critParsedText /\ critText;-I |- -V
MEANING "Terms with a TermText that differs from its ParsedText may no longer have a ParsedText"
VIOLATION (TXT "{EX} DelPair;critParsedText;Term;", SRC I, TXT ";TermText;", TGT I)

critVarNames :: Term * PropertyName -- The PropertyNames that have been detected to exist in critText
ROLE ExecEngine MAINTAINS "Term text parsing - disassociate VarNames from Terms"
RULE "Term text parsing - disassociate VarNames from Terms": 
  (I-(critParsedText;critParsedText~));critVarNames |- -V
MEANING "Terms without a ParsedText may not be associated with variable( name)s"
VIOLATION (TXT "{EX} DelPair;critVarNames;Term;", SRC I, TXT ";PropertyName;", TGT I)

--[Auto-(de)assocation/creation of Variables with Terms]--
--$These rules must be adapted in case Variables are scoped (e.g. as in Transactions)
ROLE ExecEngine MAINTAINS "Variables that are referred to in a Terms must exist"
RULE "Variables that are referred to in a Terms must exist": critVarNames |- V;varName
VIOLATION (TXT "{EX} NewStruct;Variable"
              ,TXT ";varName;Variable;_NEW;PropertyName;", TGT I
          )
ROLE ExecEngine MAINTAINS "Variables that are not mentioned in any Term may not exist"
RULE "Variables that are not mentioned in any Term may not exist": varName |- V;critVarNames
VIOLATION (TXT "{EX} DelAtom;Variable;", SRC I)

ROLE ExecEngine MAINTAINS "Variables must be associated with every Term that mentions them"
RULE "Variables must be associated with every Term that mentions them": varName;critVarNames~ |- varTerm
VIOLATION (TXT "{EX} InsPair;varTerm;Variable;", SRC I, TXT ";Term;", TGT I)

ROLE ExecEngine MAINTAINS "Variables that are associated with a Term must be referred to by that Term"
RULE "Variables that are associated with a Term must be referred to by that Term": varTerm |- varName;critVarNames~
VIOLATION (TXT "{EX} DelPair;varTerm;Variable;", SRC I, TXT ";Term;", TGT I)

--[Service body]--
{-This service ensures that runtime Terms (texts) may be shown to users with the values of variables filled in. More precisely put: this service ensures that for every TermText, a corresponding text (called the ShowText) exists that is the same as the TermText, but has replaced any variable name (i.e. any name surrounded by square brackets (`[` and `]`)) with the value of that variable provided that value exists.-}

varShowTerm  :: Variable * Term
MEANING "The value of the Variable has been substituted/is shown in the text of the Term"
RULE "varShowTerm integrity":  varShowTerm |- varTerm

varShowValue :: Variable * VarValue [UNI]
MEANING "In Terms, the Variable has been converted to the specified VarValue"
RULE "varShowValue integrity": varShowValue~;varValue |- I

critInitReq  :: Term * Term [PROP]
MEANING "A Term may have the property that its ShowText needs to be re-initialized."

ROLE ExecEngine MAINTAINS "critShowText - start initialization sequence"
RULE "critShowText - start initialization sequence": 
  (I-(critShowText;critShowText~));critText |- critInitReq;critText
MEANING "If a Term has no ShowText, it needs to be re-initialized"
VIOLATION (TXT "{EX} InsPair;critInitReq;Term;", SRC I, TXT ";Term;", SRC I)

ROLE ExecEngine MAINTAINS "critShowText - re-init cleanup"
RULE "critShowText - re-init cleanup": varShowTerm;critInitReq |- -V
MEANING "If a Term needs to be re-initialized, Variables may not be listed as being used in its ShowText"
VIOLATION (TXT "{EX} DelPair;varShowTerm;Variable;", SRC I, TXT ";Term;", TGT I)

ROLE ExecEngine MAINTAINS "critShowText - initialize"
RULE "critShowText - initialize": critInitReq-(varShowTerm~;varShowTerm) |- critText;critShowText~
MEANING "A Term that needs to be re-initialized and does not use values of Variables in its ShowText, must have the Term-text as its ShowText, which complete the re-initialization."
VIOLATION (TXT "{EX} DelPair;critInitReq;Term;", SRC I, TXT ";Term;", TGT I
          ,TXT "{EX} InsPair;critShowText;Term;", SRC I, TXT ";TermText;", TGT critText
          )

ROLE ExecEngine MAINTAINS "critShowText - request re-init after a value update"
RULE "critShowText - request re-init after a value update": 
  (I /\ (varShowValue;-I;varValue~ \/ -(varValue;varValue~)));varShowTerm |- varShowTerm;critInitReq
MEANING "If a Variable is used in the ShowText of a Term, while its value does not exist (any more), or differs from the value used in the ShowText, then the Term needs to be re-initialized."
VIOLATION (TXT "{EX} DelPair;varShowValue;Variable;", SRC I, TXT ";VarValue;", SRC varShowValue
          ,TXT "{EX} InsPair;critInitReq;Term;", TGT I, TXT ";Term;", TGT I
          )

ROLE ExecEngine MAINTAINS "critShowText - Substitute variable name by its value"
RULE "critShowText - Substitute variable name by its value": 
  (I /\ varValue;varValue~);varTerm;(I-critInitReq) |- varShowTerm
MEANING "If a Variable has a value, and is used in a Term that is not being re-initialized, then it must appear in the ShowText of the Term."
VIOLATION (TXT "{EX} InsPair;critShowText;Term;", TGT I, TXT ";TermText;"
              , TXT "{php}str_replace('['.'", SRC varName
                             , TXT "'.']','", SRC varValue
                             , TXT "','", TGT critShowText
                             , TXT "')"
          ,TXT "{EX} InsPair;varShowTerm;Variable;", SRC I, TXT ";Term;", TGT I
          ,TXT "{EX} InsPair;varShowValue;Variable;", SRC I, TXT ";VarValue;", SRC varValue
          )

--[INTERFACE]--
POPULATION Message CONTAINS [ "=" ] REPRESENT Message TYPE ALPHANUMERIC -- Needed in the INTERFACE

INTERFACE "Terms and Variables": '_SESSION'[SESSION] cRud BOX <ROWSNL>
   [ "Terms" : V[SESSION*Term] CRuD COLS
      [ "Term definition" : critText cRUd
      , "Term as shown" : critShowText cRud
      ]
   , "Variables": V[SESSION*Variable] cRud COLS
      [ "Var" : varName cRud
      , "is"  : V;'='[Message] cRud
      , "Val" : varValue cRUd
      , "Used in Terms" : varTerm cRud <ShowTerm>
      ]
   ]

INTERFACE "Debug Information": '_SESSION'[SESSION] cRud BOX <ROWSNL>
   [ "Terms" : V[SESSION*Term] CRuD COLS
      [ "TermText" : critText cRUd
      , "ParsedText" : critParsedText cRud
      , "VarNames" : critVarNames cRud
      , "ShowText" : critShowText cRud
      , "InitReq" : critInitReq cRud <TermCheckbox>
      ]
   , "Variables": V[SESSION*Variable] cRud COLS
      [ "Name" : varName cRud
      , "Value" : varValue cRUd
      , "ShowValue" : varShowValue cRud
      , "Term" : varTerm cRud
      , "ShowTerm" : varShowTerm cRud
      ]
   ]

VIEW ShowTerm: Term (critText, TXT " (i.e.: '", critShowText, TXT "')")
VIEW TermCheckbox : Term HTML TEMPLATE "View-PROPERTY.html" ENDVIEW -- Needed because there is a default view for Term

-----------------------------------------------------------
ENDCONTEXT