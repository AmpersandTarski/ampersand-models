CONTEXT Statements IN ENGLISH MARKDOWN
INCLUDE "Atoms.adl"
INCLUDE "Sequence.adl"
--INCLUDE "Statements.xlsx"

PATTERN "Making Statements"

    CLASSIFY Statement ISA Sequence
    CLASSIFY Template ISA Sequence
    CLASSIFY Prefix ISA Item
    CLASSIFY Variable ISA Item
    RELATION typeName[Variable*Identifier] [UNI]
    RELATION preText[Variable*Identifier] [UNI]
    REPRESENT Identifier TYPE ALPHANUMERIC
    RELATION type[Variable*Concept] [UNI]
    RELATION bind[Prefix*Variable] [UNI]
    RELATION template[Statement*Template] [UNI]

    RULE addTypeByName : typeName |- type;name
    VIOLATION ( TXT "{EX} NewStruct;Concept"
              , TXT ";type;Variable;", SRC I[Variable], TXT ";Concept;_NEW"
              , TXT ";name;Concept;_NEW;Identifier;", TGT I[Identifier]
              )
    RULE insPairtypeName : type;name |- typeName
    VIOLATION ( TXT "{EX} InsPair;typeName;Variable;", SRC I[Variable], TXT ";Identifier;", TGT I[Identifier] )
    ROLE ExecEngine MAINTAINS addTypeByName, insPairtypeName

    RULE addFirstPlaceholder : template;head |- head;bind
    VIOLATION ( TXT "{EX} NewStruct;Prefix"
              , TXT ";head;Sequence;", SRC I[Sequence], TXT ";Prefix;_NEW"
              , TXT ";seq;Prefix;_NEW;Sequence;", SRC I[Sequence]
              , TXT ";bind;Prefix;_NEW;Variable;" , TGT I[Item]
              )
    ROLE ExecEngine MAINTAINS addFirstPlaceholder
    RULE addSuccPlaceholder : bind;succ |- succ;bind
    VIOLATION ( TXT "{EX} NewStruct;Prefix"
              , TXT ";succ;Prefix;", SRC I[Item], TXT ";Prefix;_NEW"
              , TXT ";seq;Prefix;_NEW;Sequence;", SRC seq
              , TXT ";bind;Prefix;_NEW;Variable;" , TGT I[Item]
              )
    ROLE ExecEngine MAINTAINS addSuccPlaceholder

    RULE newTemplate: name;V;'_SESSION' |- madeIn
    VIOLATION ( TXT "{EX} InsPair;madeIn;Sequence;", SRC I, TXT ";SESSION;", TGT I )
    ROLE ExecEngine MAINTAINS newTemplate
ENDPATTERN

INTERFACE "Statement" (template) : I[Statement]
ROWS [ template : template
     , variabelen : seq~
       COLS[ naam : bind;identifier
           , type : bind;type
           ]
     ]

INTERFACE Template (name[Sequence*Name], seq, seqHead, preText, type, identifier) : I[Template]
ROWS[ "naam van dit template" : name[Sequence*Name]
    ,  "Name your items here:" : seqHead
    , items : seq~
      COLS[ identifier : identifier
          , seq        : seq
          , preText    : preText
          , type       : type
          ]
    ]

PURPOSE INTERFACE overzicht
{+This interface is meant for testing the functioning of sequences.
This overview depicts all sequences together with all pointers that connect items.+}
INTERFACE overzicht(name[Sequence*Name]) : '_SESSION'
TABS[ Templates : V[SESSION*Template]
      COLS[ template : I
          , sequence : name
          , items : seq~
            COLS[ identifier : identifier
                ]
          ]
    , Beweringen : V[SESSION*Statement]
      COLS[ items : seq~
            COLS[ "gebonden aan" : bind;identifier
                , type : bind;type
                ]
          ]
    ]

ENDCONTEXT