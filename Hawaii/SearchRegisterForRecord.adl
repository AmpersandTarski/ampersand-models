CONTEXT "Search a register for a specific record"

INCLUDE "SearchRegisterBasicTest.pop"

CONCEPT NaturalPerson "someone of flesh and blood"
RELATION npNPID[NaturalPerson*NPID] [INJ,UNI,TOT]   REPRESENT NPID       TYPE ALPHANUMERIC
RELATION npLastName[NaturalPerson*LastName] [UNI,TOT]   REPRESENT LastName   TYPE ALPHANUMERIC
RELATION npFirstNames[NaturalPerson*FirstName]             REPRESENT FirstNames TYPE ALPHANUMERIC
RELATION npBirthDate[NaturalPerson*Date] [UNI]       REPRESENT Date       TYPE DATE
RELATION npGender[NaturalPerson*Gender] [UNI]

CONCEPT SearchForm "form that contains data with which to look up a person"
RELATION sfLastName[SearchForm*LastName] [UNI]
RELATION sfFirstNames[SearchForm*FirstName]
RELATION sfBirthDate[SearchForm*Date] [UNI]
RELATION sfGender[SearchForm*Gender] [UNI]

--[Gender]
RULE "Gender can only be 'Male', 'Female' or 'Unknown'": I[Gender] = "Male" \/ "Female" \/ "Unknown"

POPULATION Gender CONTAINS [ "Male", "Female", "Unknown" ]

--[Finding NaturalPersons based on criteria in SearchForm]
RELATION sfFoundNPs[SearchForm*NaturalPerson] -- NaturalPersons that satisfy all entries in the SearchForm

ROLE ExecEngine MAINTAINS "Find NaturalPersons that satisfy all SearchForm entries"
RULE "Find NaturalPersons that satisfy all SearchForm entries":
   (  sfLastName~\npLastName~
   /\ sfFirstNames~\npFirstNames~
   /\ sfBirthDate~\npBirthDate~
   /\ sfGender~\npGender~
   ) |- sfFoundNPs
VIOLATION (TXT "{EX} InsPair;sfFoundNPs;SearchForm;", SRC I, TXT ";NaturalPerson;", TGT I)

ROLE ExecEngine MAINTAINS "Found NaturalPersons must satisfy all SearchForm entries"
RULE "Found NaturalPersons must satisfy all SearchForm entries":
   sfFoundNPs
|- (  sfLastName~\npLastName~
   /\ sfFirstNames~\npFirstNames~
   /\ sfBirthDate~\npBirthDate~
   /\ sfGender~\npGender~
   )
VIOLATION (TXT "{EX} DelPair;sfFoundNPs;SearchForm;", SRC I, TXT ";NaturalPerson;", TGT I)

--[Test GUI]
RELATION sessionSearchForm[SESSION*SearchForm] [UNI]
ROLE ExecEngine MAINTAINS "Every SESSION has its SearchForm"
RULE "Every SESSION has its SearchForm": I |- sessionSearchForm;sessionSearchForm~
VIOLATION (TXT "{EX} InsAtom;SearchForm"
          ,TXT "{EX} InsPair;sessionSearchForm;SESSION;", SRC I, TXT ";SearchForm;_NEW"
          )

INTERFACE "Search a NaturalPerson": I[SESSION] cRud BOX
[ "Enter data to search for": sessionSearchForm cRud COLS
   [ "LastName"      : sfLastName cRUd
   , "FirstNames"    : sfFirstNames cRUd
   , "BirthDate"     : sfBirthDate cRUd
   , "Gender"        : sfGender cRUd
   ]
, "Records that satisfy the search data": sessionSearchForm;sfFoundNPs cRud COLS
   [ "NPID"          : npNPID cRud
   , "LastName"      : npLastName cRud
   , "FirstNames"    : npFirstNames cRud
   , "BirthDate"     : npBirthDate cRud
   , "Gender"        : npGender cRud
   ]
]

ENDCONTEXT