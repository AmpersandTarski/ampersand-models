CONTEXT "Search a register with specializations"

INCLUDE "SearchRegisterForRecord.adl"

POPULATION Gender CONTAINS [ "Male", "Female" ]

npGender :: NaturalPerson * Gender [UNI]
CLASSIFY "Man"   ISA NaturalPerson
ROLE ExecEngine MAINTAINS "A NaturalPerson with gender 'Male' is a Man"
RULE "A NaturalPerson with gender 'Male' is a Man": I[NaturalPerson] /\ npGender;"Male";npGender~ |- I[Man]
VIOLATION (TXT "{EX} SetConcept;NaturalPerson;Man;", SRC I)
ROLE ExecEngine MAINTAINS "A Man is a NaturalPerson with gender 'Male'"
RULE "A Man is a NaturalPerson with gender 'Male'": I[Man] |- I[NaturalPerson] /\ npGender;"Male";npGender~
VIOLATION (TXT "{EX} ClearConcept;Man;", SRC I)

sfGender :: SearchForm    * Gender [UNI]

INTERFACE "Search a Man": I[SESSION] cRud BOX
[ "Enter data to search for": sessionSearchForm cRud BOX
   [ "LastName"      : sfLastName cRUd
   , "FirstNames"    : sfFirstNames cRUd
   , "BirthDate"     : sfBirthDate cRUd
   , "BirthLocation" : sfBirthLocation cRUd
   , "Gender"        : sfGender cRUd
   ]
, "Records that satisfy the search data": sessionSearchForm;sfNPID;npNPID~ cRud COLS
   [ "NPID"          : npNPID cRud
   , "LastName"      : npLastName cRud
   , "FirstNames"    : npFirstNames cRud
   , "BirthDate"     : npBirthDate cRud
   , "BirthLocation" : npBirthLocation cRud
   , "Gender"        : npGender cRUd
   ]
]

ROLE ExecEngine MAINTAINS "Satisfactory records"
RULE "Satisfactory records":
   (  sfLastName~\npLastName~
   /\ sfFirstNames~\npFirstNames~
   /\ sfBirthDate~\npBirthDate~
   /\ sfBirthLocation~\npBirthLocation~
   /\ sfGender~\npGender~
   );npNPID |- sfNPID
VIOLATION (TXT "{EX} InsPair;sfNPID;SearchForm;", SRC I, TXT ";NPID;", TGT I)

ROLE ExecEngine MAINTAINS "Unsatisfactory records"
RULE "Unsatisfactory records":
   sfNPID
|- (  sfLastName~\npLastName~
   /\ sfFirstNames~\npFirstNames~
   /\ sfBirthDate~\npBirthDate~
   /\ sfBirthLocation~\npBirthLocation~
   );npNPID
VIOLATION (TXT "{EX} DelPair;sfNPID;SearchForm;", SRC I, TXT ";NPID;", TGT I)

POPULATION npNPID          CONTAINS [ ("NP001", "9001") ]
POPULATION npLastName      CONTAINS [ ("NP001", "Puk") ]
POPULATION npFirstNames    CONTAINS [ ("NP001", "Pieter") ]
POPULATION npFirstNames    CONTAINS [ ("NP001", "Maria") ]
POPULATION npBirthDate     CONTAINS [ ("NP001", 2001-01-01) ]
POPULATION npBirthLocation CONTAINS [ ("NP001", "Amsterdam") ]

POPULATION npNPID          CONTAINS [ ("NP002", "9002") ]
POPULATION npLastName      CONTAINS [ ("NP002", "Puk") ]
POPULATION npFirstNames    CONTAINS [ ("NP002", "Madelief") ]
POPULATION npFirstNames    CONTAINS [ ("NP002", "Roos") ]
POPULATION npFirstNames    CONTAINS [ ("NP002", "Maria") ]
POPULATION npBirthDate     CONTAINS [ ("NP002", 2002-02-02) ]
POPULATION npBirthLocation CONTAINS [ ("NP002", "Beetsterzwaag") ]

POPULATION npNPID          CONTAINS [ ("NP003", "9003") ]
POPULATION npLastName      CONTAINS [ ("NP003", "van der Kluns") ]
POPULATION npFirstNames    CONTAINS [ ("NP003", "Madelief") ]
POPULATION npBirthDate     CONTAINS [ ("NP003", 2003-03-03) ]
POPULATION npBirthLocation CONTAINS [ ("NP003", "Callandsoog") ]

POPULATION npNPID          CONTAINS [ ("NP004", "9004") ]
POPULATION npLastName      CONTAINS [ ("NP004", "van der Kluns") ]
POPULATION npFirstNames    CONTAINS [ ("NP004", "Jantje") ]
POPULATION npFirstNames    CONTAINS [ ("NP004", "Maria") ]
POPULATION npBirthDate     CONTAINS [ ("NP004", 2004-04-04) ]
POPULATION npBirthLocation CONTAINS [ ("NP004", "Dordrecht") ]

POPULATION npNPID          CONTAINS [ ("NP005", "9005") ]
POPULATION npLastName      CONTAINS [ ("NP005", "Vaak") ]
POPULATION npFirstNames    CONTAINS [ ("NP005", "Klaas") ]
POPULATION npBirthDate     CONTAINS [ ("NP005", 2005-05-05) ]
POPULATION npBirthLocation CONTAINS [ ("NP005", "Enschede") ]

POPULATION npNPID          CONTAINS [ ("NP006", "9006") ]
POPULATION npLastName      CONTAINS [ ("NP006", "Vaak") ]
POPULATION npFirstNames    CONTAINS [ ("NP006", "Catharina") ]
POPULATION npFirstNames    CONTAINS [ ("NP006", "Maria") ]
POPULATION npBirthDate     CONTAINS [ ("NP006", 2006-06-06) ]
POPULATION npBirthLocation CONTAINS [ ("NP006", "Enschede") ]




ENDCONTEXT