CONTEXT Hawaii IN ENGLISH
PURPOSE CONTEXT Hawaii
{+Once in their school career, students get to go on a field trip abroad.
However, the organizer of the field trip may require that you have completed specific courses.
This application gives overviews of students and the trips for which they qualify.
The application illustrates the use of residuals.
+}

RELATION required[Subject*Destination]
MEANING "A subject that you must have passed to qualify for the school trip to a destination"

POPULATION required CONTAINS
   [ ("Surfing", "Hawaii")
   ; ("Latin", "Rome")
   ; ("World Religions", "Rome")
   ]

RELATION passed[Subject*Student]
MEANING "The subjects that have been passed by specific students"

POPULATION passed CONTAINS
   [ ("Surfing", "Brown")
   ; ("Surfing", "Conway")
   ; ("Latin", "Brown")
   ; ("World Religions", "Applegate")
   ; ("World Religions", "Brown")
   ]

RELATION registered[Student*Destination]
-- equivalent rule:  required;registered~ |- passed
PURPOSE RULE guardPrerequisites {+ This rule prevents students from registering for a trip without having passed the required courses. +}
RULE guardPrerequisites : registered~ |- required\passed
MEANING "A student can only attend a trip, if the student passed all the required courses for that destination."
MESSAGE "Attempt to register student(s) for a trip without the proper qualification."
VIOLATION (TXT "Student ", TGT I, TXT " cannot go to ", SRC I, TXT " without passing ", SRC required~)

POPULATION Destination CONTAINS  [ "Amsterdam" ]

INTERFACE Overview : "_SESSION"                       cRud
BOX <TABS>
     [ Students     : V[SESSION*Student]              cRuD
       BOX <TABLE>
            [ "Student" : I[Student]                  cRud
            , "passed" : passed~                      CRUD
            , "Qualify for" : passed~/required~       cRud
            , "registered" : registered               CRUd
            ]
     , Subjects     : V[SESSION*Subject]              cRuD
       BOX <TABLE>
            [ "Subject" : I                           cRud
            , "required for trip" : required          CRUD
            , "students that passed" : passed         CRUD
            ]
     , Destinations : V[SESSION*Destination]          cRuD
       BOX <TABLE>
            [ "Destination" : I                       cRud
            , "required subject" : required~          CRUD
            , "Qualifying students" : required\passed cRud
            , "registered" : registered~              cRUd
            ]
     ]

ENDCONTEXT