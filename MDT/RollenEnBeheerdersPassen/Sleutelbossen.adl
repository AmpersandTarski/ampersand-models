CONTEXT Sleutelbossen IN DUTCH
INCLUDE "Sleutelbossen.xlsx"
RELATION mnemonic  [KlantSleutel*SleutelType  ] [UNI,TOT]
RELATION klantIdent[KlantSleutel*KlantIdent   ] [UNI,TOT]
RELATION waarde    [KlantSleutel*SleutelWaarde] [UNI,TOT]


ROLE ExecEngine MAINTAINS "Elke klantIdent krijgt automatisch een klantsleutel van het type 'RDW-IDENT'" 
RULE "Elke klantIdent krijgt automatisch een klantsleutel van het type 'RDW-IDENT'" 
      : I[KlantIdent] |-  klantIdent~
                        ; (I[KlantSleutel] /\ (mnemonic;'RDW-IDENT';mnemonic~))
                        ; klantIdent
MEANING "Elke klantIdent krijgt automatisch een klantsleutel van het type 'RDW-IDENT'" 
VIOLATION (TXT "{EX} NewStruct;KlantSleutel"
          ,TXT ";klantIdent;KlantSleutel;_NEW;KlantIdent;", SRC I
          ,TXT ";mnemonic;KlantSleutel;_NEW;SleutelType;RDW-IDENT"
          )
ROLE ExecEngine MAINTAINS "De waarde van een sleutel van het type 'RDW-IDENT' wordt automatisch gegenereerd."
RULE "De waarde van een sleutel van het type 'RDW-IDENT' wordt automatisch gegenereerd."
      : (I[KlantSleutel] /\  (mnemonic;'RDW-IDENT';mnemonic~))
        |- (waarde;waarde~)
MEANING "De waarde van een sleutel van het type 'RDW-IDENT' wordt automatisch gegenereerd."
VIOLATION (TXT "{EX} NewStruct;SleutelWaarde"
          ,TXT ";waarde;KlantSleutel;", TGT I,TXT ";SleutelWaarde;_NEW"
          )
          

POPULATION SleutelType CONTAINS ["RDW-IDENT"]

RULE "twee sleutels aan dezelfde sleutelbos mogen niet dezelfde sleuteltype hebben":
  (mnemonic;mnemonic~) /\ (klantIdent;klantIdent~) |- I
MEANING "Er bestaat al een klantsleutel voor deze klant-ident en van dit type."

RULE "twee sleutels van hetzelfde type mogen niet dezelfde waarde hebben" :
  (mnemonic;mnemonic~) /\ (waarde;waarde~) |- I
MEANING "Er bestaat al een klantsleutel van dit type met deze waarde."

INTERFACE "KlantIdents" : '_SESSION';V[SESSION*KlantIdent] LINKTO INTERFACE "KlantIdent"
INTERFACE "KlantIdent" : I[KlantIdent]
BOX [ "sleutels" : klantIdent~ CRuD BOX <SHCOLS>
        [ "type" : mnemonic cRUd
        , "waarde" : waarde CRUd
        ]
    ]   
INTERFACE "KlantSleutels" FOR Sleutelbeheerder : '_SESSION';V[SESSION*KlantSleutel] CRuD BOX <SHCOLS> 
  [ "Sleutelbos" : klantIdent
  , "type" : mnemonic cRUD
  , "waarde" : waarde
  ]      

INTERFACE "Sleuteltypes" FOR Sleutelbeheerder : '_SESSION';V[SESSION*SleutelType] CRuD BOX <SHCOLS> 
  [ "type" : I cRuD
  ]      
INTERFACE "Sleuteltype" : I[SleutelType] CRuD BOX 
  [ sleutels: mnemonic~ CRuD BOX <SHCOLS>
     [ "Sleutelbos" : klantIdent
     , "type" : mnemonic cRud
     , "waarde" : waarde
     ]
  ]
INTERFACE "Waarde" : I[SleutelWaarde] CRUD BOX
  [ sleutels: waarde~ CRuD BOX <SHCOLS>
     [ "Sleutelbos" : klantIdent
     , "type" : mnemonic 
     , "waarde" : waarde cRud
     ]
  ]
  
ENDCONTEXT