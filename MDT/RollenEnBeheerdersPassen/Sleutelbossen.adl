CONTEXT Sleutelbossen IN DUTCH
INCLUDE "Sleutelbossen.xlsx"
RELATION mnemonic  [KlantSleutel*SleutelType  ] [UNI,TOT]
RELATION klantIdent[KlantSleutel*KlantIdent   ] [UNI,TOT]
RELATION rdwIdent  [KlantIdent  *RdwIdent     ] [UNI,TOT,SUR] 
RELATION waarde    [KlantSleutel*SleutelWaarde] [UNI,TOT]


ROLE ExecEngine MAINTAINS "TOT rdwIdent[KlantIdent*RdwIdent]",maakRdwIdent
RULE maakRdwIdent : I[KlantIdent] |- rdwIdent;rdwIdent~
MEANING "Voor elke klant-ident wordt automatisch een RDW-ident gemaakt."
VIOLATION ( TXT "{EX} NewStruct;RdwIdent"
          , TXT ";rdwIdent;KlantIdent;", SRC I, TXT ";RdwIdent;_NEW")

ROLE ExecEngine MAINTAINS "Elke sleutelbos krijgt automatisch een klantsleutel van het type 'RDW-IDENT'" 
RULE "Elke sleutelbos krijgt automatisch een klantsleutel van het type 'RDW-IDENT'" 
  : rdwIdent |- klantIdent~;mnemonic;'RDW-IDENT';mnemonic~;klantIdent;rdwIdent
MEANING "Elke sleutelbos krijgt automatisch een klantsleutel van het type 'RDW-IDENT' met als waarde de RdwIdent van die klantIdent."
VIOLATION ( TXT "{EX} NewStruct;KlantSleutel"
          , TXT ";klantIdent;KlantSleutel;_NEW;KlantIdent;", SRC I
          , TXT ";mnemonic;KlantSleutel;_NEW;SleutelType;RDW-IDENT"
          , TXT ";waarde;KlantSleutel;_NEW;SleutelWaarde;", TGT I
          )

POPULATION SleutelType CONTAINS ["RDW-IDENT"]

RULE "twee sleutels aan dezelfde sleutelbos mogen niet dezelfde sleuteltype hebben":
  (mnemonic;mnemonic~) /\ (klantIdent;klantIdent~) |- I
MEANING "Er bestaat al een klantsleutel voor deze klant-ident en van dit type."

RULE "twee sleutels van hetzelfde type mogen niet dezelfde waarde hebben" :
  (mnemonic;mnemonic~) /\ (waarde;waarde~) |- I
MEANING "Er bestaat al een klantsleutel van dit type met deze waarde."

INTERFACE "KlantIdents" : '_SESSION';V[SESSION*KlantIdent] LINKTO INTERFACE "KlantIdent"
INTERFACE "KlantIdent" : I[KlantIdent]
BOX [ "rdw-ident" : rdwIdent cRud
    , "sleutels" : klantIdent~ CRuD BOX <SHCOLS>
        [ "type" : mnemonic CRUD
        , "waarde" : waarde CRUD
        ]
    ]   
INTERFACE "KlantSleutels" : '_SESSION';V[SESSION*KlantSleutel] CRuD BOX <SHCOLS> 
  [ "Sleutelbos" : klantIdent
  , "type" : mnemonic cRUD
  , "waarde" : waarde
  ]      

INTERFACE "Sleuteltypes" : '_SESSION';V[SESSION*SleutelType] CRuD BOX <SHCOLS> 
  [ "type" : I cRuD
  ]      
INTERFACE "Sleuteltype" : I[SleutelType] CRuD BOX 
  [ sleutels: mnemonic~ CRuD BOX <SHCOLS>
     [ "Sleutelbos" : klantIdent
     , "type" : mnemonic cRud
     , "waarde" : waarde
     ]
  ]
INTERFACE "Waarde" : I[SleutelWaarde] CRUD BOX
  [ sleutels: waarde~ CRuD BOX <SHCOLS>
     [ "Sleutelbos" : klantIdent
     , "type" : mnemonic 
     , "waarde" : waarde cRud
     ]
  ]
ENDCONTEXT