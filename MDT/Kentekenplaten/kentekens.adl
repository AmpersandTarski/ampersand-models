CONTEXT Kentekens IN DUTCH
INCLUDE "kentekens.xlsx"

META "authors" "Mark Enuma"
META "authors" "Han Joosten"
PATTERN Kentekenplaten
RELATION eigenaarHouder[Voertuig*Persoon] [UNI]
MEANING "Elk voertuig heeft maximaal één eigenaarhouder."

PURPOSE RELATION opgegeven[Voertuig*Kenteken]
REF "Artikel 36 lid 1"
{+ Omdat aan de eigenaar houder van een voertuig door RDW een kenteken moet zijn opgegeven, dient dat te kunnen worden geregistreerd 
-} 
RELATION opgegeven[Voertuig*Kenteken] [UNI,TOT] 
MEANING "Voor elk voertuig is precies één kenteken opgegeven."

RELATION voorzien[KentekenPlaat*Kenteken] [UNI]
MEANING "Elke kentekenplaat is voorzien van maximaal één kenteken."

RELATION namens[Aanvraag*Persoon] [UNI,TOT]
MEANING "Elke aanvraag is namens precies één persoon."

RELATION kenteken[Aanvraag*Kenteken] [UNI,TOT]
MEANING "Elke aanvraag bevat precies één kenteken."

RELATION aan[Aanvraag*Fabrikant] [UNI,TOT]
MEANING "Elke aanvraag is aan precies één fabrikant."

RELATION overlegd[Afgifte*Kentekenbewijs] [UNI]
MEANING "Elke afgifte bevat maximaal één kentekenbewijs dat is overlegd."

RELATION voertuig[Kentekenbewijs*Voertuig] [UNI,TOT]
MEANING "Elk kentekenbewijs is voor precies één voertuig."

RELATION aan[Afgifte*Persoon] [UNI,TOT]
MEANING "Elke afgifte is aan precies één persoon."


RELATION betreft[Afgifte*KentekenPlaat] [UNI,TOT]
MEANING "Elke afgifte betreft precies één kentekenplaat."


RELATION fabrikant[KentekenPlaat*Fabrikant] [UNI,TOT]
MEANING "Elke kantekenplaat is/kan worden gefabriceerd door precies één fabrikant."

RELATION nav[KentekenPlaat*Aanvraag] [UNI,INJ]
MEANING "Een kentekenplaat kan zijn gemaakt naar aanleiding van maximaal één aanvraag"
PURPOSE RELATION voorzien[Aanvraag*Duplicaatcode]
{+Omdat de fabrikant moet weten of en zo ja welke duplicaatcode gebruikt moet worden op de te fabriceren kentekenplaat, moet een aanvraag voorzien kunnen worden van de te hanteren duplicaatcode.
-}
RELATION voorzien[Aanvraag*Duplicaatcode][UNI]
MEANING "Elke aanvraag kan zijn voorzien van de tegebruiken duplicaatcode"

PURPOSE RELATION staan[KentekenPlaat*Duplicaatcode]
{+Om grip te hebben op de kentekenplaten die in omloop zijn, worden duplicaatcodes gehanteerd bij de fabricage.
-}
RELATION staan[KentekenPlaat*Duplicaatcode][UNI]
MEANING "Op elke kentekenplaat kan maximaal één duplicaatcode staan"


PURPOSE RULE "kentekenbewijs is overlegd voordat afgifte plaats vindt" REF "Art. 9a regeling legitimatievoorschriften tenaamstelling en kentekenplaten"
{+Bij de verkrijging van kentekenplaten door een natuurlijk persoon worden overgelegd: de kentekencard, het deel IA of het deel I van het kentekenbewijs, afgegeven voor het kenteken dat op de kentekenplaten is vermeld. 
-}
RULE "kentekenbewijs is overlegd voordat afgifte plaats vindt" : I[Afgifte] |- overlegd;overlegd~
MEANING "Bij afgifte van een kentekenplaat wordt een kentekenbewijs overlegd." 


 
--RULE "aanvraag altijd namens eigenaarhouder" : namens |- kenteken;opgegeven~;eigenaarHouder
--MEANING "Elke aanvraag wordt gedaan namens de eigenaar/houder van het voertuig waarvoor het kenteken op de aanvraag is opgegeven."

{-RULE "Elke afgifte, naar aanleiding van een aanvraag, betreft een kentekenplaat die is voorzien van het kenteken waarvoor die aanvraag is gedaan." : betreft;voorzien |- nav;kenteken
MEANING "Elke afgifte, naar aanleiding van een aanvraag, betreft een kentekenplaat die is voorzien van het kenteken waarvoor die aanvraag is gedaan."
-}

RULE "controleren kentekenbewijsverkrijging door een natuurlijk persoon" -- Regeling legitimatievoorschriften tenaamstelling en kentekenplaten, artikel 9 a 
   : overlegd |- betreft;voorzien;opgegeven~;voertuig~
MEANING "Het kentekenbewijs dat wordt overlegd hoort bij het voertuig waarvoor het kenteken wordt afgegeven."
MESSAGE "Het overlegde kentekenbewijs voldoet niet."
--ROLE Test MAINTAINS "controleren kentekenbewijsverkrijging door een natuurlijk persoon"

RULE "verstrekken duplicaatcode" 
  : I[Aanvraag] |- 
       (  (voorzien[Aanvraag*Duplicaatcode];voorzien~)
        \/ afgewezen
       ) 
MEANING "Elke aanvraag wordt door RDW voorzien van de te gebruiken duplicaatcode dan wel afgewezen."
ROLE RDW MAINTAINS "verstrekken duplicaatcode"
RELATION afgewezen[Aanvraag*Aanvraag][PROP]

RULE "valideren aanvraag" : (I/\(voorzien;voorzien~)) |-
        namens;eigenaarHouder~;opgegeven;kenteken~
MEANING "Een aanvraag kan alleen van een duplicaatcode worden voorzien indien die aanvraag is gedaan namens de eigenaar/houder van het voertuig met het kenteken dat is vermeld op de aanvraag."

--RULE "registratie fabrikant" : I[KentekenPlaat]/\ voorzien;voorzien~ |- ingeslagen;ingeslagen~
--MEANING "Van elke kentekenplaat die voorzien is van een kenteken, is geregistreerd welke fabrikant die plaat heeft ingeslagen."

RULE "fabriceren kentekenplaat" : voorzien[Aanvraag*Duplicaatcode];voorzien~ |- nav~;nav
MEANING "Elke aanvraag waarvoor de duplicaatcode is vastgesteld, wordt een kentekenplaat ingeslagen."
ROLE Fabrikant MAINTAINS "fabriceren kentekenplaat"

-- TODO: EXECENGINE
RULE "bepalen juiste kenteken" : nav;kenteken |- voorzien 
RULE "bepalen juiste duplicaatcode" : nav;voorzien |- staan 
-- ROLE Fabrikant MAINTAINS "bepalen juiste kenteken","bepalen juiste duplicaatcode"


RULE "onjuist kenteken" : voorzien |- nav;kenteken
MEANING "Elke kentekenplaat die is voorzien van een kenteken is naar aanleiding van een aanvraag met dat kenteken." 
RULE "onjuist duplicaatcode" : staan |- nav;voorzien
MEANING "Elke kentekenplaat waarop een duplicaatcode staat is naar aanleiding van een aanvraag die is voorzien van die duplicaatcode."

RULE "inslaan door juiste fabrikant" : nav;aan |- fabrikant
MEANING "De fabrikant van de kentekenplaat is dezelfde als die staat vermeld op de aanvraag naar aanleiding waarvan de kentekenplaat is ingeslagen."

RULE "fabricage slechts op aanvraag" : 
  I[KentekenPlaat] /\ ((voorzien;voorzien~) \/ (staan;staan~))
    |- nav;nav~
MEANING "Elke niet-blanco kentekenplaat is ontstaan naar aanleiding van een aanvraag." 
--ROLE Test MAINTAINS "fabricage slechts op aanvraag", "onjuist kenteken", "onjuist duplicaatcode"


---AFGEVEN KENTEKENPLAAT 
RULE "afgeven kentekenplaat" : I/\(staan;staan~)/\(voorzien;voorzien~)
   |- betreft~;betreft
MEANING "Elke niet-blanco kentekenplaat moet zijn afgegeven"
ROLE Fabrikant MAINTAINS "afgeven kentekenplaat"

RULE "alleen afgeven aan eigenaar houder" : aan |- betreft;voorzien;opgegeven~;eigenaarHouder
MEANING "Een kentekenplaat wordt slechts afgegeven aan de eigenaarhouder van het voertuig"

ENDPATTERN

INTERFACE Rdw : I[SESSION] cRud
TABS [ aanvragen : I;V[SESSION*Aanvraag] cRud
     , "Voertuigen"  : "_SESSION";V[SESSION*Voertuig]  CRuD
          BOX<SCOLS> [ "VIN" : vin CRUd
                     , "Kenteken" : opgegeven CRUd
                     , "eigenaar / houder" : eigenaarHouder CRUd
                     ]
     , "Fabrikanten" : "_SESSION";V[SESSION*Fabrikant] CRuD 
          BOX<SCOLS> [ -- "Fabrikant" : naam CRUd
                       "Fabrikant" : I CRud LINKTO INTERFACE Fabrikant
                     ]
     ]

INTERFACE DuplicaatCodeVerstrekken FOR RDW : I[Aanvraag]
BOX [ kenteken : kenteken cRud
    , "Reeds verstrekte duplicaatcodes" : kenteken;voorzien~;staan
    , "eigenaar / houder" : kenteken;opgegeven~;eigenaarHouder
    , "aanvraag namens" : namens cRud
    , afwijzen : afgewezen
    , duplicaatcode : voorzien CRUd
    ]
INTERFACE Aanvraag : I[Aanvraag] cRud
BOX [ fabrikant : aan cRud
    , kenteken : kenteken cRud
    , aanvrager : namens CRud
    ]
INTERFACE Aanvragen FOR Aanvrager : I[Aanvraag] CRUD
BOX [ fabrikant : aan cRUd
    , kenteken : kenteken CRUd
    , aanvrager : namens CRUD
    ]
INTERFACE Kenteken FOR RDW : I[Kenteken]
BOX [ kenteken : cijfersletters CRUD
    ]
    
RELATION cijfersletters[Kenteken*KentekenCijfersLetters][UNI,TOT,INJ] REPRESENT KentekenCijfersLetters TYPE ALPHANUMERIC
IDENT Kenteken : Kenteken(cijfersletters)

RELATION lamineercode[KentekenPlaat*Lamineercode] [UNI,TOT,INJ]
MEANING "Een op een blanco kentekenplaat aan te brengen nummer, aan de hand waarvan iedere kentekenplaat kan worden geïdentificeerd."
IDENT KentekenPlaat : KentekenPlaat(lamineercode)
REPRESENT Lamineercode TYPE ALPHANUMERIC
VIEW KentekenPlaat : KentekenPlaat DEFAULT { "lamineercode": lamineercode } ENDVIEW

RELATION vin[Voertuig*VIN][UNI,TOT] --FIXME: Als INJ wordt toegevoegd, doet de IDENT het niet meer (goed)!!
REPRESENT VIN TYPE INTEGER
IDENT VoertuigVin : Voertuig(vin)
IDENT VoertuigKenteken : Voertuig(opgegeven)
VIEW Voertuigen : Voertuig DEFAULT { "VIN": vin } ENDVIEW
--RULE "Elk voertuig heeft een eigen, unieke VIN" : vin;vin~|- I
--MEANING "Elk voertuig heeft een eigen, unieke VIN."

INTERFACE Voertuig FOR RDW : I[Voertuig]
BOX [ vin : vin CRUd
    , kenteken : opgegeven CRUd
    , "eigenaar / houder" : eigenaarHouder CRUd
    ]
RELATION persoonId[Persoon*PersoonID][UNI,TOT]
REPRESENT PersoonID TYPE ALPHANUMERIC
IDENT Persoon : Persoon(persoonId)
VIEW Personen : Persoon DEFAULT { "bsn" : persoonId } ENDVIEW

INTERFACE Persoon FOR RDW : I[Persoon]
BOX [ persoon : persoonId CRUD
    ]


INTERFACE Fabrikant FOR RDW : I[Fabrikant] CRUD
BOX [ naam : naam CRUD
    , "kentekenplaten" : fabrikant~ CRud 
          BOX<SCOLS> [ "Kentekenplaat" : I CRud LINKTO INTERFACE KentekenPlaat
                     , voertuig : voorzien;opgegeven~ cRud
                     , kentekenbewijs : voorzien;opgegeven~;voertuig~ cRud
                     , "e/h"  : voorzien;opgegeven~;eigenaarHouder cRud
                     ]
    , "aanvragen"      : aan~ cRud
         BOX<SCOLS> 
       [ "aanvrager" : namens cRud
       , "Kenteken" : kenteken cRud
       , "Duplicaatcode" : voorzien cRud
       ] 
    ]
INTERFACE Afgeven FOR Fabrikant : I[Afgifte]
BOX [ lamineercode : betreft cRUd
    , "Voertuig" : betreft cRud COLS
        [ kenteken : voorzien cRud
        , "te overleggen" : voorzien;opgegeven~;voertuig~ cRud
        , "eigenaar houder" : voorzien;opgegeven~;eigenaarHouder cRud
        ]
    , "afgegeven aan" : aan CRUd
    , "kentekenbewijs" : overlegd CRUd
    ]

INTERFACE Fabriceren FOR Fabrikant : I[Aanvraag] cRUd
BOX [ "te produceren" : I COLS [ "kenteken"      : kenteken cRud
                           , "duplicaatcode" : voorzien cRud
                           ]
    , lamineercode : nav~ cRUd 
    , details : nav~ --INTERFACE KentekenplaatGegevensVullen
        BOX [ kenteken : voorzien cRUd
            , duplikaatcode : staan cRUd
            ]
    ]
--INTERFACE KentekenplaatGegevensVullen FOR Fabrikant : I[KentekenPlaat]


INTERFACE KentekenPlaat : I[KentekenPlaat] cRud
BOX [ lamineercode : lamineercode cRud
    , aanvraag : nav cRud INTERFACE Aanvraag
    , voorzien : voorzien cRud
    , duplicaatcode : staan cRud 
    , afgifte : betreft~ cRud 
    ]
        
RELATION naam[Fabrikant*Naam][UNI,TOT,INJ]
REPRESENT Naam TYPE ALPHANUMERIC
IDENT Fabrikant : Fabrikant(naam)
VIEW Fabrikanten : Fabrikant DEFAULT { "naam" : naam } ENDVIEW

-- The following VIEW replaces 'dirty' SESSION identifiers with the text "My Session" or "Some other Session"
VIEW Sessions: SESSION DEFAULT 
{ "uid": "_SESSION"[SESSION];V;"My Session"[LoginMessage] 
     \/ (I[SESSION]-"_SESSION");V;"Some other Session"[LoginMessage]
} ENDVIEW
POPULATION LoginMessage CONTAINS [ "My Session", "Some other Session" ]

ENDCONTEXT