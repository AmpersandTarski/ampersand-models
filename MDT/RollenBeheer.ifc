CONTEXT Rollenbeheer IN DUTCH

INCLUDE "RollenBeheer.adl"
INCLUDE "Dienstencatalogus.adl"
INCLUDE "RollenBeheer2.xlsx"
INCLUDE "Autorisaties.xlsx"

--[Top level interfaces voor RDW]--
INTERFACE "Organisaties" FOR RDW : V[SESSION*Organisatie] BOX <SCOLS>
  [ "Organisaties"     : I[Organisatie]
  ]

INTERFACE "Moeder-Ids" FOR RDW : V[SESSION*Identiteit] --; -I[Beheerder] ;I[Identiteit]; -I[Rol]  
BOX <SCOLS>
  [ "alle"  : I[Identiteit]
  , "2" : -I[Beheerder] 
  , "3" : -I[Beheerder] ;I[Identiteit]
  , "4" : -I[Beheerder] ;I[Identiteit]; -I[Rol]
  , "Moeder-Id"  : I[Identiteit] LINKTO INTERFACE "Moeder-Id"
  , "Beheerder"   : beheerderVan~   LINKTO INTERFACE "RollenBeheer"
  , "Rollen"      : beheerderVan~;aangemaaktDoor~    LINKTO INTERFACE "Rol"
  , "klantgroep"  : zitIn
  ] 

INTERFACE "Organisatie" (kvkNummer,nieuw) FOR RDW: I[Organisatie] CRUD ROWS
  [ "Organisatie"    : I[Organisatie]
  , "kvk nummer"     : kvkNummer
  , "Nieuwe identiteit" : nieuw[Organisatie*Organisatie]
  , "Identiteiten"   : I[Organisatie] TABS
    [ "Moeder-ids"   :  hoortBij~; -I[Beheerder] ;I[Identiteit]; -I[Rol] LINKTO INTERFACE "Moeder-Id"
    , "Beheerders"   :  hoortBij~; (I[Identiteit]/\( I[Beheerder])) LINKTO INTERFACE "RollenBeheer" 
    , "Rollen"       :  hoortBij~; (I[Identiteit]/\( I[Rol])) LINKTO INTERFACE "Rol"
    ]
  ]
RELATION nieuw [Organisatie*Organisatie][PROP]

INTERFACE "Moeder-Id" (hoortBij, isBeheerd) : I[Identiteit];-I[Beheerder];I[Identiteit];-I[Rol]
  ROWS [ "Beheerder / Rollen" : I[Identiteit] COLS
          [ "Identiteit"       : I[Identiteit]
          , "Organisatie"      : hoortBij 
          , "Beheerd?"         : isBeheerd 
          , "Beheerder"        : beheerderVan~ LINKTO INTERFACE "RollenBeheer" 
          ]
       , "Toekenningen" : I[Identiteit] LINKTO INTERFACE "Toekennen klantbevoegdheden"
       ]
--[Top level interfaces voor Beheerder]--
INTERFACE "Beheerders"  FOR Beheerder : V[SESSION*Identiteit]; (I[Identiteit] /\ I[Beheerder]) BOX <SHCOLS>
  [ Beheerders : I[Identiteit] LINKTO INTERFACE "RollenBeheer" 
  ]

INTERFACE "RollenBeheer" (aangemaaktDoor,nieuweRol) FOR Beheerder: I[Identiteit] /\ I[Beheerder] ROWS
  [ "beheerder van" : beheerderVan COLS
     [ "Moeder-ID" : I
     , "Organisatie"      : hoortBij
     ]
  , "toegekend"   : toegekend[Bevoegdheid*Identiteit]~
  , "autorisaties": geautoriseerd
  , "Nieuwe rol" : nieuweRol[Identiteit*Identiteit]
--  , "Nieuwe rol" : aangemaaktDoor~ CRUD -- LINKTO INTERFACE Rol
--       [ "Rolnaam" : rolnaam CRUD
--       ]
  , "Rollen" : aangemaaktDoor~  BOX
     [ "Rolnaam"          : rolnaam CRUD
     , "details" : I[Rol] LINKTO INTERFACE "Rol"
     ]
  ]
RELATION nieuweRol[Identiteit*Identiteit][PROP]
ROLE ExecEngine MAINTAINS "aanmaken nieuwe rol" 
RULE "aanmaken nieuwe rol" : I |- -nieuweRol
VIOLATION ( TXT "NewStruct;Rol"
              , TXT ";aangemaaktDoor;Identiteit;_NEW;Identiteit;", SRC I
              , TXT ";hoortBij;Identiteit;_NEW;Organisatie;", SRC I[Rol];hoortBij
          , TXT "{EX} DelPair;nieuweRol;Identiteit;", SRC I, TXT ";Identiteit;", TGT I
          )    
RULE "Alleen een beheerder kan een nieuwe rol aanmaken.": nieuweRol |- beheerderVan;beheerderVan~
MEANING "Alleen een beheerder kan een nieuwe rol aanmaken."

INTERFACE "Rol" (beschikbaar,rolnaam) FOR Beheerder: I[Identiteit] /\ I[Rol] ROWS
  [ "Moeder-ID"        : aangemaaktDoor;beheerderVan 
  , "Organisatie"      : hoortBij
  , "Rolnaam"          : rolnaam CRUD
  , "Autorisaties van Moeder-ID" :  aangemaaktDoor;beheerderVan;geautoriseerd
  , "beschikbare diensten" : beschikbaar[Dienst*Identiteit]~
  , "Autorisaties van rol" : geautoriseerd
  ]
--INTERFACE "Nieuwe Beheerder" (beheerderVan~) : I[Identiteit] BOX
--  [ "Beheerder van" : beheerderVan
--  ] 
INTERFACE "Klantgroepen" FOR RDW : V[SESSION*Klantgroep] BOX <SCOLS>
  [ "Klantgroepen"     : I[Klantgroep]
  ]
INTERFACE "Klantgroep" (zitIn,toegekend[Bevoegdheid*Klantgroep]) FOR RDW : I[Klantgroep] COLS
  [ "Klantgroep"  : I
  , "Moeder-Ids"     : zitIn~ BOX [ "" : I LINKTO INTERFACE "Toekennen klantbevoegdheden" ]
  , "Bevoegdheden": toegekend~  
  ]

INTERFACE "Toekennen klantbevoegdheden" (zitIn,afwijking,toegekend[Bevoegdheid*Identiteit]) FOR RDW
        : I[Identiteit];-I[Beheerder];I[Identiteit];-I[Rol];I[Identiteit]  
     ROWS
  [ "Moeder-Id" : I[Identiteit]
  , "Klantgroep" : zitIn 
  , "klantgroep toekenningen"  : zitIn;toegekend~
  , "individuele afwijkingen"  : afwijking~
  , "individuele toekenningen" : toegekend[Bevoegdheid*Identiteit]~
  , "autorisaties" : geautoriseerd
  ]



PATTERN Automatisch
ROLE "ExecEngine" MAINTAINS "Aanmaken moederID"
RULE "Aanmaken moederID" : nieuw |- -nieuw
VIOLATION ( TXT "NewStruct;Identiteit"
              , TXT ";hoortBij;Identiteit;_NEW;Organisatie;", SRC I
          , TXT "{EX} DelPair;nieuw;Organisatie;", SRC I, TXT ";Organisatie;", TGT I
          )    
 


RULE "Elke identiteit hoort bij een organisatie" : I[Identiteit] |- hoortBij;hoortBij~ 
                          \/ I[Beheerder]       -- Dit is hier uitgesloten, want dat doet de ExecEngine
                          \/ I[Rol] -- Dit is hier uitgesloten, want dat doet de ExecEngine
MEANING "Elke identiteit hoort bij een organisatie"  
VIOLATION (TXT "'", SRC I[Identiteit], TXT "' hoort niet bij een organisatie.")

RELATION isBeheerd[Identiteit*Identiteit][PROP]
MEANING "Een identiteit kan beheerd zijn"

ROLE "ExecEngine" MAINTAINS "Aanmaken beheerder"
RULE "Aanmaken beheerder" : isBeheerd |- I /\ beheerderVan~;beheerderVan
VIOLATION ( TXT "NewStruct;Beheerder"
              , TXT ";beheerderVan;Identiteit;_NEW;Identiteit;", SRC I
              , TXT ";hoortBij;Identiteit;_NEW;Organisatie;", SRC I[Beheerder];hoortBij
          )    




ENDPATTERN



ENDCONTEXT