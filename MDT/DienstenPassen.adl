CONTEXT "RDW-Dienstenpassen" IN DUTCH
META "authors" ""
PATTERN Organisaties

RELATION eindDatum[Organisatie*Datum]             [UNI]
MEANING "De einddatum van een organisatie is de datum waarop de relatie van RDW met een organisatie is beëindigd."
PURPOSE RELATION eindDatum[Organisatie*Datum]
{+De relatie die RDW met een organisatie heeft kan op enig moment worden beëindigd. De organisatie blijft dan geregistreerd in de klantregistratie, echter met een andere status. Daarom is het van belang dat de datum waarop de relatie is beëindigd, kan worden geregistreerd. 
-}

PURPOSE CONCEPT Organisatie MARKDOWN
{+RDW levert diensten aan bedrijven, instanties en organisaties 

  * in de voertuigenbranche (erkende bedrijven),
  * in het kader van de WA-motorrijtuigen (WAM) en
  * overige. (BBS) 
  
RDW heeft verschillende klantregistraties. Het is daarom mogelijk dat een bedrijf in meerdere klantregistraties bestaat. Zo'n bedrijf heeft dan in elk van de klantregistraties een verschillend klantnummer. Er is binnen de scope van RDW-Dienstenpassen geen verband tussen die klantnummers. Binnen deze context worden ze beschouwd als verschillende organisaties. -}

    CONCEPT Organisatie "Onder organisatie wordt verstaan een bedrijf / instantie die is geregistreerd in één klantregistratie van RDW."
ENDPATTERN
REPRESENT Datum TYPE DATE

PATTERN "RDW-Dienstenpassen en Certificaten"

RELATION serieNr[Certificaat*SerieNummer]     [TOT,UNI,INJ]
MEANING "Elk certificaat heeft precies één serienummer, waarmee het certificaat uniek is te identificeren."
IDENT Certificaten : Certificaat(serieNr)
PURPOSE RELATION serieNr[Certificaat*SerieNummer]
{+In het gebruik van certificaten is het nodig om certificaten eenduidig te kunnen identificeren. Hiertoe heeft elk certificaat een uniek serienummer.
-}

RELATION houderVan[Certificaat*Organisatie] [TOT,UNI]
MEANING "Elk certificaat heeft precies één houder."
PURPOSE RELATION houderVan[Certificaat*Organisatie]
{+Om geauthenticeerd te kunnen worden voor diensten van RDW, worden certificaten uitgegeven aan organisaties. Hiermee kan de gebruiker van de dienst aantonen dat hij in naam van die organisatie handelt. We zeggen dan dat die organisatie houder is van het certificaat.
-}

RELATION verloopDatum[Certificaat*Datum]      [TOT,UNI]
MEANING "Elk certificaat heeft precies één verloopdatum."
PURPOSE RELATION verloopDatum[Certificaat*Datum]
{+Om te voorkomen dat een certificaat oneindig lang kan worden gebruikt, krijgt een certificaat bij productie een datum waarna dat certificaat verloopt. Deze verloopdatum wordt bij het aanmaken van het certificaat vastgesteld, en is daarna niet meer wijzigbaar.
-}

RELATION intrekkingsDatum[Certificaat*Datum]      [UNI]
MEANING "Elk certificaat heeft hooguit één intrekkingsdatum."
PURPOSE RELATION intrekkingsDatum[Certificaat*Datum] 
--Er kunnen allerlei redenen ontstaan om een certificaat eerder dan de verloopdatum ongeldig te laten worden. Dat kan op verzoek van de klant, maar ook op verzoek van RDW zelf, bijvoorbeeld als er een vermoeden is van fraude, als sanctie of omdat de relatie met de organisatie wordt beëindigd. 
{+Certificaten moeten ingetrokken kunnen worden, als daar aanleiding toe is. Dat gebeurt door middel van een intrekkingsdatum, die medebepalend is voor de geldigheidsduur van het certificaat.
-}

RELATION geldigTot[Certificaat*Datum] [TOT,UNI]
MEANING "Elk certificaat is geldig tot een datum."
PURPOSE RELATION geldigTot[Certificaat*Datum]
{+Een certificaat wordt gebruikt om te kunnen authentiseren ten behoeve van (digitale) diensten van RDW. Die authenticatie vindt plaats op een bepaald moment. Op dat moment moet het certificaat geldig zijn.
-}

RULE geldigheidCertificaat : -(intrekkingsDatum;intrekkingsDatum~); verloopDatum |- geldigTot
MEANING MARKDOWN "Een certificaat is geldig op een pijldatum als:\n\n  * de pijldatum op of vóór de intrekkingsdatum ligt, indien het certificaat een intrekkingsdatum heeft die ligt vóór de verloopdatum van het certificaat. \n  * de pijldatum op of vóór de verloopdatum van het certificaat ligt, in alle overige gevallen."    
PURPOSE RULE geldigheidCertificaat
{+Om grip te hebben op het gebruik van certificaten, is het nodig te kunnen vaststellen of een certificaat geldig is. Een certificaat is geldig vanaf het moment dat het certificaat bestaat, tot aan de uiterste geldigheids datum. Die datum kan worden afgeleid.
-}
RELATION initielePin[Certificaat*PIN]         [TOT,UNI]
MEANING "Elk certificaat heeft precies één initiele PIN code"
PURPOSE RELATION initielePin[Certificaat*PIN]
{+Om een certificaat te kunnen gebruiken om te authentiseren moet de gebruiker een PIN invoeren. De initiele pin wordt tijdens het produceren van een certificaat aangemaakt. De gebruiker kan/moet bij eerste gebruik een nieuwe pin kiezen, maar die wordt niet geadministreerd.
-}

RELATION puk[Certificaat*PUK]                 [TOT,UNI]
MEANING "Elk certificaat heeft precies één PUK code"
PURPOSE RELATION puk[Certificaat*PUK]
{+De gebruiker van het certificaat heeft een PUK nodig om zonder de huidige PIN een nieuwe PIN te kunnen instellen. De PUK code kan niet worden gewijzigd.
-}
RELATION cdRomCert[Organisatie*Certificaat]
MEANING "Een certificaat kan zijn verstrekt op CD-Rom"
PURPOSE RELATION cdRomCert[Organisatie*Certificaat]
{+In de huidige situatie worden certificaten verstrekt op CD-Rom. Daarom is het van belang te weten dat een organisatie over zo'n CD-Rom beschikt. Een organisatie kan over meerdere CD-Roms beschikken. Tijdens de vervangingstermijn bijvoorbeeld kunnen er twee actief zijn. 
-} 

CONCEPT Certificaat "Een certificaat is een middel waarmee authenticatie kan worden verricht."
PURPOSE CONCEPT Certificaat
{+Omdat we zeker willen zijn dat de gebruiker van diensten ook is wie die zegt te zijn, wordt gebruik gemaakt van (client) certificaten. 
-}
CONCEPT SerieNummer "Een serienummer is een identificerend kenmerk van een certificaat."
CONCEPT PIN "Een PIN (Personal Identification Number) is een code die nodig is om een certificaat te activeren."
CONCEPT PUK "Een PUK (Personal Unlock Kode) is een code die de houder van een certificaat nodig heeft wanneer die niet meer beschikt over de PIN, en toch het certificaat wil kunnen blijven gebruiken."


RELATION pasNr[Dienstenpas*PasNummer]         [TOT,UNI,INJ]
MEANING "Elke dienstenpas heeft precies één uniek pasnummer."
PURPOSE RELATION pasNr[Dienstenpas*PasNummer]
{+Om verschillende dienstenpassen te kunnen onderscheiden, en daarover te kunnen communiceren, wordt elke pas voorzien van een pasnummer. Verschillende passen moeten verschillende pasnummers hebben. 
-}

RELATION dragerVan[Dienstenpas*Certificaat]   [TOT,UNI,INJ]
MEANING "Elke dienstenpas is drager van precies één uniek certificaat."
PURPOSE RELATION dragerVan[Dienstenpas*Certificaat]
{+Een dienstenpas heeft als doel om als drager te fungeren van een certificaat. Eenzelfde certificaat kan nooit door meerdere dienstenpassen worden gedragen. 
-}

--RELATION geprintOp[Dienstenpas*Datum]         [TOT,UNI]
--MEANING "Elke dienstenpas is geprint op één bepaalde datum."
--PURPOSE RELATION geprintOp[Dienstenpas*Datum]
--{+Het is nuttig om te weten wanneer een dienstenpas is gemaakt. Er zou verwarring kunnen ontstaan door het begrip 'aanmaakdatum' hiervoor te gebruiken. Immers, een pas wordt al eerder vervaardigd door de leverancier van de pas. Het wordt echter pas een dienstenpas op het moment dat de kaart wordt geprint.
---}
ROLE CA MAINTAINS versturenDienstenpas

CONCEPT Dienstenpas "Een (RDW-)dienstenpas is een smartcard uitgegeven door de RDW aan haar klanten, als drager van een certificaat die door de RDW wordt gebruikt om de klant te kunnen identificeren en authentiseren, waarna deze digitale diensten kan afnemen waarvoor authenticatie noodzakelijk is."
RULE cdRomOfPas : cdRomCert~;cdRomCert = I[Certificaat] /\ -(dragerVan~;dragerVan)
MEANING "Elk certificaat heeft een dienstenpas als drager of staat op een CD-Rom, maar nooit op beide."
PURPOSE RULE cdRomOfPas
{+Naast de bestaande certificaten op CD-Rom en de certificaten die op dienstenpassen staan, bestaan er nog veel meer certificaten. Die zijn echter niet van belang binnen deze context. Vandaar dat we ons beperken. Ook kan het niet zo zijn dat een certificaat dat op CD-Rom staat, opnieuw wordt verstrekt op een dienstenpas.
-}

ENDPATTERN


PATTERN Aanvragen
PURPOSE PATTERN Aanvragen MARKDOWN
{+Ten behoeve van beheer en uitgifte van certificaten worden mutaties geïnitieerd door middel van een aanvraag daartoe. De volgende soorten aanvraag wordt hierbij onderscheiden:

* Aanvraag nieuwe passen.
* Aanvraag intrekking.
* Aanvraag vervanging ad-hoc
* Aanvraag vervanging verlopen

Wanneer we spreken over _de aanvraag_, dan is het soort niet van belang. Wanneer de soort van de aanvraag van belang is, wordt de aanvraag aangeduid met de soort.   
-}
CLASSIFY Aanvraag 
IS NieuwePassen 
/\ Intrekking 
/\ VervangingVerlopen
/\ VervangingAdhoc


RELATION voor[Aanvraag*Organisatie]           [TOT,UNI]
MEANING "Elke aanvraag is voor precies één organisatie."
PURPOSE RELATION voor[Aanvraag*Organisatie]
{+Omdat aanvragen altijd betrekking hebben op passen / certificaten, is daar altijd een organisatie bij betrokken. Met één aanvraag willen we ook voorkomen, dat er verrichtingen worden gedaan die betrekking hebben op meerdere organisaties. Daarom administreren we de organisatie, waarvoor de aanvraag geldt.
-}
RELATION op[Aanvraag*Datum] [UNI,TOT]
MEANING "Elke aanvraag wordt gedaan op één datum."
PURPOSE RELATION op[Aanvraag*Datum]
{+Om in de tijd te kunnen traceren wanneer een specifieke aanvraag is gedaan, wordt de aanvraagdatum geregistreerd van elke aanvraag.
-}

RELATION beeindigd[Aanvraag*Certificaat] [INJ]
MEANING "Een aanvraag tot intrekking of vervanging vermeldt elk certificaat die daardoor wordt beëindigd. Elk certificaat kan door maximaal één aanvraag worden beëindigd."
PURPOSE RELATION beeindigd[Aanvraag*Certificaat]
{+Om specifieke certificaten te kunnen intrekken of vervangen, moet bekend zijn over welke certificaten het precies gaat. Voor certificaten die op een dienstenpas staan, wordt hiervoor het pasnummer van de dienstenpas gebruikt. Daarnaast kan de organisatie ook een certificaat op CD-Rom hebben. Over het algemeen is er daar één van actief, waardoor duidelijk is dat het om betreffend certificaat gaat. Wanneer er meerdere actief zijn, dan zal in praktijk de verloopdatum een voldoende indicatie geven om welk certificaat het gaat.
-} 

RELATION aangemaaktNAV [Certificaat*Aanvraag] [TOT,UNI]
MEANING "Elk certificaat is geproduceerd naar aanleiding van precies één aanvraag."
PURPOSE RELATION aangemaaktNAV [Certificaat*Aanvraag]
{+Om te voorkomen, dat er certificaten worden gemaakt zonder dat daar een reden toe is, moet er voor elk certificaat een aanvraag bestaan, naar aanleiding waarvan die is aangemaakt.
-}
RELATION besluitRA [Aanvraag*Besluit] [UNI,TOT]
MEANING "Elke aanvraag leidt tot precies één besluit van RA"
PURPOSE RELATION besluitRA [Aanvraag*Besluit]
{+Om grip te hebben over de uitvoering van de aanvragen, wordt elke aanvraag beoordeeld door RA.
-}
REPRESENT Besluit TYPE BOOLEAN
ROLE RA MAINTAINS "TOT besluitRA::Aanvraag*Besluit"

RULE aantalAangemaaktNAVNieuw : --TODO: Expressie beter maken. Aantal aan te maken moet kloppend worden gemaakt. 
aantemaken;aantemaken~ |- aangemaaktNAV~;aangemaaktNAV
MEANING "Het aantal dienstenpassen, dat wordt geproduceerd naar aanleiding van een aanvraag nieuwe passen, komt overeen met het aantal nieuwe passen zoals vermeld op de aanvraag."
PURPOSE RULE aantalAangemaaktNAVNieuw
{+Het is niet wenselijk dat er meer of minder passen worden gemaakt dan dat er op een aanvraag voor nieuwe passen is aangegeven. 
-}

RULE aantalAangemaaktNAVvervanging : --TODO: Expressie beter maken. Aantal aan te maken moet kloppend worden gemaakt. 
aantemaken;aantemaken~ |- aangemaaktNAV~;aangemaaktNAV
MEANING "Het aantal dientenpassen, dat wordt aangemaakt naar aanleiding van een aanvraag vervanging ad-hoc of aanvraag vervanging verlopen, komt overeen met het aantal opgegeven certificaten voor die aanvraag."
PURPOSE RULE aantalAangemaaktNAVvervanging
{+Om te voorkomen dat het aantal certificaten dat is uitgegeven aan een organisatie zonder aanleiding wijzigt, wordt elk certificaat die moet worden vervangen, vervangen door precies één certificaat. 
-}
RULE aanmakenEigenCertificaten : houderVan = aangemaaktNAV;voor
MEANING "De aanvraag naar aanleiding waarvan een certificaat is gemaakt, is gedaan voor de houder van dat certificaat."

RULE beëindigenEigenCertificaten : beeindigd;houderVan |- voor
MEANING "De organisatie, waarvoor een aanvraag is, is houder van elk certificaat, dat wordt beëindigd met die aanvraag."
PURPOSE RULE beëindigenEigenCertificaten
{+Om te voorkomen dat organisaties certificaten laten intrekken dan wel vervangen waarvan die organisatie niet de houder is, moet worden gecontroleerd dat die organisatie houder is van de te beëindigen certificaten op de aanvraag. 
-}
RULE ingetrokken : I[Certificaat]/\(intrekkingsDatum;intrekkingsDatum~) 
   |- beeindigd~;((I[Aanvraag] /\ I[Intrekking]) \/ (I[Aanvraag] /\ I[VervangingAdhoc]));beeindigd
MEANING "Voor elk ingetrokken certificaat is beeindigd door middel van een aanvraag van het type intrekking of ad-hoc vervanging."
PURPOSE RULE ingetrokken
{+Om te voorkomen dat certificaten zonder aanleiding worden ingetrokken, moet er een aanvraag bestaan die daar aanleiding toe is.
-}
RULE intrekkenCertificaat : beeindigd;beeindigd~ |- (I[Aanvraag] /\ I[Intrekking]) \/ (I[Aanvraag] /\ I[VervangingVerlopen]) \/ (I[Aanvraag] /\ I[VervangingAdhoc])
MEANING "Een certificaat kan slechts worden beëindigd naar aanleiding van een aanvraag daartoe van het type intrekking, vervanging door verlopen of een ad-hoc vervanging."
PURPOSE RULE intrekkenCertificaat
{+
-}


RELATION aantemaken[NieuwePassen*Aantal]           [UNI,TOT]
MEANING "Op elke aanvraag voor nieuwe passen staat het aan te maken aantal nieuwe passen."
PURPOSE RELATION aantemaken[NieuwePassen*Aantal]
{+Omdat certificaten alleen nog aangevraagd kunnen worden met een dienstenpas als drager, wordt het aantal te vervaardigen dienstenpassen vermeld op de aanvraag nieuwe passen.
-}
REPRESENT Aantal TYPE INTEGER

RELATION bron[Aanvraag*Document]
MEANING "Een aanvraag kan worden voorzien van een brondocument."
PURPOSE RELATION bron[Aanvraag*Document]   
{+Om aan te kunnen tonen, dat een aanvraag is gebasieerd op een authenthiek verzoek, moet een bewijsstuk kunnen worden vastgelegd.
-}

RELATION maxAantalCertificaten[Datum*Aantal]        [UNI,TOT]
MEANING "Voor elke datum is precies één maximum gesteld ten aanzien van het aantal geldige certificaten per organisatie."
PURPOSE RELATION maxAantalCertificaten[Datum*Aantal] 
{+Om ongebreidelde uitgifte van certificaten te voorkomen, wordt een limiet gesteld aan het aantal dat een organisatie zou mogen hebben. Op dit aantal hoeft slechts gecontrolleerd te worden bij de aanvraag voor nieuwe passen. Wanneer organisaties al over meer certificaten beschikken voordat de verlaging plaats vindt, hoeven die niet te laten intrekken.
-}

RULE maxAantalCertificaten : I[NieuwePassen];voor;houderVan~;houderVan;voor~;op;maxAantalCertificaten;aantemaken~ --TODO: Berekenen organiseren. De huidige expressie is er alleen maar om er voor te zorgen dat alle relaties genoemd zijn. (vor het taal-document)
MEANING "Het aantal nieuwe passen op de aanvraag plus het aantal actieve certificaten waarvan de organisatie waarvoor de aanvraag is, houder is, is kleiner of gelijk aan het maximaal aantal certificaten op de aanvraagdatum."


RELATION adres[Aanvraag*Adres]         [UNI]
MEANING "Een aanvraag kan het adres bevatten, waar bescheiden als gevolg van die aanvraag naartoe dienen te worden gestuurd."
PURPOSE RELATION adres[Aanvraag*Adres] 
{+Sommige typen aanvragen hebben als effect, dat er een dienstenpas, PIN en PUK wordt verzonden naar de houder. Hiertoe moet het adres bekend zijn, waar deze bescheiden naar kunnen worden verzonden.  
-}
--RELATION pasNr[Aanvraag*PasNummer]
--MEANING "Het pasnummer van elk van de in te trekken dan wel te vervangen dienstpassen wordt vermeld op de aanvraag."
--PURPOSE RELATION pasNr[Aanvraag*PasNummer] 
--{+Omdat een aanvraag om dienstpassen in te trekken dan wel te vervangen altijd gaat om specifieke dienstpassen, moeten deze kunnen worden aangegeven op de aanvraag. Hierdoor wordt bekend welke dienstpassen precies moeten worden beëindigd.   
---}




--RELATION eerder[Datum * Datum] [TRN,ASY] 
--MEANING "Voor elk tweetal datums is vast te stellen welk van de twee eerder is opgetreden."
--PURPOSE RELATION eerder[Datum * Datum]
--{+Om over de geldigheid van certificaten te kunnen praten willen we kunnen zeggen dat een datum eerder was dan een andere datum.
---}

ENDPATTERN

PATTERN "Adressen en versturen"
PURPOSE PATTERN "Adressen en versturen"
{+Voordat de organisatie gebruik kan maken van een certificaat op een dienstenpas, moet zowel de dienstenpas, als ook de PIN en PUK, op het juiste adres worden afgeleverd.  
-}

RELATION actuele[Organisatie*Adres]             [TOT,UNI]
MEANING "Elke organisatie heeft precies één actueel adres."
PURPOSE RELATION actuele[Organisatie*Adres]
{+Wanneer vervanging moet plaatsvinden van een (bijna) verlopen certificaat, dan moet bekend zijn waar de bescheiden naartoe moeten worden gestuurd. Daarom wordt van elke organisatie het actuele adres geadministreerd. 
-}
RULE beeindigenOrganisatie : eindDatum |- houderVan~;geldigTot --TODO:DEZE EXPERSSIE DEUGT NIET BIJ DE REGEL (OOK CDROM)
MEANING "Als een organisatie een einddatum heeft, dan moet de geldigheid van elk certificaat waarvan die organisatie houder is, worden beperkt tot die einddatum."  
PURPOSE RULE beeindigenOrganisatie 
{+Omdat het ongewenst is, dat een organisatie die wordt beëindigd, na de einddatum gebruik kan blijven maken van certificaten waarvan die organisatie houder is, moet de geldigheid van die certificaten worden beperkt. Indien die organisatie houder is van certificaten die geldig zouden blijven na de einddatum van de organisatie, moet er een aanvraag tot intrekking worden gemaakt, waarop alle betreffende certificaten kunnen worden ingetrokken. 
-}

RULE adresVervangingVerlopen : I[VervangingVerlopen] |- voor;actuele;adres~
MEANING "Het adres bij een vervanging wegens verlopen is gelijk aan het actuele adres van de organisatie waarvoor die aanvraag is."
PURPOSE RULE adresVervangingVerlopen
{+Om automatische vervanging van verlopen passen mogelijk te maken, wordt in die gevallen het actuele adres van de organisatie gebruikt als adres bij die aanvraag. 
-}
ROLE TODO MAINTAINS adresVervangingVerlopen, maxAantalCertificaten
RELATION verstuurd[Dienstenpas*Adres]         [UNI]
MEANING "Elke dienstenpas wordt verstuurd naar één adres."
PURPOSE RELATION verstuurd[Dienstenpas*Adres]
{+Nadat de dienstenpas is vervaardigd, wordt deze verstuurd. Het adres waarnaartoe de pas is verstuurd, wordt kunnen worden vastgelegd.
-}

RELATION pinEnPukVerstuurd[Certificaat*Adres] [UNI]
MEANING "De PIN en de PUK van elk certificaat worden verstuurd naar één adres."
PURPOSE RELATION pinEnPukVerstuurd[Certificaat*Adres]
{+Een dag nadat de dienstenpas is verstuurd, worden ook de PIN en PUK verstuurd. Uit veiligheidsoverwegingen is het ongewenst dat PIN en PUK tezamen met de dienstenpas worden verstuurd.  
-}

RULE versturenDienstenpas : I[Dienstenpas] |- verstuurd;adres~;aangemaaktNAV~;dragerVan~
MEANING "Elke dienstenpas wordt verstuurd naar het adres van de aanvraag naar aanleiding waarvan het certificaat op die pas is aangemaakt."
PURPOSE RULE versturenDienstenpas
{+Om de houder van het certificaat op een dienstenpas in staat te stellen er gebruik te maken, moet die pas worden opgestuurd. 
-}
ROLE CA MAINTAINS versturenDienstenpas

RULE versturenPinEnPuk : dragerVan~;verstuurd |- pinEnPukVerstuurd
MEANING "De PIN en PUK van het certificaat op de dienstenpas worden verstuurd naar hetzelfde adres als waar de dienstenpas naartoe is verstuurd."
PURPOSE RULE versturenPinEnPuk
{+Om de ontvanger van de dienstenpas in staat te stellen deze daadwerkelijk te gebruiken, moet hij/zij ook over de PIN en PUK te beschikken. Deze worden afzonderlijk van de dienstenpas verstuurd naar hetzelfde adres.
-}
ROLE CA MAINTAINS versturenPinEnPuk

ROLE CA MAINTAINS versturenPinEnPuk
ENDPATTERN

ENDCONTEXT