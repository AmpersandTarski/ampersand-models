CONTEXT GenericTransitiveClosure IN ENGLISH
------------------------------------------------------------
PROCESS TestTransitiveClosure

-- See e.g.: http://cs.winona.edu/lin/cs440/ch08-2.pdf

-- 'rrt' is a routing table. 'rrtStar' says which routers can route packages to which routers, possibly through other routers
RELATION rrt[Router*Router] PRAGMA "" " is capable of routing packages to "
=[ ("R1", "R3"); ("R2", "R1"); ("R2", "R4"); ("R4", "R2") ]

RELATION rrtCopy[Router*Router] -- a copy of rrt allows us to determine the (de)population events.
RELATION rrtStar[Router*Router] -- the transitive closure of both rrt and rrtCopy

ROLE ExecEngine MAINTAINS "rrtCompTransitiveClosure"
RULE "rrtCompTransitiveClosure": rrt = rrtCopy
VIOLATION (TXT "{EX} TransitiveClosure;rrt;Router;rrtCopy;rrtStar")

-- Note that the following is true if rrtStar is transitive, but it does not guarantee that this is the smallest closure of rrt
RULE "rrtStar Transitive closure integrity":  rrtStar;rrtStar |- rrtStar

ENDPROCESS

INTERFACE "[Routers]" (rrt) : I[ONE]
BOX[ "Routers"     : V[ONE*Router]
   BOX[ "Router"      : I
      , "rrt"     : rrt
      , "rrtStar" : rrtStar
--    , "rrtCopy" : rrtCopy
      ]
   ]

------------------------------------------------------------
ENDCONTEXT