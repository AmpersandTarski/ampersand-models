CONTEXT MergeAtoms
{- This application shows the functionality of 
the ExecEngine function `MrgAtoms($conceptA, $srcAtom, $conceptB, $tgtAtom)`.

This function allows two atoms to be 'unified' or 'merged', which means that
all links/pairs associated with `tgtAtom` are being moved over the `srcAtom`, 
using the 'update' functionality.

The arguments of `MrgAtoms($conceptA, $srcAtom, $conceptB, $tgtAtom)` are:

*`ConceptA` = The most specific concept A that contains `srcAtom`
*`srcAtom`  = The atom to be made equal to `tgtAtom`
*`ConceptB` = The most specific concept B that contains `tgtAtom`
*`tgtAtom`  = The atom to be made equal to `srcAtom`

There is one use-case that you should**be aware**of.
The first is where unification is done using a univalent relation `r`, 
that contains pairs (`srcAtom`,`X`) and (`tgtAtom`,`Y`) 
where `X` and `Y` are different atoms.
Using `uniAtom` to represent the resulting (unified) atom,
then after unification, `r` will contain (`uniAtom`,`Y`)
and it will contain neither (`srcAtom`,`X`) nor (`tgtAtom`,`Y`).
-}

RELATION ssn[Person*SocialSecurityNumber] [UNI]
RELATION name[Person*Name] [UNI]
RELATION address[Person*Address]
RELATION notes[Person*Note] [INJ]

ROLE ExecEngine MAINTAINS Person
RULE Person : ssn;ssn~ |- I
VIOLATION (TXT "{EX} MrgAtoms;Person;", SRC I, TXT ";Person;", TGT I )

INTERFACE Persons : I[SESSION] cRud
BOX [ persons : V[SESSION*Person] CRuD
      COLS[ id : I[Person] cRud
          , ssn : ssn
          , name : name
          , address : address
          , notes : notes
          ]
    ]

ENDCONTEXT