CONTEXT ReadExcelFiles IN ENGLISH

PROCESS "PortRef Resolution Service"
PURPOSE PROCESS "PortRef Resolution Service"
{+The Source and Target of a Wire may be specified by means of a 'PortRef', which consists of a component that resides in the same Scope as the wire, and the name of the Port that is being referred to. This service ensures that whenever the source or target of a wire is specified by means of a PortRef, the wire will be connected to the Port that is being refered to.-}

CONCEPT "PortRef" "a reference to a port on a sub-component"
IDENT PortRefs: PortRef(prComponent,prPortName)
RELATION prComponent[PortRef*Scope] [UNI,TOT]
RELATION prPortName[PortRef*PortName] [UNI,TOT]

RULE "The Scope of a PortRef must be a Component": 
  prComponent |- prComponent;scopeIsComponent

RULE "For every PortRef, a port with its PortName must exist on its Component":
  prComponent~;prPortName |- portComponent~;portName

--$ We rely on the assumption that ports are identifiable by their Component and PortName. 
--$ This assumption holds for as long as copying of individual ports is not supported (it will need to be, in future). 
RULE "Every PortRef must identify a Port on its Component":
 (prComponent;portComponent~ /\ prPortName;portName~) /\ (prComponent;portComponent~ /\ prPortName;portName~);-I |- -V
VIOLATION (TXT "PortRef for Component ", SRC prComponent, TXT " and Name ", SRC prPortName, TXT " refers to multiple Ports")

RELATION wSrcPortRef[Wire*PortRef] [UNI] -- The source of a wire may be defined as a PortRef
RULE "A wire with a wSrcPortRef must belong to the same component as the component that the PortRef refers to":
   wSrcPortRef~;wScope |- prComponent;scopeIPO

ROLE ExecEngine MAINTAINS "Resolving SrcRefs"
RULE "Resolving SrcRefs": wSrcPortRef;(prComponent;portComponent~ /\ prPortName;portName~) |- wSrc
VIOLATION (TXT "{EX} InsPair;wSrc;Wire;", SRC I, TXT ";Port;", TGT I
          ,TXT "{EX} DelPair;wSrcPortRef;Wire;", SRC I, TXT ";PortRef;", SRC wSrcPortRef
          )

RELATION wTgtPortRef[Wire*PortRef] [UNI] -- The target of a wire may be defined as a PortRef
RULE "A wire with a wTgtPortRef must belong to the same component as the component that the PortRef refers to":
   wTgtPortRef~;wScope |- prComponent;scopeIPO

ROLE ExecEngine MAINTAINS "Resolving TgtRefs"
RULE "Resolving TgtRefs": wTgtPortRef;(prComponent;portComponent~ /\ prPortName;portName~) |- wTgt
VIOLATION (TXT "{EX} InsPair;wTgt;Wire;", SRC I, TXT ";Port;", TGT I
          ,TXT "{EX} DelPair;wTgtPortRef;Wire;", SRC I, TXT ";PortRef;", SRC wTgtPortRef
          )

RELATION constPortRef[Constant*PortRef] -- The port to which a constant connects may be defined as a PortRef
RULE "A constant with a constPortRef must belong to a scope that contains the component that hosts the port that the PortRef refers to":
   constPortRef~;constScope |- prComponent;scopeIPO

ROLE ExecEngine MAINTAINS "Resolving constPortRefs" -- Note: constPortRef may specify multiple ports.
RULE "Resolving constPortRefs": constPortRef |- -constPortRef
VIOLATION (TXT "{EX} InsPair;constPort;Constant;", SRC I, TXT ";Port;", TGT (prComponent;portComponent~ /\ prPortName;portName~)
          ,TXT "{EX} DelPair;constPortRef;Constant;", SRC I, TXT ";PortRef;", TGT I
          )

--$ cleaning up any garbage
ROLE ExecEngine MAINTAINS "Auto-delete PortRefs after they have been resolved"
RULE "Auto-delete PortRefs after they have been resolved": I[PortRef] |- wSrcPortRef~;wSrcPortRef \/ wTgtPortRef~;wTgtPortRef
VIOLATION (TXT "{EX} DelAtom;PortRef;", SRC I)

ENDPROCESS

{-
VIEW WireSrcPortRef: Wire
{ "name"    : wSrcPortRef;prPortName
, "prefix"  : wSrcPortRef;prComponent
} HTML TEMPLATE "LinkedText.html" ENDVIEW
-}
-- VIEW WireSrcPortRef : Wire (wSrcPortRef;prComponent, TXT ":", wSrcPortRef;prPortName)
VIEW WireSrcPortRef : Wire { prefix : wSrcPortRef;prComponent, name : wSrcPortRef;prPortName } ENDVIEW -- No TXT, see bug https://github.com/AmpersandTarski/ampersand/issues/79

{-
VIEW WireTgtPortRef: Wire
{ "name"    : wTgtPortRef;prPortName
, "prefix"  : wTgtPortRef;prComponent
} HTML TEMPLATE "LinkedText.html" ENDVIEW
-}
-- VIEW WireTgtPortRef : Wire (wTgtPortRef;prComponent, TXT ":", wTgtPortRef;prPortName)
VIEW WireTgtPortRef : Wire { prefix : wTgtPortRef;prComponent, name : wTgtPortRef;prPortName } ENDVIEW -- No TXT, see bug https://github.com/AmpersandTarski/ampersand/issues/79

ENDCONTEXT