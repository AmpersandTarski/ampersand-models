CONTEXT Dossiers IN DUTCH

--INCLUDE "Gebeurtenis.adl"

META "authors" "Stef Joosten"
--THEMES "Documenten en Dossiers", Archiefselectielijst


PATTERN "Documenten en Dossiers"
PURPOSE PATTERN "Documenten en Dossiers"
{+
ASR maakt dossierstukken als de neerslag van haar handelingen
Elk document dat met een klant of een andere (externe) partij wordt gedeeld, heeft de status van archiefbescheid.
Vergelijkbaar met de Archiefwet 1995 gaat ASR ervan uit dat

- archieven worden gevormd als bijprodukt van het handelen van overheden, instellingen, personen;
- archieven het geheugen zijn van ASR; en 
- archiefstukken dienen als instrumenten van het bestuur.

Deze denkwijze passen wij toe op de levenscyclus van documenten.
Deze levenscyclus bestaat uit een maakfase, een gebruiksfase en een archieffase.
In de eerste fase, de maakfase, wordt een document geproduceerd.
ASR produceert zelf documenten,
zoals beslissingen, uitbetalingsoverzichten, facturen, polissen, enzovoorts.
Maar documenten kunnen ook afkomstig zijn van andere partijen, zoals intermediairs, ketenpartners en andere belanghebbenden.
De overgang van maak- naar gebruiksfase vindt plaats wanneer het document niet langer gewijzigd kan of mag worden.
Dat is het moment dat het document wordt uitgewisseld (verstuurd dan wel ontvangen) met een andere partij,
of het moment dat een document van een geldige handtekening is voorzien.

Gedurende de gebruiksfase horen zaakgerelateerde documenten thuis in een zaaksdossier.
Dit dossier is in beginsel toegankelijk voor belanghebbenden.
Gedurende fase 2 is ASR zorgdrager, wat inhoudt dat zij zeggenschap heeft over al hetgeen met het dossier gebeurt onder de
voorwaarde dat zij verantwoord en als een goed beheerder omgaat met alle informatie van de klant(en) die in het dossier zit.
Na afsluiting van het dossier kan het worden gearchiveerd.
Met deze overgang begint fase 3 in het leven van een document: de archiveringsfase.
In deze fase kan het dossier en alle documenten daarin worden overgedragen aan een archief.
Daarmee gaat ook het zorgdragerschap over op de archiefbeheerder.
De bewaartermijn van gearchiveerde stukken is geregeld in het bewaarbeleid, wat een onderdeel is van het documentair structuurplan van de zorgdrager.

Gedurende de eerste fase, dus voordat een document definitief is, kan de inhoud vele malen worden gewijzigd door verschillende personen.
Om veranderingen in een document te kunnen weergeven maken we onderscheid tussen een document als verwijzing (bijvoorbeeld doc39885466) en de inhoud van het document (bijvoorbeeld de tekst van een brief).
Wanneer de verwijzing gelijk is maar de inhoud verschilt, spreken we over hetzelfde document in een andere versie.
Twee verschillende verwijzingen duiden op twee verschillende documenten, zelfs al zouden ze inhoudelijk hetzelfde zijn.
-}

---------------
-- Concepten --
---------------

CONCEPT Bericht "Een bericht is een gebeurtenis, waarbij gegevens worden overgedragen van een verstrekker naar een ontvanger." "NGA"
PURPOSE CONCEPT Bericht
{+Zoals elke organisatie is ook ASR verantwoordelijk voor haar handelen tegenover de maatschappij, haar ketenpartners, en naar alle belanghebbenden in elke zaak.
De neerslag van dat handelen is de bron voor die verantwoordelijkheid.
De basis hiervoor zijn berichten.
NGA gaat ervan uit dat elk bericht elektronisch wordt geregistreerd; ook berichten die op papier worden gecommuniceerd.
-}

CONCEPT Document "Een document is een stuk dat, ongeacht zijn vorm, door ASR is ontvangen, opgemaakt of gereproduceerd." -- "Art. 1 lid c Archiefwet 1995; Art. 12 Archiefbesluit 1995"

CONCEPT Dossier "Een dossier is een samenhangende verzameling documenten, die als beheerseenheid wordt behandeld." -- "Art. 1 lid c WJG"

CONCEPT Object "Een object is een hoeveelheid gegevens, die als één geheel bestaat gedurende een aaneengesloten periode." ""
PURPOSE CONCEPT Object
{+ Het woord 'object' verwijst naar de feitelijke inhoud. Bijvoorbeeld: de inhoud van een brief, de registratie van een ongeval in een proces verbaal, of de inhoud van een gegevensbestand.
Om die inhoud te registreren is er aan de technische kant ook sprake van een object: de fysieke opslagcapaciteit die gebruikt wordt om de betreffende inhoud op te slaan.
-}

CONCEPT Zaak "Een zaak is de kleinste eenheid van behandeling, onstaan naar aanleiding van bijvoorbeeld een schadeclaim, polisaanvraag of mutatieverzoek, en gekarakteriseerd door de behandelwijze (soort procedure) en de toepasselijke regels. Zaken kunnen gesplitst of samengevoegd worden."

------------------------------------------
--Relaties en regels aangaande dossiers --
------------------------------------------

zaaksdossier :: Dossier * Zaakid PRAGMA "" "is van" ""
MEANING "Van elk dossier is eenduidig vast te stellen welke zaak het betreft."
PURPOSE RELATION zaaksdossier[Dossier*Zaakid] REF "Art. 18 lid 1 Archiefregeling"
{+ Dossiers worden per zaak(nummer) georganiseerd.
-}
	

zaakstuk :: Document * Dossier PRAGMA "" "zit in" ""
MEANING "Van elk document is eenduidig vast te stellen of het in een dossier zit."
PURPOSE RELATION zaakstuk[Document*Dossier] REF ""
{+ Een zaakstuk is een ontvangen of mede te delen bericht in een zaak, of een bericht waarbij een zaak is ontstaan.
 Dit kunnen dus ook stukken zijn die door een andere partij zijn aangeleverd, bewijsstukken, een inleidend document, of correspondentie.
 Het opslaan in een dossier betekent dat een document aan een dossier is gekoppeld.
 We zeggen dat dat het document in het dossier zit.
 Om het kopiëren van documenten te vermijden is het toegestaan om documenten aan meerdere zaken tegelijk te koppelen.
 Het is alsof het zich in meerdere dossiers tegelijk bevindt.
 -}

zaaksdocument :: Document * Zaakid [UNI] PRAGMA "" "behoort tot" "" 
MEANING "Elk ontvangen en opgemaakt document is voorzien van een zaaksnummer."
PURPOSE RELATION zaaksdocument[Document*Zaakid] REF "Art. 17c Archiefregeling"
{+ Een zaak levert de samenhang van de door ASR ontvangen en opgemaakte documenten. Daarom zorgt ASR ervoor dat van elk van de archiefbescheiden de zaak kan worden vastgesteld. Hiertoe wordt elk document voorzien van een zaaksnummer.
-}


---------------------------------------------
-- Relaties en regels aangaande documenten --
---------------------------------------------

type :: Document * Zaakstuktype [UNI] PRAGMA "" "heeft" "als documenttype" 
MEANING "Van elk document is het type eenduidig bepaald."
PURPOSE RELATION type[Document*Zaakstuktype] REF "Art. 17a Archiefregeling"
{+ Elk document kan worden getypeerd, waardoor documenten van eenzelfde type op gelijke wijze kunnen worden behandeld.
 Een beschikbare typering staat beschreven in het documentair structuurplan zoals bedoeld in Art. 18 van de Archiefregeling, en zoals bedoeld in Art. 12 van het Archiefbesluit 1995.
-}


inhoud :: Document * Object PRAGMA "" "heeft" "als inhoud"
MEANING "Van elk opgeslagen document is op elk willekeurig moment de inhoud eenduidig bepaald."
PURPOSE RELATION inhoud[Document*Object] REF "Art. 17a Archiefregeling"
{+ Elk document heeft een inhoud.
 Weliswaar kan de inhoud veranderen (zolang het document nog niet definitief is),
 maar op elk willekeurig moment heeft het document precies één inhoud.
-}

ontstaan :: Document * Gebeurtenis PRAGMA "" "is ontstaan door" ""
MEANING "Van elk document is de gebeurtenis van ontstaan eenduidig bepaald."
PURPOSE RELATION ontstaan[Document*Gebeurtenis] REF "Art. 17b Archiefregeling"
{+ ASR wil van elk document het moment van ontstaan registreren.
 Voor binnenkomende berichten is dat het moment van ontvangst. Bijvoorbeeld een datum bij een fysiek stuk en een moment (tijdstip) bij een digitaal document.
 Voor documenten die door ASR zelf zijn gemaakt wordt daarvoor de informatie uit het documentcreatiesysteem benut.
 In de taal van ASR kunnen we dus spreken van "het moment van ontstaan" van een document, wat zoveel betekent als "het moment dat het document bekend is bij ASR".
-}


zorgdrager :: Dossier * Orgaan PRAGMA "" "wordt beheerd door" ""
MEANING "Voor elk dossier is de zorgdrager eenduidig bepaald."
PURPOSE RELATION zorgdrager[Dossier*Orgaan] REF "Art. 1d Aw 1995"
{+ De wet stelt dat er een zorgdrager is, die bij of krachtens de wet belast is met de zorg voor de archiefbescheiden
 De zorgdrager is verantwoordelijk voor het rechtmatige beheer van deze bescheiden.
-}


doc ::  Bericht * Document  PRAGMA "De inhoud van" "is vastgelegd in" ""
MEANING "De inhoud van elk bericht is bepaald in de vorm van een document."
PURPOSE RELATION doc[Bericht*Document] REF ""
{+ Berichten hebben inhoud. Berichten kunnen daarmee als document behandeld en opgeslagen worden.
-}

ingevoegd :: Gebeurtenis * Document PRAGMA "" "typeert de opname van" "in een dossier"
MEANING "De gebeurtenis van het opnemen van een document in een dossier wordt geregistreerd."
PURPOSE RELATION ingevoegd[Gebeurtenis*Document] REF "Art. 17b Archiefregeling"
{+ Om te kunnen nagaan wanneer documenten in een dossier zijn opgenomen,
 registreert ASR elke opname van een document.
 -}




afgedrukt :: Document * Gebeurtenis PRAGMA "" "is afgedrukt doordat" "is opgetreden"
PURPOSE RELATION afgedrukt[Document*Gebeurtenis] REF "Art. 17d Archiefregeling"
{+ ASR houdt bij wanneer archiefstukken zijn afgedrukt.
-}


-----------------
-- Invarianten --
-----------------

RULE "onveranderlijke dossierstukken" : inhoud~;ingevoegd~;ouder;doc;inhoud |- I
MEANING "De inhoud van een dossierstuk is onveranderlijk op het moment dat het stuk is opgenomen in het dossier."
PURPOSE RULE "onveranderlijke dossierstukken" REF "Art. 17a Archiefregeling"
{+ Om partijen te garanderen dat een document ongewijzigd is, geldt de afspraak dat de inhoud van een document ongewijzigd blijft nadat het document in een dossier is opgenomen.
-}

--------------------
-- Specialisaties --
--------------------
  SPEC Bericht ISA Gebeurtenis

Verzoek ISA Document
Machtiging ISA Document
Verzoekschrift ISA Verzoek
Beroepschrift ISA Verzoek
"Veroordelingsverzoek proceskosten" ISA Verzoek
"Verzoek schadevergoeding" ISA Verzoek
Verzet ISA Verzoek
Dagvaarding ISA Verzoek
Klaagschrift ISA Verzoek
"Gezamenlijk verzoek" ISA Verzoek
VoVo ISA Uitspraak
Jaarplan ISA Document
"Inleidend document" ISA Document
Machtiging ISA Document
Werkdocument ISA Document
"Beleg dossier" ISA Dossier

ENDPATTERN

PATTERN Archiefselectielijst


PURPOSE PATTERN Archiefselectielijst
{+Ten behoeve van het archiveren van stukken heeft ASR een regeling
ten aanzien van het archiveren van documenten vastgesteld onder identificatienummer ???.
In de nu volgende tekst noemen we deze regeling het Basis Selectiedocument (BSD).
Dit is een bijzondere vorm van een selectielijst.
In de regel heeft een BSD niet zozeer betrekking op (alle) archiefbescheiden van één (enkele) organisatie, als wel op het geheel van de bescheiden die de administratieve neerslag vormen van het overheidshandelen op een bepaald beleidsterrein.

Dit BSD geldt dus voor de archiefbescheiden van verschillende overheidsorganen (veelal ook diverse zorgdragers), en wel voor zover de desbetreffende actoren op het terrein in kwestie werkzaam zijn (geweest). Dit betekent dat er geen handelingen van particuliere actoren worden opgenomen.
Door de beleidsterreingerichte benadering komen verschillende aspecten betreffende het beheer van de eigen organisatie van de zorgdrager (financieel beleid, bijvoorbeeld) niet aan bod.

Het niveau waarop geselecteerd wordt, is dus niet dat van de stukken zelf, maar dat van de handelingen waarvan die archiefbescheiden de administratieve neerslag vormen. Een BSD is derhalve geen opsomming van (categorieën) stukken, maar een lijst van handelingen van overheidsactoren, waarbij elke handeling is voorzien van een waardering en indien van toepassing een vernietigingstermijn.
-}

---------------
-- Concepten --
---------------
CONCEPT Selectielijst "Een selectielijst is een lijst zoals bedoeld in Artikel 5, Archiefbesluit 1995"
PURPOSE CONCEPT Selectielijst REF "Artikel 5 Archiefbesluit 1995"
{+Een selectielijst bestaat ten minste uit:

a. een titel waaruit blijkt op welk overheidsorgaan de selectielijst betrekking heeft;

b. een opsomming van de taken van dat overheidsorgaan;

c. een systematische opsomming van categorieën archiefbescheiden, waarin bij iedere categorie is aangegeven of de archiefbescheiden bewaard worden dan wel na welke termijn zij voor vernietiging in aanmerking komen;

d. een toelichting die in ieder geval bevat:

   1°. een verantwoording van de wijze waarop toepassing is gegeven aan artikel 2, eerste lid,

   2°. een verslag van de wijze waarop derden en met name de deskundigen, bedoeld in artikel 3, bij het ontwerpen van de selectielijst betrokken zijn en van de inhoud van het met hen gevoerde overleg, en

   3°. een verslag van de procedure, bedoeld in artikel 4;

e. een opsomming van de criteria aan de hand waarvan de zorgdrager archiefbescheiden die ingevolge de selectielijst voor vernietiging in aanmerking komen, van vernietiging kan uitzonderen.
-}

CONCEPT Archiefbeheerssysteem "Een archiefbeheerssysteem is een geheel van mensen, methoden, procedures, gegevensverzamelingen, opslag-, verwerkings- en communicatieapparatuur en andere middelen, bestemd tot het beheer van archiefbescheiden." "Art. 1 lid 1 sub a Regeling geordende en toegankelijke staat archiefbescheiden"

CONCEPT Bestand "Een bestand is een geheel van gegevens in een zelfde opslagformaat."
PURPOSE CONCEPT Bestand REF "Artikel 1 lid 1 sub c Regeling geordende en toegankelijke staat archiefbescheiden."
{+ Het woord bestand verwijst naar een verzameling gegevens die als één object kan worden opgeslagen, beheerd en verwijderd.
 De inhoud van een document kan dan ook in een bestand worden opgeslagen.
 In de strikte betekenis van het woord kan een bestand dus evengoed op papier als digitaal bestaan.
 In het praktisch spraakgebruik bedoelt men echter meestal een digitaal bestand.
-}


CONCEPT Besturingsprogramma "Een besturingsprogramma is een computerprogramma dat bestemd is ter besturing van een informatiesysteem." "Art. 1 lid 1 sub d Regeling geordende en toegankelijke staat archiefbescheiden"

CONCEPT Conversie "Een conversie is het omzetten in of het overzetten van gegevens in een ander opslagformaat." "Art. 1 lid 1 sub e Regeling geordende en toegankelijke staat archiefbescheiden"

CONCEPT "Digitaal document" "Een digitaal document is een stuk dat uitsluitend met behulp van besturings- of toepassingsprogrammatuur geraadpleegd kunnen worden." "Art. 1 lid 1 sub f Regeling geordende en toegankelijke staat archiefbescheiden"

CONCEPT "Documentair structuurplan" "Een documentair structuurplan (DSP) is een plan waarin is vastgelegd de wijze waarop de toegankelijkheid van archiefbescheiden is georganiseerd en de wijze waarop archiefbescheiden zijn ingedeeld en gerangschikt." "Art. 1 lid 1 sub g Regeling geordende en toegankelijke staat archiefbescheiden"

CONCEPT Migratie "Een migratie is het overzetten van gegevens en toepassingsprogrammatuur naar een ander platform." "Art. 1 lid 1 sub i Regeling geordende en toegankelijke staat archiefbescheiden"

CONCEPT Ontstaan "Een ontstaan is de gebeurtenis dat archiefbescheiden door ASR worden ontvangen of opgemaakt als naar hun aard bestemd om daaronder te berusten." "Art. 1 lid 1 sub j Regeling geordende en toegankelijke staat archiefbescheiden"

CONCEPT Opslagformaat "Een opslagformaat is de code volgens welke gegevens op een gegevensdrager zijn opgeslagen." "Art. 1 lid 1 sub k Regeling geordende en toegankelijke staat archiefbescheiden"

CONCEPT Platform "Een platform is het geheel van apparatuur en besturingsprogrammatuur waarop toepassingsprogrammatuur werkt." "Art. 1 lid 1 sub l Regeling geordende en toegankelijke staat archiefbescheiden"

CONCEPT Toepassingsprogramma "Een toepassingsprogramma is een computerprogramma dat bestemd is voor de ondersteuning van de uitvoering van een werkproces." "Art. 1 lid 1 sub n Regeling geordende en toegankelijke staat archiefbescheiden"

CONCEPT Vorm "Een vorm is de uiterlijke verschijning waarin de structuur en opmaak zichtbaar zijn" "Art. 1 lid 1 sub o Regeling geordende en toegankelijke staat archiefbescheiden"

CONCEPT Werkproces "Een werkproces is de uitvoering van de taak of handeling uit hoofde waarvan archiefbescheiden door ASR worden ontvangen of opgemaakt als naar hun aard bestemd om daaronder te berusten." "Art. 1 lid 1 sub p Regeling geordende en toegankelijke staat archiefbescheiden"

--------------
-- Relaties --
--------------

over :: Document * NatuurlijkPersoon PRAGMA "" "gaat over" ""
MEANING "Van documenten kan worden vastgesteld of het over een persoon gaat."
PURPOSE RELATION over[Document*NatuurlijkPersoon] REF "" --BESCHERMING PERSOONSGEGEVENS
{+ Van een willekeurig document wil ASR kunnen registreren of het over een bepaalde persoon gaat, in het kader van de Wet Bescherming Persoonsgegevens.
-}


neerslag :: Document * Gebeurtenis PRAGMA "" "is de neerslag van" ""
MEANING "Een document kan de neerslag van een gebeurtenis vastleggen." 
PURPOSE RELATION neerslag[Document*Gebeurtenis] REF ""
{+ Een overheidshandeling wordt in een document vastgelegd, omdat die handeling gevolgen kan hebben.
 Een document dat overheidshandelen documenteert heet daarom de 'neerslag' van het overheidshandelen. Dit houdt in dat het plaatsvinden van die gebeurtenis in dat document wordt vastgelegd.
 Zulke documenten zijn veelal bedoeld om op een onbepaald tijdstip in de toekomst als bewijsmiddel te dienen.
-}


handeling :: Gebeurtenis * Handeling [UNI] PRAGMA "" "is een voorkomen van" ""
MEANING "Van elke gebeurtenis die ASR registreert, wordt vastgelegd om welke soort handeling het gaat."
PURPOSE RELATION handeling[Gebeurtenis*Handeling] REF "Besluit vaststelling selectielijst neerslag handelingen beleidsterrein zorg voor de rechtspleging 1945–2002 (Minister van Justitie)"
{+ Omdat het overheidshandelen traceerbaar moet zijn, registreert een informatiesysteem elke overheidshandeling in de vorm van 'gebeurtenis'.
 Daarbij wordt de handeling zelf aan de gebeurtenis gekoppeld, zodat van elke gebeurtenis duidelijk is om welk soort overheidshandelen het gaat.
-}


-------------------------------------------------
-- Relaties de populatie in stamtabellen staat --
-------------------------------------------------

omschrijving :: Handeling->Tekst PRAGMA "" "heeft als omschrijving" ""
MEANING "Elke handeling heeft de omschrijving zoals opgenomen in het BSD."
PURPOSE RELATION omschrijving[Handeling*Tekst] REF "Besluit vaststelling selectielijst neerslag handelingen beleidsterrein bedrijfsvoering rechterlijke macht vanaf 2002 (Rechtbanken)"
{+ 
-}	

bron :: Handeling * Tekst [UNI] PRAGMA "De bron van" "is" ""
MEANING "Elke handeling heeft een omschrijving die aangeeft wat de bron, de wettelijke basis, van deze handeling is."
PURPOSE RELATION bron[Handeling*Tekst] REF "Besluit vaststelling selectielijst neerslag handelingen beleidsterrein zorg voor de rechtspleging 1945–2002 (Minister van Justitie)"
{+ De wettelijke basis van elke handeling is bekend. Dit is een wetsartikel indien een handeling een duidelijke wettelijke basis heeft, en een bronvermelding wanneer de handeling is geformuleerd op basis van interviews, literatuur of andere bronnen.
-}

rIOhandeling :: Handeling * "RIO handeling" PRAGMA "" "uit het BSD is gerelateerd aan" "uit het RIO"
MEANING "Elke handeling in het BDS is gerelateerd aan \"RIO Rechtspleging en rechtshulp. De datageschiedenis van handelingen en organisatie-eenheden van de justitiële ministeriële organisatie van de rechtspleging en rechtshulp, 1945–1992\"."
PURPOSE RELATION rIOhandeling[Handeling*"RIO handeling"] REF "Besluit vaststelling selectielijst neerslag handelingen beleidsterrein zorg voor de rechtspleging 1945–2002 (Minister van Justitie)"
{+ Een BSD is opgesteld op basis van institutioneel onderzoek. In het rapport institutioneel onderzoek (RIO) wordt het betreffende beleidsterrein beschreven, evenals de taken en  bevoegdheden van de betrokken organen. De handelingen van ASR op het beleidsterrein staan in het RIO in hun functionele context geplaatst. In het BSD zijn de handelingen gerelateerd aan het RIO.
 Het BSD is gerelateerd aan "RIO Rechtspleging en rechtshulp. De datageschiedenis van handelingen en organisatie-eenheden van de justitiële ministeriële organisatie van de rechtspleging en rechtshulp, 1945–1992".
-}

opmerking :: Handeling * Tekst [UNI] PRAGMA "Bij " "is opgemerkt: " ""
MEANING "Een handeling kan worden voorzien van een opmerking, toelichting of vrije tekst."
PURPOSE RELATION opmerking[Handeling*Tekst] REF "Besluit vaststelling selectielijst neerslag handelingen beleidsterrein zorg voor de rechtspleging 1945–2002 (Minister van Justitie)"
{+ Voor eventuele bijzonderheden ten aanzien van een handeling kan een opmerking of toelichting in vrije tekst aanwezig zijn.
-}

product :: Handeling * Tekst [UNI] PRAGMA "" "leidt tot" ""
MEANING "Een product van het neerslag van handelen is een juridische tekst."
PURPOSE RELATION product[Handeling*Tekst] REF "Besluit vaststelling selectielijst neerslag handelingen beleidsterrein zorg voor de rechtspleging 1945–2002 (Minister van Justitie)"
{+ Dit is de weergave van het juridisch-bestuurlijk niveau van het eindproduct van de handeling.
 Indien niet duidelijk is in welke soort documentaire neerslag een handeling heeft geresulteerd of als uit de beschrijving van de handeling al duidelijk is welk product de handeling oplevert, ontbreekt dit item.
-}

criterium :: Handeling * Selectiecriterium PRAGMA "" "heeft" "als selectiecriterium" ""
MEANING "Selecteren is het aanmerken van de documentaire neerslag van een handeling voor bewaren of vernietigen."
PURPOSE RELATION criterium REF "Besluit vaststelling selectielijst neerslag handelingen beleidsterrein zorg voor de rechtspleging 1945–2002 (Minister van Justitie)"
{+ Handelingen die worden gewaardeerd met B (Bewaren) zijn expliciet benoemd in het BSD.
 Bewaard worden alleen stukken die op de in deze criteria benoemde handelingen zijn terug te voeren.
 Hierbij worden nummers gehanteerd van 1 tot 6, conform de BSD.
-}

tekst :: Selectiecriterium -> Tekst PRAGMA "De tekst van" "luidt"
MEANING "De criteriumnummers uit de BSD zijn in diezelfde BSD beschreven met een tekst."
PURPOSE RELATION tekst[Selectiecriterium*Tekst] REF "Besluit vaststelling selectielijst neerslag handelingen beleidsterrein zorg voor de rechtspleging 1945–2002 (Minister van Justitie)"
{+
-}

vernietigingstermijn :: Handeling * Vernietigingstermijn [UNI] PRAGMA "De vernietigingstermijn van" "is" "jaar"
MEANING "Als de neerslag van een handeling wordt aangewezen ter vernietiging, wil dat zeggen dat deze neerslag, ongeacht de vorm waaruit zij bestaat, na verloop van de in de selectielijst vastgestelde termijn kan worden vernietigd."
PURPOSE RELATION vernietigingstermijn REF "Besluit vaststelling selectielijst neerslag handelingen beleidsterrein zorg voor de rechtspleging 1945–2002 (Minister van Justitie)"
{+ De vernietigingstermijn wordt uitgedrukt in jaren. Deze termijn is van toepassing op handelingen met waardering V.
-}

categorie :: Handeling * Categorie [UNI] PRAGMA "" "heeft" "als categorie"
MEANING "De handelingen zijn ingedeeld in categorieën."
PURPOSE RELATION categorie REF "Besluit vaststelling selectielijst neerslag handelingen beleidsterrein zorg voor de rechtspleging 1945–2002 (Minister van Justitie)"
{+
-}

actor :: Handeling * Actor [UNI] PRAGMA "" "gebeurt namens" ""
MEANING "De handelingen zijn gerubriceerd naar de actoren Minister van Justitie, Rechterlijke Organisatie, en Commissie of Raad."
PURPOSE RELATION actor REF "Besluit vaststelling selectielijst neerslag handelingen beleidsterrein zorg voor de rechtspleging 1945–2002 (Minister van Justitie)"
{+ Onderhavig BSD geldt voor actoren die onder de zorg van de Minister van Justitie vallen.
-}


  
waardering :: Handeling * Waardering PRAGMA "" "heeft waardering" ""
MEANING "Een waardering geeft aan of de neerslag van een handeling bewaard moet worden of dat het vernietigd kan worden."
PURPOSE RELATION waardering REF "Besluit vaststelling selectielijst neerslag handelingen beleidsterrein zorg voor de rechtspleging 1945–2002 (Minister van Justitie)"
{+ Aan elke handeling koppelt de BSD een waardering.
 Deze bepaalt of de neerslag van een handeling bewaard moet worden of dat deze op termijn vernietigd kan worden.
 Als de neerslag aangewezen wordt ter bewaring,
 wil dat zeggen dat deze neerslag, ongeacht de vorm waaruit zij bestaat,
 voor eeuwig bewaard moet worden.
 De bewaarplaats waar deze neerslag na het verlopen
 van de wettelijke overbrengingstermijn van twintig jaar moet worden overgebracht,
 is het Nationaal Archief.
 Bij de handeling in dit BSD staat in dit geval bij waardering een B (van bewaren).
 De "B" staat voor bewaren.
 "B1" staat voor het bewaren van één exemplaar van het uiteindelijke product.

 Als de neerslag van een handeling wordt aangewezen ter vernietiging,
 wil dat zeggen dat deze neerslag, ongeacht de vorm waaruit zij bestaat,
 na verloop van de in het BSD vastgestelde termijn kan worden vernietigd.
 De vernietigingstermijn is een minimum eis:
 stukken mogen niet eerder dan na het verstrijken van die termijn worden vernietigd
 door de voor het beheer verantwoordelijke dienst.
 De duur van de vernietigingstermijn wordt bepaald
 door de administratieve belangen en de belangen van de burgers,
 enerzijds ten behoeve van het adequaat uitvoeren van de overheidsadministratie
 en de verantwoordingsplicht van ASR
 en anderzijds voor de recht- en bewijszoekende burger.
 Bij de handeling in dit BSD staat in dit geval bij waardering een 'V' (van vernietigen).
 "Vn" staat voor een vernietigingstermijn die loopt vanaf het einde van de benoeming. Achter de 'B' of 'V' is aangegeven welk selectiecriterium, zoals geformuleerd in de inleiding, is toegepast.
 "Va" staat voor een vernietigingstermijn lopend vanaf administratieve afhandeling van het ontslag.
-}

-----------------
-- Invarianten --
-----------------

RULE TOTwaardering : I|-waardering;waardering~

RULE UNIwaardering : waardering~;waardering |- I

RULE TOTcategorie : I[Handeling] |- categorie;categorie~
MEANING "Elke handeling is ingedeeld in een categorie."
PURPOSE RULE "TOTcategorie" REF ""
{+ Om zeker te weten dat elke handeling in precies één categorie is ingedeeld, is een regel nodig die afdwingt dat er in elk geval een categorie is voor elke handeling.
-}

{-
RULE toevoegen: van;partij[Partijnaam*Zaak] |- doc;zaaksdossier
MEANING "Voor elk bericht dat afkomstig is van een persoon behorende tot de partij van een zaak, geldt dat het bericht behoort tot een document dat in een zaaksdossier is opgenomen."
PURPOSE RULE toevoegen 
{+ Alle berichten van betrokken partijen in een zaak zitten in het zaaksdossier.
-}

ENDPATTERN

ENDCONTEXT