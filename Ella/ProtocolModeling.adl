CONTEXT ProtocolModelling IN DUTCH

RELATION in[ProtocolMachine*ProtocolModel]
RELATION in[Transition*ProtocolModel]
RELATION inst[Event*Transition] [UNI,TOT]
RELATION inst[Object*ProtocolMachine] [UNI,TOT]  -- Ella's artikel zegt: ProtocolMachineType

CLASSIFY Object ISA ProtocolMachine
CLASSIFY Behaviour ISA ProtocolMachine
CLASSIFY ProtocolMachine ISA LTS

RELATION initial[Object*State] [UNI,TOT] -- The model maker does not enforce totality, but the application will not run.
RELATION allstates[ProtocolMachine*State] [TOT]
RELATION state[ProtocolMachine*State] [UNI,TOT] -- is [UNI,TOT] correct here?
RELATION transitions[ProtocolMachine*Transition]
RELATION includes[ProtocolMachine*ProtocolMachine]
RELATION from[Transition*State] [UNI,TOT]
RELATION to[Transition*State] [UNI,TOT]
RELATION label[Transition*TEXT] [UNI,TOT]

RULE state |- allstates
RULE transitions;from |- allstates
RULE transitions;to |- allstates

RELATION attribute[ProtocolMachine*Attribute]
RELATION name[Attribute*TEXT] [UNI,TOT]
RELATION type[Attribute*Type] [UNI,TOT]
RELATION value[Attribute*TEXT] [UNI,TOT]

RULE I[Object] |- attribute # I[Object]
MEANING "A protocol machine Object contains at least one attribute, the Name of the Object."

RELATION attr[Transition*Attribute] [TOT]
RELATION callback[Transition*Function] [UNI,TOT]
MEANING "For every Transition, there exists a callback function corresponding to this event"

ENDCONTEXT