CONTEXT "Toulmin_Portal" IN DUTCH

--[Home]
POPULATION IfcText CONTAINS [ "Error - this should not occur" ] 
INTERFACE "Home": "_SESSION"[SESSION] BOX <DIV>
   [ "User": sessionIsUser INTERFACE "UserHome"
   , "Anon": sessionIsAnon INTERFACE "AnonHome"
   , "Error": (I - (sessionIsUser \/ sessionIsAnon));V;"Error - this should not occur"[IfcText]
   ]

--[AnonHome]
POPULATION IfcText CONTAINS [ "There are no accounts. Please import an (account) population." ]
POPULATION isInvisible CONTAINS [ ("AnonHome", "AnonHome") ]
INTERFACE "AnonHome" FOR Anonymous: {-V[ONE*SESSION];-}"_SESSION";sessionIsAnon cRud BOX <DIV>
-- [ "LogoAndTitle": V;"AnonHome"[LogoAndTitle] cRud
   [ "Active accounts buttonbar":  I /\ V;accIsActive;V cRud BOX <DIV>
      [ "Inloggen": I INTERFACE "Login" -- By default, a Login interface allows for account registration
      ]
   , "No active accounts": (I - V;accIsActive;V);V;"There are no accounts. Please import an (account) population."[IfcText] <STRONG>
   ]

--[Initialize general application portal]
-- The general application portal must know what the current portal interface is that needs to be used.
-- This is done by populating relation `sessionCurrentPortalIfc`:

ROLE ExecEngine MAINTAINS "Initialize sessionCurrentPortalIfc as TArgumentPortal"
RULE "Initialize sessionCurrentPortalIfc as TArgumentPortal": 
   (I - sessionCurrentPortalIfc;sessionCurrentPortalIfc~);sessionTArgContext |- sessionTArgContext;sfChangePortalToTArgReq
VIOLATION (TXT "{EX} InsPair;sfChangePortalToTArgReq;TArgContext;", TGT I, TXT ";TArgContext;", TGT I)

POPULATION isInvisible CONTAINS [ ("UserHome", "UserHome") ]
INTERFACE "UserHome" FOR User: "_SESSION";sessionIsUser;sessionTArgContext INTERFACE "TArgumentPortal"

-- The general application portal should have a header bar when the application not only uses TArguments
-- but also YYYYs and/or ZZZZs. For each of these, the equivalent of `INTERFACE "TArgumentApplicationHeaderBarComponent"``
-- will be called to provide the respective contributions.

POPULATION isInvisible CONTAINS [ ("ApplicationPortalHeaderBar", "ApplicationPortalHeaderBar") ]
INTERFACE "ApplicationPortalHeaderBar" FOR User: sessionIsUser cRud BOX <CDIV>
   [ "Portal Switch buttons": I cRud BOX <CDIV>
      [ "TArgument": sessionTArgContext INTERFACE "GoToTArgumentPortalButton"
      , "TText": sessionTTextContext INTERFACE "GoToTTextPortalButton"
--    , "ZZZZ": sessionZZContext INTERFACE "GoToZZZZPortalButton"
      ]
   , "User": I cRud BOX <CDIV>
      [ "User": sessionAccount;accPersonRef cRud
      , "Uitloggen/Logout": I cRud BOX <PROPBUTTON> [ property : sessionLogoutReq cRUd ]
      ]
   ]

-- 6b. If the general application portal implements a header bar, it should do so by defining
--    `INTERFACE "ApplicationPortalHeaderBar"` -- header bar for the general application portal
--    Such a headerbar would typically show breadcrumbs or contributions from TArgument (and/or YYYY and/or ZZZZ)
--    Here is example code for such an interface:

POPULATION isInvisible CONTAINS [ ("SessionUserAndLogout", "SessionUserAndLogout") ]
INTERFACE "SessionUserAndLogout" FOR User: sessionIsUser cRud BOX <CDIV>
   [ "Logout": I cRud BOX <PROPBUTTON> [ property : sessionLogoutReq cRUd ]
   , "User": I cRud BOX <CDIV> [ "User": sessionAccount;accPersonRef cRud ]
--#IF Debugging
   , "Show Permissions": I cRud BOX <RDETAILS>
      [  "C": I cRud COLS
         [ "TArg": sessionTArgContext;sfCreateTArgIsAllowed cRud
      -- , "YY": sessionYYContext;sfCreateYYIsAllowed cRud
      -- , "ZZ": sessionZZContext;sfCreateZZIsAllowed cRud
         ]
      , "Sel": I cRud BOX <CDETAILS>
         [ "TArg": sessionTArgContext;sfAllowSelectOnTArg cRud
      -- , "YY": sessionYYContext;sfAllowSelectOnYY cRud
      -- , "ZZ": sessionZZContext;sfAllowSelectOnZZ cRud
         ]
      , "Upd": I cRud BOX <CDETAILS>
         [ "TArg": sessionTArgContext;sfAllowUpdateOnTArg cRud
      -- , "YY": sessionYYContext;sfAllowUpdateOnYY cRud
      -- , "ZZ": sessionZZContext;sfAllowUpdateOnZZ cRud
         ]
      ]
--#ENDIF Debugging
   ]

ENDCONTEXT