CONTEXT Persoonsgegevens

META "authors" "Leon Chevalking"

PATTERN Persoonsgegevens
PURPOSE PATTERN "Persoonsgegevens"
{+
De Raad van de Rechtspraak beschikt over registers met persoonsgevens, wat tevens een thema is in INDOORS. In dit pattern worden de generieke persoonsgegevens geabstraheerd uit de verschillende registers.
-}

----------
-- Keys --
----------

KEY persoon : NatuurlijkPersoon(gbaBSN;gbaBSN~;gbaVoornamen, TXT " ", gbaBSN;gbaBSN~;gbaGeslachtsnaam)

----------------------------
-- Relaties en populaties --
----------------------------

gbaVoornamen :: NatuurlijkPersoon * Voornamen PRAGMA "" "heeft als voornaam/voornamen" ""
MEANING "Een persoon heeft een of meerdere voornamen."
PURPOSE RELATION gbaVoornamen[NatuurlijkPersoon*Voornamen] IN DUTCH REF "Bijlage I lid 1 sub a WGBA"
{+ Onder de gegevens over de burgerlijke staat valt de voornaam of de voornamen.
-}

gbaGeslachtsnaam :: NatuurlijkPersoon * Geslachtsnaam PRAGMA "" "heeft als (achter)naam" ""
MEANING "Een persoon heeft een achternaam."
PURPOSE RELATION gbaGeslachtsnaam[NatuurlijkPersoon*Geslachtsnaam] IN DUTCH REF "Bijlage I lid 1 sub a WGBA"
{+ Onder de gegevens over de burgerlijke staat valt de (achter)naam.
-}

gbaBSN :: NatuurlijkPersoon * BurgerServiceNummer [UNI] PRAGMA "" "wordt geindentificeerd door" ""
MEANING "Iedereen die zich bij de gemeente heeft ingeschreven heeft een burgerservicenummer."
PURPOSE RELATION gbaBSN[NatuurlijkPersoon*BurgerServiceNummer] IN DUTCH REF "Art. 2 Wabb; Art. 7 Wabb;  Art. 8 lid 3 Wabb"
{+ Het burgerservicenummer is een voor ieder persoon uniek nummer voor zijn/haar contacten met de overheid. Andere organisaties mogen het BSN gebruiken, als dat wettelijk geregeld is.
-}

adelijketitel :: Titel * NatuurlijkPersoon PRAGMA "De titel " "is toegekend aan" ""
MEANING "Een persoon kan een of meerdere adelijke aanspreektitels hebben."
PURPOSE RELATION adelijketitel[Titel*NatuurlijkPersoon] IN DUTCH REF "Bijlage I lid 1 sub a WGBA"
{+ Adelijke titels worden als aanspreektitels beschouwd en moeten worden vermeld op officiële documenten.
-}

opleidingstitel :: Titel * NatuurlijkPersoon PRAGMA "De titel " "is toegekend aan" ""
MEANING "Een persoon kan op basis van zijn opleiding een titel toegekend worden."
PURPOSE RELATION opleidingstitel[Titel*NatuurlijkPersoon] IN DUTCH REF ""
{+
-}

geboortedatum :: NatuurlijkPersoon * Datum [UNI] PRAGMA "" "is geboren op" ""
MEANING "De dag waarop een persoon geboren is, wordt aangehaald als zijnde de geboortedatum."
PURPOSE RELATION geboortedatum[NatuurlijkPersoon*Datum] IN DUTCH REF "Bijlage I lid 1 sub b WGBA"
{+ Ieder persoon heeft een geboortedatum, die wordt weergegeven als dd-mm-jjjj.
-}

geboorteplaats :: NatuurlijkPersoon * Plaats [UNI] PRAGMA "" "is geboren te" ""
MEANING "De plaats waar een persoon geboren is, wordt aangeduid als de geboorteplaats."
PURPOSE RELATION geboorteplaats[NatuurlijkPersoon*Plaats] IN DUTCH REF "Bijlage I lid 1 sub b WGBA"
{+ De plaats waar een persoon geboren is, wordt aangeduidt als geboorteplaats en dit gegeven valt onder de geboortegevens.
-}

geboorteland :: NatuurlijkPersoon * Land [UNI] PRAGMA "" "is geboren in" ""
MEANING "Het land waar een persoon geboren is, wordt aangeduid als het geboorteland."
PURPOSE RELATION geboorteland[NatuurlijkPersoon*Land] IN DUTCH REF "Bijlage I lid 1 sub b WGBA"
{+ Een persoon heeft als geboorteland het land waarin hij of zij geboren is en dit gegeven valt onder de geboortegegevens.
-}

woonadres :: NatuurlijkPersoon * Adres
MEANING "In Nederland ingeschreven natuurlijke personen hebben een woonadres."
PURPOSE RELATION woonadres[NatuurlijkPersoon*Adres] IN DUTCH REF "Bijlage I lid 6 WGBA"
{+ Om natuurlijke personen aan te kunnen schrijven, wordt hun woonadres geregistreerd.
-}

geslacht :: NatuurlijkPersoon * Geslacht [UNI] PRAGMA "" "heeft geslachtstype" ""
MEANING "Elk persoon is van een bepaald geslacht."
PURPOSE RELATION geslacht[NatuurlijkPersoon*Geslacht] IN DUTCH REF "Art. 34 lid 1 WGBA; Bijlage I lid 1 sub c WGBA"
{+ Het geslacht van een natuurlijk persoon wordt mede gebruikt om de aanspreekvorm te bepalen van de natuurlijk persoon te bepalen.
-}

gba :: Anummer * NatuurlijkPersoon [UNI] PRAGMA "" "heeft betrekking op" "als ingeschrevene."
MEANING "Personen die in Nederland woonachtig zijn, staan geregistreerd in een gemeentelijke basisadministratie."
PURPOSE RELATION gba[Anummer*NatuurlijkPersoon] IN DUTCH REF "Art. 34 lid 1 onderdeel a sub 7° WGBA; Art. 50 lid 3 WGBA; Bijlage I lid 7 WGBA"
{+ Het GBA-nummer dient voor berichtuitwisseling uit het GBA met afnemers (ketenpartners) van de gemeenten.
-}

nationaliteit :: NatuurlijkPersoon * Nationaliteit PRAGMA "" "heeft de" "nationaliteit"
MEANING "De nationaliteit van een persoon moet worden geregistreerd."
PURPOSE RELATION nationaliteit[NatuurlijkPersoon*Nationaliteit] IN DUTCH REF "Art. 42 WGBA; Art. 43 WGBA"
{+ Gegevens over het Nederlanderschap worden opgenomen. Gegevens over een vreemde nationaliteit worden ontleend aan een beschikking of uitspraak van een daartoe volgens het ter plaatse geldend recht bevoegde administratieve of rechterlijke instantie, die tot doel heeft tot bewijs te dienen van de betreffende nationaliteit, dan wel opgenomen met toepassing van het betreffende nationaliteitsrecht.
-}

strafrechtsketennummer :: Strafrechtsketennummer -> NatuurlijkPersoon [INJ] PRAGMA "" "is toegekend aan verdachte" "in de rol van gedaagde."
MEANING "Eén strafrechtsketennummer wordt toegekend aan precies één gedaagde."
PURPOSE RELATION strafrechtsketennummer [Strafrechtsketennummer*NatuurlijkPersoon] IN DUTCH REF "Art. 27b lid 1 Sv"
{+ Onze Minister van Justitie kent aan de verdachte na de vaststelling van zijn identiteit een strafrechtsketennummer toe, tenzij aan hem reeds een strafrechtsketennummer is toegekend.
-}

straatnaam :: Adres * Tekst [UNI] PRAGMA "" "heeft als straatnaam" ""
MEANING "Een adres bevat een straatnaam."
PURPOSE RELATION straatnaam[Adres*Tekst] IN DUTCH REF ""
{+ De straatnaam is een onderdeel van het adres.
-}

huisnummer :: Adres * Nummer [UNI] PRAGMA "" "heeft als huisnummer" ""
MEANING "Een adres bevat een huisnummer."
PURPOSE RELATION huisnummer[Adres*Nummer] IN DUTCH REF ""
{+ Het huisnummer is een onderdeel van het adres.
-}

woonplaats :: Adres * Plaats [UNI] PRAGMA "" "heeft als plaatsnaam" ""
MEANING "Een adres bevat een plaatsnaam."
PURPOSE RELATION woonplaats[Adres*Plaats] IN DUTCH REF ""
{+ De plaatsnaam is een onderdeel van het adres.
-}

postcode :: Adres * Postcode PRAGMA "" "heeft als postcode" ""
MEANING "Een adres bevat een postcode."
PURPOSE RELATION postcode[Adres*Postcode] IN DUTCH REF ""
{+ De postcode is een onderdeel van het adres.
-}

voorletters :: NatuurlijkPersoon * Voorletters

--heeftpartner :: NatuurlijkPersoon * NatuurlijkPersoon

telefoonnummer :: Persoonsgegevens * Telefoonnummer

-----------------
-- Invarianten --
-----------------
RULE geslachtstypen : I[Geslacht] = 'Man' \/ 'Vrouw'
MEANING "Geslachtstypen die wettelijk geregistreerd worden, zijn 'man' en 'vrouw'."
PURPOSE RULE "geslachtstypen"
{+ Om invoerfouten te voorkomen worden de invoertypen voor geslacht beperkt tot de waarden 'man' en 'vrouw'.
-}

ENDPATTERN

ENDCONTEXT