CONTEXT EURentWEBSITEInterfaces
------------------------------------------------------------
sessionNewRC  :: SESSION * RentalCase [UNI]

INTERFACE "EU-Rent Website" 
   ( sessionNewRC
   ) FOR Developer,User : V[ONE*SESSION];'_SESSION'
BOX[ "Enter reference number for your new rental" : sessionNewRC
   ]

INTERFACE "New User Rental"
   ( sessionNewRC
-- Edit Contract Details
   , contractedStartDate
   , contractedEndDate
   , contractedCarType
   , contractedPickupBranch
   , contractedDropOffBranch
-- Edit Customer Details
   , rcRenter
   , rcDriver
   , rcDrivingLicense
-- Other stuff
   , rcUserRequestedQ
   ) FOR Developer,User : I[RentalCase]--;(I /\ sNewRc;'_SESSION';sessionNewRC~)
BOX[ "Customer details" : I --INTERFACE "Edit Customer Details"
   BOX[ "* Driver" : rcDriver
      , "VALID driving license" : rcDrivingLicense
      , "Renter (if different from driver)" : rcRenter
      ]
   , "Contract details" : I --INTERFACE "Edit Contract Details"
   BOX[ "* Pick-up branch" : contractedPickupBranch
      , "* Drop-off branch" : contractedDropOffBranch
      , "* Start date" : contractedStartDate
      , "* End date" : contractedEndDate
      , "* Car type" : contractedCarType
      , "Available car types at pick-up branch" : contractedPickupBranch;carAvailableAt~;carType
      ]
   , "Projected costs" : I  --INTERFACE "Show Projected Costs"
   BOX[ "Daily charge (Eur)" : contractedCarType;rentalTariffPerDay
      , "Period (days)"      : contractualRentalPeriod
      , "Rental charge (Eur)": contractualBasicCharge
      ]
   , "New rental request" : I
   BOX[ "Submit this rental request?" : rcUserRequestedQ
      , "Has rental been promised?" : rentalHasBeenPromised;V;'Yes'[YesNoAnswer] \/ (I /\ -rentalHasBeenPromised);V;'No'[YesNoAnswer]
      ]
   ]
-----------------------------------------------------------
PROCESS "EU-Rent: New User Rental Request - Signaling"
ROLE User MAINTAINS "Required field: Driver"
RULE "Required field: Driver"          : I /\ sessionNewRC~;'_SESSION';sessionNewRC |- rcDriver;rcDriver~
VIOLATION (TXT "Required field 'Driver' must be filled in")

ROLE User MAINTAINS "Required field: Pick-up branch"
RULE "Required field: Pick-up branch"  : I /\ sessionNewRC~;'_SESSION';sessionNewRC |- contractedPickupBranch;contractedPickupBranch~
VIOLATION (TXT "Required field 'Pick-up branch' must be filled in")

ROLE User MAINTAINS "Required field: Drop-off branch"
RULE "Required field: Drop-off branch" : I /\ sessionNewRC~;'_SESSION';sessionNewRC |- contractedDropOffBranch;contractedDropOffBranch~
VIOLATION (TXT "Required field 'Drop-off branch' must be filled in")

ROLE User MAINTAINS "Required field: Start date"
RULE "Required field: Start date"      : I /\ sessionNewRC~;'_SESSION';sessionNewRC |- contractedStartDate;contractedStartDate~
VIOLATION (TXT "Required field 'Start date' must be filled in")

ROLE User MAINTAINS "Required field: End date"
RULE "Required field: End date"        : I /\ sessionNewRC~;'_SESSION';sessionNewRC |- contractedEndDate;contractedEndDate~
VIOLATION (TXT "Required field 'End date' must be filled in")

ROLE User MAINTAINS "Required field: Car type"
RULE "Required field: Car type"        : I /\ sessionNewRC~;'_SESSION';sessionNewRC |- contractedCarType;contractedCarType~
VIOLATION (TXT "Required field 'Car type' must be filled in")

ROLE User MAINTAINS "Submitting rental request"
RULE "Submitting rental request":
 I /\ sessionNewRC~;'_SESSION';sessionNewRC 
   /\ rcDriver;rcDriver~
   /\ contractedPickupBranch;contractedPickupBranch~
   /\ contractedDropOffBranch;contractedDropOffBranch~
   /\ contractedStartDate;contractedStartDate~
   /\ contractedEndDate;contractedEndDate~
   /\ contractedCarType;contractedCarType~
|- rcUserRequestedQ;'Yes';rcUserRequestedQ~
VIOLATION (TXT "Please submit the request (by answering 'Submit this rental request?' with 'Yes').")
ENDPROCESS
-----------------------------------------------------------
PROCESS "EU-Rent website: New Rental Requests"
PURPOSE PROCESS "EU-Rent website: New Rental Requests" REF "P2:1"
{+The user interface "New User Rental" provides some automated functionality. This section describes the features for filling in or changing the contents of forms that are presented in that interface. The assumption is that this interface is provided over the Internet, allowing users to request a rental in advance (see P2:1) from any location of their choosing (e.g. at home).-}

PURPOSE RULE "Car availability"
{+When a contract is being created, cars and/or branches may only be selected if such cars are available at these branches.-}
RULE "Car availability":  contractedCarType~;(I /\ sessionNewRC~;'_SESSION';sessionNewRC);contractedPickupBranch |- carType~;carAvailableAt
MEANING "When creating a new rental contract, car types and pick-up branch must be selected such that the branch has cars of that type available."
VIOLATION (TXT "Cars of type ", SRC I, TXT " are not available at ", TGT I)

PURPOSE RULE "Submitting user rental requests"
{+When a submitted rental case has been promised, it will no longer be a new rental request.-}
ROLE ExecEngine MAINTAINS "Submitting user rental requests"
RULE "Submitting user rental requests": sessionNewRC;rentalHasBeenPromised |- sessionNewRC;-(rcUserRequestedQ;'Yes';rcUserRequestedQ~)
MEANING "User initiated rental requests must be submitted in order for them to be handled by EU-Rent."
VIOLATION (TXT "{EX} DelPair;sessionNewRC;SESSION;", SRC I, TXT ";RentalCase;", TGT I)

ENDPROCESS
-----------------------------------------------------------
ENDCONTEXT