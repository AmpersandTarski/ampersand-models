CONTEXT EURentInterfaces IN ENGLISH
------------------------------------------------------------
--[General Session Variables]-------------------------------
PROCESS "Session Initialization"
PURPOSE PROCESS "Session Initialization"
{+The interfaces provided by this system provide for user interaction with (parts of) the system. This section describes the automated functionality necessary to initialize the system to engate in such user interaction.+}

RELATION sessionBranch[SESSION*Branch] [UNI]

--[Set today's date]---------------------------------------
RELATION sessionToday[SESSION*Date] [UNI]   REPRESENT Date TYPE DATE

PURPOSE RELATION todaysDate
{+For demonstration purposes, e.g. with a fixed population, it may be necessary to fix the initial value of today's date to some specific value.+}
RELATION todaysDate[Date*Date] [PROP]
MEANING "For demo purposes the date of today may initially be set to a fixed value."

RULE "There may be at most one value in 'todaysDate'": todaysDate;V;todaysDate |- I
ROLE ExecEngine MAINTAINS "There must be at least one value in 'todaysDate'"
RULE "There must be at least one value in 'todaysDate'": '_SESSION'[SESSION] |- V;todaysDate;V
VIOLATION (TXT "{EX} InsPair;todaysDate;Date;{php}date('d-m-Y');Date;{php}date('d-m-Y')")

ROLE ExecEngine MAINTAINS "Initialize todays date"
RULE "Initialize todays date": ('_SESSION'-(sessionToday;sessionToday~));V;todaysDate |- sessionToday
VIOLATION (TXT "{EX} InsPair;sessionToday;SESSION;", SRC I, TXT ";Date;", TGT I)

ENDPROCESS
-----------------------------------------------------------
--[Rental Contract Overview]--
INTERFACE "View Rental Cases": I[RentalCase] cRud BOX <SHCOLS>
    [ "Rental ID" : I cRud LINKTO INTERFACE "Edit Rental Case"
    , "Start date" : rcStartDate cRud
    , "End date" : rcEndDate cRud
    , "Car type" : rcCarType cRud
    , "Car" : rcAssignedCar cRud
    , "Pick-up branch" : rcPickupBranch cRud
    , "Drop-off branch" : rcDropOffBranch cRud
    ]

INTERFACE "Select a starting rental case": I[RentalCase] cRuD BOX <SHCOLS>
    [ "Select" : I BOX <PropertyButton> [ property : rcStartSelectReq cRUd ]
    , "Renter" : rcRenter cRud
    , "Driver" : rcDriver cRud
    , "Driving license" : rcDrivingLicense cRud
    , "Start date" : rcStartDate cRud
    , "End date" : rcEndDate cRud
    , "Car type" : rcCarType cRud
    , "Car" : rcAssignedCar cRud
    , "Pick-up branch" : rcPickupBranch cRud
    , "Drop-off branch" : rcDropOffBranch cRud
    ]

INTERFACE "Select an ending rental case": I[RentalCase] cRuD BOX <SHCOLS>
    [ "Select" : I BOX <PropertyButton> [ property : rcStopSelectReq cRUd ]
    , "Renter" : rcRenter cRud
    , "Driver" : rcDriver cRud
    , "Driving license" : rcDrivingLicense cRud
    , "Start date" : rcStartDate cRud
    , "End date" : rcEndDate cRud
    , "Car type" : rcCarType cRud
    , "Car" : rcAssignedCar cRud
    , "Pick-up branch" : rcPickupBranch cRud
    , "Drop-off branch" : rcDropOffBranch cRud
    ]

INTERFACE "View Rental Case" : I[RentalCase] cRud BOX <ROWSNL>
   [ "Contract details" : I {-cRud-} INTERFACE "Show Contract Details"
   , "Projected Costs"  : I {-cRud-} INTERFACE "Show Projected Costs"
   , "Customer details" : I {-cRud-} INTERFACE "Show Customer Details"
   , "Drop-off details" : I {-cRud-} INTERFACE "Show Drop-off Details"
   , "Billing details"  : I {-cRud-} INTERFACE "Show Billing Details"
   , "Transaction status" : I {-cRud-} INTERFACE "Show Rental Case Status"
   ]

INTERFACE "Edit Rental Case" : I[RentalCase] cRud BOX <ROWSNL>
   [ "Contract details" : I {-cRud-} INTERFACE "Edit Contract Details"
   , "Projected Costs"  : I {-cRud-} INTERFACE "Show Projected Costs"
   , "Customer details" : I {-cRud-} INTERFACE "Edit Customer Details"
   , "Drop-off details" : I {-cRud-} INTERFACE "Edit Drop-off Details"
   , "Billing details"  : I {-cRud-} INTERFACE "Show Billing Details"
   , "Transaction status" : I {-cRud-} INTERFACE "Show Rental Case Status"
   ]

INTERFACE "Show Rental Case Status": I[RentalCase] cRud COLS --This interface can be optimized by an HTML template
   [ "Rental has been": I cRud COLS
      [ "Requested"   : rentalHasBeenRequested cRud
      , "Promised"    : rentalHasBeenPromised cRud
      , "Started"     : rentalHasBeenStarted cRud
      , "Ended"       : rentalHasBeenEnded cRud
      ]
   , "Car has been": I cRud COLS
      [ "Picked up"   : rentalCarHasBeenPickedUp cRud
      , "Dropped off" : rentalCarHasBeenDroppedOff cRud
      ]
   , "Payment has been" : I cRud COLS   
      [ "Requested"   : paymentHasBeenRequested cRud
      , "Received"    : rentalIsPaid cRud
      ]
   , "STATUSMSGs" : rcMsgText cRud
   , "TODOs" : rcWorkItem;workitemText cRud
   ]

-----------------------------------------------------------
--[Contract Views]--
INTERFACE "Show Contract Details" : I[RentalCase] cRud COLS
   [ "Pick-up" : rcPickupBranch cRud
   , "Drop-off" : rcDropOffBranch cRud
   , "Start date" : rcStartDate cRud
   , "End date" : rcEndDate cRud
   , "Car type" : rcCarType cRud
   , "Car issued" : rcAssignedCar cRud
   ]

INTERFACE "Edit Contract Details": I[RentalCase] cRud BOX <ROWSNL>
   [ "Editable contract details": I cRud BOX
      [ "* Pick-up branch" : rcPickupBranch cRUd
      , "* Drop-off branch" : rcDropOffBranch cRUd
      , "* Start date" : rcStartDate cRUd
      , "* End date" : rcEndDate cRUd
      , "* Car type" : rcCarType cRUd
      ]
   , "Derived contract detail": (I-rentalHasBeenPromised);rcPickupBranch cRud BOX <ROWSNL>
      [ "If cars are available": I /\ carAvailableAt~;carAvailableAt cRud BOX
         [ "Available cars" : carAvailableAt~;carType cRud
         ]
      , "If cars are not available": I-(carAvailableAt~;carAvailableAt) cRud BOX
         [ "Error message" : I;V;'There are no cars available at the selected branch.'[IfcMsg] cRud
         ]
      ]
   ]
POPULATION IfcMsg CONTAINS [ "There are no cars available at the selected branch." ]

-----------------------------------------------------------
--[Customer Views]--
INTERFACE "Show Customer Details" : I[RentalCase] cRud COLS
   [ "Driver" : rcDriver cRud
   , "Driving license" : rcDrivingLicense cRud
   , "Renter" : rcRenter cRud
   ]

INTERFACE "Edit Customer Details": I[RentalCase] cRud BOX <ROWSNL>
   [ "Driver details may always be changed": I cRud BOX
      [ "* Driver" : rcDriver cRUd
      , "* Driving license" : rcDrivingLicense cRUd
      ]
   , "Renter details in Customer sessions": I /\ V;'_SESSION';sessionAccount;V[Customer*RentalCase] cRud BOX
      [ "Renter" : rcRenter cRud ]
   , "Renter details in Employee sessions": I /\ V;'_SESSION';sessionAccount;V[Employee*RentalCase] cRud BOX
      [ "Renter" : rcRenter cRUd ]
   , "Renter details in Anonymous sessions": I /\ V;'_SESSION';sessionAccount;V[Employee*RentalCase] cRud BOX
      [ "You must login or register to become (or change) the renter" : I-I cRud ]
   ]
-----------------------------------------------------------
--[Car Views]--
VIEW Cars: Car(carID)
INTERFACE "Show Car Details" : I[Car] cRud BOX
   [ "Plate" : I cRud
   , "Type" : carType cRud COLS
      [ "Type" : I cRud
      , "Rental tariff" : rentalTariffPerDay cRud <EuroPerDay>
      , "Excess tariff" : excessTariffPerDay cRud <EuroPerDay>
      ]
   , "Available at (branch)" : carAvailableAt cRud
   , "Current rental" : (I-(carAvailableAt;carAvailableAt~));rcAssignedCar~ cRud
   , "All rentals" : rcAssignedCar~ cRud
   ]
-----------------------------------------------------------
--[Car Assignment, Car Pickup]--
INTERFACE "Car Assignment and Pickup": I[RentalCase] cRud BOX
   [ "Available Types" : V;'_SESSION';sessionBranch;carAvailableAt~;carType cRud COLS
      [ "Type"   : I cRud
      , "Tariff" : rentalTariffPerDay cRud <EuroPerDay>
      ]
   , "Selected Type"  : rcCarType cRUd
   , "Available cars" : rcCarType;carType~ /\ V;'_SESSION';sessionBranch;carAvailableAt~ cRud
   , "Selected car"   : rcAssignedCar cRUd
   , "Keys are handed over": I BOX <PropertyButton> [ property : rcKeysHandedOver cRUd ]
   ]
-----------------------------------------------------------
--[Projected Cost View]--
VIEW "EuroPerDay": Amount HTML TEMPLATE "EuroPerDay.html" ENDVIEW
VIEW "Euro":       Amount HTML TEMPLATE "Euro.html"       ENDVIEW
VIEW "Days": NumberOfDays HTML TEMPLATE "Days.html"       ENDVIEW
VIEW "KM":       Distance HTML TEMPLATE "km.html"         ENDVIEW

INTERFACE "Show Projected Regular Costs" : I[RentalCase] /\ rcCarType;V;contractualRentalPeriod~ cRud BOX -- Costs without the excess tariffs
   [ "Rental period"  : contractualRentalPeriod cRud <Days>
   , "Rental tariff"  : rcCarType;rentalTariffPerDay cRud <EuroPerDay>
   , "Projected Total": contractualBasicCharge cRud <Euro>
   ]

INTERFACE "Show Projected Costs": I /\ rcCarType;V;contractualRentalPeriod~ cRud COLS
   [ "Rental period"  : contractualRentalPeriod cRud <Days>
   , "Rental tariff"  : rcCarType;rentalTariffPerDay cRud <EuroPerDay>
   , "Excess tariff"  : rcCarType;excessTariffPerDay cRud <EuroPerDay>
   , "Projected Total": contractualBasicCharge cRud <Euro>
   ]
-----------------------------------------------------------
--[Drop-off Views]--
INTERFACE "Show Drop-off Details" : I[RentalCase] cRud COLS
   [ "Car" : rcAssignedCar cRud
   , "Contracted drop-off": I cRud COLS
      [ "Branch" : rcDropOffBranch cRud
      , "Date" : rcEndDate cRud
      ]
   , "Actual drop-off": I cRud COLS
      [ "Branch" : rcDroppedOffBranch cRud
      , "Date" : rcDroppedOffDate cRud
      ]
   ]

INTERFACE "Edit Drop-off Details": I[RentalCase] cRud COLS
   [ "Contract drop-off branch" : rcDropOffBranch cRud
   , "Contract drop-off end date" : rcEndDate cRud
   , "Dropped-off car" : rcAssignedCar cRud
   , "Actual drop-off branch" : rcDroppedOffBranch cRUd
   , "Acutal drop-off date" : rcDroppedOffDate cRUd
   ]
-----------------------------------------------------------
--[Billing Views]--
INTERFACE "Show Billing Details" : I[RentalCase] cRud BOX <ROWSNL>
   [ "Part 1": I /\ rentalPeriod;rentalPeriod~ /\ rcAssignedCar;rcAssignedCar~ cRud BOX
      [ "Basic Charge" : I cRud COLS
         [ "Rental Period"  : rentalPeriod cRud <Days>
         , "Rental Tariff"  : rcAssignedCar;carType;rentalTariffPerDay cRud <EuroPerDay>
         , "Rental Amount"  : rentalBasicCharge cRud <Euro>
         ]
      ]
   , "Part 2": I /\ rentalExcessPeriod;rentalExcessPeriod~ /\ rcAssignedCar;rcAssignedCar~ cRud BOX
      [ "Penalty charge for exceeding contracted duration" : I cRud COLS
         [ "Excess Period"  : rentalExcessPeriod cRud <Days>
         , "Excess Tariff"  : rcAssignedCar;carType;excessTariffPerDay cRud <EuroPerDay>
         , "Penalty Charge" : rentalPenaltyCharge cRud <Euro>
         ]
      ]
   , "Part 3": I /\ rcDropOffBranch;-I;rcDroppedOffBranch~ cRud BOX
      [ "Penalty charge for uncontractual drop-off": I cRud COLS
         [ "Contracted drop-off branch" : rcDropOffBranch cRud
         , "Actual drop-off branch" : rcDroppedOffBranch cRud
            , "Distance": (I /\ rcDroppedOffBranch;rcDropOffBranch~);V;'0'[Distance] \/ 
                          (I /\ rcDroppedOffBranch;-I;rcDropOffBranch~);(rcDroppedOffBranch;distbranch~ /\ rcDropOffBranch;distbranch~);distance cRud <KM>
         , "Drop-off Penalty" : rentalLocationPenaltyCharge cRud <Euro>
         ]
      ]
   , "TOTAL" : I /\ rentalCharge;rentalCharge~ cRud BOX
      [ "TOTAL charge" : rentalCharge <Euro>
      ]
   ]
--}
-----------------------------------------------------------
ENDCONTEXT