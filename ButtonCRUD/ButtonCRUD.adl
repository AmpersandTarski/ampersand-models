CONTEXT "ButtonCRUD Test/Demo" IN ENGLISH 

INCLUDE "./XXXX.adl"

--[XXXX_ButtonCRUD - Switching between different portals]
-- 6. If you choose to use `INTERFACE "XXXXPortal"` that is provided by this code
--    you then _MUST_ create `INTERFACE "ApplicationPortalHeaderBar"`
--    You may want this interface to provide a breadcrumb and/or buttons for navigation purposes.
--    Notes:
--    - this file specifies `INTERFACE "GoToXXXXPortalButton"`, that contains a single button that
--      when clicked, navigates the user to `INTERFACE "XXXXPortal"`.
--      You can use this as part of the headerbar navigation buttons.
--    - make sure you properly initialize the switching between portals, e.g. by checking whether
--      `sessionCurrentPortalIfc` is populated, and if not, create a request to do so. 
--    Here is some example code for you to use and adapt in your application script:

ROLE ExecEngine MAINTAINS "Initialize sessionCurrentPortalIfc as XXXXPortal"
RULE "Initialize sessionCurrentPortalIfc as XXXXPortal": 
   (I - sessionCurrentPortalIfc;sessionCurrentPortalIfc~);sessionXXContext |- sessionXXContext;sfChangePortalToXXReq
VIOLATION (TXT "{EX} InsPair;sfChangePortalToXXReq;XXContext;", TGT I, TXT ";XXContext;", TGT I)

--[Switching between ButtonCRUDPortal and other portals (e.g. YYYYPortal or ZZZZPortal)]
-- The purpose of the following code is to allow applications to switch between the ButtonCRUDPortal as defined below
-- and other portals, e.g. a YYYYPortal or a ZZZZPortal that are defined elsewhere in the application.
RELATION sessionChangeToApplicationHomePortalReq[SESSION*SESSION] [PROP]
ROLE ExecEngine MAINTAINS "Service sessionChangeToApplicationHomePortalReq"
RULE "Service sessionChangeToApplicationHomePortalReq": sessionChangeToApplicationHomePortalReq |- -I
VIOLATION (TXT "{EX} DelPair;sessionChangeToApplicationHomePortalReq;SESSION;", SRC I, TXT ";SESSION;", TGT I
          ,TXT "{EX} InsPair;sessionCurrentPortalIfc;SESSION;", SRC I, TXT ";IfcName;/ButtonCRUDPortal"
          ,TXT "{EX} SetNavToOnCommit;/ButtonCRUDPortal"
          )
-- The below interface can be called e.g. from `INTERFACE "ApplicationPortalHeaderBar"` to navigate to the ButtonCRUDPortal
POPULATION ButtonText CONTAINS [ "ButtonCRUDPortal" ]
INTERFACE "GoToButtonCRUDPortal": I[SESSION] cRud BOX <DIV>
   [  "ButtonCRUDPortal": I cRud BOX <PROPBUTTON>
      [ property: sessionChangeToApplicationHomePortalReq cRUd -- relation of type [PROP] whose value is toggled when user presses button
      , label: TXT "ButtonCRUD Portal" -- text to show on the button
--    , color: TXT "primary" -- primary=blue secondary=grey, success=green, warning=yellow, danger=red, info=lightblue, light=grey, dark=black
      , disabledcolor: TXT "success" -- precede color with `outline-` (e.g. `outline-primary`) to make an outline-button
      , disabled: I /\ sessionCurrentPortalIfc;"/ButtonCRUDPortal";sessionCurrentPortalIfc~ cRud
--    , hide: expression cRud -- when expression is not empty, button does not show on screen
--    , popovertext: TXT "text that shows when user hovers cursor over the button"
      ]
   ]

--[Interfaces]
INTERFACE "ButtonCRUDPortal": sessionXXContext cRud BOX
[  "ApplicationPortalHeaderBar": sessionXXContext~ INTERFACE "ApplicationPortalHeaderBar"
,  "XXContext": I[XXContext] cRud COLS
   [ "XXContextID": I cRud
   , "Create": sfCreateXXIsAllowed cRUd
   , "sfSelectedXX": I cRud BOX <CDIV>
      [ "XX": sfSelectedXX cRud
      , "Deassign": I cRud BOX <PROPBUTTON> 
         [ property: sfXXDeselectReq cRUd
         , disabled: I - sfSelectedXX;sfSelectedXX~ cRud
         ]
      ]
   , "sfUpdatingXX": I cRud BOX <CDIV>
      [ "XX": sfUpdatingXX cRud
      , "Done": I cRud BOX <PROPBUTTON> 
         [ property: sfXXDoneUpdReq cRUd
         , disabled: I - sfUpdatingXX;sfUpdatingXX~ cRud
         ]
      ]
   , "Upd": sfXXIsUpdating cRud
   , "Permissions": I cRud BOX <PROPBUTTON> 
      [ property: sfSetDefaultPermissionsForXXs cRUd
      , label: TXT "Set Default Permissions"
      ]
   , "Reset BCRUD": I cRud BOX <PROPBUTTON> 
      [ property: xxxBCRUDResetReq cRUd
      , label: TXT "Reset select/edit SM" 
      , disabled: xxxBCRUDResetReq cRud
      ]
   ]
, "XXXXs": V[XXContext*XXXX] CRuD COLS
   [ "XXXX": I cRud
   , "Name": xxxName cRUd
   , "Priv": I cRud <Privileges>
   , "Props": I cRud <Properties>
   , "Change privileges into": I cRud BOX <CDIV>
      [ "Set S": I  - sfAllowSelectOnXX~;sessionXXContext~;"_SESSION"[SESSION];sessionXXContext;sfAllowSelectOnXX BOX <PROPBUTTON> [ property: setSReq cRUd ]
      , "Clr S": I /\ sfAllowSelectOnXX~;sessionXXContext~;"_SESSION"[SESSION];sessionXXContext;sfAllowSelectOnXX BOX <PROPBUTTON> [ property: clrSReq cRUd ]
      , "Set U": I  - sfAllowUpdateOnXX~;sfAllowUpdateOnXX BOX <PROPBUTTON> [ property: setUReq cRUd ]
      , "Clr U": I /\ sfAllowUpdateOnXX~;sfAllowUpdateOnXX BOX <PROPBUTTON> [ property: clrUReq cRUd ]
      , "Set D": I  - sfAllowDeleteOnXX~;sfAllowDeleteOnXX BOX <PROPBUTTON> [ property: setDReq cRUd ]
      , "Clr D": I /\ sfAllowDeleteOnXX~;sfAllowDeleteOnXX BOX <PROPBUTTON> [ property: clrDReq cRUd ]
      ]
   , "Ed": xxxIsBeingEdited cRud
   , "IC": xxxIsClean cRud
   ]
]

--[ButtonCRUD testing facility]
-- In regular projects, this file would control the (minimal) portal interface functionality.
-- However, for testing purposes we add functionality to control the XXXX-ButtonCRUD context.
POPULATION Stxt CONTAINS [ "S", "s", "C", "c", "U", "u", "E", "e", "D", "d", "P", "p", "I", "i" ]

VIEW STRONG: IfcText HTML TEMPLATE "View-STRONG.html" ENDVIEW   REPRESENT IfcText TYPE ALPHANUMERIC
VIEW Privileges: XXXX
{ "S":  sfAllowSelectOnXX~;V[XXContext*Stxt];"S"
, "s": -sfAllowSelectOnXX~;V[XXContext*Stxt];"s"
, "U":  sfAllowUpdateOnXX~;V[XXContext*Stxt];"U"
, "u": -sfAllowUpdateOnXX~;V[XXContext*Stxt];"u"
, "D":  sfAllowDeleteOnXX~;V[XXContext*Stxt];"D"
, "d": -sfAllowDeleteOnXX~;V[XXContext*Stxt];"d"
} ENDVIEW

VIEW Properties: XXXX
{ "Persistent":        xxxIsPersistent;V[XXXX*Stxt];"P"
, "Non-Persistent": (I-xxxIsPersistent);V[XXXX*Stxt];"p"
, "Clean":             xxxIsClean;V[XXXX*Stxt];"C"
, "Non-Clean":      (I-xxxIsClean);V[XXXX*Stxt];"c"
, "Inited":            xxxIsInitialized;V[XXXX*Stxt];"I"
, "Non-Inited":     (I-xxxIsInitialized);V[XXXX*Stxt];"i"
} ENDVIEW

-- EQUIVALENCE xxxIsBeingEdited[XXXX] [PROP] == I /\ sfUpdatingXX~;sfUpdatingXX
RELATION xxxIsBeingEdited[XXXX*XXXX] [PROP] -- define relation xxxIsBeingEdited as shorthand for I /\ sfUpdatingXX~;sfUpdatingXX
ROLE ExecEngine MAINTAINS "Equivalence - InsPair xxxIsBeingEdited"
RULE "Equivalence - InsPair xxxIsBeingEdited": I /\ sfUpdatingXX~;sfUpdatingXX |- xxxIsBeingEdited[XXXX]
VIOLATION (TXT "{EX} InsPair;xxxIsBeingEdited;XXXX;", SRC I, TXT ";XXXX;", TGT I)
ROLE ExecEngine MAINTAINS "Equivalence - DelPair xxxIsBeingEdited"
RULE "Equivalence - DelPair xxxIsBeingEdited": xxxIsBeingEdited[XXXX] |- I /\ sfUpdatingXX~;sfUpdatingXX
VIOLATION (TXT "{EX} DelPair;xxxIsBeingEdited;XXXX;", SRC I, TXT ";XXXX;", TGT I)

RELATION setSReq[XXXX*XXXX] [PROP]
ROLE ExecEngine MAINTAINS "Allow S" 
RULE "Allow S": setSReq |- -I
VIOLATION (TXT "{EX} DelPair;setSReq;XXXX;", SRC I, TXT ";XXXX;", TGT I
          ,TXT "{EX} InsPair;sfAllowSelectOnXX;XXContext;", SRC V;"_SESSION"[SESSION];sessionXXContext, TXT ";XXXX;", TGT I)
RELATION clrSReq[XXXX*XXXX] [PROP]
ROLE ExecEngine MAINTAINS "Deny S" 
RULE "Deny S": clrSReq |- -I
VIOLATION (TXT "{EX} DelPair;clrSReq;XXXX;", SRC I, TXT ";XXXX;", TGT I
          ,TXT "{EX} DelPair;sfAllowSelectOnXX;XXContext;", SRC V;"_SESSION"[SESSION];sessionXXContext, TXT ";XXXX;", TGT I)

RELATION setUReq[XXXX*XXXX] [PROP]
ROLE ExecEngine MAINTAINS "Allow U" 
RULE "Allow U": setUReq |- -I
VIOLATION (TXT "{EX} DelPair;setUReq;XXXX;", SRC I, TXT ";XXXX;", TGT I
          ,TXT "{EX} InsPair;sfAllowUpdateOnXX;XXContext;", SRC V;"_SESSION"[SESSION];sessionXXContext, TXT ";XXXX;", TGT I)
RELATION clrUReq[XXXX*XXXX] [PROP]
ROLE ExecEngine MAINTAINS "Deny U" 
RULE "Deny U": clrUReq |- -I
VIOLATION (TXT "{EX} DelPair;clrUReq;XXXX;", SRC I, TXT ";XXXX;", TGT I
          ,TXT "{EX} DelPair;sfAllowUpdateOnXX;XXContext;", SRC V;"_SESSION"[SESSION];sessionXXContext, TXT ";XXXX;", TGT I)

RELATION setDReq[XXXX*XXXX] [PROP]
ROLE ExecEngine MAINTAINS "Allow D" 
RULE "Allow D": setDReq |- -I
VIOLATION (TXT "{EX} DelPair;setDReq;XXXX;", SRC I, TXT ";XXXX;", TGT I
          ,TXT "{EX} InsPair;sfAllowDeleteOnXX;XXContext;", SRC V;"_SESSION"[SESSION];sessionXXContext, TXT ";XXXX;", TGT I)
RELATION clrDReq[XXXX*XXXX] [PROP]
ROLE ExecEngine MAINTAINS "Deny D" 
RULE "Deny D": clrDReq |- -I
VIOLATION (TXT "{EX} DelPair;clrDReq;XXXX;", SRC I, TXT ";XXXX;", TGT I
          ,TXT "{EX} DelPair;sfAllowDeleteOnXX;XXContext;", SRC V;"_SESSION"[SESSION];sessionXXContext, TXT ";XXXX;", TGT I)

ENDCONTEXT
