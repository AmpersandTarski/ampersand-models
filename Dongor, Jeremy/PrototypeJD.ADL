CONTEXT Ampersandmodel IN DUTCH

META "Auteur" "Jeremy Dongor"

INCLUDE "Projectleiders.xlsx"
INCLUDE "Transitieteams.xlsx"
INCLUDE "Implementaties.xlsx"
INCLUDE "Contractbeheer.xlsx"
INCLUDE "Documentatie.xlsx"
INCLUDE "Gebruikers.xlsx"
INCLUDE "ICToutsourcing.xlsx"
INCLUDE "Acceptatiemodellen.xlsx"
INCLUDE "Keyusers.xlsx"
INCLUDE "Categorieen.xlsx"
INCLUDE "Calamiteitplannen.xlsx"
INCLUDE "Calamiteiten.xlsx"
INCLUDE "Noodplannen.xlsx"
INCLUDE "Softwarebeheer.xlsx"

--beschrijving van de concepten--
CONCEPT Acceptatiemodel "Gebruikersacceptatiemodel dat wordt ingezet om het nieuwe ICT te toetsen aan de vooraf gestelde verwachtingen."
CONCEPT Calamiteit "Onverwachtse gebeurtenis die het uitvoeren van het implementatieplan zodanig belemmerd dat het geen doorgang kan vinden."
CONCEPT Calamiteitenplan "Beschrijving van de organisatiestructuren, procedures en afspraken voor calamiteitensituaties. "
CONCEPT Documentatie "Alle procedures en werkinstructies die van toepassing zijn op het nieuwe ICT dat is gerealiseerd voor de klantorganisatie."
CONCEPT Gebruiker "Medewerker van de klantorganisatie die gebruik maakt van het nieuwe ICT dat is gerealiseerd in het ICT-outsourcing project."
CONCEPT ICToutsourcing "Periode waarbij de IT-leverancier alle door de klantorganisatie gebruikte hardware en software in beheer neemt."
CONCEPT Implementatieplan "Beschrijving van alle activiteiten die moeten worden uitgevoerd voor realisatie van het nieuwe ICT."
CONCEPT Keyuser "Personeelslid die de belangen van de klantorganisatie vertegenwoordigd bij de IT-leveranciers."
CONCEPT Noodplan "Beschrijving van de te verrichten activiteiten op het moment dat er een calamiteit is opgetreden die de werkzaamheden van het implementatieplan belemmerd of stopt."
CONCEPT Projectleider "Eindverantwoordelijke voor het succesvol afronden van ICT-outsourcing projecten."
CONCEPT Transitieteam "Samenstelling van personeel die onderdeel uitmaken van het ICT-outsourcing project."


--definieren van de relaties--
RELATION uitgevoerd[Implementatieplan*Transitieteam] [UNI]
PRAGMA "Implementatieplan "" is uitgevoerd door ""."
MEANING "Elke implementatieplan is door maximaal één transitieteam uitgevoerd."
PURPOSE RELATION uitgevoerd
{+Elk implementatieplan dat akkoord bevonden is dient te worden gekoppeld aan het transitieteam die de werkzaamheden zal uitvoeren.+}


RELATION begeleid [ICToutsourcing*Projectleider] [UNI]
PRAGMA "Project "" is begeleid door projectleider ""."
MEANING "Elke project dient te worden begeleid door tenminste- en maximaal één projectleider."
PURPOSE RELATION begeleid
{+Elk project dient te worden gekoppeld aan tenminste- en maximaal één projectleider.+}
RULE TOT_begeleid: I|-begeleid;begeleid~
MESSAGE "De volgende ICT-outsourcing projecten hebben nog geen projectleider:" VIOLATION(TXT "'", SRC I;heeftprojectnaam, TXT "'.")
ROLE USER MAINTAINS TOT_begeleid


RELATION geschrevendoor[Implementatieplan*Projectleider] [UNI]
PRAGMA "Implementatieplan "" is geschreven door ""."
MEANING "Een implementatieplan is door maximaal één projectleider geschreven."
PURPOSE RELATION geschrevendoor
{+Elke implementatieplan dat wordt uitgevoerd door een transitieteam dient te worden gekoppeld aan een projectleider.+} 


RELATION gekoppeld[Projectleider*Transitieteam]
PRAGMA "" "is gekoppeld aan ""."
MEANING "Elke projectleider is gekoppeld aan tenminste één transitieteam."
PURPOSE RELATION gekoppeld
{+Elke projectleider die een implementatieplan geschreven heeft dient te worden gekoppeld aan een transitieteam.+}
RULE TOT_gekoppeld: I|-gekoppeld~;gekoppeld
MESSAGE "De volgende transitieteams hebben nog geen projectleider:" VIOLATION(TXT "'", SRC teamnaam, TXT "'.")
ROLE USER MAINTAINS TOT_gekoppeld


RELATION verbonden[Implementatieplan*Acceptatiemodel][UNI]
PRAGMA "Implementatieplan "" is verbonden aan gebruikersacceptatiemodel ""."
MEANING "Elke implementatieplan is verbonden aan maximaal één gebruikersacceptatiemodel."
PURPOSE RELATION verbonden
{+Elk akkoord bevonden implementatieplan dient te worden voorzien van een gebruikersacceptatiemodel.+} 


RELATION beoordeeld[Implementatieplan*KeyuserID]
PRAGMA "Implementatieplan "" is beoordeeld door ""."
MEANING "Elke implementatieplan is door tenminste één KeyuserID beoordeeld."
PURPOSE RELATION beoordeeld
{+ Een gebruikersacceptatiemodel moet worden beoordeeld door een KeyuserID.+}
RULE TOT_beoordeeld: I|-beoordeeld;beoordeeld~
MESSAGE "De volgende implementatieplannen zijn nog niet beoordeeld:" VIOLATION(SRC documentnaam)
ROLE USER MAINTAINS TOT_beoordeeld


RELATION mededeling[Transitieteam*KeyuserID]
PRAGMA "" "heeft mededeling gekregen van ""."
MEANING "Elke transitieteam dient medegedeeld te krijgen van een Keyuser dat de klantorganisatie gereed is voor het nieuwe ICT."
PURPOSE RELATION mededeling
{+ Voor elke implementatieplan dient het transitieteam medegedeeld te gekregen van een Keyuser dat de klantorganisatie hier gereed voor is.+}
RULE TOT_mededeling: I|-mededeling;mededeling~
MESSAGE "De volgende transitieteams hebben geen mededeling gekregen dat de klantorganisatie klaar is voor het nieuwe ICT:" VIOLATION(SRC teamnaam)
ROLE USER MAINTAINS TOT_mededeling


RELATION opgestelddoor[Calamiteitenplan*Projectleider] [UNI]
PRAGMA "Calamiteitenplan "" is opgesteld door projectleider: ""." 
MEANING "Een calamiteitenplan is opgesteld door maximaal één projectleider."
PURPOSE RELATION opgestelddoor
{+Elk calamiteitenplan moet worden opgesteld door een projectleider.+}


RELATION toegekendaan[Implementatieplan*Calamiteitenplan]
PRAGMA "ICT-outsourcing project "" is gelinked aan calamiteitenplan ""."
MEANING "Elk implementatieplan is aan tenminste één calamiteitenplan toegekend."
PURPOSE RELATION toegekendaan
{+Elk implementatieplan dient te worden toegekend aan een calamiteitenplan.+}
RULE TOT_toegekendaan: I|-toegekendaan;toegekendaan~
MESSAGE "De volgende ICT-outsourcing projecten hebben geen calamiteitenplan:" VIOLATION(SRC I;documentnaam)
ROLE USER MAINTAINS TOT_toegekendaan 


RELATION bevatcalamiteit[Calamiteitenplan*Calamiteit] [SUR]
PRAGMA "Calamiteitenplan "" bevat calamiteit ""."
MEANING "Elk calamiteitenplan bevat tenminste één calamiteit."
PURPOSE RELATION bevatcalamiteit
{+Elk calamiteitenplan moet hebben beschreven wat te doen bij optreding van een of meer calamiteiten.+}


RELATION nood[Calamiteit*Noodplan] [SUR]
PRAGMA "Calamiteit "" bevat noodplan ""."
MEANING "Elke calamiteit heeft tenminste één noodplan."
PURPOSE RELATION nood
{+Voor elke calamiteit moet er een noodplan aanwezig zijn.+}
RULE SUR_nood: I|-nood;nood~
MESSAGE "De volgende calamiteiten hebben geen noodplan:" VIOLATION(SRC I)
ROLE USER MAINTAINS SUR_nood


RELATION onderdeelvan[Implementatieplan*ICToutsourcing]
PRAGMA "Implementatieplan "" is onderdeel van ICT-outsourcing project ""."
MEANING "Elke implementatieplan dient tenminste één keer te zijn gekoppeld aan een ICT-outsourcing project."
PURPOSE RELATION onderdeelvan
{+Bij elk ICT-outsourcing moet er een implementatieplan aanwezig zijn.+}
RULE TOT_onderdeelvan: I|-onderdeelvan;onderdeelvan~
MESSAGE "De volgende implementatieplannen zijn nog niet gekoppeld aan een ICT-outsourcing project:" VIOLATION(SRC documentnaam)
ROLE USER MAINTAINS TOT_onderdeelvan


RELATION opgeleverd[Documentatie*Transitieteam] [UNI]
PRAGMA "Documentatieset "" is opgeleverd door ""."
MEANING "Elke documentatieset mag door maximaal één transitieteam worden opgeleverd."
PURPOSE RELATION opgeleverd
{+Voor elk ICT-outsourcing project moet het transitieteam documentatie opleveren.+}
RULE SUR_opgeleverd: I|-opgeleverd~;opgeleverd
MESSAGE "De volgende teams moeten nog documentatie opleveren:" VIOLATION(SRC teamnaam)
ROLE USER MAINTAINS SUR_opgeleverd


RELATION opgeleid[Gebruiker*Transitieteam] [UNI]
PRAGMA "Gebruiker "" is opgeleid door transitieteam "" ."
MEANING "Een gebruiker wordt maximaal één keer opgeleid voor gebruik van het nieuwe ICT."
PURPOSE RELATION opgeleid
{+Het transitieteam dient één of meer gebruikers op te leiden voor gebruik van het nieuwe ICT.+} 


RELATION heeftkennis[Gebruiker*ICToutsourcing]
PRAGMA "Gebruiker "" heeft kennis van "" ."
MEANING "Een gebruiker dient tenminste één keer (onder begeleiding) kennis genomen te hebben van het nieuwe ICT dat is gerealiseerd in het ICT-outsourcing project."
PURPOSE RELATION heeftkennis
{+De gebruiker dient kennis te hebben van het nieuwe ICT voordat hij hier gebruik van maakt.+} 
RULE TOT_heeftkennis: I|-heeftkennis~;heeftkennis
MESSAGE "Let op! er zijn geen getrainde gebruikers gekoppeld aan de volgende ICT-outsourcing projecten:" VIOLATION(SRC heeftprojectnaam)
ROLE USER MAINTAINS TOT_heeftkennis


RELATION geevalueerd[ICToutsourcing*Transitieteam]
PRAGMA "" "is geevalueerd door ""."
MEANING "Elke ICT-outsourcing project moet tenminste één keer worden geevalueerd door het transitieteam."
PURPOSE RELATION geevalueerd
{+Elke ICT-outsourcing project moet worden geevalueerd door het transitieteam.+}
RULE TOT_geevalueerd: I|-geevalueerd;geevalueerd~
MESSAGE "De volgende ICT-outsourcing projecten zijn nog niet geëvalueerd:" VIOLATION(SRC heeftprojectnaam)
ROLE USER MAINTAINS TOT_geevalueerd


RELATION heeftset[ICToutsourcing*Documentatie]
PRAGMA "" "heeft documentatieset ""."
MEANING "Elke ICT-outsourcing project dient tenminste één documentatieset te bevatten."
PURPOSE RELATION heeftset
{+Elke ICT-outsourcing project dient een documentatieset te hebben.+}
RULE TOT_heeftset: I|-heeftset;heeftset~
MESSAGE "De volgende ICT-outsourcing projecten hebben geen documentatieset:" VIOLATION(SRC heeftprojectnaam)
ROLE USER MAINTAINS TOT_heeftset


RELATION bevatID[ICToutsourcing*ContractID]
PRAGMA "ICT-outsourcing project "" heeft alle betreffende contracten opgenomen in contractID ""."
MEANING "Elke ICT-outsourcing project bevat tenminste één contractID."
PURPOSE RELATION bevatID
{+Elke ICT-outsourcing project dient de betreffende contracten te borgen middels een contractID.+}
RULE SUR_bevatID: I|-bevatID;bevatID~
MESSAGE "De volgende ICT-outsourcing project hebben geen contractID:" VIOLATION(SRC heeftprojectnaam)
ROLE USER MAINTAINS SUR_bevatID


RELATION heeftsoftware[ICToutsourcing*SoftwareID]
PRAGMA "Implementatieplan "" heeft alle betreffende software opgenomen in softwareID ""."
MEANING "Elke implementatieplan bevat tenminste één softwareID."
PURPOSE RELATION heeftsoftware
{+Elke implementatieplan dient de betreffende software te borgen middels een softwareID.+}
RULE SUR_heeftsoftware: I|-heeftsoftware;heeftsoftware~
MESSAGE "De volgende ICT-outsourcing projecten hebben geen softwareID:" VIOLATION(SRC heeftprojectnaam)
ROLE USER MAINTAINS SUR_heeftsoftware


--relaties om interface juist weer te geven--
RELATION heeftnaam[Projectleider*PMNaam] [UNI,TOT]
VIEW PMNaam: Projectleider(heeftnaam)
RELATION documentnaam[Implementatieplan*Documentnaam] [UNI,TOT]
VIEW Documentnaam: Implementatieplan(documentnaam)
RELATION categorie[Implementatieplan*CategorieID] [UNI]
RELATION heeftleverancier[ContractID*Leverancier] [UNI]
RELATION heeftkenmerk[ContractID*Kenmerk] [UNI,TOT]
VIEW Kenmerk: ContractID(heeftkenmerk)
RELATION ingebruik[ContractID*Afname] [UNI]
RELATION teamnaam[Transitieteam*Teamnaam] [UNI,TOT]
VIEW Teamnaam: Transitieteam(teamnaam)
RELATION heeftprojectnaam [ICToutsourcing*Projectnaam] [UNI,TOT]
VIEW Project: ICToutsourcing(heeftprojectnaam)
RELATION gebruikersnaam [Gebruiker*Gebruikersnaam] [UNI,TOT]
VIEW Gebruiker: Gebruiker(gebruikersnaam)
RELATION setnaam [Documentatie*Setnaam] [UNI,TOT]
VIEW Documentatieset: Documentatie(setnaam)
RELATION modelnaam [Acceptatiemodel*Modelnaam] [UNI,TOT]
VIEW Acceptatiemodel: Acceptatiemodel(modelnaam)
RELATION keyusernaam [KeyuserID*Keyusernaam] [UNI,TOT]
VIEW Keyuser: KeyuserID(keyusernaam)
RELATION categorienaam [CategorieID*Categorienaam] [UNI,TOT]
VIEW Categorie: CategorieID(categorienaam)
RELATION calplannaam [Calamiteitenplan*Calplannaam] [UNI,TOT]
VIEW Calamiteitenplan: Calamiteitenplan(calplannaam)
RELATION noodplannaam [Noodplan*Noodplannaam] [UNI,TOT]
VIEW Noodplan: Noodplan(noodplannaam)
RELATION calamiteitnaam [Calamiteit*Calamiteitnaam] [UNI,TOT]
VIEW Calamiteit: Calamiteit(calamiteitnaam)
RELATION softwarenaam [SoftwareID*Softwarenaam] [UNI,TOT]
VIEW Software: SoftwareID(softwarenaam)
RELATION aantalgebruikers [SoftwareID*Aantallen] [UNI]
RELATION extbeschikbaar [SoftwareID*Beschikbaar]
RELATION heeftversie [SoftwareID*Versie] [UNI]
RELATION heeftkoppeling [SoftwareID*ContractID]

RELATION einddatum[ContractID*Datum] [UNI]
MEANING "Elke ContractID heeft precies één einddatum."
REPRESENT Datum TYPE DATE
RELATION omschrijving[ContractID*Details] [UNI]
RULE TOT_einddatum: I|-einddatum;einddatum~
MESSAGE "De volgende contracten hebben geen einddatum:" VIOLATION(SRC heeftkenmerk)
ROLE USER MAINTAINS TOT_einddatum


--definieren van regel 1--
PURPOSE RULE "Teamvalidatie"
{+Elk ICT-outsourcing project dat is geëvalueerd door een transitieteam moet uitgevoerd zijn door dezelfde transitieteam die de evaluatie verzorgd.+}
RULE Teamvalidatie : geevalueerd |-onderdeelvan~;uitgevoerd
MEANING "Voor elke geëvalueerde ICT-outsourcing project geldt dat de evalutie gedaan wordt door het transitieteam die het implementatieplan heeft uitgevoerd."
VIOLATION (TXT "ICT-outsourcing plan '", SRC I;heeftprojectnaam, TXT "' kan niet worden gekoppeld aan transitieteam '", TGT I;teamnaam, TXT "' (verwacht team '", TGT I;geevalueerd~;onderdeelvan~;uitgevoerd;teamnaam, TXT "').")


--definieren van regel 2--
PURPOSE RULE "PIDvalidatie"
{+Elk implementatieplan dat is uitgevoerd door een transitieteam moet zijn geschreven door een projectleider dat gekoppeld is aan dezelfde transitieteam.+}
RULE PIDvalidatie : begeleid|- onderdeelvan~;geschrevendoor
MEANING "Voor elk ICT-outsourcing project geldt de projectleider die het project begeleid ook het plan geschreven moet hebben."
VIOLATION (TXT "Projectleider '", TGT I;heeftnaam, TXT "' kan niet worden gekoppeld aan implementatieplan '", SRC I;heeftprojectnaam, TXT "' (verwacht projectleider: '", TGT I;begeleid~;onderdeelvan~;geschrevendoor;heeftnaam, TXT "'.)")


--definieren van regel 3--
PURPOSE RULE "Documentatievalidatie"
{+Elke documentatie dat is gekoppeld aan een ICT-outsourcing projecte dient opgeleverd te zijn door het transitieteam die het implementatieplan heeft uitgevoerd.+}
RULE Documentatievalidatie : heeftset|- onderdeelvan~;uitgevoerd;opgeleverd~
MEANING "Voor elk ICT-outsourcing project geldt dat de documentatie hiervan opgeleverd is door het transitieteam die heeft deelgenomen aan het project."
VIOLATION (TXT "Documentatieset '", TGT I;setnaam, TXT "' kan niet worden gekoppeld aan ICT-outsourcing project '", SRC I;heeftprojectnaam, TXT " (verwacht een keyuser: '", SRC I;onderdeelvan~;uitgevoerd;opgeleverd~;setnaam, TXT "'.)")


--definieren van regel 4--
PURPOSE RULE "Implementatievoorwaarden"
{+Elk implementatieplan dat is uitgevoerd moet zijn beoordeeld door een keyuser en de keyuser moet hebben medegedeeld dat de klantorganisatie gereed is voor het nieuwe ICT.+}
RULE Implementatievoorwaarden : uitgevoerd |- beoordeeld;mededeling~
MEANING "Voor elk positief beoordeelde implementatieplan geldt dat een keyuser moet hebben medegedeeld dat de klantorganisatie gereed is voor het nieuwe ICT dat wordt geimplementeerd door het transitieteam."
VIOLATION (TXT "Let op: implementatieplan '", SRC I;documentnaam, TXT "' voldoet nog niet aan alle voorwaarden (verwacht een keyuser: '", SRC I;beoordeeld;keyusernaam, TXT "').")
ROLE USER MAINTAINS Implementatievoorwaarden


--definieren van regel 5--
PURPOSE RULE "Kennisvalidatie"
{+Voor elke ICT-outsourcing project geldt dat er documentatie dient te worden opgeleverd en dat er een aantal gebruiker zijn opgeleid voor gebruik van het nieuwe ICT.+}
RULE Kennisvalidatie : heeftkennis~ |- heeftset;opgeleverd;opgeleid~
MEANING "Voor elke ICT-outsourcing project geldt dat er documentatie wordt opgeleverd en dat de gebruiker wordt opgeleid voor gebruik van het nieuwe ICT."
VIOLATION (TXT "Gebruiker '", TGT I;gebruikersnaam, TXT "' is nog niet opgeleid voor gebruik van het nieuwe ICT gerealiseerd in project '", SRC I;heeftprojectnaam, TXT "' (verwacht gebruiker: '", TGT I;heeftkennis;onderdeelvan~;uitgevoerd;opgeleid~;gebruikersnaam, TXT ").")


--bouwen van interface projectleiders--
INTERFACE Projectleiders : I[SESSION] 
BOX [ "Projectleiders" : V[SESSION*Projectleider] 
	COLS [ "Naam"			: heeftnaam
	,"Begeleider van" 	: gekoppeld cRUD
		COLS ["Teamnaam"	: teamnaam]
    ,"Geschreven documenten" 	: geschrevendoor~ cRUD
		COLS [ "Imp. plannaam"	: documentnaam]
	]
]


--bouwen van interface transitieteams-- 
INTERFACE Transitieteams : I[SESSION] 
BOX [ "Transitieteam" : V[SESSION*Transitieteam] 
	COLS ["Teamnaam"		: teamnaam
	,"Projectleider" 	: gekoppeld~ cRUD
		COLS ["Naam"	: heeftnaam]
	,"Mededeling van" : mededeling cRUd
		COLS [ keyuser : keyusernaam]
	,"Implementatieplan" : uitgevoerd~ cRud
	,"Opgeleide gebruikers" : opgeleid~
		COLS [ "Naam" : gebruikersnaam ]
	,"Opgeleverde documentatie" : opgeleverd~
		COLS [ "Setnaam" : setnaam ]
	]
]


--bouwen van interface implementaties-- 
INTERFACE Implementaties : I[SESSION] 
TABS [ "Implementaties" : V[SESSION*Implementatieplan]
	COLS [ "Imp. plannaam"	: documentnaam CRUD
	,"Categorie"		: categorie
		COLS [ "Catnaam" : categorienaam ]
	,"Begeleider" : geschrevendoor cRUD
		COLS ["Projectleider"	: heeftnaam]
    ,"Uitvoerder"		: uitgevoerd cRUD
		COLS ["Transitieteam"	: teamnaam]
	,"Acceptatiemodel" : verbonden
		COLS [ "Modelnaam" : modelnaam]
	,"Beoordeeld door" : beoordeeld CRUd
		COLS [ "Keyuser" : keyusernaam]	
	,"Calamiteitenplan"		: toegekendaan cRUD
		COLS [ "Cal. plannaam" : calplannaam] 
	]
]

--bouwen van interface contractbeheer-- 
INTERFACE Contractbeheer : I[SESSION] 
TABS [ "Contractbeheer" : V[SESSION*ICToutsourcing] cRUD
	COLS ["ICT-project"	: heeftprojectnaam
	,"ContractID"		: bevatID
			COLS ["Kenmerk"		: heeftkenmerk
			,"Leverancier"		: heeftleverancier
			,"Omschrijving"		: omschrijving
			,"Ingebruik"		: ingebruik
			,"Einddatum"		: einddatum
			]
		]
]

--bouwen van interface projecten-- 
INTERFACE "ICT-outsourcing projecten"  : I[SESSION] 
TABS [ "Projecten" : V[SESSION*ICToutsourcing] 
	COLS [ "Projectnaam"	: heeftprojectnaam
	,"Heeft imp. plan" : onderdeelvan~ cRUD
		COLS [ "Imp. plannaam" : documentnaam]
	,"Geevalueerd door" : geevalueerd cRUD
		COLS [ "Teamnaam" : teamnaam]
	,"Begeleid door" : 	begeleid cRUD
		COLS [ "Projectleider" : heeftnaam]
	,"Heeft documentatieset" 	: heeftset
		COLS [ "Setnaam" : setnaam ]
	,"Getrainde gebruikers"	: heeftkennis~
		COLS [ "Naam" : gebruikersnaam ]
	]
]

--bouwen van interfac calamiteitenplannen--
INTERFACE Calamiteitenplannen : I[SESSION]
BOX [ "Calamiteitenplan" : V[SESSION*Calamiteitenplan]
	COLS [ "Calamiteitenplan" : I
			COLS [ "Cal. plannaam" : calplannaam ]
    ,"Projectleider" : opgestelddoor cRUD
			COLS [ "Naam" : heeftnaam ]
	,"Calamiteit(en)" : bevatcalamiteit
			COLS [ "Cal. naam" : calamiteitnaam
					,"Noodplan" : nood
						COLS [ "Noodplannaam" : noodplannaam]]
	,"Implementatieplan" : toegekendaan~ cRUD
			COLS [ "Imp. plannaam" : documentnaam ]]
]

--bouwen van interface softwarebeheer-- 
INTERFACE Softwarebeheer : I[SESSION] 
TABS [ "Softwarebeheer" : V[SESSION*ICToutsourcing] cRUD
	COLS ["Project"	: heeftprojectnaam
	,"SoftwareID"	: heeftsoftware
			COLS ["Softwarenaam"	: softwarenaam
			,"ContractID"		: heeftkoppeling CRUD
				COLS ["Kenmerk"	: heeftkenmerk] 
			,"Aantal gebruikers"	: aantalgebruikers
			,"Extern beschikbaar" : extbeschikbaar
			,"Versie"				: heeftversie	]]
]
ENDCONTEXT