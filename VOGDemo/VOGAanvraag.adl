CONTEXT VOG
--[VOG Aanvraagformulier]----------------
INTERFACE "Aanvraagformulier VOG (NP) (zie ook ticket #294)" (invullenDoorAanvrager, invullenDoorOrganisatie, invullenDoorGemeente) FOR Aanvrager, Organisatie, Gemeente : I[VOGAanvraag]
BOX[ "Invullen door aanvrager" : invullenDoorAanvrager
   , "Invullen door organisatie / instelling die de VOG verlangt" : invullenDoorOrganisatie
   , "Invullen door gemeente" : invullenDoorGemeente
   ]
------------------------------------------------------------
PROCESS "VOG Aanvragen"

RULE "Aanvraag invullen door aanvrager": I[VOGAanvraag] |- invullenDoorAanvrager;invullenDoorAanvrager~
ROLE Aanvrager MAINTAINS "Aanvraag invullen door aanvrager"

RULE "Aanvraag invullen door organisatie": I[VOGAanvraag] |- invullenDoorOrganisatie;invullenDoorOrganisatie~
ROLE Organisatie MAINTAINS "Aanvraag invullen door organisatie"

RULE "Aanvraag invullen door gemeente": I[VOGAanvraag] |- invullenDoorGemeente;invullenDoorGemeente~
ROLE Gemeente MAINTAINS "Aanvraag invullen door gemeente"

ENDPROCESS
------------------------------------------------------------
PATTERN "VOG Aanvraag"

CONCEPT VOGAanvraag "(verklaring omtrent het gedrag) is een verklaring van de Minister van Justitie dat uit een onderzoek met betrekking tot het gedrag van de betrokken natuurlijke persoon of rechtspersoon ingesteld, gelet op het risico voor de samenleving in verband met het doel waarvoor de afgifte is gevraagd en na afweging van het belang van betrokkene, niet is gebleken van bezwaren tegen die natuurlijke persoon of rechtspersoon" "artikel 28 WJG"

invullenDoorAanvrager   :: VOGAanvraag * InvullenDoorAanvrager   [UNI]
invullenDoorOrganisatie :: VOGAanvraag * InvullenDoorOrganisatie [UNI]
invullenDoorGemeente    :: VOGAanvraag * InvullenDoorGemeente    [UNI]

ENDPATTERN
------------------------------------------------------------
--[VOG Aanvraag - sectie 1]---------------------------------
------------------------------------------------------------
INTERFACE "1. Invullen door Aanvrager" (gegevensAanvrager, aanvragerOndertekening) FOR Aanvrager: I[InvullenDoorAanvrager]
BOX[ "1.1 Gegevens aanvrager" : gegevensAanvrager
   , "1.2 De aanvrager verklaart dit formulier volledig en naar waarheid te hebben ingevuld:" : aanvragerOndertekening
   ]
------------------------------------------------------------
PATTERN "Gegevens van Aanvrager"

gegevensAanvrager :: InvullenDoorAanvrager * GegevensAanvrager [UNI] -- aanvraagformulier 1.1
PURPOSE RELATION gegevensAanvrager REF "artikel 30 lid 1 WJG" {+-}
aanvragerOndertekening :: InvullenDoorAanvrager * Ondertekening [UNI] -- aanvraagformulier 1.2

ENDPATTERN
--[1.1]-----------------------------------------------------
INTERFACE "1.1 Gegevens Aanvrager" (gaNaam,gaGeslacht,gaTussenvoegsels,gaVoornamen,gaGeboortegemeente,gaGeboorteland,gaWoonadres,gaNationaliteit,gaBSN,gaTelefoonnummer,gaEmailadres) FOR Aanvrager : I[GegevensAanvrager]
BOX[ "(Geslachts)Naam" : gaNaam
   , "Geslacht (M/V)"  : gaGeslacht
   , "Tussenvoegsel(s)": gaTussenvoegsels
   , "Voornamen (voluit)" : gaVoornamen
   , "Geboortegemeente" : gaGeboortegemeente
   , "Geboorteland" : gaGeboorteland
   , "Woonadres (postadres)" : gaWoonadres -- evt hier een BOX-substitutie doen
   , "Nationaliteit(en)" : gaNationaliteit
   , "Bergerservicenummer" : gaBSN
   , "Telefoonnummer" : gaTelefoonnummer
   , "E-mailadres" : gaEmailadres
   ]
------------------------------------------------------------
PATTERN "Gegevens Aanvrager"

CONCEPT VOGAanvrager "de natuurlijke persoond die een VOG aanvraagt" "artikel 30 lid 1 WJG"

gaGeslacht :: GegevensAanvrager -> Geslacht [UNI]
gaNaam :: GegevensAanvrager -> Achternaam [UNI]
gaTussenvoegsels :: GegevensAanvrager * Tussenvoegsels [UNI]
gaVoornamen :: GegevensAanvrager -> Voornamen [UNI]
gaGeboortegemeente :: GegevensAanvrager -> Gemeente [UNI]
gaGeboorteland :: GegevensAanvrager -> Land [UNI]
gaWoonadres :: GegevensAanvrager -> Adres [UNI]
gaNationaliteit :: GegevensAanvrager * Nationaliteit [TOT] -- Meerdere nationaliteiten zijn mogelijk
gaBSN :: GegevensAanvrager -> BurgerServiceNummer [UNI]
gaTelefoonnummer :: GegevensAanvrager * Telefoonnummer [UNI]
gaEmailadres :: GegevensAanvrager * Emailadres [UNI]

ENDPATTERN
------------------------------------------------------------
--[VOG Aanvraag - sectie 2]---------------------------------
------------------------------------------------------------
INTERFACE "2. Invullen door organisatie / instelling die de VOG verlangt" (gegevensOrganisatieInstelling, doelVanDeAanvraag, specifiekScreeningsProfiel, algemeenScreeningsProfiel, bijzondereOmstandighedenOrganisatie, verlangerOndertekening) FOR Organisatie: I[InvullenDoorOrganisatie]
BOX[ "2.1 Gegevens organisatie / instelling" : gegevensOrganisatieInstelling
   , "2.2 Doel van de aanvraag" : doelVanDeAanvraag
   , "2.3 Specifiek screeningsprofiel" : specifiekScreeningsProfiel
   , "2.4 Algemeen screeningsprofiel" : algemeenScreeningsProfiel
   , "2.5 Bijzondere omstandigheden m.b.t. de aanvraag" : bijzondereOmstandighedenOrganisatie
   , "2.6 Ondertekening - De aanvrager verklaart dit formulier volledig en naar waarheid te hebben ingevuld:" : verlangerOndertekening
   ]
------------------------------------------------------------
PROCESS "Sectie 2. invullen"

RULE "Screeningsprofielen uitsluiten": specifiekScreeningsProfiel;specifiekScreeningsProfiel~ /\ algemeenScreeningsProfiel;algemeenScreeningsProfiel~ |- -V
MEANING "Alleen als u bij vraag 2.3 geen screeningsprofiel heeft aangekruist, kruist u bij vraag 2.4 de aspecten aan bij de functie/taak of ander doel" -- Aanvraagformulier, Toelichting bij 2.4
ROLE Organisatie MAINTAINS "Screeningsprofielen uitsluiten"

ENDPROCESS
------------------------------------------------------------
PATTERN "Invullen door Organisatie"

gegevensOrganisatieInstelling :: InvullenDoorOrganisatie * Organisatie [UNI] -- aanvraagformulier 2.1 
doelVanDeAanvraag :: InvullenDoorOrganisatie * DoelVanDeAanvraag [UNI] -- aanvraagformulier 2.2
specifiekScreeningsProfiel :: InvullenDoorOrganisatie * SpecifiekScreeningsProfiel [UNI] -- aanvraagformulier 2.3
algemeenScreeningsProfiel :: InvullenDoorOrganisatie * AlgemeenScreeningsProfiel  -- aanvraagformulier 2.4
bijzondereOmstandighedenOrganisatie :: InvullenDoorOrganisatie * BijzondereOmstandighedenOrganisatie [UNI] -- aanvraagformulier 2.5
verlangerOndertekening :: InvullenDoorOrganisatie * Ondertekening [UNI] -- aanvraagformulier 2.6

ENDPATTERN
--[2.1 Gegevens organisatie / instelling]------------------
INTERFACE "2.1 Gegevens organisatie / instelling" (organisatieNaam,organisatieVertegenwoordiger,organisatieVestigingsadres,organisatieTelefoonnummer) FOR Organisatie : I[Organisatie]
BOX[ "Naam organisatie/instelling" : organisatieNaam
   , "Naam vertegenwoordiger" : organisatieVertegenwoordiger
   , "Vestigingsadres" : organisatieVestigingsadres
   , "Telefoonnummer" : organisatieTelefoonnummer
   ]
------------------------------------------------------------
PATTERN "2.1 - Gegevens organisatie-instelling"

CONCEPT Organisatie "een organisatie" ""
organisatieNaam :: Organisatie -> OrganisatieNaam [UNI]
organisatieVertegenwoordiger :: Organisatie -> Achternaam [UNI]
organisatieVestigingsadres :: Organisatie -> Adres [UNI]
organisatieTelefoonnummer :: Organisatie -> Telefoonnummer [UNI]

ENDPATTERN
--[2.2 Doel van de aanvraag]-------------------------------
INTERFACE "2.2 Doel van de aanvraag" (werkrelatie,functie,taakomschrijving,overig,overigOmschrijving) FOR Organisatie : I[DoelVanDeAanvraag]
BOX[ "Werkrelatie (Ja/Nee)" : werkrelatie
   , "Functie (waarop de aanvraag betrekking heeft)" : functie
   , "Taakomschrijving (bij voorkeur functiebeschrijving bijvoegen" : taakomschrijving
   , "Overig (Ja/Nee)" : overig
   , "Omschrijving (bijv. Chaufferskaart, Visum en emigratie, Exploitatievergunning)" : overigOmschrijving
   ]
------------------------------------------------------------
PROCESS "Subsectie 2.2. invullen"

RULE "Integriteit functie-taakomschrijving": I /\ werkrelatie;'Ja'[JaNee];werkrelatie~ |- functie;functie~ /\ taakomschrijving;taakomschrijving~
MEANING "Als de aanvraag een werkrelatie betreft, moet de functie en taakomschrijving zijn opgegeven"
MESSAGE "U moet de functie en/of taakomschrijving opgeven"
ROLE Organisatie MAINTAINS "Integriteit functie-taakomschrijving"

RULE "Integriteit overig": I /\ overig;'Ja'[JaNee];overig~ |- overigOmschrijving;overigOmschrijving~
MEANING "Als de aanvraag geen werkrelatie (dus: overig) betreft, moet het doel zijn omschreven"
MESSAGE "U moet het doel nog invullen (bij 'Omschrijving') "
ROLE Organisatie MAINTAINS "Integriteit overig"

RULE "Werkrelatie is niet 'overig'": werkrelatie;'Ja';overig~ |- -I
MEANING "De aanvraag mag niet zowel een werkrelatie als 'overig' betreffen"
MESSAGE "U mag niet zowel 'werkrelatie' als 'overig' aankruisen (met 'Ja' beantwoorden)"
ROLE Organisatie MAINTAINS "Werkrelatie is niet 'overig'"

ENDPROCESS
------------------------------------------------------------
PATTERN "2.2 Doel van de aanvraag"

CONCEPT DoelVanDeAanvraag "het doel voor welke de organisatie een VOG wil" ""

werkrelatie :: DoelVanDeAanvraag -> JaNee [UNI]
functie :: DoelVanDeAanvraag * Functie [UNI]
taakomschrijving :: DoelVanDeAanvraag * Taakomschrijving [UNI]
overig :: DoelVanDeAanvraag -> JaNee [UNI]
overigOmschrijving :: DoelVanDeAanvraag * OverigOmschrijving [UNI]

ENDPATTERN
--[2.3 Specifiek screeningsprofiel]------------------------
PROCESS "Subsectie 2.3. invullen"

RULE "Beedigd tolk/vertaler aanvraag integriteit": I[InvullenDoorOrganisatie] /\ specifiekScreeningsProfiel;'80 Beëdigd tolken/vertalers';specifiekScreeningsProfiel~ |- gegevensOrganisatieInstelling;organisatieNaam;'Raad voor Rechtsbijstand';organisatieNaam~;gegevensOrganisatieInstelling~ --Bron: VOG Aanvraag formulier
MEANING "Het profiel 'Beëdigd tolken/vertalers' mag alleen worden gebruikt door de 'Raad voor Rechtsbijstand'"
ROLE Organisatie MAINTAINS "Beedigd tolk/vertaler aanvraag integriteit"

ENDPROCESS
-----------------------------------------------------------
PATTERN "2.3 Specifiek screeningsprofiel"

RULE "Specifieke screeningsprofielen": I[SpecifiekScreeningsProfiel] = 
    '01 Politieke ambtsdragers'
 \/ '06 Visum en emigratie'
 \/ '25 (Buitengewoon) opsporingsambtenaar'
 \/ '40 Vakantiegezinnen en adoptie'
 \/ '45 Gezondheidszorg en welzijn van mens en dier'
 \/ '50 Exploitatievergunning'
 \/ '55 Juridische dienstverlening'
 \/ '60 Onderwijs'
 \/ '65 Taxibranche; chauffeurskaart'
 \/ '70 Taxibranche; ondernemersvergunning'
 \/ '75 (Gezins)voogd bij voogdijinstellingen, reclasseringswerker, raadsonderzoeker en maatschappelijk werker'
 \/ '80 Beëdigd tolken/vertalers' --Dit screeningsprofiel kan alleen worden gebruikt als de Raad voor Rechtsbijstand de organisatie is die de VOG verlangt
 \/ '85 Lidmaatschap schietvereniging'
 \/ '95 Financiële dienstverlening'
MEANING "Een specifiek screeningsprofiel moet worden gekozen uit de voorgedefinieerde profielen"
MESSAGE "U mag alleen een voorgedefinieerd profiel kiezen"

ENDPATTERN
--[2.4 Algemeen screeningsprofiel]-------------------------
PATTERN "2.4 Algemeen screeningsprofiel"

RULE "Algemene screeningsprofielen": I[AlgemeenScreeningsProfiel] = 
-- Informatie
    '11 Bevoegdheid hebben tot het raadplegen en/of bewerken van systemen'
 \/ '12 Met gevoelige/vertrouwelijke informatie omgaan'
 \/ '13 Kennis dragen van veiligheidssystemen, controlemechanismen en verificatieprocessen'
-- Geld
 \/ '21 Met contante en/of girale gelden en/of (digitale) waardepapieren omgaan'
 \/ '22 Budgetbevoegdheid hebben'
-- Goederen
 \/ '36 Het bewaken van productieprocessen'
 \/ '37 Het beschikken over goederen'
 \/ '38 Het voorhanden hebben van stoffen, objecten en voorwerpen e.d., die bij oneigenlijk of onjuist gebruik een risico vormen voor mens (en dier)'
-- Diensten
 \/ '41 Het verlenen van diensten (advies, beveiliging, schoonmaak, catering, onderhoud, etc)'
 \/ '43 Het verlenen van diensten in de persoonlijke leefomgeving'
-- Zakelijke transacties
 \/ '53 Beslissen over offertes (het voeren van onderhandelingen en het afsluiten van contracten) en het doen van aanbestedingen'
-- Proces
 \/ '61 Het onderhouden/ombouwen/bedienen van (productie)machines en/of apparaten, voertuigen en/of luchtvaartuigen'
 \/ '62 (Rijdend) vervoer waarbij goederen, producten, post en pakketten worden getransporteerd en/of bezorgd, anders dan het intern transport binnen een bedrijf'
 \/ '63 (Rijdend) vervoer waarbij personen worden vervoerd'
-- Aansturen organisatie
 \/ '71 Personen die vanuit hun functie mensen en/of een organisatie (of een deel daarvan) aansturen'
-- Personen
 \/ '84 Belast zijn met de zorg voor minderjarigen'
 \/ '85 Belast zijn met de zorg voor (hulpbehoevende) personen, zoals ouderen en gehandicapten'
MEANING "Een algemeen screeningsprofiel mag alleen worden gekozen uit de voorgedefinieerde profielen"
MESSAGE "U mag alleen een voorgedefinieerd profiel kiezen"

ENDPATTERN
--[2.5 Bijzondere omstandigheden m.b.t. de aanvraag]-------
INTERFACE "2.5 Bijzondere omstandigheden m.b.t. de aanvraag" (erZijnBijzondereOmstandighedenOrganisatie,bijzondereOmstandighedenToelichtingOrganisatie) FOR Organisatie : I[BijzondereOmstandighedenOrganisatie]
BOX[ "Zijn er bijzondere omstandigheden (Ja/Nee)?" : erZijnBijzondereOmstandighedenOrganisatie
   , "Ja, toelichting" : bijzondereOmstandighedenToelichtingOrganisatie
   ]
------------------------------------------------------------
PROCESS "Subsectie 2.5. invullen"

RULE "Integriteit toelichting bijzondere omstandigheden": I[BijzondereOmstandighedenOrganisatie] /\ erZijnBijzondereOmstandighedenOrganisatie;'Ja'[JaNee];erZijnBijzondereOmstandighedenOrganisatie~ = I /\  bijzondereOmstandighedenToelichtingOrganisatie;bijzondereOmstandighedenToelichtingOrganisatie~
MEANING "Als is aangegeven dat er bijzondere omstandigheden zijn, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzondere omstandigheden zijn. Dat moet u nog toelichten"
ROLE Organisatie MAINTAINS "Integriteit toelichting bijzondere omstandigheden"

ENDPROCESS
------------------------------------------------------------
PATTERN "2.5 Bijzondere omstandigheden m.b.t. de aanvraag"

CONCEPT BijzondereOmstandighedenOrganisatie "een omschrijving van bijzondere omstandigheden, mits van toepassing" ""

erZijnBijzondereOmstandighedenOrganisatie :: BijzondereOmstandighedenOrganisatie * JaNee
bijzondereOmstandighedenToelichtingOrganisatie :: BijzondereOmstandighedenOrganisatie * Toelichting

ENDPATTERN
--[2.6 Ondertekening door organisatie/instelling] ---------
------------------------------------------------------------
--[VOG Aanvraag - sectie 3]---------------------------------
------------------------------------------------------------
INTERFACE "3. Invullen door gemeente" (gemeenteAanvraagnummer, bijzonderhedenGeconstateerd, persisterenInDeAanvraag, covogOmAdviesGevraagd, gemeenteOndertekening) FOR Gemeente: I[InvullenDoorGemeente]
BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
   , "Bijzonderheden geconstateerd (Ja/Nee)?" : bijzonderhedenGeconstateerd
   , "Ja, toelichting (bijzonderheden)" : bijzonderhedenGeconstateerdToelichting
   , "Persisteren in de aanvraag (Ja/Nee)?" : persisterenInDeAanvraag
   , "Ja, toelichting (persisteren)" : persisterenInDeAanvraagToelichting
   , "COVOG om advies gevraagd omtrent ontvankelijkheid (Ja/Nee)?" : covogOmAdviesGevraagd
   , "Ja, toelichting (adviesvraag)" : covogOmAdviesGevraagdToelichting
   , "Aanvraagformulier is gecontroleerd op volledigheid en de identiteit van de aanvrager is vastgesteld door" : naamGemeenteambtenaar
   , "Aanvraagformulier is gecontroleerd op volledigheid en de identiteit van de aanvrager is vastgesteld." : gemeenteOndertekening
   ]
------------------------------------------------------------
PROCESS "Sectie 3. invullen"

RULE "Integriteit toelichting bijzonderheden geconstateerd": I[InvullenDoorGemeente] /\ bijzonderhedenGeconstateerd;'Ja'[JaNee];bijzonderhedenGeconstateerd~ = I /\ bijzonderhedenGeconstateerdToelichting;bijzonderhedenGeconstateerdToelichting~
MEANING "Als er bijzonderheden zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzonderheden zijn geconstateerd. Dat moet u nog toelichten"
ROLE Organisatie MAINTAINS "Integriteit toelichting bijzonderheden geconstateerd"

RULE "Integriteit toelichting persisteren in de aanvraag": I[InvullenDoorGemeente] /\ persisterenInDeAanvraag;'Ja'[JaNee];persisterenInDeAanvraag~ = I /\ persisterenInDeAanvraagToelichting;persisterenInDeAanvraagToelichting~
MEANING "Als er bijzonderheden zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzonderheden zijn geconstateerd. Dat moet u nog toelichten"
ROLE Organisatie MAINTAINS "Integriteit toelichting persisteren in de aanvraag"

RULE "Integriteit toelichting COVOG om advies gevraagd": I[InvullenDoorGemeente] /\ covogOmAdviesGevraagd;'Ja'[JaNee];covogOmAdviesGevraagd~ = I /\  covogOmAdviesGevraagdToelichting;covogOmAdviesGevraagdToelichting~
MEANING "Als er bijzonderheden zijn geconstateerd, moet dat zijn toegelicht"
MESSAGE "U heeft aangegeven dat er bijzonderheden zijn geconstateerd. Dat moet u nog toelichten"
ROLE Organisatie MAINTAINS "Integriteit toelichting COVOG om advies gevraagd"

ENDPROCESS
------------------------------------------------------------
PATTERN "InvullenDoorGemeente"
gemeenteAanvraagnummer :: InvullenDoorGemeente -> GemeenteAanvraagnummer [UNI]
KEY "VOGaanvragen": InvullenDoorGemeente(gemeenteAanvraagnummer) --!Eigenlijk is dit een identifier voor de context van de specifieke gemeente...
bijzonderhedenGeconstateerd :: InvullenDoorGemeente -> JaNee
bijzonderhedenGeconstateerdToelichting :: InvullenDoorGemeente * BijzonderhedenGeconstateerdToelichting [UNI]
persisterenInDeAanvraag :: InvullenDoorGemeente -> JaNee
persisterenInDeAanvraagToelichting :: InvullenDoorGemeente * PersisterenInDeAanvraagToelichting [UNI]
covogOmAdviesGevraagd :: InvullenDoorGemeente -> JaNee
covogOmAdviesGevraagdToelichting :: InvullenDoorGemeente * COVOGOmAdviesGevraagdToelichting [UNI]
naamGemeenteambtenaar :: InvullenDoorGemeente -> NaamGemeenteambtenaar
gemeenteOndertekening :: InvullenDoorGemeente * Ondertekening [UNI]
ENDPATTERN
------------------------------------------------------------
--[Generieke doosjes]-----------------------
------------------------------------------------------------
INTERFACE "Ondertekening" (ondertekeningPlaats,ondertekeningDatum,ondertekeningHandtekening) FOR Aanvrager, Organisatie, Gemeente: I[Ondertekening]
BOX[ "Plaats" : ondertekeningPlaats
   , "Datum" : ondertekeningDatum
   , "Handtekening" : ondertekeningHandtekening
   ]
------------------------------------------------------------
PATTERN "Ondertekening"

CONCEPT Ondertekening "een aanhangsel aan een gegevensverzameling waaruit duidelijk wordt waar, wanneer en door wie de volledigheid en juistheid van de gegevens is geconstateerd" ""
ondertekeningPlaats :: Ondertekening -> Plaats [UNI]
ondertekeningDatum :: Ondertekening -> Datum [UNI]
ondertekeningHandtekening :: Ondertekening -> Handtekening [UNI]
ENDPATTERN
------------------------------------------------------------
INTERFACE "Adres" (adresStraat,adresHuisnummer,adresPostcode,adresPlaats,adresLand) FOR Aanvrager, Organisatie, Gemeente: I[Adres]
BOX[ "Straat" : adresStraat
   , "Huisnummer" : adresHuisnummer
   , "Postcode" : adresPostcode
   , "Plaats" : adresPlaats
   , "Land"   : adresLand
   ]
------------------------------------------------------------
PATTERN Adressen

CONCEPT Adres "een aanduiding van een geografische locatie waar post kan worden bezorgd" ""
adresStraat :: Adres -> Straatnaam [UNI]
adresHuisnummer :: Adres -> Huisnummer [UNI]
adresPostcode :: Adres -> Postcode [UNI]
adresPlaats :: Adres -> Plaats [UNI]
adresLand :: Adres * Land [UNI]
ENDPATTERN
------------------------------------------------------------
INTERFACE Datum (dd,mm,yyyy) FOR Aanvrager, Organisatie, Gemeente: I[Datum] 
BOX[ "Dag (dd)" : dd
   , "Maand (mm)" : mm
   , "Jaar (jjjj)" : yyyy
   ]
------------------------------------------------------------
PATTERN Datums

CONCEPT Datum "een aanduidig van een dag" ""
dd   :: Datum -> Dag
mm   :: Datum -> Maand
yyyy :: Datum -> Jaar
nietbestaand :: Datum * Datum [PROP]

RULE Dagen: I[Dag]=
'01' \/ '02' \/ '03' \/ '04' \/ '05' \/ '06' \/ '07' \/ '08' \/ '09' \/ '10' \/
'11' \/ '12' \/ '13' \/ '14' \/ '15' \/ '16' \/ '17' \/ '18' \/ '19' \/ '20' \/
'21' \/ '22' \/ '23' \/ '24' \/ '25' \/ '26' \/ '27' \/ '28' \/ '29' \/ '30' \/ '31'
MEANING "Een dagaanduiding bestaat uit twee cijfers van 01 t/m 31"

RULE Maanden: I[Maand]= '01' \/ '02' \/ '03' \/ '04' \/ '05' \/ '06' \/ '07' \/ '08' \/ '09' \/ '10' \/ '11' \/ '12'
MEANING "Een maandaanduiding bestaat uit twee cijfers van 01 t/m 12"

RULE Jaren: I[Jaar]= '2011' \/ '2012' \/ '2013' \/ '2014' \/ '2015' 
MEANING "Een jaaraanduiding bestaat uit vier cijfers"

RULE "Datum integriteit": I[Datum] |-
 -(    (dd;'31';dd~ /\ mm;('02' \/ '04' \/ '06' \/ '09' \/ '11');mm~)
    \/ (dd;'30';dd~ /\ mm;'02';mm~)
    \/ (dd;'29';dd~ /\ mm;'02';mm~ /\ yyyy;('2011'\/'2013'\/'2014'\/'2015');yyyy~)
  )
MEANING "Een datum moet altijd een geldige waarde hebben"

ENDPATTERN
------------------------------------------------------------
ENDCONTEXT