CONTEXT TTest IN DUTCH
--?Dit bestand is nog in de testfase. Je moet het vooralsnog STANDALONE gebruiken
--!Paspoorten, rijbewijzen en wat dies meer zij. Moet nog 'gewettigd' worden....
PATTERN "Authenticatie middelen"
--!Toetsen aan de Wet op de identificatieplicht is nog niet gebeurd.
CONCEPT Identificatiemiddel "een bewijsmiddel op basis waarvan een derde kan vaststellen dat iemand is die hij/zij zegt dat hij/zij is"

KEY "Identificatiemiddelen": Identificatiemiddel(idmDisplayName)
RELATION idmDisplayName[Identificatiemiddel*IdentificatiemiddelID] [UNI,TOT] -- wordt gebruikt om namen van mensen netjes op het scherm te zetten.

RELATION idmSoort[Identificatiemiddel*IdentificatiemiddelSoort] [UNI,TOT]
RELATION idmNummer[Identificatiemiddel*IdentificatiemiddelNummer] [UNI]
RELATION idmUserid[Identificatiemiddel*Userid] [UNI]
RELATION idmPassword[Identificatiemiddel*Password] [UNI]
RELATION idmHouder[Identificatiemiddel*BurgerServiceNummer] [UNI] -- identificeert een natuurlijk persoon in de GBA
RELATION idmOrganisatie[Identificatiemiddel*Organisatie] [UNI] -- identificeert een organisatie

RULE "Identificatie dmv idmDisplayNamedelen": (idmSoort;idmSoort~ /\ idmNummer;idmNummer~) |- I[Identificatiemiddel]
MEANING "De combinatie van identificatiemiddel soort en serienummer moet uniek identificerend zijn" -- voor fysieke idmDisplayNamedelen
MESSAGE "De combinatie van identificatiemiddel soort en serienummer moet uniek identificerend zijn" -- voor fysieke idmDisplayNamedelen

--[Paspoorten]--

RULE "Paspoorten": I[Identificatiemiddel] /\ idmSoort;'Paspoort';idmSoort~ |- idmNummer;idmNummer~ /\ idmHouder;idmHouder~
MEANING "Elk paspoort heeft een nummer en een houder, en identificeert een natuurlijk persoon"

RULE "Paspoorten identificeren natuurlijke personen": (idmSoort;'Paspoort';idmSoort~ /\ idmNummer;idmNummer~) |- I[Identificatiemiddel]
MEANING "Elk paspoort heeft een uniek nummer"

--[Rijbewijzen]--

RULE "Rijbewijzen": I[Identificatiemiddel] /\ idmSoort;'Rijbewijs';idmSoort~ |- idmNummer;idmNummer~ /\ idmHouder;idmHouder~
MEANING "Elk rijbewijs heeft een nummer en een houder, en identificeert een natuurlijk persoon"

RULE "Rijbewijzen identificeren natuurlijke personen": (idmSoort;'Rijbewijs';idmSoort~ /\ idmNummer;idmNummer~) |- I[Identificatiemiddel]
MEANING "Elk rijbewijs heeft een uniek nummer"

--[DigiDs]--
RULE "DigiDs": I[Identificatiemiddel] /\ idmSoort;'DigiD';idmSoort~ |- idmUserid;idmUserid~ /\ idmPassword;idmPassword~ /\ idmHouder;idmHouder~
MEANING "Elk DigiD heeft een gebruikersnaam en wachtwoord, en identificeert een natuurlijk persoon"

RULE "DigiDs identificeren natuurlijke personen": (idmSoort;'DigiD';idmSoort~ /\ idmUserid;idmUserid~ /\ idmPassword;idmPassword~) |- I[Identificatiemiddel]
MEANING "Elk DigiD heeft een unieke combinatie van gebruikersnaam en wachtwoord"

--[E-Herkenning voor Bedrijven]--
RULE "EHvBs": I[Identificatiemiddel] /\ idmSoort;'EHvB';idmSoort~ |- idmUserid;idmUserid~ /\ idmPassword;idmPassword~ /\ idmOrganisatie;idmOrganisatie~
MEANING "Elk EHvB heeft een gebruikersnaam en wachtwoord, en identificeert een organisatie"

RULE "EHvBs identificeren ortanisaties": (idmSoort;'EHvB';idmSoort~ /\ idmUserid;idmUserid~ /\ idmPassword;idmPassword~) |- I[Identificatiemiddel]
MEANING "Elk EHvB heeft een unieke combinatie van gebruikersnaam en wachtwoord"

--[GemeenteIDs]--

RELATION idmGemeente[Identificatiemiddel*Gemeente] [UNI] -- identificeert een gemeenteambtenaar
RELATION idmGemeentePlaats[Identificatiemiddel*Plaats] [UNI] -- Plaats waar het gemeentehuis is gevestigd

RULE "GemeenteIDs": I[Identificatiemiddel] /\ idmSoort;('GemeenteID');idmSoort~ |- idmUserid;idmUserid~ /\ idmPassword;idmPassword~ /\ idmGemeente;idmGemeente~
MEANING "Elk GemeenteID heeft een gebruikersnaam en wachtwoord, en identificeert een gemeente"

RULE "GemeenteIDs identificeren ortanisaties": (idmSoort;'GemeenteID';idmSoort~ /\ idmUserid;idmUserid~ /\ idmPassword;idmPassword~) |- I[Identificatiemiddel]
MEANING "Elk GemeenteID heeft een unieke combinatie van gebruikersnaam en wachtwoord"

ENDPATTERN
-----------------------------------------------------------
PATTERN SessionPortals

RELATION portalAuthenticationMeans[PortaalFocus*IdentificatiemiddelSoort] [UNI,TOT] 
PRAGMA "On in te loggen op " " moet een " " worden gebruikt"
=[ ("Organisatie", "EHvB")
 ; ("Betrokkene (aanvrager)", "DigiD")
 ; ("Gemeente", "GemeenteID")
 ]

RELATION sessionPortal[SESSION*PortaalFocus] [UNI]
RELATION sessionUserid[SESSION*Userid] [UNI]
RELATION sessionPassword[SESSION*Password] [UNI]

RULE "Login": '_SESSION'[SESSION];(sessionPortal;portalAuthenticationMeans;idmSoort~ /\ sessionUserid;idmUserid~) |- '_SESSION';sessionPassword;idmPassword~
MEANING "Bij inloggen moet een geldige (Userid,Password) combinatie worden opgegeven."
MESSAGE "Ongeldig wachtwoord"

RULE "Session login integrity": I[SESSION] /\ sessionUserid;sessionUserid~ /\ sessionPassword;sessionPassword~
|- (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~ /\ sessionPortal;portalAuthenticationMeans;idmSoort~);V[SESSION*SESSION]
MEANING "Je mag alleen inloggen met een voor het portaal geldig account"
MESSAGE "Je mag alleen inloggen met een voor het portaal geldig account"

ENDPATTERN
-----------------------------------------------------------
--[Interfaces]------------------------------

-- INTERFACE die je moet gebruiken om nieuwe Sessions mee te maken:
INTERFACE OVERZICHT : I[ONE]
BOX[ "Mijn sessie": V[ONE*SESSION];'_SESSION'[SESSION]
   , "Sessies"  : V[ONE*SESSION]
   BOX[ "Sessie"  : I[SESSION]
      , "Portaal" : sessionPortal
      , "Portaalfocus" : sessionPortal;portalAuthenticationMeans
      , "Userid"  : sessionUserid
      , "Password": sessionPassword
      ]
   , "Identificatiemiddelen" : V[ONE*Identificatiemiddel]
   BOX[ "idmDisplayName" : idmDisplayName
      , "idmSoort" : idmSoort
      , "idmNummer" : idmNummer
      , "idmUserid" : idmUserid
      , "idmPassword" : idmPassword
      , "idmHouder" : idmHouder
      , "idmOrganisatie" : idmOrganisatie
      , "idmGemeente" : idmGemeente
      , "idmGemeentePlaats" : idmGemeentePlaats
      ]
   ]

INTERFACE "EloVOG Portaal" (sessionPortal) FOR Organisatie, Betrokkene, Gemeente : V[ONE*SESSION];'_SESSION'[SESSION]
BOX[ "Bent (of vertegenwoordigt) u (a) een organisatie, (b) een betrokkene (aanvrager) of (c) een gemeente?" : sessionPortal
   ]
-----------------------------------------------------------
PROCESS "Role definitions"

RULE "Role definitions": V[SESSION*SESSION]
ROLE Organisatie, Betrokkene, Gemeente MAINTAINS "Role definitions"
ENDPROCESS
-----------------------------------------------------------
INTERFACE "EloVOG (Organisatie)" (sessionUserid,sessionPassword) FOR Organisatie:
  V[ONE*SESSION];'_SESSION'[SESSION];(I[SESSION] /\ sessionPortal;'Organisatie';sessionPortal~)
BOX[ "U moet inloggen d.m.v.: " : sessionPortal;portalAuthenticationMeans 
   , "Gebruikersnaam"           : sessionUserid
   , "Wachtwoord"               : sessionPassword
   , "u bent ingelogd als"      : (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~ /\ sessionPortal;portalAuthenticationMeans;idmSoort~);idmDisplayName
   , "Organisatie"              :  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~ /\ sessionPortal;portalAuthenticationMeans;idmSoort~);idmOrganisatie
   ]

INTERFACE "EloVOG (Betrokkene)" (sessionUserid,sessionPassword) FOR Betrokkene:
  V[ONE*SESSION];'_SESSION'[SESSION];(I[SESSION] /\ sessionPortal;'Betrokkene (aanvrager)';sessionPortal~)
BOX[ "U moet inloggen d.m.v.: " : sessionPortal;portalAuthenticationMeans 
   , "Gebruikersnaam"           : sessionUserid
   , "Wachtwoord"               : sessionPassword
   , "u bent ingelogd als"      : (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~ /\ sessionPortal;portalAuthenticationMeans;idmSoort~);idmDisplayName
   , "Houder"                   : (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~ /\ sessionPortal;portalAuthenticationMeans;idmSoort~);idmHouder
   ]

INTERFACE "EloVOG (Gemeente)" (sessionUserid,sessionPassword) FOR Gemeente:
  V[ONE*SESSION];'_SESSION'[SESSION];(I[SESSION] /\ sessionPortal;'Gemeente';sessionPortal~)
BOX[ "U moet inloggen d.m.v.: " : sessionPortal;portalAuthenticationMeans 
   , "Gebruikersnaam"           : sessionUserid
   , "Wachtwoord"               : sessionPassword
   , "u bent ingelogd als"      : (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~ /\ sessionPortal;portalAuthenticationMeans;idmSoort~);idmDisplayName
   , "Gemeente"                 :  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~ /\ sessionPortal;portalAuthenticationMeans;idmSoort~);idmGemeente
   ]

-----------------------------------------------------------
{-
POPULATION idmDisplayName    CONTAINS [ ("Ident_0", "IdentificatiemiddelID") ] -- 'printable' naam.
POPULATION idmSoort          CONTAINS [ ("Ident_0", "IdentificatiemiddelSoort") ]
POPULATION idmNummer         CONTAINS [ ("Ident_0", "IdentificatiemiddelNummer") ]
POPULATION idmHouder         CONTAINS [ ("Ident_0", "BurgerServiceNummer") ] -- verplicht voor type 'DigiD'
POPULATION idmOrganisatie    CONTAINS [ ("Ident_0", "Organisatie") ] -- verplicht voor type 'EHvB'
POPULATION idmGemeente       CONTAINS [ ("GemID_0", "Gemeente") ] -- verplicht voor type 'GemeenteID'
POPULATION idmGemeentePlaats CONTAINS [ ("GemID_0", "Plaats") ] -- Plaats waar het gemeentehuis is gevestigd
POPULATION idmUserid         CONTAINS [ ("Ident_0", "Userid") ]
POPULATION idmPassword       CONTAINS [ ("Ident_0", "Password") ]
-}

POPULATION idmDisplayName    CONTAINS [ ("DigiD_1", "Dhr. K.L. Jansen") ]
POPULATION idmSoort          CONTAINS [ ("DigiD_1", "DigiD") ]
POPULATION idmNummer         CONTAINS [ ("DigiD_1", "0123456789") ]
POPULATION idmHouder         CONTAINS [ ("DigiD_1", "204958374") ]
POPULATION idmUserid         CONTAINS [ ("DigiD_1", "kljansen") ]
POPULATION idmPassword       CONTAINS [ ("DigiD_1", "*****") ]

POPULATION idmDisplayName    CONTAINS [ ("EHvB_1", "ACME B.V.") ]
POPULATION idmSoort          CONTAINS [ ("EHvB_1", "EHvB") ]
POPULATION idmNummer         CONTAINS [ ("EHvB_1", "87654321") ]
POPULATION idmOrganisatie    CONTAINS [ ("EHvB_1", "Org_1") ]
POPULATION idmUserid         CONTAINS [ ("EHvB_1", "acmebv") ]
POPULATION idmPassword       CONTAINS [ ("EHvB_1", "******") ]

POPULATION idmDisplayName    CONTAINS [ ("EHvB_2", "BuitelBank B.V.") ]
POPULATION idmSoort          CONTAINS [ ("EHvB_2", "EHvB") ]
POPULATION idmNummer         CONTAINS [ ("EHvB_2", "12345678") ]
POPULATION idmOrganisatie    CONTAINS [ ("EHvB_2", "Org_2") ]
POPULATION idmUserid         CONTAINS [ ("EHvB_2", "buitelbank") ]
POPULATION idmPassword       CONTAINS [ ("EHvB_2", "******") ]

POPULATION idmDisplayName    CONTAINS [ ("Rijbewijs_RDW678214", "Dhr. K.L. Jansen") ]
POPULATION idmSoort          CONTAINS [ ("Rijbewijs_RDW678214", "Rijbewijs") ]
POPULATION idmNummer         CONTAINS [ ("Rijbewijs_RDW678214", "RDW678214") ]
POPULATION idmHouder         CONTAINS [ ("Rijbewijs_RDW678214", "204958374") ]

POPULATION idmDisplayName    CONTAINS [ ("P_453027177", "Dhr. A.M. van der Putten")  ]
POPULATION idmSoort          CONTAINS [ ("P_453027177", "Paspoort")  ]
POPULATION idmNummer         CONTAINS [ ("P_453027177", "P453027177")]
POPULATION idmHouder         CONTAINS [ ("P_453027177", "389385475") ]

POPULATION idmDisplayName    CONTAINS [ ("GemID_1", "P. Puk") ] -- 'printable' naam.
POPULATION idmSoort          CONTAINS [ ("GemID_1", "GemeenteID") ]
POPULATION idmNummer         CONTAINS [ ("GemID_1", "GID_Veere_001") ]
POPULATION idmGemeente       CONTAINS [ ("GemID_1", "Veere") ]
POPULATION idmGemeentePlaats CONTAINS [ ("GemID_1", "Veere") ] -- Plaats waar het gemeentehuis is gevestigd
POPULATION idmUserid       CONTAINS [ ("GemID_1", "ppuk") ]
POPULATION idmPassword       CONTAINS [ ("GemID_1", "*****") ]

POPULATION idmDisplayName    CONTAINS [ ("GemID_2", "N. Oordenveld") ] -- 'printable' naam.
POPULATION idmSoort          CONTAINS [ ("GemID_2", "GemeenteID") ]
POPULATION idmNummer         CONTAINS [ ("GemID_2", "GID_Noordenveld_001") ]
POPULATION idmGemeente       CONTAINS [ ("GemID_2", "Noordenveld") ]
POPULATION idmGemeentePlaats CONTAINS [ ("GemID_2", "Roden") ] -- Plaats waar het gemeentehuis is gevestigd
POPULATION idmUserid       CONTAINS [ ("GemID_2", "noordenveld") ]
POPULATION idmPassword       CONTAINS [ ("GemID_2", "*****") ]
-----------------------------------------------------------
ENDCONTEXT
