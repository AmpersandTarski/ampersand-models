CONTEXT COVOG
------------------------------------------------------------
--[Beslissen over een VOG aanvraag]-------------------------
{-Naast de Wjg en het Bjg is een en ander gebaseerd op de BeleidsregelBeleidsregels 2008 voor het beoordelen van aanvragen ter verkrijging van een verklaring omtrent het gedrag van natuurlijke personen en rechtspersonen [Beleidsregels VOG-NP-RP 2008] 
Het proces loopt grofweg als volgt. Nadat is vastgesteld dat er een vergoeding (leges) zijn betaald voor het behandelen van de VOG aanvraag (en dat de aanvraag volledig is en de identiteit is vastgesteld), moet worden besloten of de aanvraag ontvankelijk is (zie beleidsregels). Mocht dat zo zijn, dan volgt een onderzoek in de justitiele documentatie (zie beleidsregels), op basis waarvan wordt besloten tot het al dan niet afgeven van een VOG.
-}
------------------------------------------------------------
INTERFACE "Overzicht (COVOG)" FOR COVOG : I[ONE]
BOX[ "In behandeling te nemen aanvragen" : V[ONE*VOGAanvraag];
      (I /\ (  ({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~
            \/ gemVOGAanvraag~;({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~;gemVOGAanvraag
            )
         /\ -(covogZaakNummer;covogZaakNummer~)
      )
   , "Ontvankelijkheid van de aanvraag toetsen c.q. vaststellen": V[ONE*VOGAanvraag];
      (I /\ (  (({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~)
            \/ (gemVOGAanvraag~;({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~;gemVOGAanvraag)
            )
         /\ (  (zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~)
            \/ (gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag)
            )
         /\ -(isAanvraagOntvankelijk;isAanvraagOntvankelijk~)
      );covogZaakNummer
   , "Te beoordelen aanvragen" : V[ONE*VOGAanvraag];
      (I /\ (isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~) -- preconditie moet gelden
         /\ -(vogAfgifteBesluit;vogAfgifteBesluit~) -- postconditie mag niet gelden
      );covogZaakNummer
   , "Aanvragen waarop een VOG is afgegeven" : V[ONE*VOGAanvraag];
      (I /\ (vogAfgifteBesluit;'Ja';vogAfgifteBesluit~));covogZaakNummer
   , "Aanvragen waarop geen VOG is afgegeven": V[ONE*VOGAanvraag];
      (I /\ (vogAfgifteBesluit;'Nee';vogAfgifteBesluit~));covogZaakNummer
   , "Aanvragen die zijn uitgevallen" : V[ONE*VOGAanvraag];
      (I /\ (
              (({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~)
             \/
              (gemVOGAanvraag~;({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~;gemVOGAanvraag)
            )
         /\ (
              (zijnDeLegesBetaald;'Nee';zijnDeLegesBetaald~)
             \/
              (isAanvraagOntvankelijk;'Nee';isAanvraagOntvankelijk~)
            )
      );covogZaakNummer
   ]
------------------------------------------------------------
--[Aanvraag in behandeling nemen]--
INTERFACE "Aanvraag in behandeling nemen" (covogZaakNummer) FOR COVOG : I[VOGAanvraag];
      (I /\ (  ({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~
            \/ gemVOGAanvraag~;({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~;gemVOGAanvraag
            )
         /\ -(covogZaakNummer;covogZaakNummer~)
      )
BOX[ "COVOG zaaknummer" : covogZaakNummer
   , "Aanvrager" : ((vogAanvragerUserid;idmUserid~ /\ vogAanvragerPassword;idmPassword~) \/ (vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmDisplayName
   , "Leges zijn betaald door aanvrager" : zijnDeLegesBetaald
   , "Leges zijn betaald via de gemeente" : gemVOGAanvraag~;gemLegesBetaald
   ]
------------------------------------------------------------
PROCESS "Aanvraag in behandeling nemen"

RULE "Vergoeding voor het behandelen van een VOG aanvraag": I[VOGAanvraag] /\
   (  (({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~)
   \/ (gemVOGAanvraag~;({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~;gemVOGAanvraag)
   )
 |-  gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag
   \/ zijnDeLegesBetaald;zijnDeLegesBetaald~ -- hier geen 'Ja' tussen zetten. Als er 'Nee' komt, dan verdwijnen deze zaken naar 'uitgevallen zaken'.
MEANING "Voor elke VOGAanvraag zijn leges verschuldigd"
MESSAGE "De aanvraag kan pas in behandeling worden genomen als de verschuldigde leges zijn ontvangen"
ROLE COVOG MAINTAINS "Vergoeding voor het behandelen van een VOG aanvraag"
PURPOSE RULE "Vergoeding voor het behandelen van een VOG aanvraag" REF "artikel 36 tweede lid Bjg"
{+Onze Minister neemt een aanvraag tot afgifte van een verklaring omtrent het gedrag eerst in behandeling nadat de bij ministeriële regeling vastgestelde vergoeding voor de kosten van deze behandeling is ontvangen (artikel 36 tweede lid Bjg)-}

RULE "COVOG Zaaknummer toekennen": I[VOGAanvraag]
/\ (  (({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~)
   \/ (gemVOGAanvraag~;({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~;gemVOGAanvraag)
   )
/\ ( gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag
   \/ zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~
   )
|- covogZaakNummer;covogZaakNummer~
MEANING "Voor elke VOG Aanvraag die in behandeling genomen kan worden, moet een zaaknummer zijn toegekend"
MESSAGE "U moet een zaaknummer toekennen aan de VOG Aanvraag"
ROLE COVOG MAINTAINS "COVOG Zaaknummer toekennen"

ENDPROCESS
------------------------------------------------------------
--[Paragraaf 2. Ontvankelijkheid van de aanvraag]--
{- Samenvatting: Het COVOG toetst of een aanvraag voor een VOG ontvankelijk is. Hierbij wordt getoetst of een onderzoek naar het gedrag van de aanvrager noodzakelijk is om, gelet op het doel van de aanvraag, een risico voor de samenleving te beperken.-}
------------------------------------------------------------
INTERFACE "Ontvankelijkheid van de aanvraag toetsen c.q. vaststellen" 
(covogZaakNummer,
 isVOGWettelijkVerplicht,
 isAanvraagVereistDoorBuitenlandseWet,
 isAanvraagNoodzakelijkOmRisicoVoorSamenlevingTeBeperken,
 isErEenAanwijsbareBelanghebbendeDerde,
 zijnDeTeBeschermenBelangenVanPriveAard,
 isAanvraagOntvankelijk
) FOR COVOG : I[COVOGZaakNummer];covogZaakNummer~;
      (I /\ (
              (({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~)
             \/
              (gemVOGAanvraag~;({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);({-gemAmbtenaarHandtekening-}(gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)~;gemVOGAanvraag)
            )
         /\ ( gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag
              \/ zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~
            )
         /\ -(isAanvraagOntvankelijk;isAanvraagOntvankelijk~)
      )
BOX[ "COVOG zaaknummer" : covogZaakNummer
-- Identiteit is ofwel middels DigiD, ofwel door de gemeente vastgesteld 
-- Toets op ontvankelijkheid:
   , "Is de VOG wettelijk verplicht?" : isVOGWettelijkVerplicht
-- De toets op een werkrelatie wordt automatisch gedaan, de rest doen we 'met de hand'.
   , "Is een verklaring vereist door de toepasselijkheid van een buitenlandse wet?" : isAanvraagVereistDoorBuitenlandseWet
   , "Is een verklaring noodzakelijk om, gelet op het doel van de aanvraag, een risico voor de samenleving te beperken?" : isAanvraagNoodzakelijkOmRisicoVoorSamenlevingTeBeperken
   , "Is er een aanwijsbare belanghebbende derde?" : isErEenAanwijsbareBelanghebbendeDerde
   , "Zijn de te beschermen belangen van privé-aard?" : zijnDeTeBeschermenBelangenVanPriveAard
-- Toetsen moeten leiden tot een al dan niet ontvankelijk verklaren van de aanvraag
   , "Is, gezien het voorafgaande, de aanvraag ontvankelijk?" : isAanvraagOntvankelijk
   ]
------------------------------------------------------------
PROCESS "Ontvankelijkheid van de aanvraag toetsen" --Paragraaf 2 [Beleidsregels VOG-NP-RP 2008] 

RULE "Ontvankelijkheid vaststellen": covogZaakNummer~;
   (  I[VOGAanvraag]
   /\ zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~
   /\ gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag
   )
 |- covogZaakNummer~;(I /\ isAanvraagOntvankelijk;isAanvraagOntvankelijk~)
MEANING "Van elke aanvraag moet de ontvankelijkheid zijn vastgesteld"
MESSAGE "De ontvankelijkheid van de aanvraag moet worden vastgesteld"
ROLE COVOG MAINTAINS "Ontvankelijkheid vaststellen"

RULE "Ontvankelijkheid op basis van wettelijke verplichting": 
   I /\ isVOGWettelijkVerplicht;'Ja';isVOGWettelijkVerplicht~
|- isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~ 
MEANING "De aanvraag voor een VOG is ontvankelijk wanneer de VOG wettelijk verplicht is"
MESSAGE "De aanvraag is ontvankelijk op basis van een wettelijke verplichting"
PURPOSE RULE "Ontvankelijkheid op basis van wettelijke verplichting" REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+De aanvraag voor een VOG is ontvankelijk wanneer de VOG wettelijk verplicht is-} 
ROLE COVOG MAINTAINS "Ontvankelijkheid op basis van wettelijke verplichting"

RULE "Ontvankelijkheid op basis van werkrelatie": covogZaakNummer~;
(I[VOGAanvraag]
   /\ ( gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag
      \/ zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~
      )
   /\ vogAanvraagOrganisatieTemplate;werkrelatie;werkrelatie~;vogAanvraagOrganisatieTemplate~
)
|- covogZaakNummer~;(I /\ isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~)
MEANING "Elke aanvraag voor het aangaan dan wel bestendigen van een werkrelatie is ontvankelijk"
MESSAGE "De aanvraag is ontvankelijk op grond van het aangaan dan wel bestendigen van een werkrelatie"
PURPOSE RULE "Ontvankelijkheid op basis van werkrelatie" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+de aanvraag voor een VOG-NP is in elk geval ontvankelijk wanneer deze wordt aangevraagd voor het aangaan dan wel bestendigen van een werkrelatie. Onder een werkrelatie wordt mede verstaan het verrichten van werkzaamheden voor een vereniging of een vrijwilligersorganisatie. (**Dit laatste is niet meegemodelleerd**)-}
ROLE COVOG MAINTAINS "Ontvankelijkheid op basis van werkrelatie"

RULE "Ontvankelijkheid op basis van een buitenlandse wet": 
covogZaakNummer~;(I /\ isAanvraagVereistDoorBuitenlandseWet;'Ja';isAanvraagVereistDoorBuitenlandseWet~)
|- covogZaakNummer~;(I /\ isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~)

PURPOSE RULE "Ontvankelijkheid op basis van een buitenlandse wet" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+Indien een aanvraag ziet op een doel en/of belanghebbende derde in het buitenland, wordt een aanvraag voor een VOG-NP of VOG-RP alleen dan in behandeling genomen indien de verklaring in een buitenlandse wet als vereiste is opgenomen, ten behoeve van een in die wet duidelijk omschreven doel. De aanvrager dient dit wettelijke vereiste aan te tonen en indien mogelijk het in het betreffende land gehanteerde screeningsprofiel bij te voegen. Indien er geen buitenlands screeningsprofiel bestaat wordt aangesloten bij een vergelijkbaar Nederlands screeningsprofiel.-}
ROLE COVOG MAINTAINS "Ontvankelijkheid op basis van een buitenlandse wet"

RULE "Niet-ontvankelijkheid door gebrek aan noodzaak":
covogZaakNummer~;(I /\ isAanvraagNoodzakelijkOmRisicoVoorSamenlevingTeBeperken;'Nee';isAanvraagNoodzakelijkOmRisicoVoorSamenlevingTeBeperken~)
|- covogZaakNummer~;(I /\  isAanvraagOntvankelijk;'Nee';isAanvraagOntvankelijk~)
PURPOSE RULE "Niet-ontvankelijkheid door gebrek aan noodzaak" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+Een aanvraag voor een VOG-NP of VOG-RP is niet ontvankelijk wanneer een onderzoek naar het gedrag van de aanvrager kennelijk niet noodzakelijk is om, gelet op het doel van de aanvraag, een risico voor de samenleving te beperken;-}
ROLE COVOG MAINTAINS "Niet-ontvankelijkheid door gebrek aan noodzaak"

RULE "Niet-ontvankelijkheid door gebrek aan belanghebbende derde":
covogZaakNummer~;(I /\ isErEenAanwijsbareBelanghebbendeDerde;'Nee';isErEenAanwijsbareBelanghebbendeDerde~)
|-covogZaakNummer~;(I /\  isAanvraagOntvankelijk;'Nee';isAanvraagOntvankelijk~)
PURPOSE RULE "Niet-ontvankelijkheid door gebrek aan belanghebbende derde" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+Een aanvraag voor een VOG-NP of VOG-RP is niet ontvankelijk wanneer er geen aanwijsbare belanghebbende derde is-}
ROLE COVOG MAINTAINS "Niet-ontvankelijkheid door gebrek aan belanghebbende derde"

RULE "Niet-ontvankelijkheid omdat belangen van privé-aard zijn":
covogZaakNummer~;(I /\ zijnDeTeBeschermenBelangenVanPriveAard;'Ja';zijnDeTeBeschermenBelangenVanPriveAard~)
|- covogZaakNummer~;(I /\ isAanvraagOntvankelijk;'Nee';isAanvraagOntvankelijk~)
PURPOSE RULE "Niet-ontvankelijkheid omdat belangen van privé-aard zijn" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+Een aanvraag voor een VOG-NP of VOG-RP is niet ontvankelijk wanneer de te beschermen belangen van privé-aard zijn-}
ROLE COVOG MAINTAINS "Niet-ontvankelijkheid omdat belangen van privé-aard zijn"

ENDPROCESS
------------------------------------------------------------
--[Paragraaf 3. Beoordeling van de aanvraag]--
{-Samenvatting: Bij de beoordeling van de aanvraag wordt in beginsel gekeken naar de justitiële gegevens die zijn opgenomen in de justitiële documentatie in de voor het doel van de aanvraag relevante termijn.
Aan de aanvrager die in het geheel niet voorkomt in de justitiële documentatie wordt zonder meer een VOG afgegeven.
Wanneer de aanvrager wel voorkomt in de justitiële documentatie wordt de vraag of een VOG kan worden afgegeven beoordeeld aan de hand van een objectief criterium en een subjectief criterium.-}
------------------------------------------------------------
INTERFACE "Te beoordelen aanvraag"
(vogAfgifteBesluit
) FOR COVOG : I[COVOGZaakNummer];covogZaakNummer~;
      (I /\ (isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~) -- preconditie moet gelden
         /\ -(vogAfgifteBesluit;vogAfgifteBesluit~) -- postconditie mag niet gelden
      )
BOX[ "VOG Zaaknummer" : covogZaakNummer
   , "Wordt de VOG afgegeven?" : vogAfgifteBesluit
   ]
------------------------------------------------------------
PROCESS "Te beoordelen aanvragen"

RULE "Aanvraag beoordelen integriteit": I /\ vogAfgifteBesluit;vogAfgifteBesluit~ |- isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~
MEANING "Een aanvraag mag alleen worden beoordeeld als deze ontvankelijk is."
MESSAGE "U mag de aanvraag niet beoordelen omdat niet is vastgesteld dat deze ontvankelijk is."

ENDPROCESS
------------------------------------------------------------
INTERFACE "VOG Aanvraag (Aanvrager deel)" FOR COVOG : I[COVOGZaakNummer];covogZaakNummer~
BOX[ "VOG Zaaknummer" : covogZaakNummer
   , "Contactgegevens": I
   BOX[ "Telefoonnummer" : aanvragerTelefoonnummer
      , "E-mailadres" : aanvragerEmailAddress
      ]
   , "1 Gegevens Aanvrager": {-vogBSNAanvrager-}(((vogAanvragerUserid;idmUserid~ /\ vogAanvragerPassword;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~)
   BOX[ "Geslacht (M/V)"  : gbaGeslacht
      , "(Geslachts)Naam" : gbaGeslachtsnaam
   -- , "Tussenvoegsel(s)": gbaTussenvoegsels
      , "Voornamen" : gbaVoornamen -- art 32 eerste lid Wjg
      , "Geboortedatum" : gbaGeboortedatum -- art 32 eerste lid Wjg
      , "Geboortegemeente" : gbaGeboorteplaats
      , "Geboorteland" : gbaGeboorteland
      , "Woonadres (postadres)" : I -- adresHaakje~
      BOX[ "Straat" : gbaStraatnaam
         , "Huisnummer" : gbaHuisnummer
         , "Postcode" : gbaPostcode
         , "Plaats" : gbaGemeentedeel
         , "Land" : V;'Nederland'[Land]
         ]
      , " " : I
      BOX[ "Nationaliteit(en)" : gbaNationaliteit
        , "Burgerservicenummer" : gbaBSN
      ]  ]
   , "2 - Ondertekening door aanvrager" : I -- ondertekeningHaakje~
   BOX[ "De aanvrager verklaart dit formulier volledig en naar waarheid te hebben ingevuld" : I
      BOX[ "Plaats" : aanvragerOndertekeningPlaats
         , "Datum" : aanvragerOndertekeningDatum
         , "Handtekening" : ({-aanvragerHandtekening-}(aanvragerDigitaleHandtekening;idmDigitaleHandtekening~ /\ aanvragerPasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)
      ]  ]
   ]
-----------------------------------------------------------
INTERFACE "VOG Aanvraag (Organisatie deel)" FOR COVOG :
  I[COVOGZaakNummer];covogZaakNummer~;vogAanvraagOrganisatieTemplate
BOX[ "2. VOG Aanvraag (Organisatie template)" : I 
   , "2.1 - Gegevens organisatie-instelling" : {-organisatieOrganisatie-}
          ( ( (organisatieUserid;idmUserid~ /\ organisatiePassword;idmPassword~)
              ;(I /\ idmSoort;'EHvB';idmSoort~) 
            /\ ({-organisatieHandtekening-}(organisatieDigitaleHandtekening;idmDigitaleHandtekening~ /\ organisatiePasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening);idmDisplayHandtekening~
            );idmOrganisatie
          )
   BOX[ "Naam organisatie/instelling" : hrOrganisatieNaam
      , "Vestigingsadres" : hrVestigingsAdres
      ]
   , "Contactpersoon" : I
   BOX[ "Naam vertegenwoordiger" : {-organisatieVertegenwoordiger-}
          ( (organisatieUserid;idmUserid~ /\ organisatiePassword;idmPassword~)
            ;(I /\ idmSoort;'EHvB';idmSoort~);idmDisplayName
          )
      , "Telefoonnummer" : orgTelefoonnummer
      ]
   , "2.2 - Doel van de aanvraag" : I
   BOX[ "Werkrelatie (Ja/Nee)" : werkrelatie
      , "Functie (waarop de aanvraag betrekking heeft)" : functie
      , "Taakomschrijving (bij voorkeur functiebeschrijving bijvoegen)" : taakomschrijving
      , "Overig (Ja/Nee)" : overig
      , "Omschrijving (bijv. Chaufferskaart, Visum en emigratie, Exploitatievergunning)" : overigOmschrijving
      ]
   , "2.3 - Specifiek screeningsprofiel" : I
   BOX[ "Heeft de aanvraag betrekking op één van de hieronder genoemde functies of doelen?" : specifiekScreeningsProfielRelevant
      , "Ja, namelijk op: (u kunt slechts één vakje aankruisen!)" : specifiekScreeningsProfiel
      ]
   , "2.4 - Algemeen screeningsprofiel" : I
   BOX[ " ": algemeenScreeningsProfiel
      ]
   , "2.5 - Bijzondere omstandigheden m.b.t. de aanvraag" : I
   BOX[ "Zijn er bijzondere omstandigheden (Ja/Nee)?" : orgErZijnBijzondereOmstandigheden
      , "Ja, toelichting" : orgErZijnBijzondereOmstandighedenToelichting
      ]
   , "2.6 - Ondertekening door organisatie/instelling" : I
   BOX[ "Plaats" : orgOndertekeningPlaats
      , "Datum" : orgOndertekeningDatum
      , "Handtekening" : ({-organisatieHandtekening-}(organisatieDigitaleHandtekening;idmDigitaleHandtekening~ /\ organisatiePasswordHandtekening;idmPasswordHandtekening~);idmDisplayHandtekening)
      ]
   ]
-----------------------------------------------------------
INTERFACE "VOG aanvraag (Gemeentelijk deel)" FOR COVOG :
  I[COVOGZaakNummer];covogZaakNummer~;gemVOGAanvraag~
BOX[ "COVOG Zaaknummer" : gemVOGAanvraag;covogZaakNummer
   , "Gemeentelijk Aanvraagnummer" : gemeenteAanvraagnummer
   , "VOG Aanvraag" : gemVOGAanvraag
   BOX[ "Doel van de aanvraag" : vogAanvraagOrganisatieTemplate
      , "Soort identiteitsbewijs (paspoort, rijbewijs)" : vogAanvragerIDMSoort
      , "Serienummer" : vogAanvragerIDMNummer
      , "Komen deze gegevens overeen met de persoon die u voor zich heeft?" : vogAanvragerIDVastgesteld
      , "DigiD" : vogAanvragerUserid
      , "Wachtwoord" : vogAanvragerPassword
      , "GBA gegevens van aanvrager" : 
             {-vogBSNAanvrager-}(((vogAanvragerUserid;idmUserid~ /\ vogAanvragerPassword;idmPassword~);(I /\ idmSoort;'DigiD';idmSoort~) \/ (I/\vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~);(vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmHouder;gbaBSN~)
      BOX[ "BSN" : I
         , "Geslachtsnaam" : gbaGeslachtsnaam
         , "Voornamen" : gbaVoornamen -- art 32 eerste lid Wjg
         , "Geboortedatum" : gbaGeboortedatum -- art 32 eerste lid Wjg
         , "Gemeente van inschrijving": gbaGemeenteVanInschrijving
         ]
      ]
   , "Bijzonderheden" : I
   BOX[ "Bijzonderheden geconstateerd (Ja/Nee)?" : bijzonderhedenGeconstateerd
      , "Ja, toelichting" : bijzonderhedenGeconstateerdToelichting
      ]
   , "Persisteren" : I
   BOX[ "Persisteren in de aanvraag (Ja/Nee)?" : persisterenInDeAanvraag
      , "Ja, toelichting" : persisterenInDeAanvraagToelichting
      ]
   , "COVOG advies" : I
   BOX[ "COVOG om advies gevraagd omtrent ontvankelijkheid (Ja/Nee)?" : covogOmAdviesGevraagd
      , "Ja, toelichting" : covogOmAdviesGevraagdToelichting
      ]
   , "Aanvraagformulier is gecontroleerd op volledigheid en de identiteit van de aanvrager is vastgesteld" : I
   BOX[ "Datum" : gemOndertekeningDatum
      , "Plaats" : (gemAmbtenaarUserid;idmUserid~ /\ gemAmbtenaarPassword;idmPassword~);(I /\ idmSoort;'GemeenteID';idmSoort~)
                   ;idmGemeentePlaats
      , "Handtekening" : 
                  ({-gemAmbtenaarHandtekening-}
                    (gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\
                     gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~
                    );idmDisplayHandtekening
                  )
      ]
   ]
-----------------------------------------------------------
ENDCONTEXT