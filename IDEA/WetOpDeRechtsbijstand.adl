CONTEXT Rechtsbijstand 
--INCLUDE "GBA.adl"
INCLUDE "Persoonsgegevens.adl"
LATEX
META "authors" "Han Joosten"
PURPOSE CONTEXT Rechtsbijstand
{+
De Wet op de rechtsbijstand regelt de rechtsbijstand of rechtshulp aan minder draagkrachtigen. Het is de Raad voor Rechtsbijstand die aan de hand van een verzoek en de wet beoordeelt of de verzoeker in aanmerking komt voor (toevoeging van) een advocaat en kostenvergoeding van de rechtsbijstand.
Op (delen uit) deze wet is een Ampersand analyse gedaan, om inzicht te krijgen in de wijze van modelleren en de geproduceerde artefacten van Ampersand.
Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, zonder de naam van de wet erbij te noemen,
wordt de Wet op de rechtsbijstand bedoeld.
-}

PROCESS Toevoegen
PURPOSE PROCESS Toevoegen REF "www.rvr.org"
{+
Rechtsbijstandverleners kunnen cliënten bijstaan die in aanmerking komen voor gesubsidieerde rechtsbijstand. In de praktijk heet dit de toevoeging. Een toevoeging wordt door de rechtsbijstandsverlener aangevraagd bij de Raad. Een dergelijke aanvraag wordt door de Raad zowel financieel als inhoudelijk getoetst.
-}

door :: Aanvraag * Rechtsbijstandverlener [UNI,TOT] 
PRAGMA "" "is ingediend door" 
MEANING "Elke aanvraag om een toevoeging is ingediend door één rechtsbijstandserlener."
PURPOSE RELATION door[Aanvraag * Rechtsbijstandverlener] REF "Artikel 24 lid 2"
{+Omdat een aanvraag door een rechtsbijstandverlener wordt gedaan, moet op elk moment duidelijk zijn wie de indiener van de aanvraag is.
-}

namens :: Aanvraag * Rechtzoekende [UNI,TOT]
PRAGMA "" "is ingediend namens"
MEANING "Elke aanvraag om een toevoeging is ingediend namens één rechtzoekende."
PURPOSE RELATION namens[Aanvraag * Rechtzoekende] REF "Artikel 24 lid 2"
{+Omdat een aanvraag namens een rechtzoekende wordt gedaan, moet op elk moment duidelijk zijn namens wie de aanvraag is ingediend.
-}

partner :: Aanvraag * NatuurlijkPersoon 
PRAGMA "Op ""is""vermeld als persoon zoals bedoeld in artikel 34 lid 3."
MEANING "Aanvragen kunnen personen vermelden, die relevant zijn voor de inkomenstoets van de rechtzoekende"
PURPOSE RELATION partner REF "Artikel 34 lid 3"
{+In de Wet is vastgelegd dat rechtsbijstand vanuit overheidswege alleen aan minder draagkrachtigen wordt toegekend. Hierbij wordt rekening gehouden worden met het inkomen of vermogen van andere personen, meestal die personen met wie de rechtzoekende samen woont. In artikel 34 lid 3 staat vermeldt welke personen hieronder worden verstaan. Deze personen dienen op de aanvraag te worden vermeld.
-}
grond :: Aanvraag * Grond [TOT]
PRAGMA "" "op grond van" 
MEANING "Elke aanvraag bevat een omschrijving van de omstandigheden op grond waarvan rechtsbijstand wordt gevraagd."
PURPOSE RELATION grond[Aanvraag*Grond] REF "Artikel 24 lid 3"
{+Omdat het gaat om een aanvraag om een toevoeging, gedaan vanuit de rechtsbijstandverlener, is het beschrijven van de feiten en omstandigheden met betrekking tot het rechtskundig proleem waarvoor de toevoeging wordt aangevraagd, verplicht.
Artikel 12, lid 2.a stelt bovendien dat rechtsbijstand niet wordt verleend indien de aanvraag kennelijk van elke grond is ontbloot. Dit impliceert dat een aanvraag een omschrijving van deze omstandigheden moet bevatten.
-}
kosten :: Aanvraag * Bedrag [UNI]
PRAGMA "de verbonden kosten aan de te verlenen rechtsbijstand voor " " bedraagt " 
MEANING "Een aanvraag kan de aan de te verlenen rechtsbijstand verbonden kosten vermelden."
PURPOSE RELATION kosten REF "Artikel 12 lid 2"
{+De wet schrijft voor dat de aan de rechtsbijstand verbonden kosten in redelijke verhouding dienen te staan tot het belang van de zaak. Daarom moet bij de beoordeling van een aanvraag deze afweging kunnen worden gemaakt. 
-}
op :: Beschikking -> Aanvraag
PRAGMA "" "op"
MEANING "Elke beschikking verwijst naar de aanvraag waarop de beslissing genomen is."
PURPOSE RELATION op REF "Artikel 24 lid 1"
{+Duidelijkheid is nodig over de vraag op welke aanvraag een beslissing gevraagd wordt.
De tekst van de wet rept van ``De beslissing op de aanvraag ...''
Daarom bevat elk besluit een verwijzing naar de aanvraag waarop de betreffende beslissing genomen wordt.
-}
tot :: Beschikking * Toevoeging [UNI] 
PRAGMA "" "tot"
MEANING "Elke beschikking beschrijft de toevoeging waartoe is besloten, tenzij de aanvraag is afgewezen."
PURPOSE RELATION tot[Beschikking*Toevoeging] REF "Artikel 24 lid 5"
{+De toevoeging die wordt verleend kan afwijken van de toevoeging die is aangevraagd.
Daarom wordt aan het besluit nadrukkelijk de toevoeging verbonden waartoe de Raad heeft besloten.
-}

terzake :: Toevoeging * Belang [TOT,UNI]
PRAGMA "" "terzake" 
MEANING "Elke toevoeging vermeldt een omschrijving van het rechtsbelang terzake."
PURPOSE RELATION terzake[Toevoeging * Belang] REF "Artikel 24 lid 5"
{+Omdat de toevoeging uitsluitend geldt voor het rechtsbelang ter zake waarvoor zij is verleend (artikel 32), wordt het rechtsbelang vermeld bij de toevoeging.
-}
datum :: Beschikking * Datum [UNI,TOT]
PRAGMA "de beschikkingsdatum van "" is "
MEANING "Elke beschikking is besloten op precies één datum"
PURPOSE RELATION datum [Beschikking * Datum] 
{+Omdat een beschikking een vastlegging is van een besluit dat op een specifieke datum is genomen, wordt de datum van het besluit vastgelegd in de beschikking.-}


zaakcode :: Toevoeging * Zaakcode [TOT,UNI]
PRAGMA "" "gecodeerd als"
MEANING "Elke toevoeging is naar de aard van de zaak gecodeerd."
PURPOSE RELATION zaakcode [Toevoeging * Zaakcode] REF "kenniswijzer.rvr.org"
{+
Elke toevoeging wordt naar de aard van de zaak gecodeerd. Aan elke code is het gewicht in punten voor procedures gekoppeld.
-}

omschrijving :: Zaakcode -> ZaakcodeOmschrijving
PRAGMA "" ": "
MEANING "Elke zaakcode is omschreven"
PURPOSE RELATION omschrijving [Zaakcode * ZaakcodeOmschrijving] REF "kenniswijzer.rvr.org"
{+Een 
terzake :: Aanvraag * Belang 

eigenBijdrage :: Beschikking -> Bedrag [TOT,UNI]
PRAGMA "De eigen bijdrage voor" "bedraagt"
MEANING "Elk besluit tot toevoeging vermeldt het bedrag van de eigen bijdrage."
PURPOSE RELATION eigenBijdrage REF "Artikel 24 lid 5"
{+Omdat de rechtzoekende een eigen bijdrage verschuldigd is voor de verlening van rechtsbijstand, en omdat de hoogte daarvan door de Raad wordt vastgesteld (artikel 35), wordt het bedrag van de eigen bijdrage vermeld bij het besluit
-} 

ingediend :: Aanvraag * Tijdstip [UNI]
PRAGMA "" "is ingediend"
MEANING "Een aanvraag kan hooguit een keer worden ingediend."
PURPOSE RELATION ingediend REF ""
{+Omdat het tijdstip van indienen van de aanvraag relevant is voor de ontvankelijkheid ervan, moeten we het registreren.
-}
ROLE Rechtsbijstandverlener MAINTAINS "indienen aanvraag"
RULE "indienen aanvraag" : I[Aanvraag] |- ingediend;ingediend~
MEANING "Deze regel bewaakt dat een aanvraag om toevoeging volledig is."
MESSAGE ""
VIOLATION (SRC I[Aanvraag], TXT " is nog niet compleet. ")


-- VIEW rechtzoekende : Verzoek(TXT "Ga naar de gegevens")

ENDPROCESS


--SPEC Vreemdeling ISA NatuurlijkPersoon
SPEC Rechtzoekende ISA NatuurlijkPersoon
SPEC Rechtsbijstandverlener ISA NatuurlijkPersoon
SPEC Advocaat ISA Rechtsbijstandverlener

gbaInschrijving :: NatuurlijkPersoon * BurgerServiceNummer [UNI]
PRAGMA """is in het GBA ingeschreven onder nummer " 
MEANING "Een persoon kan een BSN-nummer hebben."


onvolledig :: Aanvraag * Reden [UNI]
PRAGMA "" "is onvolledig vanwege "
MEANING "Een aanvraag kan een motivatie bevatten omtrent het ontbreken van bepaalde gegevens."
PURPOSE RELATION onvolledig [Aanvraag * Reden] REF "TODO"
{+Omdat het kan voorkomen dat bepaalde gegevens, zoals de geboortedatum, onbekend zijn, kunnen deze niet op een aanvraag worden vermeld. In deze gevallen is er echter een verplichting dat er een motivatie wordt gegeven omtrent de ontbrekende gegevens."
-}
RULE achternaamRechtzoekende    : I[Aanvraag]/\ingediend;ingediend~ |- onvolledig;onvolledig~ \/ namens;geslachtsnaam;(namens;geslachtsnaam)~
RULE voornamenRechtzoekende     : I[Aanvraag]/\ingediend;ingediend~ |- onvolledig;onvolledig~ \/ namens;voornamen;(namens;voornamen)~
RULE geboortedatumRechtzoekende : I[Aanvraag]/\ingediend;ingediend~ |- onvolledig;onvolledig~ \/ (namens;geboortedatum;(namens;geboortedatum)~ )
RULE postadresRechtzoekende     : I[Aanvraag]/\ingediend;ingediend~ |- namens;woonadres;(namens;woonadres)~ 
MEANING "Deze regel bewaakt dat een ingediende aanvraag om toevoeging een adres bevat dat als postadres kan dienen."
MESSAGE "Elke aanvraag bevat het postadres van de rechtzoekende."
VIOLATION (TXT "Het postadres van ", SRC I[Aanvraag];namens, TXT " ontbreekt.")



vnummer :: NatuurlijkPersoon * Vreemdelingennummer [UNI] PRAGMA "Het V-nummer van " "is"
MEANING "Een vreemdeling komt in aanmerking voor een vreemdelingennummer"
PURPOSE RELATION vnummer [NatuurlijkPersoon * Vreemdelingennummer] 
{+De wet lijkt onduidelijk als het gaat over de uitgifte van vreemdelingennummers. Schijnbaar krijgt elke vreemdeling, zodra die met een overheidsinstantie te maken krijgt, een vreemdelingennummer toebedeeld. Artikel 2 punt f van het Besluit identiteitsvaststelling verdachten en veroordeelden suggereert dit.

Op www.yurist.nl staat te lezen: Het V-nummer staat voor vreemdelingennummer. Dit is een uniek nummer, waarmee iedere vreemdeling bij de IND en de ketenpartners (zoals de vreemdelingenpolitie) is te identificeren. Dit nummer wordt toegekend zodra iemand een toelatingsprocedure start. Het nummer staat vermeld in de correspondentie die de vreemdeling of de referent van onder meer de IND ontvangt én op het verblijfsdocument zelf.-}

prNummer :: Advocaat * PRNummer PRAGMA "" "is ingeschreven als rechtshulpverlener onder nummer "
MEANING "Een advocaat kan door de raad zijn ingeschreven"
PURPOSE RELATION prNummer REF "Artikel 14"
{+De Raad houdt een register bij van advocaten die zijn ingeschreven.-}

CONCEPT NatuurlijkPersoon "Een natuurlijk rechtspersoon in de zin van de wet."
PURPOSE CONCEPT NatuurlijkPersoon 
{+Bij de uitvoering van deze wet zijn verschillende personen betrokken, ook in verschillende rollen. Om geen verwarring te laten bestaan over de verschillende personen, wordt het begrip ''persoon'' gehanteerd. Nederlands ingezetenen zijn te identificeren door middel van een burgerservicenummer. Vreemdelingen, die niet over een burgerservicenummer beschikken, beschikken over een vreemdelingennummer (V-nummer), dat bij hun verblijfsaanvraag is verstrekt.
-}
CONCEPT BurgerServiceNummer "Identificatie van een natuurlijk persoon die is geregistreerd in de gemeentelijke basisadministratie."
PURPOSE CONCEPT BurgerServiceNummer REF "Wet algemene bepalingen burgerservicenummer"
{+De Gemeentelijke Basisadministratie Persoonsgegevens (GBA) bevat persoonsgegevens van iedereen die in Nederland woont of gewoond heeft. De overheid heeft deze gegevens nodig om bijvoorbeeld een paspoort, identiteitskaart (ID-kaart) of rijbewijs te maken. Ook gebruikt de overheid de gegevens uit de GBA om te weten wie er mogen stemmen bij verkiezingen en bij het verstrekken van uitkeringen.-}

CONCEPT Vreemdelingennummer "Identificatie van een niet-ingezetene, die een aanvraag tot verblijf heeft ingediend."
PURPOSE CONCEPT Vreemdelingennummer
{+Vreemdelingen kunnen onder bepaalde voorwaarden in Nederland blijven. Bij hun aanvraag daartoe worden zij geregistreerd, waardoor zij beschikken over een vreemdelingennummer.
-}
CONCEPT Aanvraag  "een verzoek van een belanghebbende, een besluit te nemen."
PURPOSE CONCEPT Aanvraag REF "Artikel 1:3, awb"
{+De wet stelt expliciete eisen aan de inhoud van de aanvraag, waardoor het verzoek het karakter van een document krijgt.
In de context van dit document beperken we ons vooralsnog tot aanvragen om een toevoeging van een rechtsbijstandsverlener, zoals beschreven in artikel 24.
-}
CONCEPT Rechtsbijstandverlener "de advocaat, de medewerker van de voorziening voorzover belast met de verlening van rechtsbijstand en de personen, bedoeld in artikel 13, eerste lid, onder c;" 
PURPOSE CONCEPT Rechtsbijstandverlener REF "Artikel 1"
{+De wet geeft een definitie van de persoon die rechtsbijstand verleent aan een rechtzoekende.
-}
CONCEPT Rechtzoekende "degene die op grond van onvoldoende financiële draagkracht aanspraak kan maken op rechtsbijstand of mediation, voorzover in deze wet en de daarop berustende bepalingen geregeld, alsmede degene die zijn schade wil vorderen als slachtoffer van een misdrijf tegen de zeden of een geweldsmisdrijf;"
PURPOSE CONCEPT Rechtzoekende REF "Artikel 1"
{+Wanneer iemand betrokken is bij een ruzie of conflict, waarop 
-}
CONCEPT Grond "motief, aanleiding: op grond van op basis van"
PURPOSE CONCEPT Grond REF "van Dale"
{+De wet schrijft voor, dat elke aanvraag voldoende gemotiveerd wordt ingediend (artikel 24 lid 3). Wat hier inhoudelijk precies onder wordt verstaan laat de wetgever over aan de Raad. Het bestaan van een dergelijke omschrijving kan echter wel worden vastgesteld.
-}
CONCEPT Beschikking "een besluit dat niet van algemene strekking is, met inbegrip van de afwijzing van een aanvraag daarvan."
PURPOSE CONCEPT Beschikking REF "Artikel 1:3, awb"
{+In de context van dit document gaat het specifiek over het besluit naar aanleiding van een aanvraag aan de Raad voor rechtsbijstand.
-}
CONCEPT Toevoeging "de toevoeging ten behoeve van rechtsbijstand als bedoeld in artikel 24, eerste lid, of mediation als bedoeld in artikel 33a;"
PURPOSE CONCEPT Toevoeging REF "Artikel 1"
{+Het begrip toevoeging wordt gebruikt om de formele relatie tussen rechtsbijstandverlener en rechtzoekende te objectiveren. 
-}
CONCEPT Belang "een omschrijving van een rechtsbelang"
PURPOSE CONCEPT Belang REF ""
{+De wet stelt, dat een toevoeging een omschrijving bevat terzake waarvan de toevoeging is verleend. Waaruit deze omschijving precies bestaat, wordt in eerste instantie niet duidelijk uit de wet. 
-}
CONCEPT Bedrag "een hoeveelheid geld"
PURPOSE CONCEPT Bedrag REF "van Dale"
{+De wet spreekt op diverse plaatsen over dit gegrip, dat van belang is voor het vaststellen van de aanspraak op rechtsbijstand. 
-}
CONCEPT Tijdstip ""
PURPOSE CONCEPT Tijdstip REF ""
{+
-}

PATTERN Advocatentableau
---CONCEPTEN OVERGENOMEN UIT ADVOCATENTABLEAU  (en eenigszins aangepast, omdat me dat handig leek om de demo voor Raad voor Rechtsbijstand te beperken.  HJO20130531----
CONCEPT Advocaat "Een raadsheer die de praktijk uitoefent overeenkomstig de bevoegdheden en vereisten, bij de Wetboeken van Burgerlijke Rechtsvordering en Strafvordering en bij de bijzondere wetten en besluiten gegeven en gevorderd, en overeenkomstig deze wet en de daarop berustende verordeningen en besluiten." "Art. 10 Advw"
CONCEPT Kantoor "Het kantoor van de advocaat geldt voor al zijn handelingen als zodanig als gekozen woonplaats." "Art. 12 lid 3 Advw"
{-
CONCEPT "BAR nummer" "Uniek, persoonlijk nummer dat bij inschrijving aan advocaten toegekend wordt, om administratieve handelingen te vergemakkelijken" "www.barbeheer.nl"
barnummer :: Advocaat * Barnummer [UNI,SUR,INJ] PRAGMA "" "is in het advocatentableau ingeschreven onder nummer "
MEANING "Elke advocaat heeft een uniek BAR-nummer."
PURPOSE RELATION barnummer[Advocaat * Barnummer] IN DUTCH REF "www.barbeheer.nl"
{+ In het Advocatentableau ingeschreven advocaten hebben een uniek nummer waarmee koppelingen gelegd kunnen worden met externe applicaties om advocaten uniek te identificeren.
-}
houdtkantoor :: Advocaat -> Kantoor [UNI] PRAGMA "" "staat ingeschreven bij advocatenkantoor" "" 
MEANING "Een advocaat is verplicht kantoor te houden."
PURPOSE RELATION houdtkantoor[Advocaat*Kantoor] IN DUTCH REF "Art. 12 lid 1 Advw."
{+ Advocaten zijn verplicht kantoor te houden. Zij mogen slechts in één arrondissement op één locatie kantoor houden.
-}
telefoonnummer :: Advocaat -> Telefoonnummer PRAGMA "" "is telefonisch bereikbaar op" ""
MEANING "Van een advocaat is het telefoonnummer bekend."
PURPOSE RELATION telefoonnummer[Advocaat*Telefoonnummer] IN DUTCH REF "Landelijk procesreglement verzoekschriftprocedures rechtbank sector civiel handel/voorzieningenrechter Art 1.2.4"
{+ Ingeschreven advocaten hebben een telefoonnummer waarop zij voor de gerechten bereikbaar zijn.
-}
ENDPATTERN


INTERFACE Overview : I[ONE]
BOX [ Aanvragen     : V[ONE*Aanvraag],
      Beschikkingen : V[ONE*Beschikking],
      Toevoegingen  : V[ONE*Toevoeging],
      Rechtsbijstandverleners      : V[ONE*Rechtsbijstandverlener]
]


INTERFACE Rechtzoekende ( geslachtsnaam
                        , voorletters
                        , geboortedatum
                        , geboorteland
                        , geslacht
                        , woonadres, gbaInschrijving,vnummer
                        ) FOR Rechtsbijstandverlener: I[Rechtzoekende]/\namens~;namens
BOX 
    [ -- Naam : I[Rechtzoekende] 
      Geslachtsnaam : geslachtsnaam
    , Voorletters   : voorletters
    , Geboortedatum    :  geboortedatum
    , "Land v. herkomst"   : geboorteland
    , Geslacht     : geslacht
    , Adresgegevens    :  woonadres
    , "BSN"      : gbaInschrijving
    , "V-nummer": vnummer
    , "Rechtzoekende via aanvraag" : namens~
    ] 
INTERFACE Advocaat (voornamen, geslachtsnaam, prNummer, telefoonnummer[Advocaat*Telefoonnummer], houdtkantoor) FOR Rechtsbijstandverlener: I[Rechtsbijstandverlener]
BOX [ -- Naam : I[Advocaat]
      voornamen : voornamen
    , geslachtsnaam : geslachtsnaam
    , registratienummer : prNummer
    , telefoonnummer : telefoonnummer[Advocaat*Telefoonnummer]
    , kantoor : houdtkantoor
    ] 

VIEW aanvraag : Aanvraag (TXT "Aanvraag door " , door;voornamen,TXT " ", door;geslachtsnaam , TXT " namens ", namens;voornamen,TXT " ", namens;geslachtsnaam , TXT " op grond van ", grond)
INTERFACE "aanvraag om toevoeging indienen" (door,namens,kosten,geboorteland,geboortedatum,ingediend,grond,onvolledig)FOR Rechtsbijstandverlener: I[Aanvraag]--(ingediend;ingediend~)
BOX [ Indiener : door
    , "Aanvraag om toevoeging" : I
  --  , "Nieuwe aanvraag" : I 
     BOX [  namens : namens
          , "omschrijving rechtsprobleem": grond 
          , "aan de te verlenen rechtsbijstand verbonden kosten" : kosten
          , "reden onvolledig" : onvolledig
          , "datum indiening": ingediend
   ]      ]

CONCEPT Adres "Door het bevoegde gemeentelijke orgaan aan een verblijfsobject, een standplaats of een ligplaats toegekende benaming, bestaande uit een combinatie van de naam van een openbare ruimte, een nummeraanduiding en de naam van een woonplaats" "Art. 1 lid a Wet BAG"

POPULATION door[Aanvraag*Rechtsbijstandverlener] CONTAINS
   [ ("Aanvraag 392a", "Mr. Ad Vokaat")
   ; ("Aanvraag 18", "Mr. Lioe Tan")
   ]
POPULATION prNummer [Advocaat*PRNummer] CONTAINS
   [ ("Mr. Ad Vokaat", "BAR-0245")
   ; ("Mr. Lioe Tan", "BAR-432")
   ]
POPULATION telefoonnummer [Advocaat*Telefoonnummer] CONTAINS
   [ ("Mr. Ad Vokaat", "050-3145817")
   ; ("Mr. Lioe Tan", "0610930606")
   ]
POPULATION houdtkantoor [Advocaat*Kantoor] CONTAINS
   [ ("Mr. Ad Vokaat", "Tiebout Advocaten")
   ; ("Mr. Lioe Tan", "Advocatenkantoor De Binnenstad")
   ]

POPULATION namens[Aanvraag*Rechtzoekende] CONTAINS
   [ ("Aanvraag 392a", "Freddy Muhizi")
   ; ("Aanvraag 18", "Ehsanullah Ehsan")
   ]
POPULATION grond[Aanvraag*Grond] CONTAINS
   [ ("Aanvraag 392a", "Hoger beroep vreemdelingenbewaring")
   ; ("Aanvraag 18", "Beroep tegen negatieve beschikking")
   ]
POPULATION ingediend[Aanvraag*Tijdstip] CONTAINS
   [ ("Aanvraag 392a", "12-12-2012")
   ; ("Aanvraag 18", "31-05-2013")
   ]
POPULATION geslachtsnaam CONTAINS
   [ ("Freddy Muhizi","Muhizi")
   ; ("Ehsanullah Ehsan","Ehsan")
   ; ("Mr. Ad Vokaat", "Vokaat")
   ; ("Mr. Lioe Tan", "Tan")
   ]
POPULATION geboortedatum CONTAINS
   [ ("Freddy Muhizi","03-11-1983")
   ]
POPULATION vnummer CONTAINS
   [ ("Hu Jia","643")
   ; ("Ehsanullah Ehsan","V13579")
   ]
POPULATION gbaInschrijving CONTAINS
   [ ("Freddy Muhizi","305.462.12")
   ]
POPULATION onvolledig CONTAINS
   [ ("Aanvraag 18","vreemdeling zonder brondocumenten")
   ]
POPULATION voornamen CONTAINS
   [ ("Freddy Muhizi","F.A.")
   ; ("Ehsanullah Ehsan","E.S.")
   ; ("Mr. Ad Vokaat", "Mr. Ad")
   ; ("Mr. Lioe Tan", "Mr. L.")
   ]
POPULATION woonadres CONTAINS
   [ ("Freddy Muhizi","Witte Zandpad 1, 7854 TW  Aalden")
   ; ("Ehsanullah Ehsan","Imstenrade 6, 6419 PL  Heerlen")
   ]

ENDCONTEXT

--TODO: Opgave Inkomen & Vermogen voorzien van relevante speciﬁcaties (bij ontbreken van zowel BSN en vreemdelingennummer (zie http://www.rvr.org/binaries/over-rvr/amsterdam/checklist_amsterdam.pdf)
--TODO: Nalopen checklist: http://www.rvr.org/binaries/rbv-downloads/versie-1_0-checklist-aanvragen-toevoegingen.pdf
--TODO: Artikel 24 rept over welke vestiging moet worden gebruikt door de rechtsbijstandverlener. Daar komt ook nog wel een en ander achter weg....