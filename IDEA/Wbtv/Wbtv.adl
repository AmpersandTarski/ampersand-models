CONTEXT WBTV
INCLUDE "Persoonsgegevens.adl"
THEMES "Rbtv en uitwijklijst","Permanente Educatie",Aanvragen,Beëdiging
--THEMES "Permanente Educatie"
META "authors" "Han Joosten, Ordina"
PURPOSE CONTEXT WBTV  REF "motivatie WBTV"
{+
De Wet beëdigde tolken en vertalers (hierna: Wet) strekt onder meer tot het instellen van een register
voor beëdigde tolken en beëdigd vertalers (hierna: register). In dit register worden de vertalers en de
tolken die aan kwaliteits- en integriteitseisen voldoen op hun verzoek opgenomen. Via dit register
kunnen partijen die behoefte hebben aan bijstand van een beëdigd tolk of vertaler kennis nemen van
het beschikbare aanbod. Tolken en vertalers die willen worden opgenomen in het register als
beëdigde tolk of vertaler, moeten voldoen aan de in artikel 3 van de Wet gestelde (opleidings)eisen.
Daarnaast wordt een lijst bijgehouden van tolken en vertalers die wegens het ontbreken van opleidingen
of het ontbreken van onafhankelijke deskundigen die de kennis kunnen toetsen, niet kunnen
aantonen te beschikken over een aantal vereiste competenties, maar wel een verklaring omtrent
gedrag kunnen overleggen.

Op deze wet is een conceptuele analyse gedaan, om inzicht te krijgen in de benodigde processen om deze 
regeling uit te voeren. 

Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, zonder de naam van de wet erbij te noemen,
wordt de Wet beëdigde tolken en vertalers bedoeld.
-}



----------------------
-- Register beëdigde tolken en vertalers --
----------------------

PATTERN "Rbtv en uitwijklijst"
PURPOSE PATTERN "Rbtv en uitwijklijst"
{+
Het Rbtv is een register waarin tolken en vertalers staan ingeschreven die bevoegd zijn om in Nederland als beëdigd tolk en/of beëdigd vertaler werkzaam te zijn. 
Sinds 1 januari 2009 zijn bepaalde afnemers uit het justitiedomein verplicht om, in het kader van het straf- en vreemdelingenrecht, in beginsel een tolk of vertaler uit het Rbtv in te zetten. Gewaarmerkte vertalingen mogen zelfs alleen nog maar gemaakt worden door vertalers die in het Rbtv staan ingeschreven en beëdigd zijn. 
De tolken en vertalers uit het Rbtv hebben aangetoond in voldoende mate te beschikken over de in artikel 3 WBTV vereiste competenties. De afnemer kan dus bepaalde garanties ten aanzien van kwaliteit en integriteit ontlenen aan een inschrijving in het Rbtv. 
Zodra een tolk/vertaler is ingeschreven in het Rbtv kunnen afnemers zijn gegevens vinden via het raadplegen van het Rbtv. 

Naast het Rbtv wordt er ook een ander register bijgehouden, de zogenaamde uitwijklijst. Op de Uitwijklijst zijn tolken en vertalers geplaatst die niet aan de eisen van het Rbtv kunnen voldoen, maar wel aan de lagere eisen voor de Uitwijklijst.  (bron: http://www.bureaubtv.nl/faq/)

Dit thema gaat over het soort informatie dat in het Rbtv en uitwijklijst kan worden beheerd.
-}

--------------
-- Relaties --
--------------

vaardigheid :: Taalervaring * Vaardigheid [UNI,TOT] PRAGMA "De taalervaring `" "' betreft de vaardigheid als " 
MEANING "Elke taalervaring vermeldt precies één vaardigheid."
PURPOSE RELATION vaardigheid [Taalervaring * Vaardigheid] 
{+Nu volgen enlkele afspraken met betrekking tot taalervaring, die om technische redenen in dit document zijn opgenomen, maar die waarschijnlijk als 'open deur' zullen worden ervaren:

Elke taalervaring staat voor een vaardigheid als tolk of als vertaler in een bron- en doeltaal.-}
POPULATION vaardigheid CONTAINS
 [ ("tolkvaardigheid tussen Engels en Duits" , "Tolk")
 ]
brontaal :: Taalervaring * Taal [UNI,TOT] PRAGMA "De brontaal van `" "' is " 
MEANING "Elke taalervaring heeft precies één brontaal."
PURPOSE RELATION brontaal [Taalervaring * Taal] 
{+Elke taalervaring staat voor een vaardigheid als tolk of als vertaler in een bron- en doeltaal. 
-}
POPULATION brontaal CONTAINS
 [ ("tolkvaardigheid tussen Engels en Duits" , "Engels")
 ]

doeltaal :: Taalervaring * Taal [UNI,TOT] PRAGMA "De doeltaal van `" "' is " 
MEANING "Elke taalervaring heeft precies één doeltaal."
PURPOSE RELATION doeltaal [Taalervaring * Taal] 
{+Elke taalervaring staat voor een vaardigheid als tolk of als vertaler in een bron- en doeltaal.-}
POPULATION doeltaal CONTAINS
 [ ("vertaalvaardigheid van Engels naar Duits" , "Duits")
 ]

ingeschrevene :: WBTVnummer * NatuurlijkPersoon [UNI,TOT,INJ] PRAGMA "WBTVnummer " "is toegekend aan" ""
MEANING "Elke ingeschreve heeft precies één uniek WBTV-nummer."
PURPOSE RELATION ingeschrevene[WBTVnummer*NatuurlijkPersoon] IN DUTCH REF ""
{+Tolken en vertalers die daarvoor in aanmerking komen en die daartoe een 
verzoek indienen, worden door de Raad ingeschreven in het register. 

De Raad wijst aan ingeschrevenen een uniek nummer toe, waarmee koppelingen 
gelegd kunnen worden met externe applicaties om beëdigd tolken of vertalers 
uniek te identificeren.-}

wbtvnummer :: Inschrijvingsbesluit -> WBTVnummer PRAGMA "het WBTV-nummer van "" is "
MEANING "Elke inschrijving bevat het WBTV-nummer van de ingeschreven beëdigd tolk of vertaler."

besluit :: Inschrijving -> Inschrijvingsbesluit PRAGMA "" " besloten in "
MEANING "Elke inschrijving is besloten in één inschrijvingsbesluit."

inschrijfdatum :: Inschrijvingsbesluit -> Datum [TOT,UNI] PRAGMA "De datum van inschrijving van " " is "
MEANING "Elk inschrijvingsbesluit is gedaan op één datum."
PURPOSE RELATION inschrijfdatum [Inschrijvingsbesluit * Datum] REF "Art. 8 lid 1 WBTV"
{+Een inschrijving geschiedt voor de periode van vijf jaar. Om dit te kunnen 
vaststellen, is het noodzakelijk om de datum van het inschrijvingsbesluit te registreren.-}

taalervaring :: Inschrijving -> Taalervaring PRAGMA "Inschrijving " " is als " ""
MEANING "Elke inschrijving betreft één taalervaring."
PURPOSE RELATION taalervaring[Inschrijving * Taalervaring] IN DUTCH REF "Art. 2 lid 1b en Art. 2 lid 1c WBTV"
{+Een persoon kan verschillende taalervaringen hebben, die niet tegelijkertijd geregistreerd hoeven te zijn. Daarom moet het mogelijk zijn dat een persoon meerdere inschrijvingen heeft. Een logisch ontwerpbeslissing is daarom dat elke taalervaring van een persoon tot een inschrijving leidt.-}

in :: Inschrijving -> RbtvOfUwl PRAGMA "" "is een gedaan in de "
MEANING "Elke inschrijving is gedaan in ófwel het Rbtv (als inschrijving) ófwel de uitwijklijst (als plaatsing), maar niet in beide."
PURPOSE RELATION in[Inschrijving * RbtvOfUwl]
{+In het huidige taalgebruik bij het bureau tolken en vertalers wordt onderscheid gemkaakt tussen plaatsing en inschrijving. 
In beide gevallen betreft het de registratie van dezelfde gegevens, met dit verschil dat bij plaatsing de uitwijklijst wordt bedoeld, en bij inschrijving het Rbtv.

De term *Inschrijving* kan in beide gevallen worden gehanteerd, zolang maar bekend is of het om de Rbtv of om de uitwijklijst gaat. -}
 
-----------------
-- Invarianten --
-----------------


ENDPATTERN

PATTERN InschrijvingseisenBUITENSCOPE
PURPOSE PATTERN InschrijvingseisenBUITENSCOPE REST REF "Art. 3 WBTV"
{+Om voor inschrijving in het register in aanmerking te komen dient 
de tolk dan wel de vertaler te voldoen aan de bij of krachtens 
algemene maatregel van bestuur te stellen eisen ten aanzien van 
in dit artikel specifiek benoemde competenties. 
Afhankelijk van de competenties moet er daarom bepaalde informatie geregistreerd kunnen worden in het systeem. 
Bij de volgende eisen worden de competenties genoemd, gevolgd door de concrete afspraak ten aanzien van het systeem:-}
attitudeAls :: NatuurlijkPersoon*Vaardigheid PRAGMA "" " voldoet aan de gestelde attitude-eisen voor een "
MEANING "Geregistreerd moet kunnen worden dat een persoon voldoet aan de gestelde attitude-eisen voor een vaardigheid"
PURPOSE RELATION attitudeAls REF "Art. 3 WBTV"
{+* attitude van een tolk voor de tolk;
* attitude van een vertaler voor de vertaler;
 -}

isInteger :: NatuurlijkPersoon*NatuurlijkPersoon [PROP] PRAGMA "De integriteit van " " is vastgesteld "
MEANING "Geregistreerd moet kunnen worden dat de integriteit van een peroon is vastgesteld."
PURPOSE RELATION isInteger REF "Art. 3 WBTV"
{+* integriteit;
 -}

taalvaardig :: NatuurlijkPersoon*Taal PRAGMA "De taalvaardigheid van " " in de taal " " voldoet aan de daaraan gestelde eisen."
MEANING "Geregistreerd moet kunnen worden dat de taalvaardigheid van een persoon in een specifieke taal voldoet aan de daaraan gestelde eisen."
PURPOSE RELATION taalvaardig REF "Art. 3 WBTV"
{+* taalvaardigheid in de brontaal;
* taalvaardigheid in de doeltaal;
 -}

cultuurKennisVan :: NatuurlijkPersoon*Taal PRAGMA "" "voldoet aan de gestelde eisen ten aanzien van de kennis en cultuur van het land of gebied van de taal ""."
MEANING "Geregistreerd moet kunnen worden dat een persoon voldoet aan de gestelde eisen ten aanzien van de kennis en cultuur van het land of gebied van een taal."
PURPOSE RELATION cultuurKennisVan REF "Art. 3 WBTV"
{+* kennis van de cultuur van het land of gebied van de brontaal;
* kennis van de cultuur van het land of gebied van de doeltaal;
 -}

vaardigAls :: NatuurlijkPersoon*Vaardigheid PRAGMA "" " voldoet aan de gestelde vaardigheidseisen voor een "
MEANING "Geregistreerd moet kunnen worden dat de tolk- dan wel vertaalvaardigheid van een persoon voldoet aan de daaraan gestelde eisen."
PURPOSE RELATION vaardigAls REF "Art. 3 WBTV"
{+* tolkvaardigheid voor de tolk;
* vertaalvaardigheid voor de vertaler.
 -}

-----------------
-- Invarianten --
-----------------
RULE attitudeEis: ingeschrevene~;wbtvnummer~;besluit~;taalervaring;vaardigheid |- attitudeAls
MEANING "Van iedere persoon die is ingeschreven met een taalervaring die een vaardigheid vermeldt, is vastgesteld dat die persoon voldoet aan aan die vaardigheid gestelde attitude-eisen."
PURPOSE RULE attitudeEis REF "Art. 3 WBTV"
{+Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde attitude-eisen als tolk dan wel als vertaler.
-}
RULE integriteit: ingeschrevene~;wbtvnummer~;wbtvnummer;ingeschrevene |- isInteger
MEANING "Van iedere persoon die is ingeschreven is vastgesteld dat die persoon voldoet aan de gestelde integriteits eisen."
PURPOSE RULE integriteit REF "Art. 3 WBTV"
{+Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde eisen ten aanzien van integriteit.
-}
RULE taalvaardigheid: ingeschrevene~;wbtvnummer~;besluit~;taalervaring;(brontaal \/ doeltaal) |- taalvaardig
MEANING "Van iedere persoon die is ingeschreven met een taalervaring met een bepaalde taal als bron- of doeltaal, is vastgesteld dat die persoon voldoet aan de voor die taal gestelde taalvaardigheid-eisen."
PURPOSE RULE taalvaardigheid REF "Art. 3 WBTV"
{+Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde taalvaardigheids-eisen voor elke bron- of doeltaal van zijn of haar inschrijving.
-}
RULE cultuurkennis: ingeschrevene~;wbtvnummer~;besluit~;taalervaring;(brontaal \/ doeltaal) |- cultuurKennisVan
MEANING "Van iedere persoon die is ingeschreven met een taalervaring met een bepaalde taal als bron- of doeltaal, is vastgesteld dat die persoon voldoet aan de voor die taal gestelde eisen met betrekking tot kennis van cultuur van het land of gebied voor die taal."
PURPOSE RULE cultuurkennis REF "Art. 3 WBTV"
{+Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde kennis-van-cultuur-eisen voor elke bron- of doeltaal van zijn of haar inschrijving.
-}
RULE vaardigheidsEis: ingeschrevene~;wbtvnummer~;besluit~;taalervaring;vaardigheid |- vaardigAls
MEANING "Van iedere persoon die is ingeschreven met een taalervaring die een vaardigheid vermeldt, is vastgesteld dat die persoon voldoet aan aan die vaardigheid gestelde vaardigheids-eisen."
PURPOSE RULE vaardigheidsEis REF "Art. 3 WBTV"
{+Om voor inschrijving in het register in aanmerking te komen dient de tolk dan wel de vertaler te voldoen aan de gestelde vaardigheids-eisen als tolk dan wel als vertaler.
-}

ENDPATTERN

PATTERN Aanvragen
PURPOSE PATTERN Aanvragen
{+Teneinde het register actueel te houden, kunnen aanvragen worden ingediend, 
die door Bureau btv, als beheerder van het register, conform de WBTV worden behandeld. 
Dit thema beschrijft het taalmodel van dergelijke verzoeken.
-}
PURPOSE PATTERN Aanvragen REF "Art. 5 BBTV"
{+Voor de aanvraag tot inschrijving in het register wordt gebruik gemaakt van een daartoe ter beschikking gesteld formulier.-}
aanvrager :: Aanvraag * NatuurlijkPersoon [TOT,UNI] PRAGMA "" "is het verzoek van" ""
MEANING "Elke aanvraag wordt gedaan door een natuurlijk persoon."
PURPOSE RELATION aanvrager[Aanvraag*NatuurlijkPersoon] IN DUTCH REF "Art. 4:2 lid 1a AWB; Art. 4 WBTV"
{+Elke aanvraag heeft een aanvrager. In geval van een verzoek tot inschrijving is dit de tolk of vertaler die om zijn inschrijving verzoekt. -}

dagtekening :: Aanvraag * Datum [TOT,UNI] PRAGMA "" " heeft als dagtekening " 
MEANING "Elke aanvraag bevat de dagtekening" 
PURPOSE RELATION dagtekening REF "Art. 4:2 lid 1b AWB"
{+Om te weten waneer een aanvraag is gedaan, is de aanvraag gedagtekend -}

SPEC Inschrijfverzoek ISA Aanvraag

voor :: Inschrijfverzoek * RbtvOfUwl [UNI,TOT] PRAGMA "" " is een aanvraag tot inschrijving in het "
MEANING "Elk inschrijfverzoek is voor ófwel het Rbtv (inschrijving) ófwel de uitwijklijst (plaatsing), maar niet voor beide."
PURPOSE RELATION voor[Inschrijfverzoek * RbtvOfUwl] 
{+Voor zowel de Rbtv als voor de uitwijklijst is een formulier ter beschikking die gebruikt dient te worden voor verzoeken om opgenomen te worden in de desbetreffende registratie. De aanvrager geeft daarmee specifiek aan voor welke van beide de aanvraag is bedoeld.-}

woonadres :: Inschrijfverzoek * Adres [UNI] PRAGMA "" " bevat als woonadres "
MEANING "Een verzoek tot inschrijving kan een woonadres bevatten."
PURPOSE RELATION woonadres[Inschrijfverzoek * Adres] REF "Inschrijfformulier Rbtv"
{+Het aavraagformulier bevat ruimte voor de opgave van een adres als woonadres.
-}
postadres :: Inschrijfverzoek * Adres [UNI] PRAGMA "" " bevat als woonadres "
MEANING "Een verzoek tot inschrijving kan een woonadres bevatten."
PURPOSE RELATION postadres REF "Inschrijfformulier Rbtv"
{+Het aavraagformulier bevat ruimte voor de opgave van een adres als postadres.
-}


bevat :: Inschrijfverzoek * Taalervaring [] PRAGMA "In " " wordt verzocht tot inschrijving als" ""
MEANING "Een verzoek tot inschrijving kan taalervaringen bevatten, waarvoor inschrijving wordt verzocht."
PURPOSE RELATION bevat[Inschrijfverzoek * Taalervaring] IN DUTCH REF "Inschrijfformulier Rbtv"
{+
Het aanvraagforumier bevat ruimte voor taalcombinaties, waarin de aanvrager kan opgeven voor welke (als tolk of vertaler, bron- en doeltaal) een inschijving wordt verzocht. 
-}


ENDPATTERN


PATTERN Beëdiging
beëdigde :: Beëdiging -> NatuurlijkPersoon [UNI] PRAGMA "" "is beëdiging van" ""
MEANING "Elke beëdiging is van één persoon."
PURPOSE RELATION beëdigde[Beëdiging*NatuurlijkPersoon] IN DUTCH REF "Art. 12 WBTV"
{+Een persoon die als tolk of als vertaler is ingeschreven, dient binnen twee maanden na inschrijving als tolk of als vertaler te worden beëdigd. 
-}
als1 :: Beëdiging * Vaardigheid [TOT] PRAGMA "" " is een beëdiging als "
MEANING "Elke beëdiging gaat over de eed of belofte in tenminste één rol als tolk of als vertaler."
PURPOSE RELATION als1 [Beëdiging*Vaardigheid] REF "Art. 13 WBTV; Art. 14 WBTV"
{+Elke beëdiging is als tolk of als vertaler. De wet is onduidelijk over de gang van zaken waneer een ingeschrevene als vertaler na verloop van tijd ook als tolk wil worden ingeschreven.
-}
datumBeëdiging :: Beëdiging -> Datum [UNI] PRAGMA "" "vond plaats op" ""
MEANING "Een beëdiging vindt plaats op een datum."
PURPOSE RELATION datumBeëdiging[Beëdiging*Datum] IN DUTCH REF "Art. 13 WBTV; Art. 14 WBTV"
{+Een beëdiging vindt plaats op één datum.
-}


--RULE TODO--alleen ingeschrevenen kunnen worden beedigd.

ENDPATTERN

PATTERN "Permanente Educatie"
PURPOSE PATTERN "Permanente Educatie" REF "Besluit PE"
{+Voor een tolk of vertaler die in het Rbtv is ingeschreven en/of op de 
Uitwijklijst is geplaatst geldt een bijscholingsverplichting. 
Van een dergelijke tolk of vertaler wordt verlangd dat wordt geïnvesteerd in 
kennis en vaardigheden. Met het oog op de goede uitoefening van het beroep van tolk of vertaler 
wordt dat noodzakelijk geacht. Het Rbtv en de Uitwijklijst beogen immers een bepaald niveau van 
kwaliteit te garanderen. Dat kan alleen maar als die kwaliteit wordt onderhouden. De verantwoording 
daarvoor ligt bij de tolk of vertaler.

Dit thema gaat over het taalmodel omtrent permanente educatie (PE).
-}

datumBesluit :: "PE-puntentoekenning" * Datum [UNI,TOT] PRAGMA "" " zijn toegekend op "
MEANING "Elke PE-puntentoekenning is besloten op één datum."
PURPOSE RELATION datumBesluit REF "Art. 9 BPEWbtv; Art. 13 BPEWbtv"
{+Omdat PE-punten worden toegekend naar aanleiding van een verzoek tot toekenning dan wel een verzoek tot vrijstelling, ligt er een beslissing 
ten grondslag aan elke PE-puntentoewijzing.
-}

toegekendAan :: "PE-puntentoekenning" * WBTVnummer [UNI,TOT] PRAGMA ""  " zijn toegekend aan de houder van "
MEANING "Elke PE-puntentoekenning betreft de houder van één WBTV-nummer."

activiteit   :: "PE-puntentoekenning" * Scholingsactiviteit [UNI] PRAGMA "" " op grond van het volgen of verzorgen van "
MEANING "Een PE-puntentoekenning is vanwege tijdsbesteding bij maximaal één scholingsactiviteit."
PURPOSE RELATION activiteit ["PE-puntentoekenning" * Scholingsactiviteit] REF "Art. 7 BPEWbtv; Art. 13 BPEWbtv"
{+Uitgezonderd bij het besluit om vrijstelling, worden punten alleen toegekend op basis van bestede tijd aan een scholingsactiviteit. -}

punten :: "PE-puntentoekenning" * "PE-punten" [TOT] PRAGMA "" " is de toekenning van " 
aantal :: "PE-punten" -> Aantal PRAGMA "Het aantal punten van " " bedraagt "
MEANING "PE-punten hebben altijd één aantal."
categorie :: "PE-punten" -> Categorie PRAGMA "" " zijn van de categorie "
MEANING "PE-punten zijn altijd van één categorie."

aanbieder ::  "PE-activiteit" * Aanbieder [UNI,TOT] PRAGMA "De aanbieder van "" is "
MEANING "Elke PE-activiteit wordt aangeboden door één aanbieder."
PURPOSE RELATION aanbieder ["PE-activiteit" * Aanbieder] REF "Art. 10 BPEWbtv"
{+Een scholingsactiviteit kan als PE-activiteit zijn aangewezen, doordat de organisatie die de schilingsactiviteit aanbiedt, daarom heeft verzocht. Daarom 
heeft elke PE-activiteit een aanbieder.-}
van :: "PE-activiteit" * Datum [UNI,TOT] 
tot :: "PE-activiteit" * Datum [UNI]
naam :: "PE-activiteit" * ActiviteitNaam [UNI,TOT]

verbonden :: "PE-activiteit" * "PE-punten" [TOT] 
MEANING "Aan een PE-activiteit zijn altijd PE-punten verbonden."
PURPOSE RELATION verbonden ["PE-activiteit" * "PE-punten"] REF "Art. 11 BPEWbtv"
{+Een overzicht  van  de  aangewezen  PE-activiteiten , onder vermelding van  het daaraan verbonden aantal PE-punten, wordt gepubliceerd op www.bureaubtv.nl.-}


ENDPATTERN



---------------
-- Concepten --
---------------
CONCEPT Vaardigheid "Een vaardigheid is een generieke term voor de vaardigheid als tolk of de vaardigheid als vertaler."
PURPOSE CONCEPT Vaardigheid
{+De wet maakt onderschrijd naar de vaardigheden van een persoon als tolk of als vertaler. Om over dit verschil 
te kunnen spreken, introduceren we een term, die tweewaardig is, `tolk` of `vertaler`.
-}
CONCEPT Taal "Een taal is een taal of dialect die als zodanig is opgenomen in de taallijst."
PURPOSE CONCEPT Taal 
{+Er bestaat een lijst van talen, waarvoor tolken en vertalers in het Rbtv kunnen 
worden ingeschreven of op de Uitwijklijst worden geplaatst. De adviescommissie Talen 
is formeel aangewezen als de beheerder van deze lijst. 
-} 
 
CONCEPT Taalervaring "Een taalervaring is de combinatie van een vaardigheid, een bron- en een doeltaal." 
PURPOSE CONCEPT Taalervaring
{+De Wet schrijft in artikel 2, lid 1.b en c, minimale eisen voor 
waaraan de geregistreerde gegevens van een ingeschreve moeten voldoen. 
Deze minimale gegevens zijn echter op zichzelf niet toereikend, omdat 
het om verschillende combinaties kan gaan van bron- en doeltaal, 
en de of het de vaardigheid als tolk of die van vertaler betreft. 
Hiertoe is het concept `Taalervaring` geïntroduceerd.
Bij vertalingservaring is ook de richting van belang. Dan wordt ook wel gesproken van 
vertaalrichting, maar in deze analyse wordt daarin geen onderscheid 
gemaakt. Bij tolkervaring is de richting niet van belang.
-}

CONCEPT Inschrijfverzoek "Een inschrijfverzoek is een verzoek tot inschrijving in het Rbtv dan wel plaatsing op de uitwijklijst" "Art. 5 BBTV; Art. 3 besluit_uitwijklijst_WBTV"
PURPOSE CONCEPT Inschrijfverzoek REF "Brochure `Wbtf voor tolken en vertalers`"  -- http://www.bureaubtv.nl/pdf/Wbtv%20voor%20tolken%20en%20vertalers.pdf
{+
Voor de aanvraag tot inschrijving in het register wordt gebruik gemaakt
van een daartoe ter beschikking gesteld formulier. Bij de aanvraag tot inschrijving 
dienen de in artikel 6 van het besluit btv genoemde bescheiden te worden overlegd. 

Om op de Uitwijklijst geplaatst te worden dient de tolk of vertaler te voldoen aan 
de in het Besluit Uitwijklijst genoemde voorwaarden. Om te kunnen beoordelen of een 
tolk of vertaler aan de voorwaarden voldoet, dient deze een verzoek om plaatsing op 
de Uitwijklijst in te dienen, waarbij de in artikel 4 genoemde bescheiden dienen te 
worden overgelegd.
-}
CONCEPT Inschrijvingsbesluit "Een inschrijvingsbesluit is een besluit naar aanleiding van een inschrijfverzoek."
SPEC Inschrijvingsbesluit ISA Besluit

CONCEPT Inschrijving "Een inschrijving is de registratie van één taalervaring van een specifiek persoon."
PURPOSE CONCEPT Inschrijving 
{+In het Besluit beëdigde tolken en vertalers, artikel 10, is een opsomming gegeven van gegevens 
die in elk geval in het register zijn opgenomen. Hieronder vallen de taalervaringen van de tolk of 
vertaler. Om onduidelijkheid met betrekking tot verlenging en pas-uitgifte te voorkomen, wordt elke 
taalervaring van een persoon door middel van een aparte inschrijving geregistreerd.-}

CONCEPT Wbtvnummer "Het Wbtv-nummer is een nummer waaronder de beëdigd tolk of beëdigd vertaler is geregistreerd in het register"
PURPOSE CONCEPT Wbtvnummer
{+In de Regeling legitimatiebewijs beëdigde tolken en vertalers, art. 2.1 is aangegeven dat er een nummer is waarmee tolken en vertalers kunnen zijn geregistreerd. 
In praktijk hebben ook tolken en vertalers die zijn geplaatst op de uitwijklijst een Wbtv nummer. Dit nummer wordt gebruikt om op een eenduidige manier naar personen te kunnen refereren. -}

CONCEPT "PE-puntentoekenning" "Een puntentoekenning is een besluit om een aantal PE-punten toe te kennen aan een tolk of vertaler die op de uitwijklijst is geplaatst of is ingeschreven in het Rbtv." "Art. 9 BPEWbtv"
CONCEPT "Scholingsactiviteit" "Een scholingsactiviteit is een scholingsactiviteit zoals bedoeld in artikel 11 lid b van het besluit beëdigde tolken en vertalers" "Art. 11 lid b BBTR"
CONCEPT "PE-activiteit" "Een PE-activiteit is een scholingsactiviteit die als PE-activiteit is aangewezen." "Art. 10 BPEWbtv"
SPEC "PE-activiteit" ISA Scholingsactiviteit






PATTERN Overige
RULE vaardigheden : I[Vaardigheid] = 'Tolk' \/ 'Vertaler'
MEANING "Vaardigheden die wettelijk geregistreerd worden, zijn 'Tolk' en 'Vertaler'."
PURPOSE RULE "vaardigheden"
{+Om invoerfouten te voorkomen worden de invoertypen voor vaardigheid beperkt tot de waarden 'tolk' en 'vertaler'. 
-}
RULE rbtvOfuwl : I[RbtvOfUwl] = 'Rbtv' \/ 'Uitwijklijst'
MEANING "De registraties waarvan in de Wbtv sprake is zijn 'Rbtv' en 'Uitwijklijst'."
PURPOSE RULE "rbtvOfuwl"
{+Om invoerfouten te voorkomen worden de invoertypen voor rbtvOfuwl beperkt tot de waarden 'Rbtv' en 'Uitwijklijst'. 
-}
ENDPATTERN
ENDCONTEXT