CONTEXT "MPTrxUserInterfaces" IN ENGLISH

--[Copying MPTrxs (and trigger MPTrx copying)]--
{-It is not obvious what the attributes of a newly created target `MPTrx` should be.
This holds in particular for the `mptrxIsaCasus` attribute,
which determines whether or not the MPTrx serves as a template (example)
or as a real working case.
To facilitate the creation of different MPTrx-flavours, we define a set of 'request'-relations
which, when populaed, will act as requests to create a target MPTrx of a particular flavour:
- `mptrxCreateTemplateReq` requests that the new target MPTrx 
  is a template (sets `mptrxIsaCasus`);
- `mptrxCreateCasusReq` requests that the new target MPTrx 
  is a casus (clears `mptrxIsaCasus`);
The set of requests can easily be expanded by adding rules 
in the application that uses this module.-}
RELATION mptrxCreateCasusReq[Scope*ScopeID] [UNI] -- Create a Casus (with the specified ID) based on an existing MPTrx
ROLE ExecEngine MAINTAINS "Create a new casus (non-template) from an existing MPTrx"
RULE "Create a new casus (non-template) from an existing MPTrx": mptrxCreateCasusReq |- scopeIII;scopeID
VIOLATION (TXT "{EX}_;NewStruct_;Scope"
              ,TXT "_;scopeID_;Scope_;_NEW_;ScopeID_;", TGT I
              ,TXT "_;scopeIII_;Scope_;", SRC I[Scope], TXT "_;Scope_;_NEW"
              ,TXT "_;mptrxIsaCasus_;Scope_;_NEW_;Scope_;_NEW"
          ,TXT "{EX}_;DelPair_;mptrxCreateCasusReq_;Scope_;", SRC I[Scope], TXT "_;ScopeID_;", TGT I
          )

{- One could argue that the following two relation and rule 
should be moved to some INTERFACE-context. 
One argument would be that the [UNI] constraint is only there 
to make such INTERFACEs work nicely.-}
RELATION mptrxCreateTemplateReq[Scope*ScopeID] [UNI] -- MPTrx is being copied to another MPTrx that has (another) ScopeID
ROLE ExecEngine MAINTAINS "Create a new template (non-casus) from an existing MPTrx"
RULE "Create a new template (non-casus) from an existing MPTrx": mptrxCreateTemplateReq |- scopeIII;scopeID
VIOLATION (TXT "{EX}_;NewStruct_;Scope"
              ,TXT "_;scopeID_;Scope_;_NEW_;ScopeID_;", TGT I
              ,TXT "_;scopeIII_;Scope_;", SRC I[Scope], TXT "_;Scope_;_NEW"
          ,TXT "{EX}_;DelPair_;mptrxCreateTemplateReq_;Scope_;", SRC I[Scope], TXT "_;ScopeID_;", TGT I
          )

--[Deleting a transaction]--
-- Correctly deleting a transaction (Scope) requires that all artefacts within the Scope are deleted as well. This is a requirement for stuff that uses scopes. Hence, we can rest assured that all TTexts (Objectives, Variables, TParties) are also (automatically) deleted.
scopeDelReq :: Scope * Scope [PROP] -- Request for deleting the Scope
ROLE ExecEngine MAINTAINS "Delete a Scope upon request"
RULE "Delete a Scope upon request": scopeDelReq |- -V
VIOLATION (TXT "{EX} DelAtom;Scope;", SRC I) -- This also removes the scopeDelReq.

--[Creating a new casus]--
accCreateCasusReq :: Account * Account [PROP]
accNewCasusID :: Account * ScopeID [UNI]
accMPTrxTemplate :: Account * Scope [UNI]
RULE "You can only copy from Templates, not from cases": accMPTrxTemplate |- accMPTrxTemplate;(I-mptrxIsaCasus)

ROLE ExecEngine MAINTAINS "Create a new, user-owned casus from an existing MPTrx"
RULE "Create a new, user-owned casus from an existing MPTrx":
   accCreateCasusReq
    /\ accMPTrxTemplate;accMPTrxTemplate~
    /\ accNewCasusID;accNewCasusID~ 
|- scopeOwner~;mptrxIsaCasus
   ;(  scopeIII;accMPTrxTemplate~
    /\ scopeID;accNewCasusID~
    )
VIOLATION (TXT "{EX}_;NewStruct_;Scope"
              ,TXT "_;mptrxIsaCasus_;Scope_;_NEW_;Scope_;_NEW"
              ,TXT "_;scopeIII_;Scope_;", SRC accMPTrxTemplate, TXT "_;Scope_;_NEW"
              ,TXT "_;scopeID_;Scope_;_NEW_;ScopeID_;", SRC accNewCasusID
              ,TXT "_;scopeOwner_;Scope_;_NEW_;Account_;", SRC I
          ,TXT "{EX} DelPair;accCreateCasusReq;Account;", SRC I, TXT ";Account;", TGT I
          ,TXT "{EX} DelPair;accMPTrxTemplate;Account;", SRC I, TXT ";Scope;", TGT accMPTrxTemplate
          ,TXT "{EX} DelPair;accNewCasusID;Account;", SRC I, TXT ";ScopeID;", TGT accNewCasusID
          )
ROLE ExecEngine MAINTAINS "Reset `accCreateCasusReq`"
RULE "Reset `accCreateCasusReq`":
   accCreateCasusReq |- accMPTrxTemplate;V;accNewCasusID~
VIOLATION (TXT "{EX} DelPair;accCreateCasusReq;Account;", SRC I, TXT ";Account;", TGT I)

INTERFACE "CreateNewCaseForAccount" FOR User: I[Account] /\ sessionAccount~;"_SESSION";sessionAccount cRud BOX <COLSNL>
   [ "Required arguments": I[Account] cRud COLS
      [ "CasusID": accNewCasusID cRUd
      , "Template": accMPTrxTemplate cRUd
      ]
   , "Buttons": I cRud BOX <DIV>
      [ "empty line": I-I cRud
      , "another empty line": I-I cRud
      , "Setup new transaction":  I[Account] cRud BOX <PropBtnPrimary> 
         [ "property": accCreateCasusReq cRUd
         , "disabled": I-(accMPTrxTemplate;V;accNewCasusID~) cRud
--       , "popovertext": V;"To enable, fill in the Template and CasusID fields"[IfcText] cRud
         ]
      ]
   ]
POPULATION IfcText CONTAINS [ "To enable, fill in the Template and CasusID fields"]

--[Deleting/Disabeling Objectives]--
-- You can delete an objective if it is not a copy, or if it is a copy and its meaning has been altered.
ttDelObjectiveReq :: TText * TText [PROP] -- Request for deleting the TText
RULE "ttDelObjectiveReq is only valid for Objectives": ttDelObjectiveReq |- ttIsaObjective

ROLE ExecEngine MAINTAINS "Delete an Objective that is not a copy of a (template) Objective"
RULE "Delete an Objective that is not a copy of a (template) Objective":
   ttDelObjectiveReq |- ttICO;ttICO~
VIOLATION (TXT "{EX} DelAtom;TText;", SRC I) -- This also removes the ttDelObjectiveReq.

ROLE ExecEngine MAINTAINS "Delete an Objective that is a copy of a (template) Objective with a modified template text"
RULE "Delete an Objective that is a copy of a (template) Objective with a modified template text":
   ttDelObjectiveReq;ttICO;ttTemplate |- ttTemplate
VIOLATION (TXT "{EX} DelAtom;TText;", SRC I) -- This also removes the ttDelObjectiveReq.

ROLE ExecEngine MAINTAINS "Delete an Objective that is a copy of a (template) Objective with a modified description"
RULE "Delete an Objective that is a copy of a (template) Objective with a modified description":
   ttDelObjectiveReq;ttICO;ttDescr |- ttDescr
VIOLATION (TXT "{EX} DelAtom;TText;", SRC I) -- This also removes the ttDelObjectiveReq.

-- You cannot delete an objective if it is a copy (with an unchanged meaning), as it will automatically be re-created.
-- You can, however, disable it. **2do: work objvIsDisabled through the entire application**
objvIsDisabled :: TText * TText [PROP]
RULE "objvIsDisabled is only valid for Objectives": objvIsDisabled |- ttIsaObjective

--[TParties]--
tPartyClaimSeatReq :: TText * TText [PROP] -- Party starts to participate as a TParty.
ROLE ExecEngine MAINTAINS "Claim a free TParty SHRole"
RULE "Claim a free TParty SHRole":
   (ttIsaTParty-(tPartyAcc;tPartyAcc~));tPartyClaimSeatReq;V;"_SESSION";sessionAccount |- tPartyAcc
VIOLATION (TXT "{EX} InsPair;tPartyAcc;TText;", SRC I, TXT ";Account;", TGT I)
ROLE ExecEngine MAINTAINS "Reset tPartyClaimSeatReq"
RULE "Reset tPartyClaimSeatReq": tPartyClaimSeatReq |- ttIsaTParty-(tPartyAcc;tPartyAcc~)
VIOLATION (TXT "{EX} DelPair;tPartyClaimSeatReq;TText;", SRC I, TXT ";TText;", TGT I)

-- Leaving a seat means that the account that 'occupies' a TParty/SHRole is discarded. As a consequence, all objectives and variables owned by that account are also deleted. If any of these discarded objectives/variables was a copy of a objective/variable of a MPTrx that was included, it will be automatically re-created.
tPartyLeaveSeatReq :: TText * TText [PROP] -- TParty leaves the game, either on its own request or he is kicked by the Scope owner.
ROLE ExecEngine MAINTAINS "Leave or Kick a TParty SHRole"
RULE "Leave or Kick a TParty SHRole": 
      tPartyLeaveSeatReq;(tPartyAcc \/ ttScope;scopeOwner)
   /\ V;"_SESSION";sessionAccount
|- (ttIsaTParty-(tPartyAcc;tPartyAcc~));V
VIOLATION (TXT "{EX} DelPair;tPartyAcc;TText;", SRC I, TXT ";Account;", TGT I)

ROLE ExecEngine MAINTAINS "Reset tPartyLeaveSeatReq"
RULE "Reset tPartyLeaveSeatReq": 
   tPartyLeaveSeatReq -- a tPartyLeaveSeatReq is valid if:
|- (ttIsaTParty /\ tPartyAcc;tPartyAcc~) -- the TParty is occupied
   ;(tPartyAcc \/ ttScope;scopeOwner) -- and either the TParty or the scope Owner
   ;sessionAccount~;"_SESSION";V -- have issued the request
MEANING "No longer valid, or invalid tPartyLeaveSeatReqests are discarded"
VIOLATION (TXT "{EX} DelPair;tPartyLeaveSeatReq;TText;", SRC I, TXT ";TText;", SRC I)

--[Overviews]--
INTERFACE "Transactions" FOR User: "_SESSION"[SESSION];sessionAccount cRud TABS
   [ "My cases": I[Account] cRud BOX <DIV>
      [ "Create new case": I[Account] INTERFACE "CreateNewCaseForAccount"
      , "Empty line" : I-I cRud
      , "Only show cases when there is somtheing to whow":
         I /\ (tPartyAcc~;ttScope \/ scopeOwner~);mptrxIsaCasus;V cRud BOX <DIV>
         [ "Header": V;"Cases in which I participate:"[IfcText] cRud <STRONG>
         , "Cases in which I participate": (tPartyAcc~;ttScope \/ scopeOwner~);mptrxIsaCasus INTERFACE "ShowMPTrxSummary"
         ]
      ]
   , "Cases I may join":                     -- First, we look for TParties (seats, SHRoles) where 
      ( ( V;(I-tPartyReqdOrg;tPartyReqdOrg~) -- either the TParty does not require a specific organization
        \/ accOrg;tPartyReqdOrg~             -- or the user(account) is one of a req'd organization
        );(ttIsaTParty-tPartyAcc;tPartyAcc~) -- Then, the TParty must still be unoccupied
        ;ttScope;mptrxIsaCasus               -- and it must be a TParty in an MPTrx-casus
      - (tPartyAcc~;ttScope \/ scopeOwner~)  -- and we exclude cases in which the user is already a TParty or owner
      ) INTERFACE "ShowMPTrxSummary"
   ]
POPULATION IfcText CONTAINS [ "Cases in which I participate:"]
--INTERFACE "My Profile" FOR User: "_SESSION"[SESSION];sessionAccount cRud TABS
--   [ "MPTrx templates": 
--      scopeOwner~;(I-mptrxIsaCasus) CRuD -- Create and/or maintain my templates
--      INTERFACE "CreateOrEditMyMPTrxTemplate"
--   ]

--[MPTrxs/Scopes]--
INTERFACE "New MPTrx" FOR User: I[Scope] INTERFACE "CreateOrEditMyMPTrxTemplate"
INTERFACE "CreateOrEditMyMPTrxTemplate" FOR User: -- Define/edit a MPTrx / template that I own
   (I[Scope]-mptrxIsaCasus) /\ scopeOwner;sessionAccount~;"_SESSION";V cRuD COLS 
   [ "MPTrx template": I cRud LINKTO INTERFACE "MPTrx(TemplateOwnedByUser)"
   , "Name/ID": scopeID cRUd -- needed in case a new template is being created
   , "Description" : scopeDescr cRUd
   ]

--[MPTrx templates]--
INTERFACE "MPTrx(TemplateOwnedByUser)" FOR User: 
   (I[Scope]-mptrxIsaCasus) /\ scopeOwner;sessionAccount~;"_SESSION";V cRuD TABS
   [ "MPTrx Definition": I[Scope] cRud BOX <DIV>
      [ "MPTrxs": I[Scope] cRud COLS
         [ "Template ID" : scopeID cRud
         , "Object" : mptrxObjName cRud 
         , "Description" : scopeDescr cRUd
         , "Owner" : scopeOwner cRud
      -- , "Save as template (ID)": mptrxCreateTemplateReq cRUd -- Create a copy that is a template
      -- , "New case (ID)": mptrxCreateCasusReq   cRUd -- Create a copy that is not a template
      -- , "Copy to (ID)": scopeCarbonCopyReq   cRUd -- Create a carbon copy (maintain 'template' property)
         , "Status" : I[Scope] cRud <MPTrxStatus>
         ]
      , "Parties" : I[Scope] INTERFACE "TPartyManagement" -- <TPartyManagement>
      , "Objectives": ttScope~;ttIsaObjective INTERFACE "EditMyObjectives" -- The INTERFACE selects the objectives owned by the user
      ]
   , "TParties": ttScope~ cRud COLS
      [ "Name" : ttName cRud
      , "Description" : ttDescr cRUd
      , "Req'd Org(s)" : tPartyReqdOrg cRUd --**Identity issje: This should become tPartyReqdOrgRef, for the purpose of automatically creating Organizations.
      ]
   , "Data Management/Semantics": ttScope~;ttIsaVariable cRud BOX <SHCOLS>
      [ "Name" : ttName cRud -- We do not allow for name changes, as this requires an update of all ttTemplate's in which it is used.
      , "Value supplier" : ttValSupName cRUd
      , "LoA" : ttReqdLoA cRUd -- supplier required authentication level
--    , "Vlvl" : ttReqdLoV cRUd -- TText reuqired validation level
      , "Specification" : ttTemplate cRUd
      , "Prm" : ttPromise cRUd <TTextCheckbox> -- evt. nog een template met uitleg over 'Prm'
      , "Acc" : ttAccept cRUd <TTextCheckbox> -- evt. nog een template met uitleg over 'Acc'
      , "Needed for" : ttIsUsedBy;ttIsaVariable cRud LINKTO INTERFACE "EditTemplateVariables"
      ]
   ]

tPartyNewObjectiveName :: TText * TTName [UNI] -- Request a new (named) objective to be created for this TParty
ROLE ExecEngine MAINTAINS "Service request for creating a new TParty-objective"
-- file "../TText/TParty.adl" specifies that ttValSuppliers are created/maintained by reference (i.e. by their name) rather than by the TParty object.
-- file "./MPTrx_Basics.als" specifies that `objvSHRoleName` is the name/reference to be used for Objectives (so that we can ensure that every Objective is related to precisely one stakeholder)
RULE "Service request for creating a new TParty-objective":
   (ttIsaTParty /\ tPartyAcc;tPartyAcc~ /\ ttName;ttName~);tPartyNewObjectiveName
|- ttName;objvSHRoleName~;ttIsaObjective;ttName
VIOLATION (TXT "{EX} NewStruct;TText"
              ,TXT ";ttIsaObjective;TText;_NEW;TText;_NEW"
              ,TXT ";ttScope;TText;_NEW;Scope;", SRC ttScope -- Objective is in same scope as the TParty that created it.
              ,TXT ";ttValSupplier;TText;_NEW;TText;", SRC I -- ValSupplier of the Objective is the TParty that created it.
              ,TXT ";ttValSupName;TText;_NEW;TTName;", SRC ttName -- ValSupplier of the Objective is the TParty that created it.
              ,TXT ";objvSHRoleName;TText;_NEW;TTName;", SRC ttName
              ,TXT ";ttName;TText;_NEW;TTName;", TGT I
              ,TXT ";ttOwner;TText;_NEW;Account;", SRC tPartyAcc -- Objective is owned by the TParty (Account) that created it.
          ,TXT "{EX} DelPair;tPartyNewObjectiveName;TText;", SRC I, TXT ";TTName;", TGT I
          )
ROLE ExecEngine MAINTAINS "Delete request for creating a new TParty-objective"
RULE "Delete request for creating a new TParty-objective":
   ttIsaTParty;ttName;ttValSupName~;ttIsaObjective;ttName |- -tPartyNewObjectiveName
VIOLATION (TXT "{EX} DelPair;tPartyNewObjectiveName;TText;", SRC I[TText], TXT ";TTName;", TGT I)

--[MPTrx cases]--
INTERFACE "MPTrxCasus" FOR User: mptrxIsaCasus cRud TABS
   [ "Parties" : I[Scope] cRud BOX <DIV> 
      [ "Transaction": I INTERFACE "ShowMPTrxSummary"
      , "Parties": I[Scope] INTERFACE "TPartyManagement" -- <TPartyManagement>
      ]
   , "My to-do list" : I[Scope] cRud BOX <DIV>
      [ "Transaction": I INTERFACE "ShowMPTrxSummary"
      , "Only show unevaluatable Variables if they exist":
           I /\ V;"_SESSION"
                ;(  sessionAuthISOLevel;-isoLevelGTE;ttReqdLoA~
                 /\ sessionAccount;ttValSupAcc~
                 )
                ;ttScope cRud BOX <DIV>
         [ "Unevaluatable Variables": I INTERFACE "UnEvaluatableVariables"
         ]
      , "Only show to-do Variables if they exist":
           I /\ 
             ttScope~ -- All TTexts in the MPTrx
             ;(  (ttIsaVariable-ttValue;ttValue~) -- with the property that they are a Variable without an assigned value
              /\ V[TText*SESSION];"_SESSION"  -- and the user is authenticated with a LoA that is
                 ;(  sessionAuthISOLevel;isoLevelGTE;ttReqdLoA~ -- sufficiently large to allow editing
                  /\ sessionAccount;ttValSupAcc~ -- and the user occupies the SHRole that must provide the value
              )   )
              ;V
           cRud BOX <DIV>
         [ "My to-do Variables": 
             ttScope~ -- All TTexts in the MPTrx
             ;(  (ttIsaVariable-ttValue;ttValue~) -- with the property that they are a Variable without an assigned value
              /\ V[TText*SESSION];"_SESSION"  -- and the user is authenticated with a LoA that is
                 ;(  sessionAuthISOLevel;isoLevelGTE;ttReqdLoA~ -- sufficiently large to allow editing
                  /\ sessionAccount;ttValSupAcc~ -- and the user occupies the SHRole that must provide the value
              )   ) 
               INTERFACE "FillInVariables"
         ]
      , "Only show to-do Objectives if they exist": 
           I /\ ttScope~ -- All TTexts in the MPTrx
                ;(objvIsEnabled-objvIsAddressed) -- with the property that they are enabled, unaddressed Objectives
                ;ttValSupAcc;sessionAccount~;"_SESSION" -- and the user occupies the SHRole that must provide the value
                ;V
           cRud BOX <DIV>
         [ "Objectives": 
             ttScope~ -- All TTexts in the MPTrx
             ;(objvIsEnabled-objvIsAddressed) -- with the property that they are enabled, unaddressed Objectives
              INTERFACE "EvaluateObjective" -- Ownership is considered by the interface.
         ]
      ]
   , "Revise/Supply Data" : I[Scope] cRud BOX <DIV> -- These are only TTexts that the user must supply values for
      [ "Transaction": I INTERFACE "ShowMPTrxSummary"
      , "Only show evaluatable Variables if they exist":
           I /\ V;"_SESSION"
                ;(  sessionAuthISOLevel;isoLevelGTE;ttReqdLoA~
                 /\ sessionAccount;ttValSupAcc~
                 )
                ;ttScope cRud BOX <DIV>
         [ "Variables to be valuated": I INTERFACE "EvaluatableVariables"
         ]
      , "Only show unevaluatable Variables if they exist":
           I /\ V;"_SESSION"
                ;(  sessionAuthISOLevel;-isoLevelGTE;ttReqdLoA~
                 /\ sessionAccount;ttValSupAcc~
                 )
                ;ttScope cRud BOX <DIV>
         [ "Current authentication level": V[Scope*SESSION];"_SESSION" cRud ROWS [ "You are authenticated at level" : sessionAuthISOLevel cRud <ShowISOLevel> ]
         , "Unevaluatable Variables": I INTERFACE "UnEvaluatableVariables"
         ]
      ]
   , "(Re)evaluate Objectives" : I[Scope] cRud BOX <DIV> -- These are objectives the user must evaluate
      [ "Transaction": I INTERFACE "ShowMPTrxSummary"
      , "Only show Objectives if they exist": I /\ ttScope~;objvIsEnabled;ttScope cRud BOX <DIV>
         [ "Objectives" : ttScope~;objvIsEnabled INTERFACE "EvaluateObjective"
         ]
      ]
   , "Data transfers & Privacy" : I[Scope] cRud BOX <DIV>
      [ "Transaction": I INTERFACE "ShowMPTrxSummary"
      , "Only show DataTransfers if they exist for the user": 
         I /\ dtrfScope~;(dtrfSrc\/dtrfTgt)
              ;tPartyAcc;sessionAccount~;"_SESSION";V 
         cRud BOX <DIV>
         [ "Data transfers" : I[Scope] INTERFACE "ShowDataTransfers"
         ]
      ]
   , "Revise Objective Semantics" : I[Scope] cRud BOX <DIV> -- These are objectives owned by the user in his capacity as a TParty
      [ "Transaction": I[Scope] INTERFACE "ShowMPTrxSummary"
      , "My Seat(s)" : ttScope~;(ttIsaTParty /\ tPartyAcc;sessionAccount~;"_SESSION";V) 
         cRud BOX <DIV>
         [ "TParty header": I{-TParty-} cRud BOX <COLSNL>
            [ "TParty text": V;"SHRole:"[IfcText] cRud
            , "TParty name": I{-TParty-} cRud
            , "New objective text": V;"New objective:"[IfcText] cRud
            , "New objective name": tPartyNewObjectiveName cRUd
--          , "Specify new objective": I cRud BOX <PropBtnPrimary> [ "property": tPartyNewObjectiveName cRUd ]
--          , "Include template text": V;"    Include/import template"[IfcText] cRud
--          , "Include template": (ttScope~;tPartyOrg /\ V;"_SESSION";sessionOrg);tPartyImportScope
            ]
         ]
      , "My Objectives" : 
           ttScope~;(ttIsaTParty /\ tPartyAcc;sessionAccount~;"_SESSION";V) -- this is the list of TParties/SHRoles/seats that the user occupies
           ;ttValSupplier~;ttIsaObjective
         INTERFACE "EditMyObjectives" -- The INTERFACE selects the objectives owned by the user
      , "My Variables": 
           ttScope~;(ttIsaTParty /\ tPartyAcc;sessionAccount~;"_SESSION";V)  -- RULE "Re-assign variable ownership" ensures appropriate ownership of variables
           ;tPartyAcc;(I /\ V;"_SESSION";sessionAccount);ttOwner~;ttIsaVariable
         INTERFACE "EditTemplateVariables" -- The INTERFACE selects the variables owned by the user
      , "My ValSuppliers" : 
           ttScope~;(ttIsaTParty /\ ttOwner;sessionAccount~;"_SESSION";V) -- this is the list of TParties/SHRoles/seats that the user occupies
         INTERFACE "EditMyTParties" 
      ]
   ]
POPULATION IfcText CONTAINS [ "SHRole:", "New objective:"]
POPULATION IfcText CONTAINS [ "    Include/import template"]

--[TParties]--
INTERFACE "EditMyTParties" FOR User:
   ttIsaTParty /\ ttIsaCasus /\ ttOwner;sessionAccount~;"_SESSION";V
   cRud BOX <SHCOLS>
   [ "Name" : ttName cRUd
   , "Description" : ttDescr cRUd
   , "Req'd Org(s)" : tPartyReqdOrg cRUd
-- , "Owner" : ttOwner cRud -- This is automatically filled in to be the session user
   , "ValSupplier for": ttValSupplier~ cRud 
   ]

INTERFACE "TPartyManagement" FOR User: ttScope~;ttIsaTParty cRud BOX <SHCOLS>
   [ " ": ttIsaTParty /\ ttScope;(I-mptrxIsPromisable);ttScope~ cRud BOX <COLSNL> -- Buttons, but only when transaction is NOT yet promised
      [  "Claim Role": -- user can claim a SHRole if...
            I-(tPartyAcc;tPartyAcc~)  -- the SHRole is not occupied
            /\ ( (ttIsaTParty-(tPartyReqdOrg;tPartyReqdOrg~)) -- and either no specific organization is required for this TParty
               \/ V;"_SESSION";sessionOrg;tPartyReqdOrg~ -- or the user is in (one of) the required organization(s).
               )
            cRud BOX <PropBtnSuccess> [ "property": tPartyClaimSeatReq cRUd ]
      , "Leave Role": -- user can leave a SHRole if... 
            I /\ tPartyAcc;sessionAccount~;"_SESSION";V -- (s)he occupies that SHRole
            cRud BOX <PropBtnWarning> [ "property": tPartyLeaveSeatReq cRUd, "popovertext": ttName cRud ]
      , "Kick Party": -- user can kick another party/user off a SHRole if...
            I /\ tPartyAcc;-I;sessionAccount~;"_SESSION";V -- it is another part that occupies the SHRole
              /\ ttScope;scopeOwner;sessionAccount~;"_SESSION";V -- and the user himself owns the Casus
            cRud BOX <PropBtnDanger> [ "property" : tPartyLeaveSeatReq cRUd, "popovertext": ttName cRud ]
      , "Reserved": -- user cannot claim the (unoccupied) SHRole, because he does not belong to a required organization
            I-(tPartyAcc;tPartyAcc~)  -- the SHRole is not occupied
            /\ tPartyReqdOrg;tPartyReqdOrg~ -- and the SHRole requires someone from a specific organization
            /\ V;"_SESSION";-(sessionOrg;tPartyReqdOrg~) -- and the user is not of such an organization.
            cRud BOX <PropBtnLink> [ "property": tPartyLeaveSeatReq cRUd, "popovertext": V;"You are not in one of the required organizations"[PopOverText] cRud ]
      ]
   , "Occupied by": tPartyAcc cRud
   , "Name": ttName cRud
   , "Description": ttDescr cRud
   , "Req'd Org(s)": tPartyReqdOrg cRud
   ]
POPULATION PopOverText CONTAINS [ "You are not in one of the required organizations" ] REPRESENT PopOverText TYPE ALPHANUMERIC

--[Objectives]--
INTERFACE "EditTemplateObjectives" FOR User: -- Allows you to specify objectives for generic stakeholders
   ttIsaObjective-ttIsaCasus cRud BOX <DIV>
   [ "Owned Template Objectives": ttIsaObjective /\ ttOwner;sessionAccount~;"_SESSION";V
      cRud BOX <SHCOLS>
      [ "Objective" : ttName cRUd -- rough description of the objective
      , "for SHRole": objvSHRoleName cRUd -- SHRole that decides whether or not the objective is addressed
      , "Specification" : ttTemplate cRUd -- criteria by which SHRole decides whether or not the objective is addressed
      , "Prm" : ttPromise cRUd <TTextCheckbox> -- evt. nog een template met uitleg over 'Prm'
      , "Acc" : ttAccept cRUd <TTextCheckbox> -- evt. nog een template met uitleg over 'Acc'
--**Hieronder uitbreiden naar 'afgeleide objectives' (a la MirrorMe)**
--      , "Criteria/TTexts" : ttIsUsedBy~ cRud BOX <COLSNL> -- ttIsUsedBy :: TText * TText [IRF,...
--         [ "Var" : I cRud LINKTO INTERFACE "EditTemplateVariables"
--         , "suptext" : V;"SHRole:"[IfcText] cRud
--         , "Supplier" : ttValSupName cRUd
--         , "loatext" : V;"LOA:"[IfcText] cRud
--         , "RqLoA" : ttReqdLoA cRUd
--         ]
--       , "Owner" : ttOwner cRud -- This is automatically filled in to be the session user
      ]
   , "Unowned Template Objectives":  ttIsaObjective - (ttOwner;sessionAccount~;"_SESSION";V)
      cRud BOX <DIV>
      [ "Error" : V;"You cannot specify this Objective because you are not its owner."[ErrMsg] cRud
      ]
   ]
POPULATION IfcText CONTAINS [ "SHRole:", "LOA:"]
POPULATION ErrMsg CONTAINS [ "You cannot specify this Objective because you are not its owner." ] 

INTERFACE "EditMyObjectives" FOR User: -- A (runtime) Objective is mine iff I play the objvSHRrole
   ttIsaObjective /\ ttIsaCasus /\ ttValSupAcc;sessionAccount~;"_SESSION";V
   cRuD BOX <SHCOLS> -- Adding objectives must be done with a request, because all sorts of other stuff needs to be initialized at the same time.
   [ " ":    (ttIsaObjective-ttICO;ttICO~)                 -- You can delete an objective if it is not a copy, 
          \/ (ttIsaObjective /\ ttICO;ttDescr;-I;ttDescr~) -- or if it is a copy and its meaning (description or template) has been altered.
          \/ (ttIsaObjective /\ ttICO;ttTemplate;-I;ttTemplate~)
         cRud BOX <DIV>
      [ "Delete": I cRud BOX <PropBtnPrimary> 
         [ "property": ttDelObjectiveReq cRUd
         , "disabled": ttIsaObjective-(V;"_SESSION";sessionAccount;ttOwner~) cRud
         ]
      ]
   , "  ": ttIsaObjective -- You can enable/disable an objective if it is a copy with unchanged meaning, i.e.:
          /\ ttICO;(ttDescr<>ttDescr~) -- its description has not changed and 
          /\ ttICO;(ttTemplate<>ttTemplate~) -- its template has not changed
         cRud BOX <COLSNL>
      [ "Disable": I-objvIsDisabled cRud BOX <PropBtnPrimary> 
         [ "property": objvIsDisabled cRUd
         , "disabled": ttIsaObjective-(V;"_SESSION";sessionAccount;ttOwner~) cRud
         ]
      , "Enable": objvIsDisabled cRud BOX <PropBtnPrimary> 
         [ "property": objvIsDisabled cRUd
         , "disabled": ttIsaObjective-(V;"_SESSION";sessionAccount;ttOwner~) cRud
         ]
      ]
   , "Objective" : ttName cRUd -- rough description of the objective
-- , "of SHRole": objvSHRoleName cRUd -- SHRole that decides whether or not the objective is addressed
   , "Specification" : ttTemplate cRUd -- criteria by which SHRole decides whether or not the objective is addressed
   , "Prm" : ttPromise cRUd <TTextCheckbox> -- evt. nog een template met uitleg over "Prm"
   , "Acc" : ttAccept cRUd <TTextCheckbox> -- evt. nog een template met uitleg over "Acc"
--**Hieronder uitbreiden naar 'afgeleide objectives' (a la MirrorMe)**
--   , "Criteria/TTexts" : ttIsUsedBy~ cRud BOX <COLSNL> -- ttIsUsedBy :: TText * TText [IRF,...
--      [ "Var" : I cRud LINKTO INTERFACE "EditTemplateVariables"
--      , "suptext" : V;"SHRole:"[IfcText] cRud
--      , "Supplier" : ttValSupName cRUd
--      , "loatext" : V;"LOA:"[IfcText] cRud
--      , "RqLoA" : ttReqdLoA cRUd
--      ]
--    , "Owner" : ttOwner cRud -- This is automatically filled in to be the session user
   ]
POPULATION IfcText CONTAINS [ "SHRole:", "LOA:"]

INTERFACE "EvaluateObjective" FOR User:
   ttIsaCasus
   /\ objvIsEnabled
   /\ ttValSupAcc;sessionAccount~;"_SESSION";V cRud 
   BOX <SCOLS>
   [ " " : I cRud BOX <COLSNL>
      [ "Mark as Addressed": I-objvIsAddressed cRud BOX <PropBtnPrimary>  [ "property": objvIsAddressed cRUd, "popovertext": V;"Clicking the button indicates that you have decided that this objective is addressed"[PopOverText] cRud ]
      , "Reopen Objective" :   objvIsAddressed cRud BOX <PropBtnSecondary> [ "property": objvIsAddressed cRUd, "popovertext": V;"If you decide that this objective is NOT satisfied, you should click this button"[PopOverText] cRud ]
      ]
   , "Objective": I cRud
   , "Specification" : I cRud <TTextShowInstance>
   , "PAVS": I cRud <PAVS> -- Promise, Accept, Valid, Satisfied
   ] 
POPULATION PopOverText CONTAINS [ "Clicking the button indicates that you have decided that this objective is addressed" ]
POPULATION PopOverText CONTAINS [ "If you decide that this objective is NOT satisfied, you should click this button" ]

--[Variables]--
{-'Ownership' is the ability to define or control the definition/specification
of whatever it is that is owned. Hence, if the session user is the ttOwner
of the Variable, it may be edited.-}
INTERFACE "EditTemplateVariables" FOR User: -- Allows you to specify variables for generic stakeholders.
   ttIsaVariable /\ ttOwner;sessionAccount~;"_SESSION";V cRud BOX <SHCOLS>
   [ "Variable" : ttName cRud -- name of the variables
   , "ValSupplier": ttValSupName cRUd -- SHRole that decides whether or not the objective is addressed
   , "Specification" : ttTemplate cRUd -- criteria by which SHRole decides whether or not the objective is addressed
-- , "Defined by" : ttOwner cRud -- This is automatically filled in to be the session user
   , "Reqd LoA" : ttReqdLoA cRUd
-- , "Reqd Validation": ttReqdLoV cRUd
   , "Needed by" : ttIsUsedBy cRud
-- , "Needs" : ttIsUsedBy~ cRud
   ]

INTERFACE "FillInVariables" FOR User: ttIsaVariable-ttIsaTParty cRud BOX <SCOLS>
   [ "Variable": I[TText] cRud
   , "Value": ttValue cRUd
   , "Specification": I[TText] cRud <TTextShowInstance>
--    , "PAVS": I[TText] cRud <PAVS> -- Promise, Accept, Valid, Satisfied
-- , "Validity level": ttActualLoV cRUd
-- , "Required level": ttReqdLoV cRud
--    , "Reqd LoA": ttReqdLoA cRud <ShowISOLevel>
   ] 

INTERFACE "EvaluatableVariables" FOR User: I[Scope] cRud BOX <DIV>
   [ "Headerline 1": V;"Variables to be filled in"[IfcText] cRud <STRONG>
   , "Variables to be filled in": 
        ttScope~ -- All TTexts in the MPTrx
           ;(  (ttIsaVariable) -- with the property that they are a Variable
            /\ V[TText*SESSION];"_SESSION"  -- and the user is authenticated with a LoA that is
               ;(  sessionAuthISOLevel;isoLevelGTE;ttReqdLoA~ -- sufficiently large to allow editing
                /\ sessionAccount;ttValSupAcc~ -- and the user occupies the SHRole that must provide the value
            )   )
--      ;(I/\compVar;compVar~) -- for which there is a Computation
--      ;(I- compVar~;compVar) -- for which there is NOT a Computation
        INTERFACE "FillInVariables"
   ]
POPULATION IfcText CONTAINS [ "Variables to be filled in" ]

INTERFACE "UnEvaluatableVariables" FOR User: -- Unevaluatable because TParty LoA is too low
   ttScope~;V;"_SESSION"
   ;(  sessionAuthISOLevel;-isoLevelGTE;ttReqdLoA~
    /\ sessionAccount;ttValSupAcc~
    );ttIsaVariable cRud BOX <SCOLS>
   [ "LoA required": ttReqdLoA cRud <ShowISOLevelRed>
   , "for Variable": I[TText] cRud
-- , "Value": ttValue cRud
   , "Specification" : I[TText] cRud <TTextShowInstance>
-- , "PAVS": I[TText] cRud <PAVS> -- Promise, Accept, Valid, Satisfied
   ] 

--[Computations]--
INTERFACE "Computations" FOR User: I[Computation] cRud COLS
   [ "Result": compRes cRUd
   , "Specification": compVar cRud <TTextShowInstance>
-- , "Arguments": compArgs cRud
   ]         

--[Viewing stuff]--
INTERFACE "ShowMPTrxSummary" FOR User: I[Scope] cRud BOX <DIV>
   [ "First, we show the MPTrx summary": I[Scope] cRud COLS
      [ " ": I[Scope]-mptrxIsPromisable cRud BOX <DIV> -- Only show the button for unpromised cases
         [ "Delete": I cRud BOX <PropBtnPrimary> 
            [ "property": scopeDelReq cRUd
            , "disabled": I-(V;"_SESSION";sessionAccount;scopeOwner~) cRud
            ]
         ]
      , "MPTrx" :  I[Scope] cRud LINKTO INTERFACE "MPTrxCasus"
      , "Description" : scopeDescr cRud
      , "Owner" : scopeOwner cRud
      , "Status" : I[Scope] cRud <MPTrxStatus>
      , "My Auth.Level" : V[Scope*SESSION];"_SESSION" cRud BOX <DIV> 
         [ "ALvl": sessionAuthISOLevel cRud <ShowISOLevel> ]
      ]
   , "Then, we instruct the user (when needed) to select a Party":    
        (V[Scope*SESSION]-(ttScope~;tPartyAcc;sessionAccount~;"_SESSION"))
        ;V;"If you want to participate, please claim a Party for yourself"[IfcText] <STRONG>
   ]     
POPULATION IfcText CONTAINS [ "If you want to participate, please claim a Party for yourself" ]

INTERFACE "MPTrxStatus" FOR User: I[Scope] cRud BOX <DIV>
   [ "Status": -- We have a casus in which the user is a TParty
         (mptrxIsaCasus /\ V;"_SESSION";sessionAccount;tPartyAcc~;ttScope)
         ;(    (I-mptrxIsPromisable               );V[Scope*MPTrxStatus];"Progressing" -- -p
            \/ (mptrxIsPromisable -mptrxIsAcceptable);V[Scope*MPTrxStatus];"Promised"  --  p /\ -a
            \/ (mptrxIsPromisable/\mptrxIsAcceptable);V[Scope*MPTrxStatus];"Accepted"  --  p /\  a
          )
         \/ -- We have a casus in which the user is not a TParty
            (mptrxIsaCasus-(V;"_SESSION";sessionAccount;tPartyAcc~;ttScope))
            ;V[Scope*MPTrxStatus];"Hidden from non-participants"
         \/ -- We have a template
            (I-mptrxIsaCasus);V;"Template"[MPTrxStatus] cRud
   ] REPRESENT MPTrxStatus TYPE ALPHANUMERIC
POPULATION MPTrxStatus CONTAINS [ "Template", "Hidden from non-participants", "Progressing", "Promised", "Accepted" ]

ENDCONTEXT