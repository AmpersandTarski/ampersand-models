CONTEXT "TPartyIfc" IN ENGLISH

VIEW TParties: TParty(ttName)

INTERFACE "[TParties]" FOR Developer: V[SESSION*MPTrx] cRud BOX <ROWSNL>
   [ "TParties" : I cRud INTERFACE "[TPartyList]"
   ]

INTERFACE "[TPartyList]" FOR Developer: V[MPTrx*TParty] /\ ttScope~ cRud BOX <SHCOLS>
   [ "TParty": I[TParty] cRud LINKTO INTERFACE "[TParty]"
   , "is also a":
             I[Concern];V;'Concern'[TTextType]
          \/ I[Variable];V;'Variable'[TTextType] 
--        \/ I[TParty];V;'TParty'[TTextType]
          \/ (I[TText]-ttScope;I[MPTrx];ttScope~);V;'Out of scope'[TTextType] cRud
   , "Value": ttValue cRud
   , "Description": ttDescr cRud
   , "Account" : tPartyAcc cRud
   , "Req.Orgs": tPartyReqdOrg cRud
   , "Supplies value for": ttValSupplier~;ttName cRud
   , "Owner" : ttOwner cRud
   ]

INTERFACE "[TParty]" FOR Developer: I[TParty] cRud BOX <ROWSNL>
   [ "Definition": I[TParty] cRud COLS
      [ "TParty": I[TParty] cRud
      , "is also a":
             I[Concern];V;'Concern'[TTextType]
          \/ I[Variable];V;'Variable'[TTextType] 
--        \/ I[TParty];V;'TParty'[TTextType]
          \/ (I[TText]-ttScope;I[MPTrx];ttScope~);V;'Out of scope'[TTextType] cRud
      , "Specification": I[TText] cRud <TTextShowInstance>
      , "Description (if different from spec)":
             (I[TText] /\ ttDescr;-I;ttInstance~
                       /\ ttDescr;-I;ttTemplate~
             );ttDescr cRud
      , "Owner" : ttOwner cRud
      ]
   , "Instantiation": I cRud COLS
      [ "Value": ttValue cRud
      , "Account" : tPartyAcc cRud
      , "Req.Orgs": tPartyReqdOrg cRud
      , "Supplies value for": ttValSupplier~;ttName cRud
      , "Is mentioned in": ttIsUsedBy cRud
      ]
   , "Context/Scope": ttScope cRud INTERFACE "[MPTrxSummary]" 
   ]

ENDCONTEXT