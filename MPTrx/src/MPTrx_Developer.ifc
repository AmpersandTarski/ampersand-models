CONTEXT MPTrxDeveloperInterfaces IN ENGLISH

--[Copying Scopes (and trigger Scope copying)]--
RELATION scopeCreateCopyReq[Scope*ScopeID] [UNI] -- Create a copy of an existing scope, (with the newly specified ScopeID)
ROLE ExecEngine MAINTAINS "Create a copy of the Scope"
RULE "Create a copy of the Scope": scopeCreateCopyReq |- scopeIII;scopeID
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeID;Scope;_NEW;ScopeID;", TGT I
              ,TXT ";scopeIII;Scope;", SRC I, TXT ";Scope;_NEW"
          ,TXT "{EX} DelPair;scopeCreateCopyReq;Scope;", SRC I, TXT ";ScopeID;", TGT I
          )

--[Views]--
--VIEW ScopeCheckbox: Scope HTML TEMPLATE "View-PROPERTY.html" ENDVIEW -- Enable Scope-properties to be shown as checkboxes
VIEW Scopes: Scope(scopeID)
{- BOX <PROPBUTTON> -- is a custom HTML Template (i.e. it is in the local templates/views directory)
   [ disabled : <expr1> -- The button is disabled when `expr1` has one or more target atoms.
   , property : <expr2> -- `expr2` **must be a [PROP]-type relation**; it gets populated whenever the button is clicked. 
   ]
-}

CLASSIFY ScopeID ISA IfcText -- This allows ScopeIDs to viewed by the <STRONG> template

INTERFACE "[DEV Overview]" FOR Developer: "_SESSION" cRud TABS
   [ "MPTrxs"       : V[SESSION*Scope] INTERFACE "[MPTrxSummary]"
   , "Objectives"   : V[SESSION*Scope] cRud BOX <RAW> [ "MPTrx" : scopeID <STRONG>, "Objectvs": I INTERFACE "[ObjectiveList]"    ,"separator": I-I cRud ]
   , "Variables"    : V[SESSION*Scope] cRud BOX <RAW> [ "MPTrx" : scopeID <STRONG>, "Variabls": I INTERFACE "[VariableList]"     ,"separator": I-I cRud ]
   , "DataTransfers": V[SESSION*Scope] cRud BOX <RAW> [ "MPTrx" : scopeID <STRONG>, "Dtrxfers": I INTERFACE "[DataTransferList]" ,"separator": I-I cRud ]
   , "TParties"     : V[SESSION*Scope] cRud BOX <RAW> [ "MPTrx" : scopeID <STRONG>, "TParties": I INTERFACE "[TTPartyList]"       ,"separator": I-I cRud ]
--now follows general stuff
   , "Scopes"       : V[SESSION*Scope] INTERFACE "[ScopeSummary]"
   , "TTexts"       : V[SESSION*Scope] cRud BOX <RAW> [ "Scope" : scopeID <STRONG>, "TTexts"  : I INTERFACE "[TTextList]"        ,"separator": I-I cRud ]
-- , "TParties"     : V[SESSION*Scope] cRud BOX <RAW> [ "Scope" : scopeID <STRONG>, "TParties": I INTERFACE "[TTPartyList]"       ,"separator": I-I cRud ]
   , "Assignments"  : V[SESSION*Scope] cRud BOX <RAW> [ "Scope" : scopeID <STRONG>, "Assgnmts": I INTERFACE "[AssignmentList]"   ,"separator": I-I cRud ]
   , "Computations" : V[SESSION*Scope] cRud BOX <RAW> [ "Scope" : scopeID <STRONG>, "Compttns": I INTERFACE "[ComputationList]"  ,"separator": I-I cRud ]
   ]

--[MPTrxs]--
POPULATION IfcText CONTAINS [ "Out of scope" ]

INTERFACE "[MPTrxSummary]" FOR Developer: I[Scope] cRuD BOX <TABLE>
   [ "MPTrx ID": I LINKTO INTERFACE "[MPTrx]"
   , "Description": scopeDescr cRud
--   , "CC?" : scopeIsaCC cRUd -- Carbon Copy?
   , "includes" : scopeIII~ cRud
   , "incl. by" : scopeIII cRud
   , "Owner": scopeOwnerAcc cRud
   ]

INTERFACE "[MPTrx]" FOR Developer: I[Scope] cRuD BOX <RAW>
   [ "Header" : I cRud BOX <TABLE>
      [ "MPTrx ID": scopeID cRUd
      , "Description": scopeDescr cRUd
--      , "CC?" : scopeIsaCC cRUd -- Carbon Copy?
      , "includes" : scopeIII~ cRUd
      , "incl. by" : scopeIII cRud
      , "Owner": scopeOwnerAcc cRud
      ]
   , "TParties" : I INTERFACE "[TTPartyList]"
   , "Objectives" : I INTERFACE "[ObjectiveList]"
   , "Variables": I INTERFACE "[VariableList]"
   ]

--[TParties]--
POPULATION IfcText CONTAINS [ "TTParty" ]

INTERFACE "[TTPartyList]" FOR Developer: ttScope~;ttIsaTTParty cRuD BOX <TABLE hideNoRecords sortable>
   [ "TTParty": ttIsaTTParty LINKTO INTERFACE "[TTParty]"
   , "ISA:":
             ttIsaObjective;V;"Objective"[IfcText]
          \/ ttIsaVariable;V;"Variable"[IfcText] 
          \/ ttIsaTTParty;V;"TTParty"[IfcText]
          \/ (I[TText]-ttScope;I[Scope];ttScope~);V;"Out of scope"[IfcText] cRud
   , "Value": ttValue cRud
   , "Description": ttDescr cRud
   , "Account" : ttPartyAcc cRud
   , "Req.Orgs": ttPartyReqdPartyRef cRud
   , "Supplies value for": ttValSupplier~;ttName cRud
   , "Owner" : ttOwnerAcc cRud
   , "ValSupplier": ttValSupplier cRud
   ]

INTERFACE "[TTParty]" FOR Developer: ttIsaTTParty cRud BOX <RAW>
   [ "Definition": ttIsaTTParty cRud BOX <TABLE>
      [ "TTParty": ttIsaTTParty cRud
      , "ISA:":
             ttIsaObjective;V;"Objective"[IfcText]
          \/ ttIsaVariable;V;"Variable"[IfcText] 
          \/ ttIsaTTParty;V;"TTParty"[IfcText]
          \/ (I[TText]-ttScope;I[Scope];ttScope~);V;"Out of scope"[IfcText] cRud
      , "Specification": I[TText] cRud <TTextShowInstance>
      , "Description (if different from spec)":
             (I[TText] /\ ttDescr;-I;ttInstance~
                       /\ ttDescr;-I;ttTemplate~
             );ttDescr cRud
      , "Owner" : ttOwnerAcc cRud
      , "ValSupplier": ttValSupplier cRud
      ]
   , "Instantiation": I cRud BOX <TABLE>
      [ "Value": ttValue cRud
      , "Account" : ttPartyAcc cRud
      , "Req.Orgs": ttPartyReqdPartyRef cRud
      , "Supplies value for": ttValSupplier~;ttName cRud
      , "Is mentioned in": ttIsUsedBy cRud
      ]
   , "Context/Scope": ttScope INTERFACE "[MPTrxSummary]" 
   ]
   
--[Objectives]--
POPULATION IfcText CONTAINS [ "Objective" ]

INTERFACE "[ObjectiveList]" FOR Developer: ttScope~;ttIsaObjective cRuD BOX <TABLE hideNoRecords sortable>
   [ "Objective": ttIsaObjective LINKTO INTERFACE "[Objective]"
   , "of TTParty": ttValSupName cRud -- SHRole that decides whether or not the objective is addressed
   , "ISA:":
             ttIsaTTParty;V;"TTParty"[IfcText]
          \/ ttIsaObjective;V;"Objective"[IfcText]
          \/ ttIsaVariable;V;"Variable"[IfcText] 
          \/ (I[TText]-ttScope;I[Scope];ttScope~);V;"Out of scope"[IfcText] cRud
-- , "Value": ttValue cRud
   , "Specification": I[TText] cRud <TTextShowInstance>
   , "PAS": I[TText] cRud <PAVS> -- Promise, Accept, (Valid,) Satisfied
   , "Owner" : ttOwnerAcc cRud
   , "ValSupplier": ttValSupplier cRud
   ]

INTERFACE "[Objective]" FOR Developer: ttIsaObjective cRud BOX <RAW>
   [ "Definition": ttIsaObjective cRud BOX <TABLE>
      [ "Objective": ttIsaObjective cRud
      , "ISA:":
                ttIsaTTParty;V;"TTParty"[IfcText]
             \/ ttIsaObjective;V;"Objective"[IfcText]
             \/ ttIsaVariable;V;"Variable"[IfcText] 
             \/ (I[TText]-ttScope;I[Scope];ttScope~);V;"Out of scope"[IfcText] cRud
      , "Specification": I[TText] cRud <TTextShowInstance>
      , "Description (if different from spec)":
             (I[TText] /\ ttDescr;-I;ttInstance~
                       /\ ttDescr;-I;ttTemplate~
             );ttDescr cRud
      , "Owner" : ttOwnerAcc cRud
      ]
   , "Instantiation": I cRud BOX <TABLE>
      [ "Value": ttValue cRud
      , "Objective of": ttValSupName cRud
      , "PAS": I[TText] cRud <PAVS> -- Promise, Accept, (Valid,) Satisfied/Addressed
      , "ValSupplier": ttValSupplier cRud
      ]
   , "Context/Scope": ttScope INTERFACE "[MPTrxSummary]" 
   ]

--[Variables]--
POPULATION IfcText CONTAINS [ "Variable" ]

INTERFACE "[VariableList]" FOR Developer: ttScope~;ttIsaVariable cRuD BOX <TABLE hideNoRecords sortable>
   [ "Variable": I[TText] LINKTO INTERFACE "[Variable]"
   , "ISA:":
             ttIsaTTParty;V;"TTParty"[IfcText]
          \/ ttIsaObjective;V;"Objective"[IfcText]
          \/ ttIsaVariable;V;"Variable"[IfcText] 
          \/ (I[TText]-ttScope;I[Scope];ttScope~);V;"Out of scope"[IfcText] cRud
   , "Value": ttValue cRud
   , "Specification": I[TText] cRud <TTextShowInstance>
   , "PA": I[TText] cRud <PA> -- Promise, Accept
   , "Owner" : ttOwnerAcc cRud
   , "ValSupplier": ttValSupplier cRud
   ]

INTERFACE "[Variable]" FOR Developer: ttIsaVariable cRud BOX <RAW>
   [ "Definition": I[TText] cRud BOX <TABLE>
      [ "Variable": I[TText] cRud
      , "ISA:":
             ttIsaTTParty;V;"TTParty"[IfcText]
          \/ ttIsaObjective;V;"Objective"[IfcText]
          \/ ttIsaVariable;V;"Variable"[IfcText] 
          \/ (I[TText]-ttScope;I[Scope];ttScope~);V;"Out of scope"[IfcText] cRud
      , "Specification": I[TText] cRud <TTextShowInstance>
      , "Description (if different from spec)":
             (I[TText] /\ ttDescr;-I;ttInstance~
                       /\ ttDescr;-I;ttTemplate~
             );ttDescr cRud
      , "Used by (directly)": ttIsUsedBy cRud
--      , "Indirectly": ttIsUsedByStar - ttIsUsedBy cRud
      , "Owner" : ttOwnerAcc cRud
      ]
   , "Instantiation": I cRud BOX <TABLE>
      [ "Value": ttValue cRud
      , "PA": I[TText] cRud <PA> -- Promise, Accept
      , "ValSupplier": ttValSupplier cRud
      ]
   , "Context/Scope": ttScope INTERFACE "[MPTrxSummary]" 
   ]

--[[For debugging at TText level]]--

--[Scopes]--
INTERFACE "[ScopeSummary]" FOR Developer: I[Scope] cRuD BOX <TABLE hideNoRecords sortable>
   [ "Scope ID": I LINKTO INTERFACE "[Scope]"
   , "Description": scopeDescr cRud
   , "CC?" : scopeIsaCC cRUd -- Carbon Copy?
   , "includes" : scopeIII~ cRud
   , "incl. by" : scopeIII cRud
   ]

INTERFACE "[Scope]" FOR Developer: I[Scope] CRuD BOX <RAW>
   [ "Header" : I cRud BOX <TABLE>
      [ "Scope ID": scopeID cRUd
      , "Description": scopeDescr cRUd
      , "CC?" : scopeIsaCC cRUd -- Carbon Copy?
      , "includes" : scopeIII~ cRUd
      , "incl. by" : scopeIII cRud
      ]
   , "TParties" : I INTERFACE "[TTPartyList]"
   , "TTexts"   : I INTERFACE "[TTextList]"
   ]

--[TTexts]--
VIEW TTexts: TText(ttName)

INTERFACE "[TTextList]" FOR Developer: I[Scope];ttScope~ CRuD BOX <TABLE hideNoRecords sortable>
   [ "Scope": ttScope LINKTO INTERFACE "[Scope]"
   , "ICO" : ttICO LINKTO INTERFACE "[TText]"
   , "CC?": ttICCO cRUd
   , "Name": I LINKTO INTERFACE "[TText]"
   , "Value": ttValue cRUd
   , "Used by TText" : ttIsUsedBy LINKTO INTERFACE "[TText]"
   , "Instance": ttInstance cRud
   , " " : compVar~ cRud
   ]
--POPULATION IfcText CONTAINS [ "---" ]

INTERFACE "[TText]" FOR Developer: I[TText] cRud BOX <RAW>
   [ "TText Summary": I cRud BOX <TABLE>
      [ "Scope": ttScope LINKTO INTERFACE "[Scope]"
      , "Name": I LINKTO INTERFACE "[TText]"
      , "Value": ttValue cRUd
      , "Used by TText" : ttIsUsedBy LINKTO INTERFACE "[TText]"
      ]
   , "Source of copying": I cRud BOX <TABLE noLabels>
      [ "Carbon copy?": I cRud BOX <TABLE>
         [ "CC?": ttICCO cRUd
         ]
      , "Regular copy of" : ttICO cRud BOX <TABLE>
         [ "Name": I LINKTO INTERFACE "[TText]"
         , "Scope" : ttScope LINKTO INTERFACE "[Scope]"
         , "Value" : ttValue cRud
         , "Used by TText" : ttIsUsedBy LINKTO INTERFACE "[TText]"
         ]
      ]
   , "TText Details": I cRud BOX <FORM>
      [ "Name": ttName cRud
      , "Description" : ttDescr cRUd  
      , "Value": ttValue cRUd
      , "Template": ttTemplate cRUd
      , "Instance": ttInstance cRud
      , "    ": compVar~ cRud
      , "Assignments": ttTrace LINKTO INTERFACE "[Assignment]"
      ]
   ]

--[Assignments]--
VIEW Assignments: Assignment( asmVar;ttName, TXT " = '", asmVal, TXT "'" )
--VIEW AssignmentCheckbox: Assignment HTML TEMPLATE "View-PROPERTY.html" ENDVIEW -- Enable Assignment-properties to be shown as checkboxes

INTERFACE "[AssignmentList]": I[Scope] cRud BOX <TABLE>
   [ "Current Assignments (Traces)" : ttScope~;ttTrace cRud BOX <TABLE hideNoRecords sortable>
      [ "asmVar" : asmVar LINKTO INTERFACE "[TText]"
      , "asmVal" : asmVal cRud
      , "time"   : asmPOT cRud
--#IF UseAssignmentHistory
      , "parent" : asmHasPred LINKTO INTERFACE "[Assignment]"
--#ENDIF UseAssignmentHistory
      ]
   , "Historical Assignments" : ttScope~;(asmVar~ - ttTrace) cRud BOX <TABLE hideNoRecords sortable>
      [ "asmVar" : asmVar LINKTO INTERFACE "[TText]"
      , "asmVal" : asmVal cRud
      , "time"   : asmPOT cRud
--#IF UseAssignmentHistory
      , "parent" : asmHasPred LINKTO INTERFACE "[Assignment]"
      , "child"  : asmHasPred~ LINKTO INTERFACE "[Assignment]"
--#ENDIF UseAssignmentHistory
      ]
   ]

INTERFACE "[Assignment]" FOR Developer: I[Assignment] cRuD BOX <TABLE noLabels>
   [ "asmVar" : asmVar LINKTO INTERFACE "[TText]"
   , "asmVal" : asmVal cRud
   , "time"   : asmPOT cRud
--#IF UseAssignmentHistory
   , "parent" : asmHasPred LINKTO INTERFACE "[Assignment]"
   , "child"  : asmHasPred~ LINKTO INTERFACE "[Assignment]"
--#ENDIF UseAssignmentHistory
   ]

--[Computations]--
VIEW Computations: Computation( TXT "Compute" )

INTERFACE "[ComputationList]" FOR Developer: I[Scope] cRud BOX <RAW>
   [ "Computations": (compVar;ttScope)~ cRud BOX <TABLE hideNoRecords sortable>
      [ "TText"     : compVar LINKTO INTERFACE "[TText]"
      , "Result"    : compRes cRUd
      , "Procedure" : compVar;ttInstance cRud
      ]
   ]

INTERFACE "Computation" FOR Developer: I[Computation] cRuD BOX <TABLE noLabels>
   [ "Column 0": I cRud BOX <TABLE> [ "Scope"     : compVar;ttScope LINKTO INTERFACE "[Scope]" ]
   , "Column 1": I cRud BOX <TABLE> [ "TText"     : compVar cRud ]
   , "Column 2": I cRud BOX <TABLE> [ "Result"    : compRes cRUd ]
   , "Column 3": I cRud BOX <TABLE> [ "Procedure" : compVar;ttInstance cRud ]
   , "Column 4": compArg cRud BOX <TABLE> 
      [ "Arguments": asmVar;ttName cRud
      , "Values"   : asmVal cRud
      ]
   ]

ENDCONTEXT