CONTEXT TValSup_CarbonCopyService IN ENGLISH

INCLUDE "./TValSup_Inclusion.svc" -- include necessary relations/declarations

--[Carbon Copying TTParty extensions]--
-- TTParty: carbon copying `ttIsaTTParty[TText*TText]` is not done, as it is automatically maintained.

-- TTParty: carbon copying `ttPartyReqdPartyRef[TText*Organization]`:
ROLE ExecEngine MAINTAINS "Create/Update copied ttPartyReqdPartyRef"
RULE "Create/Update copied ttPartyReqdPartyRef": ttICCO;ttPartyReqdPartyRef |- ttPartyReqdPartyRef
VIOLATION (TXT "{EX}_;InsPair_;ttPartyReqdPartyRef_;TText_;", SRC ttIsaTTParty, TXT "_;OrgRef_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted ttPartyReqdPartyRef"
RULE "Update deleted ttPartyReqdPartyRef": ttICCO;(ttIsaTTParty-ttPartyReqdPartyRef;ttPartyReqdPartyRef~) |- (ttIsaTTParty-ttPartyReqdPartyRef;ttPartyReqdPartyRef~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;ttPartyReqdPartyRef_;TText_;", SRC ttIsaTTParty, TXT "_;OrgRef_;", SRC ttPartyReqdPartyRef)

-- TTParty: carbon copying `ttPartyAcc[TText*Account]`:
ROLE ExecEngine MAINTAINS "Create/Update copied ttPartyAcc"
RULE "Create/Update copied ttPartyAcc": ttICCO;ttPartyAcc |- ttPartyAcc
VIOLATION (TXT "{EX}_;InsPair_;ttPartyAcc_;TText_;", SRC ttIsaTTParty, TXT "_;Account_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted ttPartyAcc"
RULE "Update deleted ttPartyAcc": ttICCO;(ttIsaTTParty-ttPartyAcc;ttPartyAcc~) |- (ttIsaTTParty-ttPartyAcc;ttPartyAcc~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;ttPartyAcc_;TText_;", SRC ttIsaTTParty, TXT "_;Account_;", SRC ttPartyAcc)

--[Carbon Copying TText extensions]--
-- TText: carbon copying `ttValSupName[TText*TTName]`:
ROLE ExecEngine MAINTAINS "Create/Update copied ttValSupName"
RULE "Create/Update copied ttValSupName": ttICCO;ttValSupName |- ttValSupName
VIOLATION (TXT "{EX}_;InsPair_;ttValSupName_;TText_;", SRC I[TText], TXT "_;TTName_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted ttValSupName"
RULE "Update deleted ttValSupName": ttICCO;(I-ttValSupName;ttValSupName~) |- (I-ttValSupName;ttValSupName~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;ttValSupName_;TText_;", SRC I[TText], TXT "_;TTName_;", SRC ttValSupName)

-- TText: carbon copying copied `ttValSupplier[TText*TText]`:
ROLE ExecEngine MAINTAINS "Create/Update copied ttValSupplier"
RULE "Create/Update copied ttValSupplier": (ttICCO;ttValSupplier;ttICCO~ /\ ttScope;ttScope~);ttIsaTTParty |- ttValSupplier
VIOLATION (TXT "{EX}_;InsPair_;ttValSupplier_;TText_;", SRC I[TText], TXT "_;TText_;", TGT I[TText])
ROLE ExecEngine MAINTAINS "Update deleted ttValSupplier"
RULE "Update deleted ttValSupplier": ttICCO;(I-ttValSupplier;ttValSupplier~) |- (I-ttValSupplier;ttValSupplier~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;ttValSupplier_;TText_;", SRC I[TText], TXT "_;TText_;", SRC I[TText])

ENDCONTEXT