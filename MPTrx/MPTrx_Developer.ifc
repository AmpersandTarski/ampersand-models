CONTEXT MPTrxDeveloperInterfaces IN ENGLISH

--[Copying Scopes (and trigger Scope copying)]--
RELATION scopeCreateCopyReq[Scope*ScopeID] [UNI] -- Create a copy of an existing scope, (with the newly specified ScopeID)
ROLE ExecEngine MAINTAINS "Create a copy of the Scope"
RULE "Create a copy of the Scope": scopeCreateCopyReq |- scopeIII;scopeID
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeID;Scope;_NEW;ScopeID;", TGT I
              ,TXT ";scopeIII;Scope;", SRC I, TXT ";Scope;_NEW"
          ,TXT "{EX} DelPair;scopeCreateCopyReq;Scope;", SRC I, TXT ";ScopeID;", TGT I
          )

--[Views]--
--VIEW ScopeCheckbox: Scope HTML TEMPLATE "View-PROPERTY.html" ENDVIEW -- Enable Scope-properties to be shown as checkboxes
VIEW Scopes: Scope(scopeID)
{- BOX <PROPBUTTON> -- is a custom HTML Template (i.e. it is in the local templates/views directory)
   [ disabled : <expr1> -- The button is disabled when `expr1` has one or more target atoms.
   , property : <expr2> -- `expr2` **must be a [PROP]-type relation**; it gets populated whenever the button is clicked. 
   ]
-}

CLASSIFY ScopeID ISA IfcText -- This allows ScopeIDs to viewed by the <STRONG> template

INTERFACE "[DEV Overview]" FOR Developer: "_SESSION" cRud TABS
   [ "MPTrxs"       : V[SESSION*Scope] INTERFACE "[MPTrxSummary]"
   , "Objectives"   : V[SESSION*Scope] cRud BOX <DIV> [ "MPTrx" : scopeID <STRONG>, "Objectvs": I INTERFACE "[ObjectiveList]"    ,"separator": I-I cRud ]
   , "Variables"    : V[SESSION*Scope] cRud BOX <DIV> [ "MPTrx" : scopeID <STRONG>, "Variabls": I INTERFACE "[VariableList]"     ,"separator": I-I cRud ]
   , "DataTransfers": V[SESSION*Scope] cRud BOX <DIV> [ "MPTrx" : scopeID <STRONG>, "Dtrxfers": I INTERFACE "[DataTransferList]" ,"separator": I-I cRud ]
   , "TParties"     : V[SESSION*Scope] cRud BOX <DIV> [ "MPTrx" : scopeID <STRONG>, "TParties": I INTERFACE "[TPartyList]"       ,"separator": I-I cRud ]
--now follows general stuff
   , "Scopes"       : V[SESSION*Scope] INTERFACE "[ScopeSummary]"
   , "TTexts"       : V[SESSION*Scope] cRud BOX <DIV> [ "Scope" : scopeID <STRONG>, "TTexts"  : I INTERFACE "[TTextList]"        ,"separator": I-I cRud ]
-- , "TParties"     : V[SESSION*Scope] cRud BOX <DIV> [ "Scope" : scopeID <STRONG>, "TParties": I INTERFACE "[TPartyList]"       ,"separator": I-I cRud ]
   , "Assignments"  : V[SESSION*Scope] cRud BOX <DIV> [ "Scope" : scopeID <STRONG>, "Assgnmts": I INTERFACE "[AssignmentList]"   ,"separator": I-I cRud ]
   , "Computations" : V[SESSION*Scope] cRud BOX <DIV> [ "Scope" : scopeID <STRONG>, "Compttns": I INTERFACE "[ComputationList]"  ,"separator": I-I cRud ]
   ]

--[MPTrxs]--
POPULATION IfcText CONTAINS [ "Out of scope" ]

INTERFACE "[MPTrxSummary]" FOR Developer: I[Scope] cRuD COLS
   [ "MPTrx ID": I LINKTO INTERFACE "[MPTrx]"
   , "Description": scopeDescr cRud
--   , "CC?" : scopeIsaCC cRUd -- Carbon Copy?
   , "includes" : scopeIII~ cRud
   , "incl. by" : scopeIII cRud
   , "Owner": scopeOwner cRud
   ]

INTERFACE "[MPTrx]" FOR Developer: I[Scope] cRuD BOX <DIV>
   [ "Header" : I cRud COLS
      [ "MPTrx ID": scopeID cRUd
      , "Description": scopeDescr cRUd
--      , "CC?" : scopeIsaCC cRUd -- Carbon Copy?
      , "includes" : scopeIII~ cRUd
      , "incl. by" : scopeIII cRud
      , "Owner": scopeOwner cRud
      ]
   , "TParties" : I INTERFACE "[TPartyList]"
   , "Objectives" : I INTERFACE "[ObjectiveList]"
   , "Variables": I INTERFACE "[VariableList]"
   ]

--[TParties]--
POPULATION IfcText CONTAINS [ "TParty" ]

INTERFACE "[TPartyList]" FOR Developer: ttScope~;ttIsaTParty cRuD BOX <SHCOLS>
   [ "TParty": ttIsaTParty LINKTO INTERFACE "[TParty]"
   , "ISA:":
             ttIsaObjective;V;"Objective"[IfcText]
          \/ ttIsaVariable;V;"Variable"[IfcText] 
          \/ ttIsaTParty;V;"TParty"[IfcText]
          \/ (I[TText]-ttScope;I[Scope];ttScope~);V;"Out of scope"[IfcText] cRud
   , "Value": ttValue cRud
   , "Description": ttDescr cRud
   , "Account" : tPartyAcc cRud
   , "Req.Orgs": tPartyReqdOrgRef cRud
   , "Supplies value for": ttValSupplier~;ttName cRud
   , "Owner" : ttOwner cRud
   , "ValSupplier": ttValSupplier cRud
   ]

INTERFACE "[TParty]" FOR Developer: ttIsaTParty cRud BOX <DIV>
   [ "Definition": ttIsaTParty cRud COLS
      [ "TParty": ttIsaTParty cRud
      , "ISA:":
             ttIsaObjective;V;"Objective"[IfcText]
          \/ ttIsaVariable;V;"Variable"[IfcText] 
          \/ ttIsaTParty;V;"TParty"[IfcText]
          \/ (I[TText]-ttScope;I[Scope];ttScope~);V;"Out of scope"[IfcText] cRud
      , "Specification": I[TText] cRud <TTextShowInstance>
      , "Description (if different from spec)":
             (I[TText] /\ ttDescr;-I;ttInstance~
                       /\ ttDescr;-I;ttTemplate~
             );ttDescr cRud
      , "Owner" : ttOwner cRud
      , "ValSupplier": ttValSupplier cRud
      ]
   , "Instantiation": I cRud COLS
      [ "Value": ttValue cRud
      , "Account" : tPartyAcc cRud
      , "Req.Orgs": tPartyReqdOrgRef cRud
      , "Supplies value for": ttValSupplier~;ttName cRud
      , "Is mentioned in": ttIsUsedBy cRud
      ]
   , "Context/Scope": ttScope INTERFACE "[MPTrxSummary]" 
   ]
   
--[Objectives]--
POPULATION IfcText CONTAINS [ "Objective" ]

INTERFACE "[ObjectiveList]" FOR Developer: ttScope~;ttIsaObjective cRuD BOX <SHCOLS>
   [ "Objective": ttIsaObjective LINKTO INTERFACE "[Objective]"
   , "of TParty": ttValSupName cRud -- SHRole that decides whether or not the objective is addressed
   , "ISA:":
             ttIsaTParty;V;"TParty"[IfcText]
          \/ ttIsaObjective;V;"Objective"[IfcText]
          \/ ttIsaVariable;V;"Variable"[IfcText] 
          \/ (I[TText]-ttScope;I[Scope];ttScope~);V;"Out of scope"[IfcText] cRud
-- , "Value": ttValue cRud
   , "Specification": I[TText] cRud <TTextShowInstance>
   , "PAS": I[TText] cRud <PAVS> -- Promise, Accept, (Valid,) Satisfied
   , "Owner" : ttOwner cRud
   , "ValSupplier": ttValSupplier cRud
   ]

INTERFACE "[Objective]" FOR Developer: ttIsaObjective cRud BOX <DIV>
   [ "Definition": ttIsaObjective cRud COLS
      [ "Objective": ttIsaObjective cRud
      , "ISA:":
                ttIsaTParty;V;"TParty"[IfcText]
             \/ ttIsaObjective;V;"Objective"[IfcText]
             \/ ttIsaVariable;V;"Variable"[IfcText] 
             \/ (I[TText]-ttScope;I[Scope];ttScope~);V;"Out of scope"[IfcText] cRud
      , "Specification": I[TText] cRud <TTextShowInstance>
      , "Description (if different from spec)":
             (I[TText] /\ ttDescr;-I;ttInstance~
                       /\ ttDescr;-I;ttTemplate~
             );ttDescr cRud
      , "Owner" : ttOwner cRud
      ]
   , "Instantiation": I cRud COLS
      [ "Value": ttValue cRud
      , "Objective of": ttValSupName cRud
      , "PAS": I[TText] cRud <PAVS> -- Promise, Accept, (Valid,) Satisfied/Addressed
      , "ValSupplier": ttValSupplier cRud
      ]
   , "Context/Scope": ttScope INTERFACE "[MPTrxSummary]" 
   ]

--[Variables]--
POPULATION IfcText CONTAINS [ "Variable" ]

INTERFACE "[VariableList]" FOR Developer: ttScope~;ttIsaVariable cRuD BOX <SHCOLS>
   [ "Variable": I[TText] LINKTO INTERFACE "[Variable]"
   , "ISA:":
             ttIsaTParty;V;"TParty"[IfcText]
          \/ ttIsaObjective;V;"Objective"[IfcText]
          \/ ttIsaVariable;V;"Variable"[IfcText] 
          \/ (I[TText]-ttScope;I[Scope];ttScope~);V;"Out of scope"[IfcText] cRud
   , "Value": ttValue cRud
   , "Specification": I[TText] cRud <TTextShowInstance>
   , "PA": I[TText] cRud <PA> -- Promise, Accept
   , "Owner" : ttOwner cRud
   , "ValSupplier": ttValSupplier cRud
   ]

INTERFACE "[Variable]" FOR Developer: ttIsaVariable cRud BOX <DIV>
   [ "Definition": I[TText] cRud COLS
      [ "Variable": I[TText] cRud
      , "ISA:":
             ttIsaTParty;V;"TParty"[IfcText]
          \/ ttIsaObjective;V;"Objective"[IfcText]
          \/ ttIsaVariable;V;"Variable"[IfcText] 
          \/ (I[TText]-ttScope;I[Scope];ttScope~);V;"Out of scope"[IfcText] cRud
      , "Specification": I[TText] cRud <TTextShowInstance>
      , "Description (if different from spec)":
             (I[TText] /\ ttDescr;-I;ttInstance~
                       /\ ttDescr;-I;ttTemplate~
             );ttDescr cRud
      , "Used by (directly)": ttIsUsedBy cRud
--      , "Indirectly": ttIsUsedByStar - ttIsUsedBy cRud
      , "Owner" : ttOwner cRud
      ]
   , "Instantiation": I cRud COLS
      [ "Value": ttValue cRud
      , "PA": I[TText] cRud <PA> -- Promise, Accept
      , "ValSupplier": ttValSupplier cRud
      ]
   , "Context/Scope": ttScope INTERFACE "[MPTrxSummary]" 
   ]

--[[For debugging at TText level]]--

--[Scopes]--
INTERFACE "[ScopeSummary]" FOR Developer: I[Scope] cRuD BOX <SHCOLS>
   [ "Scope ID": I LINKTO INTERFACE "[Scope]"
   , "Description": scopeDescr cRud
   , "CC?" : scopeIsaCC cRUd -- Carbon Copy?
   , "includes" : scopeIII~ cRud
   , "incl. by" : scopeIII cRud
   ]

INTERFACE "[Scope]" FOR Developer: I[Scope] CRuD BOX <DIV>
   [ "Header" : I cRud COLS
      [ "Scope ID": scopeID cRUd
      , "Description": scopeDescr cRUd
      , "CC?" : scopeIsaCC cRUd -- Carbon Copy?
      , "includes" : scopeIII~ cRUd
      , "incl. by" : scopeIII cRud
      ]
   , "TParties" : I INTERFACE "[TPartyList]"
   , "TTexts"   : I INTERFACE "[TTextList]"
   ]

--[TTexts]--
VIEW TTexts: TText(ttName)

INTERFACE "[TTextList]" FOR Developer: I[Scope];ttScope~ CRuD BOX <SHCOLS>
   [ "Scope": ttScope LINKTO INTERFACE "[Scope]"
   , "ICO" : ttICO LINKTO INTERFACE "[TText]"
   , "CC?": ttICCO cRUd
   , "Name": I LINKTO INTERFACE "[TText]"
   , "Value": ttValue cRUd
   , "Used by TText" : ttIsUsedBy LINKTO INTERFACE "[TText]"
   , "Instance": ttInstance cRud
   , " " : compVar~ cRud
   ]
--POPULATION IfcText CONTAINS [ "---" ]

INTERFACE "[TText]" FOR Developer: I[TText] cRud BOX <DIV>
   [ "TText Summary": I cRud COLS
      [ "Scope": ttScope LINKTO INTERFACE "[Scope]"
      , "Name": I LINKTO INTERFACE "[TText]"
      , "Value": ttValue cRUd
      , "Used by TText" : ttIsUsedBy LINKTO INTERFACE "[TText]"
      ]
   , "Source of copying": I cRud BOX <COLSNL>
      [ "Carbon copy?": I cRud COLS
         [ "CC?": ttICCO cRUd
         ]
      , "Regular copy of" : ttICO cRud COLS
         [ "Name": I LINKTO INTERFACE "[TText]"
         , "Scope" : ttScope LINKTO INTERFACE "[Scope]"
         , "Value" : ttValue cRud
         , "Used by TText" : ttIsUsedBy LINKTO INTERFACE "[TText]"
         ]
      ]
   , "TText Details": I cRud ROWS
      [ "Name": ttName cRud
      , "Description" : ttDescr cRUd  
      , "Value": ttValue cRUd
      , "Template": ttTemplate cRUd
      , "Instance": ttInstance cRud
      , "    ": compVar~ cRud
      , "Assignments": ttTrace LINKTO INTERFACE "[Assignment]"
      ]
   ]

--[Assignments]--
VIEW Assignments: Assignment( asmVar;ttName, TXT " = '", asmVal, TXT "'" )
--VIEW AssignmentCheckbox: Assignment HTML TEMPLATE "View-PROPERTY.html" ENDVIEW -- Enable Assignment-properties to be shown as checkboxes

INTERFACE "[AssignmentList]": I[Scope] cRud COLS
   [ "Current Assignments (Traces)" : ttScope~;ttTrace cRud BOX <SHCOLS>
      [ "asmVar" : asmVar LINKTO INTERFACE "[TText]"
      , "asmVal" : asmVal cRud
      , "time"   : asmPOT cRud
      , "parent" : asmHasPred LINKTO INTERFACE "[Assignment]"
      ]
   , "Historical Assignments" : ttScope~;(asmVar~ - ttTrace) cRud BOX <SHCOLS>
      [ "asmVar" : asmVar LINKTO INTERFACE "[TText]"
      , "asmVal" : asmVal cRud
      , "time"   : asmPOT cRud
      , "parent" : asmHasPred LINKTO INTERFACE "[Assignment]"
      , "child"  : asmHasPred~ LINKTO INTERFACE "[Assignment]"
      ]
   ]

INTERFACE "[Assignment]" FOR Developer: I[Assignment] cRuD BOX <COLSNL>
   [ "asmVar" : asmVar LINKTO INTERFACE "[TText]"
   , "asmVal" : asmVal cRud
   , "time"   : asmPOT cRud
   , "parent" : asmHasPred LINKTO INTERFACE "[Assignment]"
   , "child"  : asmHasPred~ LINKTO INTERFACE "[Assignment]"
   ]

--[Computations]--
VIEW Computations: Computation( TXT "Compute" )

INTERFACE "[ComputationList]" FOR Developer: I[Scope] cRud BOX <DIV>
   [ "Computations": (compVar;ttScope)~ cRud BOX <SHCOLS>
      [ "TText"     : compVar LINKTO INTERFACE "[TText]"
      , "Result"    : compRes cRUd
      , "Procedure" : compVar;ttInstance cRud
      ]
   ]

INTERFACE "Computation" FOR Developer: I[Computation] cRuD BOX <COLSNL>
   [ "Column 0": I cRud COLS [ "Scope"     : compVar;ttScope LINKTO INTERFACE "[Scope]" ]
   , "Column 1": I cRud COLS [ "TText"     : compVar cRud ]
   , "Column 2": I cRud COLS [ "Result"    : compRes cRUd ]
   , "Column 3": I cRud COLS [ "Procedure" : compVar;ttInstance cRud ]
   , "Column 4": compArg cRud COLS 
      [ "Arguments": asmVar;ttName cRud
      , "Values"   : asmVal cRud
      ]
   ]

ENDCONTEXT