CONTEXT EURentWEBSITEInterfaces IN ENGLISH
------------------------------------------------------------
{- You can do the following things on the EURent Website:
1. Register yourself (i.e. create an account)
   After registration, you are automatically logged in with that account
   This part of the functionality is provided by the SIAM module
2. Login.
   This allows you to view the RentalCases that you have requested (as well as historical ones)
   This part of the functionality is provided by the SIAM module
3. Apply for a rental (i.e.: create a RentalCase, fill in the required fields, and submit the request)
   Applying for a rental requires you to register yourself, or use an existing account
   This part of the functionality is what this file is about.
-}

--[Customer website main interfaces]-----------------------

   INCLUDE "EURent Interfaces.adl"          -- Use predefined (smaller) user interfaces


--! Uitlog mogelijkheid toevoegen (zie EURent Employees)
--! Inzien van eigen rentals toevoegen (vgl EURent Employees)

INTERFACE "Customer Car Rentals": '_SESSION'[SESSION] cRud BOX <ROWSNL>
   [ "Anonymous sessions" : (I-(sessionAccount;sessionAccount~)) cRud BOX <ROWSNL>
      [ "Create new car rental" : I-(sessionRC;sessionRC~) cRud BOX -- Show 'button' if there is no current rental case
         [ "Login or Register" : I-(sessionAccount;sessionAccount~) cRud INTERFACE "Login/Register"
         , "Create new rental?" : sessionRCCreateReq cRUd
         ]
      , "Edit unpromised car rental" : I /\ sessionRC;(I-rentalHasBeenPromised);sessionRC~ 
                                       cRud INTERFACE "Edit unpromised car rental"
      ]
   , "Customer sessions" : I /\ sessionAccount;I[Customer];sessionAccount~ cRud BOX <ROWSNL>
      [ "Create new car rental" : I-(sessionRC;sessionRC~) cRud BOX -- Show 'button' if there is no current rental case
         [ "Create new rental?" : sessionRCCreateReq cRUd
         ]
      , "Edit unpromised car rental" : I /\ sessionRC;(I-rentalHasBeenPromised);sessionRC~ 
                                       cRud INTERFACE "Edit unpromised car rental"
      , "Show current promised car rentals" :  sessionRC;rentalHasBeenPromised cRud INTERFACE "View Rental Case" 
      , "List other promised car rentals" : ('_SESSION';V - sessionRC);rentalHasBeenPromised cRud INTERFACE "View Rental Cases"
      ]
   , "Employee sessions": I /\ sessionAccount;I[Employee];sessionAccount~ cRud BOX <ROWSNL>
      [ "Message": V;'You must be logged in as a customer to use this interface.'[IfcMsg] cRud
      , "Login or Register" : I-(sessionAccount;sessionAccount~) cRud INTERFACE "Login/Register"
      ]
   ]
POPULATION IfcMsg CONTAINS [ "You must be logged in as a customer to use this interface." ]

INTERFACE "Edit unpromised car rental" : I /\ sessionRC;(I-rentalHasBeenPromised);sessionRC~ cRud BOX <ROWSNL> -- If we have an unpromised current rental case
   [ "Login or Register" : I-(sessionAccount;sessionAccount~) cRud INTERFACE "Login/Register"
   , "Customer" : sessionRC cRud INTERFACE "Edit Customer Details"
   , "Rental"   : sessionRC cRud INTERFACE "Edit Contract Details"
   , "Workitems" : (sessionActiveRoles;workitemRole~ /\ sessionRC;rcWorkItem);workitemText cRud
--! following 3 lines do not generate a proper interface - to be discussed with @Michiel, so we do a workaround !--
--!, "Workitems" : '_SESSION' /\ sessionRC;rcWorkItem;workitemRole;sessionActiveRoles~ cRud ROWS
--!   [ "To do" : (sessionActiveRoles;workitemRole~ /\ sessionRC;rcWorkItem);workitemText cRud
--!   ]
   , "Show costs?" : sessionRC;(I /\ rcCarType;V;contractualRentalPeriod~) cRud BOX
      [ "Projected costs" : I cRud INTERFACE "Show Projected Regular Costs"
      ]
   , "Request submittal" : sessionRC;(rcIsClean-rentalHasBeenPromised) cRud BOX
      [ "Submit?" : rentalHasBeenRequested cRUd
      ]
   ]

--[Login and Registration for Customers (and Developers)]--
INTERFACE "Login/Register": '_SESSION'[SESSION] cRud BOX <ROWSNL>
   [ "Login": '_SESSION'-(sessionAccount;sessionAccount~) cRud ROWS
      [ "Login": I cRud BOX <HCOLS>
         [ "Userid": loginUserid cRUd
         , "Password": loginPassword crUd -- crUd is needed for Passwords
--$The following two lines are meant for demonstration/development contexts only
         , "Please help": sessionLoginAssist cRUd
         , " ": sessionLoginAssist;(V-(V;I[Customer];V));'Cannot help - there are no Customer-accounts'[LoginMessage] cRud
         ]
      ]
--$This is for an easy login, for ease of demonstration/development
   , "LoginAssist ": sessionLoginAssist;V[SESSION*Customer] cRud ROWS
      [ " ": I cRud BOX <SHCOLS>
         [ "Login?": autoLoginAccount cRUd
         , "Userid": accUserid cRud
         , "Organization": accOrg cRud
         , "Person": accPerson cRud
         ]
      ]
   , "Register": ('_SESSION'-(sessionAccount;sessionAccount~))-sessionLoginAssist cRud ROWS
      [ "Register": I cRud BOX <HCOLS>
         [ "Userid" : loginUserid cRUd
         , "Password" : loginPassword crUd -- crUd is needed for Passwords
         , "Register?" : loginCreateAccount cRUd
         ]
      ]
   , "Show logged in user": I /\ sessionAccount;sessionAccount~ cRud BOX
      [ "Logged in as" : sessionAccount;accPersonRef cRud
      ]
   ]
POPULATION LoginMessage CONTAINS [ "Cannot help - there are no Customer-accounts" ]

--[Customer account management]----------------------------
--! Following Ifc is not (yet?) being used by -- "Register" : accPersonRef~ INTERFACE "Customer Registration"
INTERFACE "Customer Registration": I[Customer] CRud ROWS
   [ "Userid": accUserid cRUd
   , "Password": accPassword crUd -- crUd is needed for Passwords
   , "Name": accPersonRef cRUd
   , "Driving License": custDrivingLicense cRUd
   ]

ROLE ExecEngine MAINTAINS "The userid of a customer is its (initial) personref"
RULE "The userid of a customer is its (initial) personref":
   (I[Customer]-(accUserid;accUserid~));accPersonRef |- accUserid;V
VIOLATION (TXT "{EX} InsPair;accUserid;Account;", SRC I, TXT ";UserID;", TGT I)

-----------------------------------------------------------
ENDCONTEXT