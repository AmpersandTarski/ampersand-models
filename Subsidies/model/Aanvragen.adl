CONTEXT "SubsidieaanvragenAwb"
PURPOSE CONTEXT "SubsidieaanvragenAwb"
{+ Dit hoofdstuk analyseert de "taal van de business", om functionele eisen ten behoeve van ‘Subsidieaanvragen Awb’ te kunnen bespreken.
Deze analyse beoogt om bij te dragen aan een bouwbare, maar oplossingsonafhankelijke specificatie.
Het begrijpen van tekst vereist deskundigheid op het gebied van conceptueel modelleren
+}

PATTERN "Besluiten"
CONCEPT Grondslag "Onder grondslag wordt verstaan: een artikel of lid daarvan dat verwijst naar een juridische fundament, om juridische traceerbaarheid te realiseren."

PURPOSE CONCEPT Regeling
{+ We spreken over een regeling om ernaar te kunnen verwijzen.
Hiertoe gebruiken we de gestandaardiseerde verwijzingen die op de site wetten.nl te vinden zijn,
zodat de gelijkheid van regelingen samenloopt met de gelijkheid van hun URL.
+}
CONCEPT Regeling "Onder regeling wordt verstaan: elke wettelijk geldende verordening, die in de Staatscourant gepubliceerd is."

RELATION regeling[Grondslag*Regeling] [UNI,TOT]
RELATION artikel[Grondslag*Artikel] [UNI]

CONCEPT Besluit "Onder besluit wordt verstaan: een schriftelijke beslissing van een bestuursorgaan, inhoudende een publiekrechtelijke rechtshandeling." "Art. 1:3 lid 1 Awb"
CONCEPT Beschikking "2Onder beschikking wordt verstaan: een besluit dat niet van algemene strekking is, met inbegrip van de afwijzing van een aanvraag daarvan." "Art. 1:3 lid 2 Awb"
CLASSIFY Beschikking ISA Besluit

CONCEPT Reden "Onder reden verstaan we een uitspraak van een bestuursorgaan, die een besluit motiveert."
REPRESENT Reden TYPE BIGALPHANUMERIC

PURPOSE RELATION grondslag[Besluit*Grondslag]
{+ De overheid neemt alleen besluiten op basis van een grondslag.
Daarmee typeert de grondslag een besluit.
Om die reden stelt de applicatie het vermelden van de grondslag verplicht.
+}
RELATION grondslag[Besluit*Grondslag] [UNI,TOT]
PURPOSE RELATION tijdstip[Besluit*Tijdstip]
{+ Om de authenticiteit van een besluit vast te leggen registreert de applicatie het tijdstip waarop het besluit genomen is.
Zolang het besluit niet genomen is, blijft dit veld leeg.
+}
RELATION tijdstip[Besluit*Tijdstip] [UNI]

PURPOSE RELATION orgaan[Besluit*Bestuursorgaan]
{+ Om de authenticiteit van een besluit vast te leggen registreert de applicatie
het orgaan dat het besluit genomen heeft.
Zolang het besluit niet genomen is, blijft dit veld leeg.
+}
RELATION orgaan[Besluit*Bestuursorgaan] [UNI]

PURPOSE RELATION document[Besluit*Document]
{+ Omdat een besluit een schriftelijke beslissing is, registreert de applicatie het document dat het besluit bevat.
+}
RELATION document[Besluit*Document] [UNI]

ENDPATTERN

PATTERN "Subsidies"
PURPOSE PATTERN "Subsidies" REF "Afdeling 4.2.1. Awb"
{+ Dit hoofdstuk modelleert de inleidend bepalingen uit Afdeling 4.2.1. Awb.+}

CONCEPT Subsidie "Onder subsidie wordt verstaan: de aanspraak op financiële middelen, door een bestuursorgaan verstrekt met het oog op bepaalde activiteiten van de aanvrager, anders dan als betaling voor aan het bestuursorgaan geleverde goederen of diensten." "Art. 4:21 lid 1 Awb"

CONCEPT Subsidieplafond "Onder subsidieplafond wordt verstaan: het bedrag dat gedurende een bepaald tijdvak ten hoogste beschikbaar is voor de verstrekking van subsidies krachtens een bepaald wettelijk voorschrift." "Art. 4:22 Awb"

CONCEPT Aanvraag "Onder aanvraag wordt verstaan: een verzoek van een belanghebbende, een besluit te nemen" "Art. 1:3 lid 3 Awb"

CONCEPT Subsidieaanvraag "Onder subsidieaanvraag wordt verstaan: een aanvraag tot het verstrekken van subsidie die kan worden ingediend op grond van een wettelijk voorschrift dat regelt voor welke activiteiten subsidie kan worden verstrekt." "Art. 4:23 Awb"
CLASSIFY Subsidieaanvraag ISA Aanvraag

PURPOSE RELATION voorschrift[Subsidie*Regeling] REF "Art. 4:23 lid 1 Awb"
{+Een bestuursorgaan verstrekt slechts subsidie op grond van een wettelijk voorschrift dat regelt voor welke activiteiten subsidie kan worden verstrekt.
+}
RELATION voorschrift[Subsidie*Regeling] [UNI]

PURPOSE RELATION voorschrift[Aanvraag*Regeling] REF "Art. 4:23 lid 1 Awb"
{+ De aanvraag tot subsidieverlening vermeldt de subsidieregeling op grond waarvan de subsidie wordt aangevraagd.
+}
RELATION voorschrift[Aanvraag*Regeling] [UNI]

PURPOSE RELATION vanToepassing[Subsidie*Grondslag] REF "Art. 4:23 lid 2 Awb"
{+ De subsidieverstrekker registreert welke wettelijke regels nog meer van toepassing zijn op de subsidieaanvraag,
zodat duidelijk is op welke grondslagen een subsidie verstrekt is.
+}
RELATION vanToepassing[Subsidie*Grondslag]

PURPOSE RELATION effect[Subsidie*Tekst] REF "Art. 4:24 Awb"
{+ Ten behoeve van het verslag over doeltreffendheid en effecten subsidie
kan de subsidiegever aantekeningen kwijt over de doeltreffendheid en effecten van elke specifieke subsidie in de praktijk. +}
RELATION effect[Subsidie*Tekst]
ENDPATTERN

PATTERN "De aanvraag"
PURPOSE PATTERN "De aanvraag" REF "Awb Afdeling 4.1.1."
{+ Dit hoofdstuk modelleert de aanvraag uit Afdeling 4.1.1. Awb.+}

PURPOSE RELATION ingediend[Aanvraag*Bestuursorgaan] REF "Art. 4:1 Awb"
{+ De aanvraag wordt ingediend bij het bestuursorgaan dat bevoegd is op de aanvraag te beslissen. +}
RELATION ingediend[Aanvraag*Bestuursorgaan]

CONCEPT Aanvrager "Onder aanvrager wordt verstaan: degene die aan een bestuursorgaan verzocht heeft een besluit te nemen." "Awb"
CLASSIFY Aanvrager ISA Persoon

PURPOSE RELATION aanvrager[Aanvraag*Aanvrager] REF "Art. 4:2 lid 1 Awb"
{+ De aanvraag wordt ingediend bij het bestuursorgaan dat bevoegd is op de aanvraag te beslissen.
Het is voldoende dat de aanvrager zijn of haar naam en adres vermeldt.
+}
RELATION aanvrager[Aanvraag*Aanvrager] [UNI]

PURPOSE RELATION belanghebbende[Aanvraag*Persoon] REF "Art. 1:2 Awb"
{+ Onder belanghebbende wordt verstaan: degene wiens belang rechtstreeks bij een besluit is betrokken.
+}
RELATION belanghebbende[Aanvraag*Persoon]

PURPOSE RELATION aanduiding[Aanvraag*Tekst] REF "Art. 4:2 lid 1 sub c Awb"
{+Om te voldoen aan Art. 4:2 lid 1 Awb registreert de aanvrager een aanduiding van de beschikking die wordt gevraagd.+}
RELATION aanduiding[Aanvraag*Tekst] [UNI]
ENDPATTERN

PATTERN "Kennisgeving"
PURPOSE PATTERN "Kennisgeving" REF "Art. 3:20 Awb"
{+ Dit hoofdstuk modelleert de kennisgeving uit Art. 3:20 Awb.+}

PURPOSE RELATION door[Kennisgeving*Bestuursorgaan] REF "Art. 3:20 Awb"
{+ De kennisgeving meldt per besluit in ieder geval de naam en adres van het bestuursorgaan, dat bevoegd istot het nemen van het besluit.+}
RELATION door[Kennisgeving*Bestuursorgaan] [UNI]
PURPOSE RELATION grondslag[Kennisgeving*Regeling] REF "Art. 3:20 Awb"
{+ De kennisgeving meldt per besluit in ieder geval krachtens welk wettelijk voorschrift het besluit wordt genomen.+}
RELATION grondslag[Kennisgeving*Regeling] [UNI]
ENDPATTERN

PATTERN "Persoonsgegevens"
CONCEPT Persoon "Onder persoon wordt verstaan: een natuurlijke persoon of een rechtspersoon."
PURPOSE RELATION adres[Persoon*Adres]
{+Om te voldoen aan Art. 4:2 lid 1 Awb registreert het systeem het adres van personen.+}
RELATION adres[Persoon*Adres] [UNI]

PURPOSE RELATION naam[Persoon*Naam]
{+Om te voldoen aan Art. 4:2 lid 1 Awb registreert het systeem de naam van personen.+}
RELATION naam[Persoon*Naam] [UNI]

PURPOSE RELATION straatnaam[Adres*Straatnaam] IN DUTCH REF "Art. 1 lid a Wet BAG; Art. 20 lid c Awb"
{+ De straatnaam is een onderdeel van het adres.
Een gebruiker moet de straatnaam verplicht invullen zolang er geen automaat is,
die een postcode/huisnummer combinatie vertaalt in een straatnaam en woonplaats.+}
RELATION straatnaam[Adres*Straatnaam] [UNI] PRAGMA "" "heeft als straatnaam" ""
MEANING "Een adres bevat een straatnaam."
ROLE User MAINTAINS TOTstraatnaamENwoonplaats
RULE TOTstraatnaamENwoonplaats : I[Adres] |- straatnaam;straatnaam~ /\ woonplaats;woonplaats~
MESSAGE "Zolang ik de postcode/huisnummer combinatie nog niet kan opzoeken, zult u de straatnaam en woonplaats nog zelf moeten invullen."
VIOLATION (TXT "Zoek alstublieft de straatnaam en woonplaats op bij de postcode ", SRC postcode, TXT " en huisnummer", SRC huisnummer, TXT ".")

RELATION huisnummer[Adres*Huisnummer] [UNI,TOT] PRAGMA "" "heeft als huisnummer" ""
MEANING "Een adres bevat een huisnummer."
PURPOSE RELATION huisnummer[Adres*Huisnummer] IN DUTCH REF "Art. 21 lid 1 sub d Wet BAG Awb"
{+ Het huisnummer is een onderdeel van het adres.+}

RELATION huisletter[Adres*Huisletter] [UNI] PRAGMA "" "heeft als huisletter" ""
MEANING "Een adres bevat een huisletter."
PURPOSE RELATION huisletter[Adres*Huisletter] IN DUTCH REF "Art. 21 lid 1 sub e Wet BAG Awb"
{+ Het huisnummer is een onderdeel van het adres.+}

RELATION woonplaats[Adres*Plaatsnaam] [UNI] PRAGMA "" "heeft als plaatsnaam" ""
MEANING "Een adres bevat een plaatsnaam."
PURPOSE RELATION woonplaats[Adres*Plaatsnaam] IN DUTCH REF "Art. 1 lid 1 sub r Wet BAG Awb"
{+ Een woonplaats wordt door het bevoegde gemeentelijke orgaan als zodanig aangewezen en van een naam voorzien gedeelte van het grondgebied van de gemeente.+}

RELATION postcode[Adres*Postcode] [UNI,TOT] PRAGMA "" "heeft als postcode" ""
MEANING "Een adres bevat een postcode."
PURPOSE RELATION postcode[Adres*Postcode] IN DUTCH REF "Art. 22 lid 1 sub a Wet BAG Awb"
{+ De postcode is een onderdeel van het adres.+}

ENDPATTERN

PATTERN "Het subsidieplafond"
PURPOSE PATTERN "Het subsidieplafond" REF "Afdeling 4.2.2. Awb"
{+Dit hoofdstuk modelleert het subsidieplafond uit Afdeling 4.2.2. Awb.+}

PURPOSE RELATION besluit[Subsidieplafond*Besluit]
{+
De applicatie registreert het besluit waarin een subsidieplafond is vastgesteld.
+}
RELATION besluit[Subsidieplafond*Besluit]

PURPOSE RELATION regeling[Subsidieplafond*Regeling] REF "Art. 4:25 lid 1 Awb"
{+
Een subsidieplafond kan slechts bij of krachtens wettelijk voorschrift worden vastgesteld.
+}
RELATION regeling[Subsidieplafond*Regeling] [UNI,TOT]

PURPOSE RELATION bekendmaking[Subsidieplafond*Datum] REF "Art. 4:27 lid 1 Awb"
{+
Het subsidieplafond wordt bekendgemaakt voor de aanvang van het tijdvak waarvoor het is vastgesteld.
Zolang het subsidieplafond niet bekend is gemaakt, blijft dit veld leeg.
+}
RELATION bekendmaking[Subsidieplafond*Datum] [UNI]

PURPOSE RELATION verdeling[Subsidieplafond*Tekst] REF "Art. 4:26 Awb"
{+
Bij of krachtens wettelijk voorschrift wordt bepaald hoe het beschikbare bedrag wordt verdeeld.
Bij de bekendmaking van het subsidieplafond wordt de wijze van verdeling vermeld.
Indien het subsidieplafond of een verlaging daarvan later wordt bekendgemaakt, heeft deze bekendmaking geen gevolgen voor voordien ingediende aanvragen.
+}
RELATION verdeling[Subsidieplafond*Tekst] [UNI]

ENDPATTERN

PATTERN "De subsidieverlening"
PURPOSE PATTERN "De subsidieverlening" REF "Afdeling 4.2.3. Awb"
{+ Dit hoofdstuk modelleert de subsidieverlening uit Afdeling 4.2.3. Awb.+}

PURPOSE RELATION activiteit[Beschikking*Activiteit] REF "Art. 4:30 Awb"
{+ 
De beschikking tot subsidieverlening bevat een omschrijving van de activiteiten waarvoor subsidie wordt verleend.
De omschrijving kan later worden uitgewerkt, voor zover de beschikking tot subsidieverlening dit vermeldt.
+}
PURPOSE RELATION activiteit[Beschikking*Activiteit] REF "Art. 4:43 Awb"
{+ 
Indien geen beschikking tot subsidieverlening is gegeven, bevat de beschikking tot subsidievaststelling een aanduiding van de activiteiten waarvoor subsidie wordt verstrekt.
+}
RELATION activiteit[Beschikking*Activiteit]

PURPOSE RELATION activiteit[Subsidieaanvraag*Activiteit] REF "Art. 4:61 sub a Awb"
{+ 
De aanvraag van de subsidie gaat in ieder geval vergezeld van een activiteitenplan, tenzij redelijkerwijs kan worden aangenomen dat daaraan geen behoefte is.
+}
RELATION activiteit[Subsidieaanvraag*Activiteit]

PURPOSE RELATION begroting[Subsidieaanvraag*Document] REF "Art. 4:61 sub a Awb"
{+ 
De aanvraag van de subsidie gaat in ieder geval vergezeld van een begroting, tenzij deze voor de berekening van het bedrag van de subsidie niet van belang is..
+}
RELATION begroting[Subsidieaanvraag*Document] [UNI]

PURPOSE RELATION bedrag[Beschikking*Bedrag] REF "Art. 4:31 Awb"
{+ 
De beschikking tot subsidieverlening vermeldt het bedrag van de subsidie, dan wel de wijze waarop dit bedrag wordt bepaald.
Indien de beschikking tot subsidieverlening het bedrag van de subsidie niet vermeldt, vermeldt zij het bedrag waarop de subsidie ten hoogste kan worden vastgesteld, tenzij bij wettelijk voorschrift anders is bepaald.
+}
RELATION bedrag[Beschikking*Bedrag] [UNI]
RELATION bepalingswijze[Beschikking*Tekst] [UNI]

PURPOSE RELATION tijdvak[Beschikking*Tijdvak] REF "Art. 4:32 Awb"
{+ 
Een subsidie in de vorm van een periodieke aanspraak op financiële middelen wordt verleend voor een bepaald tijdvak, dat in de beschikking tot subsidieverlening wordt vermeld.
+}
RELATION tijdvak[Beschikking*Tijdvak] [UNI]
RELATION betaalwijze[Beschikking*Tekst] [UNI]

PURPOSE RELATION voorwaarde[Subsidie*Tekst]
{+ Om de voorwaarden voor de subsidie te kunnen registreren in de beschikking, koppelt de applicatie deze voorwaarden aan de subsidie. +}
RELATION voorwaarde[Subsidie*Tekst]

PURPOSE RELATION weigering[Aanvraag*Besluit] REF "Art. 4:35 Awb"
{+ Om de redenen van weigering te kunnen registreren in het besluit, koppelt de applicatie dit besluit aan de aanvraag. +}
RELATION weigering[Aanvraag*Besluit] [UNI]
MEANING "Het systeem registreert de koppeling van een besluit tot weigering aan de geweigerde aanvraag."

PURPOSE RELATION nietBehandelen[Aanvraag*Besluit] REF "Artikel 4:5 lid 1 Awb"
{+ Het bestuursorgaan kan besluiten de aanvraag niet te behandelen, indien:
a.de aanvrager niet heeft voldaan aan enig wettelijk voorschrift voor het in behandeling nemen van de aanvraag, of
b.de aanvraag geheel of gedeeltelijk is geweigerd op grond van artikel 2:15, of
c.de verstrekte gegevens en bescheiden onvoldoende zijn voor de beoordeling van de aanvraag of voor de voorbereiding van de beschikking,
mits de aanvrager de gelegenheid heeft gehad de aanvraag binnen een door het bestuursorgaan gestelde termijn aan te vullen.
+}
RELATION nietBehandelen[Aanvraag*Besluit] [UNI]

RELATION tekst[Reden*Tekst] [UNI]
PURPOSE RELATION grondslag[Reden*Grondslag]
{+ Om de grondslag van weigering te traceren, registreert de applicatie de grondslag van elke reden afzonderlijk.
Het kan zo zijn dat de grondslag nog niet bekend is op het moment dat het systeem de tekst van de reden registreert.
Daarom mag deze grondslag (tijdelijk) open blijven staan, zolang het bij het afdrukken van de beschikking maar is ingevuld.
+}
RELATION grondslag[Reden*Grondslag] [UNI]

PURPOSE RELATION reden[Besluit*Reden]
{+ In geval een besluit met redenen is omkleed, registreert de applicatie deze redenen, zodat het hierover op enig later moment verantwoording kan afleggen. +}
RELATION reden[Besluit*Reden]

PURPOSE RELATION overeenkomst[Subsidie*Overeenkomst]  REF "Art. 4:36 Awb"
{+
Ter uitvoering van de beschikking tot subsidieverlening kan een overeenkomst worden gesloten.
Tenzij bij wettelijk voorschrift anders is bepaald of de aard van de subsidie zich daartegen verzet, kan in de overeenkomst worden bepaald dat de subsidie-ontvanger verplicht is de activiteiten te verrichten waarvoor de subsidie is verleend.
+}
RELATION overeenkomst[Subsidie*Overeenkomst]
ENDPATTERN

PATTERN "Verplichtingen van de subsidie-ontvanger"
PURPOSE PATTERN "Verplichtingen van de subsidie-ontvanger" REF "Afdeling 4.2.4. Awb"
{+Dit hoofdstuk modelleert de verplichtingen van de subsidie-ontvanger uit Afdeling 4.2.4. Awb.+}

PURPOSE RELATION verplichting[Subsidie*Tekst] MARKDOWN REF "Art. 4:37 lid 1 Awb"
{+
Het bestuursorgaan kan de subsidie-ontvanger verplichtingen opleggen met betrekking tot:
a. aard en omvang van de activiteiten waarvoor subsidie wordt verleend;
b. de administratie van aan de activiteiten verbonden uitgaven en inkomsten;
c. het vóór de subsidievaststelling verstrekken van gegevens en bescheiden die nodig zijn voor een beslissing omtrent de subsidie;
d. de te verzekeren risico’s;
e. het stellen van zekerheid voor verleende voorschotten;
f. het afleggen van rekening en verantwoording omtrent de verrichte activiteiten en de daaraan verbonden uitgaven en inkomsten, voor zover deze voor de vaststelling van de subsidie van belang zijn;
g. het beperken of wegnemen van de nadelige gevolgen van de subsidie voor derden;
h. het uitoefenen van controle door een accountant als bedoeld in artikel 393, eerste lid, van Boek 2 van het Burgerlijk Wetboek op het door het bestuursorgaan gevoerde financiële beheer en de financiële verantwoording daarover.
+}
RELATION verplichting[Subsidie*Tekst]

PURPOSE RELATION verplichting[Regeling*Verplichting] REF "Art. 4:38 lid 2 Awb"
{+ Indien de subsidie op een wettelijk voorschrift berust, worden de verplichtingen opgelegd bij wettelijk voorschrift of krachtens wettelijk voorschrift bij de subsidieverlening.
Om deze verplichtingen automatisch te kunnen toevoegen aan de subsidie, registreert de applicatie ze al bij de subsidieregeling.
Indien de subsidie niet op een wettelijk voorschrift berust, kunnen de verplichtingen worden opgelegd bij de subsidieverlening.
+}
RELATION verplichting[Regeling*Verplichting]
REPRESENT Verplichting TYPE BIGALPHANUMERIC

PURPOSE RELATION vergoeding[Subsidie*Tekst] MARKDOWN REF "Art. 4:41 Awb"
{+ Om eventuele vergoedingen te regelen, registreert de applicatie een tekstveld. De wet zegt hierover:

Subsidie-ontvanger is vergoeding verschuldigd

1.In de gevallen, genoemd in het tweede lid, is de subsidie-ontvanger, voor zover het verstrekken van de subsidie heeft geleid tot vermogensvorming, daarvoor een vergoeding verschuldigd aan het bestuursorgaan, mits:
a. dit bij wettelijk voorschrift of, indien de subsidie niet op een wettelijk voorschrift berust, bij de subsidieverlening is bepaald, en
b. daarbij is aangegeven hoe de hoogte van de vergoeding wordt bepaald.

2.De vergoeding is slechts verschuldigd indien:
a. de subsidie-ontvanger voor de gesubsidieerde activiteiten gebruikte of bestemde goederen vervreemdt of bezwaart of de bestemming daarvan wijzigt;
b. de subsidie-ontvanger een schadevergoeding ontvangt voor verlies of beschadiging van voor de gesubsidieerde activiteiten gebruikte of bestemde goederen;
c. de gesubsidieerde activiteiten geheel of gedeeltelijk worden beëindigd;
d. de subsidieverlening of de subsidievaststelling wordt ingetrokken of de subsidie wordt beëindigd, of
e. de rechtspersoon die de subsidie ontving wordt ontbonden.

3.De vergoeding wordt vastgesteld binnen een jaar nadat het bestuursorgaan op de hoogte is gekomen of kon zijn van de gebeurtenis die het recht op vergoeding deed ontstaan, doch in ieder geval binnen vijf jaren na de bekendmaking van de laatste beschikking tot subsidievaststelling.
+}
RELATION vergoeding[Subsidie*Tekst]

ENDPATTERN

PATTERN "De subsidievaststelling"
PURPOSE PATTERN "De subsidievaststelling" REF "Afdeling 4.2.5. Awb"
{+Dit hoofdstuk modelleert de subsidievaststelling uit Afdeling 4.2.5. Awb.+}

PURPOSE RELATION bedrag[Subsidie*Bedrag] REF "Art. 4:42 Awb"
{+ 
De beschikking tot subsidievaststelling stelt het bedrag van de subsidie vast en geeft aanspraak op betaling van het vastgestelde bedrag overeenkomstig afdeling 4.2.7.
+}
RELATION bedrag[Subsidie*Bedrag] [UNI]
RELATION bepalingswijze[Subsidie*Tekst] [UNI]

PURPOSE RELATION vaststellingsaanvraag[Subsidie*Aanvraag] MARKDOWN REF "Art. 4:44 Awb"
{+
Artikel 4:44Aanvraag tot vaststelling van de subsidie

1.Indien een beschikking tot subsidieverlening is gegeven, dient de subsidie-ontvanger na afloop van de activiteiten of het tijdvak waarvoor de subsidie is verleend een aanvraag tot vaststelling van de subsidie in, tenzij:
a. de subsidie met toepassing van artikel 4:47, onderdeel a , ambtshalve wordt vastgesteld;
b. bij wettelijk voorschrift of bij de subsidieverlening is bepaald dat de aanvraag wordt ingediend telkens na afloop van een gedeelte van het tijdvak waarvoor de subsidie is verleend, of
c. de vaststelling van de subsidie bij een overeenkomst als bedoeld in artikel 4:36, eerste lid, anders is geregeld.

2.Indien bij wettelijk voorschrift geen termijn is bepaald, wordt de aanvraag tot vaststelling ingediend binnen een bij de subsidieverlening te bepalen termijn.

3.Indien voor de indiening van de aanvraag tot vaststelling geen termijn is bepaald of de aanvraag na afloop van de daarvoor bepaalde termijn niet is ingediend kan het bestuursorgaan de subsidie-ontvanger een termijn stellen binnen welke de aanvraag moet zijn ingediend.

4.Indien na afloop van deze termijn geen aanvraag is ingediend, kan de subsidie ambtshalve worden vastgesteld.
+}
RELATION vaststellingsaanvraag[Subsidie*Aanvraag]

PURPOSE RELATION verantwoording[Subsidie*Document] REF "Art. 4:45 Awb"
{+
Bij de aanvraag tot subsidievaststelling toont de aanvrager aan dat de activiteiten hebben plaatsgevonden overeenkomstig de aan de subsidie verbonden verplichtingen, tenzij de subsidie voor de aanvang van de activiteiten wordt vastgesteld.
Bij de aanvraag tot subsidievaststelling legt de aanvrager rekening en verantwoording af omtrent de aan de activiteiten verbonden uitgaven en inkomsten, voor zover deze voor de vaststelling van de subsidie van belang zijn.
+}
RELATION verantwoording[Subsidie*Document]

PURPOSE RELATION verlening[Subsidie*Besluit]
{+
Het besluit tot subsidieverlening verschilt in beginsel met het besluit tot subsidievaststelling.
Indien deze besluiten samenlopen, verwijzen de relaties `verlening` en `vaststelling` naar hetzelfde besluit.
+}
RELATION verlening[Subsidie*Besluit]

PURPOSE RELATION vaststelling[Subsidie*Besluit] MARKDOWN REF "Art. 4:46 Awb"
{+
Vaststellen subsidie

1.Indien een beschikking tot subsidieverlening is gegeven, stelt het bestuursorgaan de subsidie overeenkomstig de subsidieverlening vast.

2.De subsidie kan lager worden vastgesteld indien:
a. de activiteiten waarvoor subsidie is verleend niet of niet geheel hebben plaatsgevonden;
b. de subsidie-ontvanger niet heeft voldaan aan de aan de subsidie verbonden verplichtingen;
c. de subsidie-ontvanger onjuiste of onvolledige gegevens heeft verstrekt en de verstrekking van juiste of volledige gegevens tot een andere beschikking op de aanvraag tot subsidieverlening zou hebben geleid, of
d. de subsidieverlening anderszins onjuist was en de subsidie-ontvanger dit wist of behoorde te weten.

3.Voor zover het bedrag van de subsidie afhankelijk is van de werkelijke kosten van de activiteiten waarvoor subsidie is verleend, worden kosten die in redelijkheid niet als noodzakelijk kunnen worden beschouwd bij de vaststelling van de subsidie niet in aanmerking genomen.

Artikel 4:47Vaststellen subsidie

Het bestuursorgaan kan de subsidie geheel of gedeeltelijk ambtshalve vaststellen, indien:
a. bij wettelijk voorschrift of bij de subsidieverlening een termijn is bepaald binnen welke de subsidie ambtshalve wordt vastgesteld;
b. toepassing wordt gegeven aan artikel 4:44, vierde lid, of
c. de beschikking tot subsidieverlening of de beschikking tot subsidievaststelling wordt ingetrokken of ten nadele van de ontvanger wordt gewijzigd.
+}
RELATION vaststelling[Subsidie*Besluit]
ENDPATTERN

PURPOSE INTERFACE Overzicht
{+ Een subsiedieambtenaar krijgt een overzicht van alle aanvragen die voldoen aan de condities:
1. De regeling waarbinnen de aanvraag valt is ingevuld.
2. De aanvrager is ingevuld. Dit houdt in dat naam, straatnaam, huisnummer, postcode en woonplaats ingevuld zijn.
3. De aanduiding van de activiteit is ingevuld.
+}
INTERFACE Overzicht FOR Subsidieambtenaar: "_SESSION"[SESSION] cRud BOX<TABLE sortable sortBy="postcode" order="asc">
   [ Aanvragen : V[SESSION*Aanvraag];(I/\voorschrift;voorschrift~/\aanvrager;aanvrager~/\aanduiding;aanduiding~) cRud BOX<TABLE>
         [ aanvrager: aanvrager;naam[Persoon*Naam]
         , adres: aanvrager;adres
         , aanduiding: aanduiding
         , voorschrift: voorschrift
         ]
   ]

RELATION sessionAccount[SESSION*Persoon] 
ENFORCE sessionAccount >: ("_SESSION"#I[Persoon]);(naam;"Pietje Puk";naam~/\I)

ROLE ExecEngine MAINTAINS PersoonIdentifier
RULE PersoonIdentifier : naam;naam~ |- I[Persoon]
VIOLATION ( TXT "{EX} MrgAtoms;Persoon;", SRC I, TXT ";Persoon;", TGT I )

ROLE ExecEngine MAINTAINS AdresIdentifier
RULE AdresIdentifier : postcode;postcode~ /\ huisnummer;huisnummer~ /\ (huisletter;huisletter~ \/ -(huisletter;huisletter~)) |- I[Adres]
VIOLATION ( TXT "{EX} MrgAtoms;Adres;", SRC I, TXT ";Adres;", TGT I )

ROLE ExecEngine MAINTAINS EnkeleAanvraag
RULE EnkeleAanvraag : ((aanvrager;aanvrager~ - nietBehandelen;nietBehandelen~) - weigering;weigering~) - vaststellingsaanvraag~;verlening;verlening~;vaststellingsaanvraag |- I[Aanvraag]
VIOLATION ( TXT "{EX} MrgAtoms;Aanvraag;", SRC I, TXT ";Aanvraag;", TGT I )

VIEW Adres: Adres DEFAULT
    { straat: straatnaam, s1 : TXT " "
    , huisnummer : huisnummer
    , huisletter : huisletter, s2 : TXT ", "
    , postcode  : postcode, s3 : TXT "  "
    , woonplaats : woonplaats
    }
ENDVIEW

INTERFACE Persoon : I[Persoon]
BOX [ naam: naam 
    , adres : adres
    ]

{-
PATTERN "Intrekking en wijziging"
PURPOSE PATTERN "Intrekking en wijziging" REF "Afdeling 4.2.6. Awb"
{+Dit hoofdstuk modelleert de intrekking en wijziging van een subsidieverlening of een subsidievaststelling uit Afdeling 4.2.6. Awb.+}

Artikel 4:48Subsidieverlening intrekken of wijzigen

1.Zolang de subsidie niet is vastgesteld kan het bestuursorgaan de subsidieverlening intrekken of ten nadele van de subsidie-ontvanger wijzigen, indien:
a. de activiteiten waarvoor subsidie is verleend niet of niet geheel hebben plaatsgevonden of zullen plaatsvinden;
b. de subsidie-ontvanger niet heeft voldaan aan de aan de subsidie verbonden verplichtingen;
c. de subsidie-ontvanger onjuiste of onvolledige gegevens heeft verstrekt en de verstrekking van juiste of volledige gegevens tot een andere beschikking op de aanvraag tot subsidieverlening zou hebben geleid;
d. de subsidieverlening anderszins onjuist was en de subsidie-ontvanger dit wist of behoorde te weten, of
e. met toepassing van artikel 4:34, vijfde lid, een beroep wordt gedaan op de voorwaarde dat voldoende gelden ter beschikking worden gesteld.

2.De intrekking of wijziging werkt terug tot en met het tijdstip waarop de subsidie is verleend, tenzij bij de intrekking of wijziging anders is bepaald.



Artikel 4:49Subsidievaststelling intrekken of wijzigen

1.Het bestuursorgaan kan de subsidievaststelling intrekken of ten nadele van de ontvanger wijzigen:
a. op grond van feiten of omstandigheden waarvan het bij de subsidievaststelling redelijkerwijs niet op de hoogte kon zijn en op grond waarvan de subsidie lager dan overeenkomstig de subsidieverlening zou zijn vastgesteld;
b. indien de subsidievaststelling onjuist was en de subsidie-ontvanger dit wist of behoorde te weten, of
c. indien de subsidie-ontvanger na de subsidievaststelling niet heeft voldaan aan aan de subsidie verbonden verplichtingen.

2.De intrekking of wijziging werkt terug tot en met het tijdstip waarop de subsidie is vastgesteld, tenzij bij de intrekking of wijziging anders is bepaald.

3.De subsidievaststelling kan niet meer worden ingetrokken of ten nadele van de ontvanger worden gewijzigd indien vijf jaren zijn verstreken sedert de dag waarop zij is bekendgemaakt dan wel, in het geval, bedoeld in het eerste lid, onderdeel c, sedert de dag waarop de handeling in strijd met de verplichting is verricht of de dag waarop aan de verplichting had moeten zijn voldaan.



Artikel 4:50Subsidieverlening intrekken of wijzigen

1.Zolang de subsidie niet is vastgesteld kan het bestuursorgaan de subsidieverlening met inachtneming van een redelijke termijn intrekken of ten nadele van de subsidie-ontvanger wijzigen:
a. voor zover de subsidieverlening onjuist is;
b. voor zover veranderde omstandigheden of gewijzigde inzichten zich in overwegende mate tegen voortzetting of ongewijzigde voortzetting van de subsidie verzetten, of
c. in andere bij wettelijk voorschrift geregelde gevallen.

2.Bij intrekking of wijziging op grond van het eerste lid, onderdeel a of b, vergoedt het bestuursorgaan de schade die de subsidie-ontvanger lijdt doordat hij in vertrouwen op de subsidie anders heeft gehandeld dan hij zonder subsidie zou hebben gedaan.



Artikel 4:51Weigering van de subsidie

1.Indien aan een subsidie-ontvanger voor drie of meer achtereenvolgende jaren subsidie is verstrekt voor dezelfde of in hoofdzaak dezelfde voortdurende activiteiten, geschiedt gehele of gedeeltelijke weigering van de subsidie voor een daarop aansluitend tijdvak op de grond, dat veranderde omstandigheden of gewijzigde inzichten zich tegen voortzetting of ongewijzigde voortzetting van de subsidie verzetten, slechts met inachtneming van een redelijke termijn.

2.Voor zover aan het einde van het tijdvak waarvoor subsidie is verleend sedert de bekendmaking van het voornemen tot weigering voor een daarop aansluitend tijdvak nog geen redelijke termijn is verstreken, wordt de subsidie voor het resterende deel van die termijn verleend, zo nodig in afwijking van artikel 4:25, tweede lid.
» Praktijkvoorbeeld rechterlijke uitspraak.


ENDPATTERN

PATTERN "Betaling en terugvordering"
PURPOSE PATTERN "Betaling en terugvordering" REF "Afdeling 4.2.7. Awb"
{+Dit hoofdstuk modelleert de betaling en terugvordering van een subsidie uit Afdeling 4.2.7. Awb.+}

Artikel 4:52Betaling subsidiebedrag

1. Het subsidiebedrag wordt overeenkomstig de subsidievaststelling betaald.

2. Indien de subsidie niet op een wettelijk voorschrift berust, kan bij de subsidieverlening, of, indien geen beschikking tot subsidieverlening is gegeven, bij de subsidievaststelling een van artikel 4:87, eerste lid, afwijkende termijn voor de betaling van het subsidiebedrag worden vastgesteld.



Artikel 4:53Subsidiebedrag kan in gedeelten worden betaald

1.Het subsidiebedrag kan in gedeelten worden betaald, mits bij wettelijk voorschrift is bepaald hoe de gedeelten worden berekend en op welke tijdstippen zij worden betaald.

2.Indien de subsidie niet op een wettelijk voorschrift berust, kan het subsidiebedrag in gedeelten worden betaald, mits bij de subsidieverlening, of indien geen beschikking tot subsidieverlening is gegeven, bij de subsidievaststelling, is bepaald hoe de gedeelten worden berekend en op welke tijdstippen zij worden betaald.



Artikel 4:54 en 4:55
[Vervallen per 01-07-2009]



Artikel 4:56Verplichting tot betaling wordt opgeschort

De verplichting tot betaling van een subsidiebedrag of een voorschot wordt opgeschort met ingang van de dag waarop het bestuursorgaan aan de subsidie-ontvanger schriftelijk kennis geeft van het ernstige vermoeden dat er grond bestaat om toepassing te geven aan artikel 4:48 of 4:49, tot en met de dag waarop de beschikking omtrent de intrekking of wijziging is bekendgemaakt of de dag waarop sedert de kennisgeving van het ernstige vermoeden dertien weken zijn verstreken.



Artikel 4:57Onverschuldigd betaalde subsidiebedragen terugvorderen

1. Het bestuursorgaan kan onverschuldigd betaalde subsidiebedragen terugvorderen.

2. Het bestuursorgaan kan het terug te vorderen bedrag bij dwangbevel invorderen.

3. Het bestuursorgaan kan het terug te vorderen bedrag verrekenen met een aan dezelfde subsidie-ontvanger voor dezelfde activiteiten verstrekte subsidie voor een ander tijdvak.

4. Terugvordering van een subsidiebedrag of een voorschot vindt niet plaats voor zover na de dag waarop de subsidie is vastgesteld, dan wel de handeling, bedoeld in artikel 4:49, eerste lid, onderdeel c, heeft plaatsgevonden, vijf jaren zijn verstreken.


ENDPATTERN

PATTERN "Per boekjaar verstrekte subsidies aan rechtspersonen"
PURPOSE PATTERN "Per boekjaar verstrekte subsidies aan rechtspersonen" REF "Afdeling 4.2.8. Awb"
{+Dit hoofdstuk modelleert de per boekjaar verstrekte subsidies aan rechtspersonen uit Afdeling 4.2.8. Awb.+}

§ 4.2.8.1 Inleidende bepalingen

Artikel 4:58Werking afdeling

1.Deze afdeling is van toepassing op per boekjaar verstrekte subsidies, indien dat bij wettelijk voorschrift of bij besluit van het bestuursorgaan is bepaald.

2.Bij algemene maatregel van bestuur kan worden bepaald dat deze afdeling van toepassing is op daarbij aangewezen subsidies.



Artikel 4:59Aanwijzen toezichthouders

1.Het bestuursorgaan dat met toepassing van deze afdeling een subsidie verleent kan een of meer toezichthouders aanwijzen die zijn belast met het toezicht op de naleving van de aan de ontvanger van die subsidie opgelegde verplichtingen.

2.De toezichthouder beschikt niet over de bevoegdheden, vermeld in de artikelen 5:18 en 5:19.



§ 4.2.8.2 De aanvraag

Artikel 4:60Aanvraag subsidie

Tenzij bij wettelijk voorschrift anders is bepaald, wordt de aanvraag van de subsidie uiterlijk dertien weken voor de aanvang van het boekjaar ingediend.



Artikel 4:61Aanvraag gaat vergezeld van plan en begroting

1.De aanvraag van de subsidie gaat in ieder geval vergezeld van:
a. een activiteitenplan, tenzij redelijkerwijs kan worden aangenomen dat daaraan geen behoefte is, en
b. een begroting, tenzij deze voor de berekening van het bedrag van de subsidie niet van belang is.

2.Indien de aanvrager beschikt over een egalisatiereserve als bedoeld in artikel 4:72, vermeldt de aanvraag de omvang daarvan.



Artikel 4:62Overzicht in activiteitenplan

Het activiteitenplan behelst een overzicht van de activiteiten waarvoor subsidie wordt gevraagd en de daarmee nagestreefde doelstellingen en vermeldt per activiteit de daarvoor benodigde personele en materiële middelen.



Artikel 4:63Overzicht in begroting

1.De begroting behelst een overzicht van de voor het boekjaar geraamde inkomsten en uitgaven van de aanvrager, voor zover deze betrekking hebben op de activiteiten waarvoor subsidie wordt gevraagd.

2.De begrotingsposten worden ieder afzonderlijk van een toelichting voorzien.

3.Tenzij voor de activiteiten waarop de aanvraag betrekking heeft nog niet eerder subsidie werd verstrekt, behelst de begroting een vergelijking met de begroting van het lopende boekjaar en de gerealiseerde inkomsten en uitgaven van het jaar, voorafgaand aan het lopende boekjaar.



Artikel 4:64Aanvraag vergezeld van een afschrift en een jaarrekening

1.Tenzij de aanvraag wordt ingediend door een krachtens publiekrecht ingestelde rechtspersoon, gaat deze, indien voor het jaar voorafgaand aan het subsidiejaar geen subsidie werd aangevraagd, voorts vergezeld van:
a. een afschrift van de oprichtingsakte van de rechtspersoon dan wel van de statuten zoals deze laatstelijk zijn gewijzigd, en
b. de laatst opgemaakte jaarrekening als bedoeld in artikel 361 van Boek 2 van het Burgerlijk Wetboek dan wel de balans en de staat van baten en lasten en de toelichting daarop of, indien deze bescheiden ontbreken, een verslag over de financiële positie van de aanvrager op het moment van de aanvraag.

2.De in het eerste lid, onderdeel b, bedoelde bescheiden dan wel het verslag over de financiële positie zijn voorzien van een van een accountant als bedoeld in artikel 393, eerste lid, van Boek 2 van het Burgerlijk Wetboek afkomstige schriftelijke verklaring omtrent de getrouwheid onderscheidenlijk een mededeling, inhoudende dat van onjuistheden niet is gebleken.

3.Bij wettelijk voorschrift of bij besluit van het bestuursorgaan kan vrijstelling of ontheffing worden verleend van het in het tweede lid bepaalde.



Artikel 4:65Subsidie aangevraagd

Voor zover de aanvrager voor dezelfde begrote uitgaven tevens subsidie heeft aangevraagd bij een of meer andere bestuursorganen, doet hij daarvan mededeling in de aanvraag, onder vermelding van de stand van zaken met betrekking tot de beoordeling van die aanvraag of aanvragen.



§ 4.2.8.3 De subsidieverlening

Artikel 4:66Subsidie verleend aan een rechtspersoon

De subsidie wordt slechts verleend aan een rechtspersoon met volledige rechtsbevoegdheid.



Artikel 4:67Subsidie voor een boekjaar verleend

1.De subsidie wordt voor een boekjaar of voor een bepaald aantal boekjaren verleend.

2.Indien de subsidie voor twee of meer boekjaren wordt verleend, wordt aan de subsidie de verplichting verbonden tot het periodiek aan het bestuursorgaan verstrekken van de gegevens die voor de vaststelling van de subsidie van belang zijn.

3.De beschikking tot subsidieverlening vermeldt welke gegevens de subsidie-ontvanger krachtens het tweede lid moet verstrekken, alsmede op welke tijdstippen de gegevens moeten worden verstrekt.



§ 4.2.8.4 Verplichtingen van de subsidie-ontvanger

Artikel 4:68Boekjaar gelijk aan kalenderjaar

Tenzij bij wettelijk voorschrift of bij de subsidieverlening anders is bepaald, stelt de subsidie-ontvanger het boekjaar gelijk aan het kalenderjaar.



Artikel 4:69Ingerichte administratie

1.De subsidie-ontvanger voert een zodanig ingerichte administratie, dat daaruit te allen tijde de voor de vaststelling van de subsidie van belang zijnde rechten en verplichtingen alsmede de betalingen en de ontvangsten kunnen worden nagegaan.

2.De administratie en de daartoe behorende bescheiden worden gedurende zeven jaren bewaard.



Artikel 4:70Verschillen tussen werkelijkheid en begroting

Indien gedurende het boekjaar aanmerkelijke verschillen ontstaan of dreigen te ontstaan tussen de werkelijke uitgaven en inkomsten en de begrote uitgaven en inkomsten doet de subsidie-ontvanger daarvan onverwijld mededeling aan het bestuursorgaan onder vermelding van de oorzaak van de verschillen.



Artikel 4:71Subsidie-ontvanger behoeft toestemming bestuursorgaan

1.Indien dit bij wettelijk voorschrift of bij de subsidieverlening is bepaald, behoeft de subsidie-ontvanger de toestemming van het bestuursorgaan voor:
a. het oprichten van dan wel deelnemen in een rechtspersoon;
b. het wijzigen van de statuten;
c. het in eigendom verwerven, het vervreemden of het bezwaren van registergoederen, indien zij mede zijn verworven door middel van de subsidiegelden, dan wel de lasten daarvoor mede worden bekostigd uit de subsidiegelden;
d. het aangaan en beëindigen van overeenkomsten tot verkrijging, vervreemding of bezwaring van registergoederen of tot huur, verhuur of pacht daarvan, indien deze goederen geheel of gedeeltelijk zijn verworven door middel van de subsidie dan wel de uitgaven daarvoor mede zijn bekostigd uit de subsidie;
e. het aangaan van kredietovereenkomsten en van overeenkomsten van geldlening;
f. het aangaan van overeenkomsten waarbij de subsidie-ontvanger zich verbindt tot zekerheidsstelling met inbegrip van zekerheidsstelling voor schulden van derden of waarbij hij zich als borg of hoofdelijk medeschuldenaar verbindt of zich voor een derde sterk maakt;
g. het vormen van fondsen en reserveringen;
h. het vaststellen of wijzigen van tarieven voor door de subsidie-ontvanger in de gewone uitoefening van zijn gesubsidieerde activiteiten te verrichten prestaties;
i. het ontbinden van de rechtspersoon;
j. het doen van aangifte tot zijn faillissement of het aanvragen van zijn surséance van betaling.

2.Het bestuursorgaan beslist binnen vier weken omtrent de toestemming.

3.De beslissing kan eenmaal voor ten hoogste vier weken worden verdaagd.

4.Paragraaf 4.1.3.3 is van toepassing.



Artikel 4:72De ontvanger vormt een egalisatiereserve

1.Indien dit bij wettelijk voorschrift of bij de subsidieverlening is bepaald, vormt de ontvanger een egalisatiereserve.
2.Het verschil tussen de vastgestelde subsidie en de werkelijke kosten van de activiteiten waarvoor subsidie werd verleend komt ten gunste onderscheidenlijk ten laste van de egalisatiereserve.
3.De egalisatiereserve wordt zo hoog rentend en zo veilig als redelijkerwijs mogelijk is belegd.
4.De van de egalisatiereserve genoten rente wordt aan de egalisatiereserve toegevoegd.
5.In de gevallen bedoeld in artikel 4:41, tweede lid, onderdelen c, d en e, is de subsidie-ontvanger ter zake van de egalisatiereserve vergoedingsplichtig naar evenredigheid van de mate waarin de subsidie aan de egalisatiereserve heeft bijgedragen.



§ 4.2.8.5 De subsidievaststelling

Artikel 4:73Subsidie per boekjaar vastgesteld

De subsidie wordt per boekjaar vastgesteld.



Artikel 4:74Aanvraag tot vaststelling subsidie

De subsidie-ontvanger dient binnen zes maanden na afloop van het boekjaar een aanvraag tot vaststelling van de subsidie in, tenzij bij wettelijk voorschrift anders is bepaald of de subsidie met toepassing van artikel 4:67, tweede lid, voor twee of meer boekjaren is verleend.



Artikel 4:75Aanvraag gaat vergezeld van verslagen

1.De aanvraag tot vaststelling gaat in ieder geval vergezeld van een financieel verslag en een activiteitenverslag.

2.Indien de subsidie-ontvanger ingevolge wettelijk voorschrift verplicht is tot het opstellen van een jaarrekening als bedoeld in artikel 361 van Boek 2 van het Burgerlijk Wetboek, of indien dit bij de subsidieverlening is bepaald, legt hij in plaats van het financieel verslag de jaarrekening over, onverminderd artikel 4:45, tweede lid.



Artikel 4:76Inhoud financiële verslag

1.Indien de subsidie-ontvanger zijn inkomsten geheel ontleent aan de subsidie omvat het financiële verslag de balans en de exploitatierekening met de toelichting en zijn het tweede tot en met vijfde lid van toepassing.

2.Het financiële verslag geeft volgens normen die in het maatschappelijk verkeer als aanvaardbaar worden beschouwd, een zodanig inzicht dat een verantwoord oordeel kan worden gevormd omtrent:
a. het vermogen en het exploitatiesaldo, en
b. voor zover de aard van het financiële verslag dat toelaat, omtrent de solvabiliteit en de liquiditeit van de subsidie-ontvanger.

3.De balans met de toelichting geeft getrouw, duidelijk en stelselmatig de grootte en de samenstelling in actief- en passiefposten van het vermogen op het einde van het boekjaar weer.

4.De exploitatierekening met de toelichting geeft getrouw, duidelijk en stelselmatig de grootte van het exploitatiesaldo van het boekjaar weer.

5.Het financiële verslag sluit aan op de begroting waarvoor subsidie is verleend en behelst een vergelijking met de gerealiseerde inkomsten en uitgaven van het jaar, voorafgaand aan het boekjaar.



Artikel 4:77Artikel 4:76 van toepassing

Indien de subsidie-ontvanger zijn inkomsten in overwegende mate ontleent aan de subsidie kan bij wettelijk voorschrift of bij de subsidieverlening worden bepaald dat artikel 4:76 van overeenkomstige toepassing is.



Artikel 4:78Onderzoek van het financiële verslag

1.De subsidie-ontvanger geeft opdracht tot onderzoek van het financiële verslag aan een accountant als bedoeld in artikel 393, eerste lid, van Boek 2 van het Burgerlijk Wetboek.

2.De accountant onderzoekt of het financiële verslag voldoet aan de bij of krachtens de wet gestelde voorschriften en of het activiteitenverslag, voor zover hij dat verslag kan beoordelen, met het financiële verslag verenigbaar is.

3.De accountant geeft de uitslag van zijn onderzoek weer in een schriftelijke verklaring omtrent de getrouwheid van het financiële verslag.

4.De aanvraag tot vaststelling van de subsidie gaat vergezeld van de in het derde lid bedoelde verklaring.

5.Bij wettelijk voorschrift of bij de subsidieverlening kan vrijstelling of ontheffing worden verleend van het eerste tot en met het vierde lid.



Artikel 4:79Onderzoek van de naleving van de verplichtingen

1.Bij wettelijk voorschrift of bij de subsidieverlening kan worden bepaald dat de in artikel 4:78, eerste lid, bedoelde opdracht tevens strekt tot onderzoek van de naleving van aan de subsidie verbonden verplichtingen.

2.Bij toepassing van het eerste lid gaat de opdracht vergezeld van een bij of krachtens wettelijk voorschrift of bij de subsidieverlening vast te stellen aanwijzing over de reikwijdte en de intensiteit van de controle.

3.Bij toepassing van het eerste lid, gaat het financiële verslag tevens vergezeld van een schriftelijke verklaring van de accountant over de naleving door de subsidie-ontvanger van de aan de subsidie verbonden verplichtingen.



Artikel 4:80Inhoud activiteitenverslag

Het activiteitenverslag beschrijft de aard en omvang van de activiteiten waarvoor subsidie werd verleend en bevat een vergelijking tussen de nagestreefde en de gerealiseerde doelstellingen en een toelichting op de verschillen.
-}

ENDCONTEXT