CONTEXT "Organizations" IN ENGLISH

CONCEPT Organization "an organized group of people with a particular purpose, such as a business or government department" "Oxford English Dictionary"

--[Organization Reference]

orgRef :: Organization * OrgRef  [UNI] -- The human readable name/identifier to refer to the organization

REPRESENT OrgRef TYPE ALPHANUMERIC

--[Organizations are Parties]
-- See file: "SIAM_AccountSessionRoles.adl" for details about the underlying concepts.

CLASSIFY Organization ISA Party
CLASSIFY OrgRef       ISA PartyRef -- and thereby an IdentRef

RULE "An Organization cannot own itself": I[Organization] |- -isOwnedBy -- Ownership lies with a Person or another Organization.

ROLE ExecEngine MAINTAINS "InsPair partyRef from orgRef"
RULE "InsPair partyRef from orgRef": orgRef |- partyRef
VIOLATION (TXT "{EX} InsPair;partyRef;Party;", SRC I[Party], TXT ";PartyRef;", TGT I[PartyRef])

--[Generic Organization attributes]

orgFullName :: Organization * OrgFullName [UNI] -- the long (full) name by which the organization may be identified
orgLogoURL  :: Organization * URL [UNI] -- URL at which the logo of the organization can be found

ENDCONTEXT