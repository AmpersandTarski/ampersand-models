CONTEXT "AutoLogin" IN ENGLISH
-- This file implements the auto-login feature, i.e. it defines the property `accAutoLoginReq`.

RELATION accAutoLoginReq[Account*Account] [PROP] -- An account that has this property is automatically logged in.

RULE "AutoLoginAccounts must be active": accAutoLoginReq |- accIsActive

ROLE ExecEngine MAINTAINS "Auto login" -- When multiple accounts have this property, only one will be selected.
RULE "Auto login": accAutoLoginReq /\ accIsActive |- V;"_SESSION";sessionAccount
VIOLATION (TXT "{EX} InsPair;sessionAccount;SESSION;", SRC V;"_SESSION"[SESSION], TXT ";Account;", TGT I
          ,TXT "{EX} DelPair;accAutoLoginReq;Account;", SRC I, TXT ";Account;", TGT I
          ,TXT "{EX} SetNavToOnCommit;", SRC V;"_SESSION"[SESSION];sessionUserIfc -- Setup for default user interface
          ,TXT "{EX} SetNavToOnCommit;", TGT accUserIfc -- Override if Account has a specific setting
          )

ROLE ExecEngine MAINTAINS "Reset accAutoLoginReq" -- when there is a sessionAccount, auto-login requests from active accounts are denied
RULE "Reset accAutoLoginReq": accAutoLoginReq /\ accIsActive /\ V;"_SESSION";sessionAccount;V |- -V
VIOLATION (TXT "{EX} DelPair;accAutoLoginReq;Account;", SRC I, TXT ";Account;", TGT I)

ENDCONTEXT