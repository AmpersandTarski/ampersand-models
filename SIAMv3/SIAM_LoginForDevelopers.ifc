CONTEXT "LoginForDevelopers" IN ENGLISH LATEX
{-This script is intended to be reusable, and for INCLUDEsion in your own scripts.
This interface provides you with LOGIN functionality.
It is particularly suited for developers, since it allows you to select an account
by which you then will be logged in, rather than that you have to type the actual
username and password.
-}
-----------------------------------------------------------
  INCLUDE "../SIAMv3/SIAM_PersonRefs.adl"          -- a standard way for refering to people, not associated with the Person concept.
  INCLUDE "../SIAMv3/SIAM_OrgRefs.adl"             -- a standard way for refering to organizations, not associated with the Person concept.
  INCLUDE "../SIAMv3/SIAM_LoginWithUPW.adl"        -- Login using U/PW (Usernames and Passwords).
  INCLUDE "../SIAMv3/SIAM_RegisterWithUPW.adl"     -- Extension that registers not-yet existing users that specify a U/PW.
  INCLUDE "../SIAMv3/SIAM_LoginWithUID.adl"        -- Login using third party identity providers.
--INCLUDE "../SIAMv3/SIAM_ISO_LoA.adl"             -- Extension that introduces ISO authentication levels in a basic fashion.
  INCLUDE "../SIAMv3/SIAM_AutoLoginAccount.adl"    -- Extension that implements the `accAutoLoginReq` property for accounts.

  INCLUDE "../SIAMv3/SIAM_RegisterWithUPW.ifc"     -- Registration interface
  INCLUDE "../SIAMv3/SIAM_Logout.ifc"              -- Interface for logging out.

POPULATION LoginMessage CONTAINS [ "Cannot help - there are no (active) accounts" ]   REPRESENT LoginMessage TYPE ALPHANUMERIC
POPULATION PopoverText  CONTAINS [ "Click this button to create your account" ]       REPRESENT PopoverText  TYPE ALPHANUMERIC

INTERFACE "Login (DEV)" FOR Anonymous: "_SESSION"[SESSION];sessionIsAnon cRud BOX <DIV>

--$This section allows users to login or register themselves
   [ "Login": I-sessionAccRegForm;sessionAccRegForm~ cRud BOX <HCOLS>
      [ "Userid": loginUserid cRUd
      , "Password": loginPassword cRUd -- cRUd is needed for Passwords
      , "IdP": loginIdP cRUd
--      , "Auth.level": sessionAuthLoA cRUd

      --$Button that allows user to login
      , "Login": I cRud BOX <PROPBUTTON> 
         [ "property": sessionLoginReq cRUd
         , "disabled": I - (loginUserid;loginUserid~ /\ loginPassword;loginPassword~) cRud
         ]

      --$Button that enables developers to login by selecting an existing account
      , "  ": I BOX <DIV>
         [ "Show Accounts": I-sessionLoginAssist BOX <PROPBUTTON> [ "property": sessionLoginAssist cRUd ]
         , "Hide Accounts":   sessionLoginAssist BOX <PROPBUTTON> [ "property": sessionLoginAssist cRUd ]
         , " ": sessionLoginAssist;(V-(V;accIsActive;V));"Cannot help - there are no (active) accounts"[LoginMessage] cRud
         ]

      --$Button that allows account-registration in anonymous sessions      
      , "   ": I-sessionLoginAssist cRud BOX <DIV> -- Only allow registration when we're not trying to login.
         [ "Register": I BOX <PROPBUTTON> 
            [ "property": sessionCreateARFReq cRUd
--          , "hide": I /\ sessionAccRegForm;sessionAccRegForm~ cRud
            , "disabled": I /\ sessionAccRegForm;sessionAccRegForm~ cRud
            , "popovertext": V;"Click this button to create your account"[PopoverText] cRud
            ]
         ]
      ]

--$This section allows users of anonymous sessions to register an account
   , "Register": sessionAccRegForm INTERFACE "Register"

--$This section allows (enabled) developers to login by selecting an exsting account 
   , "Active accounts": sessionLoginAssist;V;accIsActive cRuD BOX <SHCOLS>
      [ "Login?" : I cRud BOX <PROPBUTTON> [ "property": accAutoLoginReq cRUd ]
      , "Userid": accUserid cRud
      , "Person": accPersonRef cRud
--    , "Person": accPerson cRud
      , "Organization": accOrgRef cRud
--    , "Organization": accOrg cRud
      , "UIDs": accUID cRud
--    , "Persona" : accPersona cRud
--    , "Allowed roles": accAllowedRoles cRUd
--    , "Default roles": accDefaultRoles cRUd
      , "De-activate?" : I cRud BOX <PROPBUTTON> [ "property": accIsActive cRUd ]
      ]

--$This section allows (enabled) developers to activate de-activated accounts (so that they can be used for logging in)
   , "Inactive accounts": sessionLoginAssist;V;(I-accIsActive) cRuD BOX <SHCOLS>
      [ "Activate?" : I cRud BOX <PROPBUTTON> [ "property": accIsActive cRUd ]
      , "Userid": accUserid cRud
      , "Person": accPersonRef cRud
--    , "Person": accPerson cRud
      , "Organization": accOrgRef cRud
--    , "Organization": accOrg cRud
      , "UIDs": accUID cRud
--    , "Persona" : accPersona cRud
--    , "Allowed roles": accAllowedRoles cRUd
--    , "Default roles": accDefaultRoles cRUd
      ]

   ]

-----------------------------------------------------------
ENDCONTEXT