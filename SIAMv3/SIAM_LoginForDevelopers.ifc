CONTEXT "LoginForDevelopers" IN ENGLISH LATEX
{-This script is intended to be reusable, and for INCLUDEsion in your own scripts.
This interface provides you with LOGIN functionality.
It is particularly suited for developers, since it allows you to select an account
by which you then will be logged in, rather than that you have to type the actual
username and password.
-}
-----------------------------------------------------------
INCLUDE "./SIAM_AutoLoginAccount.adl"    -- Extension that implements the `accAutoLoginReq` property for accounts.
INCLUDE "./SIAM_RegisterWithUPW.adl"     -- Extension that registers not-yet existing users that specify a U/PW.
INCLUDE "./SIAM_RegisterWithUPW.ifc"     -- Registration interface

POPULATION PopoverText  CONTAINS [ "Click this button to create your account" ]
POPULATION LoginMessage CONTAINS [ "Cannot help - there are no (active) accounts" ]

INTERFACE "Login (DEV)": "_SESSION"[SESSION] cRud BOX <DIV>

--$This section allows users to login or register themselves
   [ "Login": sessionIsAnon-sessionAccRegForm;sessionAccRegForm~ cRud BOX <HCOLS>
      [ "Userid": loginUserid cRUd
      , "Password": loginPassword crUd -- crUd is needed for Passwords
      , "IdP": loginIdP cRUd
      , "Auth.level": sessionAuthLoA cRUd

      --$Button that allows user to login
      , "Login": I cRud BOX <PROPBUTTON> 
         [ "property": loginReq cRUd
         , "disabled": I - (loginUserid;loginUserid~ /\ loginPassword;loginPassword~) cRud
         ]

      --$Button that enables developers to login by selecting an existing account
      , "  ": I BOX <DIV>
         [ "Show Accounts": I-sessionLoginAssist BOX <PROPBUTTON> [ "property": sessionLoginAssist cRUd ]
         , "Hide Accounts":   sessionLoginAssist BOX <PROPBUTTON> [ "property": sessionLoginAssist cRUd ]
         , " ": sessionLoginAssist;(V-(V;accIsActive;V));"Cannot help - there are no (active) accounts"[LoginMessage] cRud
         ]

      --$Button that allows account-registration in anonymous sessions      
      , "   ": I-sessionLoginAssist cRud BOX <DIV> -- Only allow registration when we're not trying to login.
         [ "Register": I BOX <PROPBUTTON> 
            [ "property": sessionCreateARFReq cRUd
--          , "hide": I /\ sessionAccRegForm;sessionAccRegForm~ cRud
            , "disabled": I /\ sessionAccRegForm;sessionAccRegForm~ cRud
            , "popovertext": V;"Click this button to create your account"[PopoverText] cRud
            ]
         ]
      ]

--$This section allows users of anonymous sessions to register an account
   , "Register": sessionIsAnon;sessionAccRegForm INTERFACE "Register"

--$This section allows a user that is logged in, to logout.
   , "Logout": sessionIsUser INTERFACE "Logout"

--$This section allows (enabled) developers to login by selecting an exsting account 
   , "Active accounts": sessionLoginAssist;V;accIsActive cRuD BOX <SHCOLS>
      [ "Login?" : I cRud BOX <PROPBUTTON> [ "property": accAutoLoginReq cRUd ]
      , "Userid": accUserid cRud
      , "Person": accPersonRef cRud
--    , "Person": accPerson cRud
      , "Organization": accOrgRef cRud
--    , "Organization": accOrg cRud
      , "UIDs": accUID cRud
--    , "Persona" : accPersona cRud
--    , "Allowed roles": accAllowedRoles cRUd
--    , "Default roles": accDefaultRoles cRUd
      , "De-activate?" : I cRud BOX <PROPBUTTON> [ "property": accIsActive cRUd ]
      ]

--$This section allows (enabled) developers to activate de-activated accounts (so that they can be used for logging in)
   , "Inactive accounts": sessionLoginAssist;V;(I-accIsActive) cRuD BOX <SHCOLS>
      [ "Activate?" : I cRud BOX <PROPBUTTON> [ "property": accIsActive cRUd ]
      , "Userid": accUserid cRud
      , "Person": accPersonRef cRud
--    , "Person": accPerson cRud
      , "Organization": accOrgRef cRud
--    , "Organization": accOrg cRud
      , "UIDs": accUID cRud
--    , "Persona" : accPersona cRud
--    , "Allowed roles": accAllowedRoles cRUd
--    , "Default roles": accDefaultRoles cRUd
      ]

   ]

-----------------------------------------------------------
ENDCONTEXT