CONTEXT "Login" IN ENGLISH LATEX
-----------------------------------------------------------
{- This file contains an INTERFACE by which a user can:
- Login (with a username and password)
- Register himself (i.e. just the username and password)
- Logout.
This interface is here for the following purposes:
- Testing the SIAM module
- providing a template that Ampersand modelers can draw inspiration from
-}

INCLUDE "./SIAM_RegisterWithUPW.adl"     -- Extension that registers not-yet existing users that specify a U/PW.
INCLUDE "./SIAM_RegisterWithUPW.ifc"     -- Interface for RegisterWithUPW Extension
INCLUDE "./SIAM_Logout.ifc"              -- Interface for logging out.

POPULATION LoginMessage CONTAINS [ "You are currently logged in as " ]
POPULATION PopoverText  CONTAINS [ "Click this button to create your account" ]
INTERFACE "Login (U/PW)" FOR Anonymous: "_SESSION"[SESSION] cRud BOX <DIV>
   [ "Login": sessionIsAnon-sessionAccRegForm;sessionAccRegForm~ cRud BOX <HCOLS>
      [ "Userid": loginUserid cRUd
      , "Password": loginPassword crUd -- crUd is needed for Passwords
--    , "IdP": loginIdP cRUd
--    , "Auth.level": sessionAuthLoA cRUd

      --$Button that allows user to login
      , "Login": I cRud BOX <PROPBUTTON> 
         [ "property": loginReq cRUd
         , "disabled": I - (loginUserid;loginUserid~ /\ loginPassword;loginPassword~) cRud
         ]

      --$Button that allows account-registration in anonymous sessions      
      , "   ": I-sessionLoginAssist cRud BOX <DIV> -- Only allow registration when we're not trying to login.
         [ "Register": I BOX <PROPBUTTON> 
            [ "property": sessionCreateARFReq cRUd
--          , "hide": I /\ sessionAccRegForm;sessionAccRegForm~ cRud
            , "disabled": I /\ sessionAccRegForm;sessionAccRegForm~ cRud
          , "popovertext": V;"Click this button to create your account"[PopoverText] cRud
            ]
         ]
      ]
   , "Register": sessionIsAnon;sessionAccRegForm INTERFACE "Register"

   , "Logout": sessionIsUser INTERFACE "Logout"
   ]

-----------------------------------------------------------
ENDCONTEXT