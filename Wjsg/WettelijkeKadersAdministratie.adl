CONTEXT JustGegevens
 -- omdat we de structuur van de Nederlandse wetgeving nodig hebben.

    CONCEPT URL "a uniform resource locator as defined by RFC3986"
    REPRESENT URL TYPE BIGALPHANUMERIC
    VIEW URL : URL DEFAULT HTML TEMPLATE "View-URL.html" ENDVIEW

PURPOSE PATTERN Wetsartikelen
{+ Dit thema beschrijft de structuur van wet- en regelgeving (oba: een juridisch metamodel) om referenties naar juridische regels op een goede manier te kunnen toepassen.
+}
PATTERN Wetsartikelen
-- VIEW RechtsGrond : RechtsGrond DEFAULT {url:url, regelingTitel:regeling;titel, artikel:artikel, lid:lid} HTML TEMPLATE "Rechtsgrondlink.html" ENDVIEW
RELATION url[RechtsGrond*URL]
RELATION regeling[RechtsGrond*Regeling] [UNI]
RELATION bijlage[RechtsGrond*Bijlage] [UNI]
RELATION aanwijzing[RechtsGrond*Aanwijzing] [UNI]
RELATION artikel[RechtsGrond*Artikel]
RELATION lid[RechtsGrond*Lid]
RELATION sub[RechtsGrond*Sub]
VIEW Regeling : Regeling DEFAULT {url:url, regelingTitel:titel} HTML TEMPLATE "Regelinglink.html" ENDVIEW
RELATION bwb[Regeling*BasisWettenBestand] [UNI]
RELATION titel[Regeling*Titel] [UNI,TOT]
RELATION url[Regeling*URL]
RELATION gedelegeerdUit[Regeling*Regeling] [UNI]
RELATION regelgeving[Domein*Regeling]
RELATION soort[Regeling*Regelingsoort] [UNI]
RELATION onderwerp[Regeling*Concept]
RELATION afkorting[Regeling*Tekst] [UNI]

ROLE ExecEngine MAINTAINS RechtsGrond2URL, Regeling2URL
RULE RechtsGrond2URL : I[RechtsGrond] |- url;url~
VIOLATION (TXT "{EX} InsPair;url;RechtsGrond;", SRC I, TXT ";URL;https://www.wetten.nl/", TGT I)
RULE Regeling2URL : I[Regeling] |- url;url~
VIOLATION (TXT "{EX} InsPair;url;Regeling;", SRC I, TXT ";URL;https://www.wetten.nl/", TGT I)

ENDPATTERN

INTERFACE RechtsGrond : I[RechtsGrond] cRud BOX
   [ url :        url
   , regeling :   regeling
   , bijlage :    bijlage
   , aanwijzing : aanwijzing
   , artikel :    artikel
   , lid :        lid
   , sub :        sub
   ]

PURPOSE PATTERN Wetsartikelen
{+ Dit thema geeft een definitie met classificatie van een aantal juridische begrippen op basis van wet- en regelgeving,
om de aansluiting met de betekenis vanuit wet- en regelgeving te kunnen behouden.
+}
PATTERN "Juridische begrippen"
CONCEPT Sepot "Onder sepot wordt verstaan de beslissing van het openbaar ministerie tot niet vervolging of niet verdere vervolging van de zaak." "Artikel 1 sub b BJG"
CONCEPT "Strafrechtelijke procedure" "Onder strafrechtelijke procedure wordt verstaan de fase die aan het strafproces voorafgaat, het strafproces zelf en de tenuitvoerlegging van de veroordeling." "Artikel 1 sub e BJG"
CONCEPT RechtsPersoon "een rechtspersoon is een organisatie als bedoeld in boek 2 van het Burgerlijk Wetboek, alsmede de daarmee gelijkgestelde organisaties als bedoeld in artikel 51, derde lid, van het Wetboek van Strafrecht." "Artikel 1 sub d Wjsg"
CLASSIFY NatPersoon ISA Persoon
CLASSIFY RechtsPersoon ISA Persoon

--CONCEPT Verstrekkingsgrond
--CONCEPT Doel
--CONCEPT Rol
--CONCEPT GegevenSoort

ENDPATTERN

ENDCONTEXT