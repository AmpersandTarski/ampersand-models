CONTEXT JustGegevens IN DUTCH
PURPOSE CONTEXT JustGegevens
{+Justitiële gegevens zijn limitatief gedefinieerd op grond van Artikel 7, tweede lid, Wjsg:
Toegang tot justitiële gegevens is uitsluitend voorbehouden aan personen die onder het beheer van de Onze Minister ressorteren of op grond van zijn instructie, behoudens een bij of krachtens de wet gegeven voorschrift.
Welke gegevens justitieel zijn wordt in hfst. 2 afdeling 1 van het Besluit justitiële en strafvorderlijke gegevens (BJG) bepaald.
+}

-- INCLUDE "Portaal.adl"           -- 
-- INCLUDE "Autorisatie.adl"           -- 
-- INCLUDE "Abonnementen.adl"           -- 
-- INCLUDE "Opdrachtenadministratie.adl"           -- 
-- INCLUDE "InformatieproductenAssemblage.adl"           -- 
-- INCLUDE "VerstrekkingenAdministratie.adl"        -- omdat we de structuur van de regels ten aanzien van verstrekkingen.
-- INCLUDE "AfnamesAdministratie.adl"           -- 
-- INCLUDE "Informatieproductencatalogus.adl"           -- 
-- INCLUDE "WettelijkeKadersAdministratie.adl"           -- omdat we de structuur van de Nederlandse wetgeving nodig hebben.
-- INCLUDE "JDSregister.adl"           -- 
-- INCLUDE "Monitoring.adl"           -- 
-- INCLUDE "Logging.adl"           -- 
-- INCLUDE "Procesbesturing.adl"           -- 

INCLUDE "Berichtencentrum.adl"           -- 
INCLUDE "BJGartikel4.adl"  -- voor de overtredingssoorten die als justitiële gegevens aangemerkt worden.
INCLUDE "Justitieel.adl"
INCLUDE "Verstrekkingen.adl"
INCLUDE "JustGegevensPop.xlsx"
INCLUDE "Persoongegevens.adl"
INCLUDE "Uittreksel.adl"

REPRESENT Tekst TYPE ALPHANUMERIC
REPRESENT TEKST TYPE BIGALPHANUMERIC

PURPOSE PATTERN "Beslissingen"
{+Het systeem JDS zien we als een opslagstructuur van zaken en feiten.
Dit hoofdstuk werkt de structuur van zaken uit, ten behoeve van justitiële documentatie.
+}
PATTERN "Beslissingen"
RELATION zak_id[Beslissing*Zaak] [UNI]  -- zie Artikel 3 en 5 BJG
MEANING "de zaak waarin een beslissing genomen, c.q. een beschikking uitgevaardigd is"
RELATION nietVervolgd[Zaak*Beslissing]  -- zie Artikel 7 lid 1 BJG
MEANING "Er is een beslissing tot niet vervolgen omdat de betrokken persoon ten onrechte als verdachte is aangemerkt, of na vaststelling van een rechtmatige geweldsaanwending van een ambtenaar als bedoeld in artikel 1, eerste lid, van de Ambtsinstructie voor de politie, de Koninklijke marechaussee en andere opsporingsambtenaren."
RELATION gerecht[Beslissing*Gerecht] [UNI]  -- zie Artikel 7 lid 1 sub j. BJG
RELATION parketnummer[Zaak*Parketnummer] [UNI,INJ]  -- Artikel 7 lid 1 sub b. BJG
RELATION onherroepelijk[Beslissing*Datum] [UNI,INJ]  -- Artikel 7 lid 1 sub b. BJG
ENDPATTERN

PATTERN "Bewijsmiddel"
CLASSIFY Beschikking ISA Document
RELATION bewijsmiddel[Feit*Document]  -- zie Artikel 3 en 5 BJG
MEANING "Documenten, die als justitiële of strafvorderlijke gegevens zijn opgenomen in het systeem, en als bewijs gelden voor enig feit, worden in het systeem aan dat feit gekoppeld."
ENDPATTERN

PATTERN "Feiten"
CLASSIFY Misdrijf ISA Feit
CLASSIFY Overtreding ISA Feit
CLASSIFY Beschikking ISA Beslissing
CLASSIFY Sepot ISA Beslissing
CLASSIFY RechterlijkeUitspraak ISA Beslissing
CLASSIFY Beslissing ISA Gebeurtenis
RELATION strafbaar[ProcesVerbaal*Feit]  -- Artikel 7 lid 1 sub c. BJG
{- uitzoeken: wat is de juiste definitie van strafbaar feit. -}
CLASSIFY Feit ISA Gebeurtenis
{- 
-}
PURPOSE RELATION procesVerbaal[Zaak*ProcesVerbaal]
{+
+}
RELATION procesVerbaal[Zaak*ProcesVerbaal]

RELATION tav[Feit*Beslissing]
MEANING "een feit waarover een beslissing genomen, c.q. een beschikking uitgevaardigd is"

ROLE ExecEngine MAINTAINS "Beslissing over een feit bij de juiste zaak opbergen"
RULE "Beslissing over een feit bij de juiste zaak opbergen":
   procesVerbaal;strafbaar;tav |- beslissing
MEANING "Een beslissing ten aanzien van een feit heeft betrekking op de zaak waarin dat feit ten laste is gelegd."
VIOLATION (TXT "{EX} InsPair;beslissing;Zaak;", SRC I, TXT ";Beslissing;", TGT I)


PURPOSE RELATION afdoeningOM[Feit*Beschikking]
{+De gebeurtenis dat het openbaar ministerie een beslissing tot afdoening van de zaak heeft genomen,
dient geregistreerd te worden.
+}
RELATION afdoeningOM[Feit*Beschikking]  -- Artikel 3 lid a. BJG
PURPOSE RELATION lageBoete[Feit*Beschikking]
{+Als het OM uitsluitend een geldboete oplegt die minder dan € 100,– beloopt,
dan wordt dit geregistreerd omdat deze boetes niet tot de justitiële gegevens behoren.
+}
RELATION lageBoete[Feit*Beschikking]  -- Artikel 3 lid a. BJG
MEANING "Het OM heeft uitsluitend een geldboete opgelegd die minder dan € 100,– beloopt."
RELATION hogeBoete[Feit*Beschikking]  -- Artikel 3 lid a. BJG
MEANING "Het OM heeft een geldboete opgelegd die € 100,– of meer beloopt."

RELATION bepaling[Beslissing*TEKST]  -- Artikel 8 lid 1 sub b. BJG
MEANING "de aan een beslissing verbonden bepalingen"
RELATION strafbepaling[Beslissing*TEKST]  -- Artikel 7 lid 1 sub c,h. BJG
MEANING "de strafbepalingen van het strafbare feit, de opgelegde straffen, maatregelen en aanwijzingen"

RELATION omschrijving[Feit*TEKST] [UNI]  -- ?? alleen voor demo doeleinden.
MEANING "de letterlijke tekst van een Feit"
RELATION kwalificatie[Feit*Tekst] [UNI]  -- Artikel 7 lid 1 sub d. BJG
RELATION maatschappelijkeClassificatie[Feit*Tekst] [UNI]  -- Artikel 7 lid 1 sub e. BJG
RELATION plaats[Feit*Plaats] [UNI]  -- Artikel 7 lid 1 sub f,g,h. BJG
RELATION voorgedaan[Gebeurtenis*Tijdstip] [UNI]  -- Artikel 7 lid 1 sub f,g,h. BJG
MEANING "de datum waarop een beslissing genomen, c.q. een beschikking uitgevaardigd is, c.q. een feit zich heeft voorgedaan"
RELATION voorgedaan[Feit*Periode] [UNI]  -- Artikel 7 lid 1 sub f. BJG
RELATION sepot[Feit*Sepot] [UNI]  -- Artikel 7 lid 1 sub g. BJG
RELATION sepotcode[Sepot*Sepotcode] [UNI]  -- Artikel 7 lid 1 sub g. BJG
RELATION grond[Sepot*Tekst]  -- Artikel 7 lid 1 sub g. BJG
RELATION voorwaarde[Beslissing*TEKST]  -- Artikel 7 lid 1 sub g. BJG
MEANING "de bij de beslissing tot voorwaardelijk seponeren gestelde voorwaarden"
RELATION voldaan[Sepot*Datum] [UNI]  -- Artikel 7 lid 1 sub g. BJG
MEANING "de datum waarop aan alle gestelde voorwaarden is voldaan"
RELATION dt_onherroepelijk[Beslissing*Datum] [UNI]  -- Artikel 7 lid 1 sub h. BJG
MEANING "de datum waarop waarop de strafbeschikking onherroepelijk is geworden"
RELATION ogv[Beslissing*RechtsGrond]   -- Artikel 7 lid 1 sub h. BJG
MEANING "een rechtsgrond waarop een beslissing is gebaseerd"
RELATION uitgevoerd[Beslissing*Datum] [UNI]  -- Artikel 7 lid 1 sub h en k BJG
MEANING "de datum waarop de beslissing volledig ten uitvoer is gelegd"
RELATION wijze[Beslissing*TEKST] [UNI]  -- Artikel 7 lid 1 sub k BJG
MEANING "de wijze waarop de beslissing ten uitvoer is gelegd"
RELATION gemerkt101112[Beslissing*Datum] [UNI]  -- Artikel 7 lid 1 sub h en j. BJG
MEANING "Als een datum bestaat in deze relatie, kan de strafbeschikking tot genoemde datum worden aangemerkt als een gegeven als bedoeld in de artikelen 10, 11 of 12 van de wet"
ENDPATTERN


INTERFACE Zaken : "_SESSION" cRud BOX
    [ zaken    : V[SESSION*Zaak] cRud
      BOX [ Parketnummer : I cRud
          , tenlastelegging : procesVerbaal;strafbaar;kwalificatie
          ]
    ]

INTERFACE Feiten : "_SESSION" cRud BOX
    [ feiten    : V[SESSION*Feit] cRud
    ]

VIEW "Feit": Feit
   { "Op ": voorgedaan, " te ": plaats, ": ": kwalificatie
   } ENDVIEW

INTERFACE Feit : I[Feit] cRud BOX
    [ PV : strafbaar~ cRud
    , voorgedaan : voorgedaan
    , plaats : plaats
    , omschrijving : omschrijving
    , "maatschappelijke classificatie" : maatschappelijkeClassificatie
    , kwalificatie : kwalificatie
    ]

PURPOSE INTERFACE "Proces-verbaal"
{+
+}
INTERFACE "Proces-verbaal" : I[ProcesVerbaal]
BOX [ "Ontvangen op parket" : ontvangstParket                -- Artikel 7 lid 2 sub b BJG
      BOX [ tijdstip : voorgedaan
          ]
    , "in behandeling Hoge Raad" : inBehandelingPG                -- Artikel 7 lid 2 sub c BJG
      BOX [ tijdstip : voorgedaan
          ]
    ]

VIEW Zaak : Zaak ( parketnummer )

PURPOSE INTERFACE JustitiëleGegevens
{+Om de justitiële gegevens van een specifieke zaak te bepalen hanteren we hetgeen in Artikel 7 BJG is vermeld.
De analyse is gebaseerd op BWBR0016544-geldend op 15-09-2017.
+}
INTERFACE JustitiëleGegevens : I[Zaak]
BOX [ Justitiabele : justitiabele
    , Beslissingen : zak_id~ - nietVervolgd                                   -- Artikel 7 lid 1 sub a BJG
    , Parketnummer : parketnummer                                             -- Artikel 7 lid 1 sub b BJG
    , Parket : parket                                                         -- Artikel 7 lid 2 sub a BJG
    , "Proces-verbaal" : procesVerbaal INTERFACE "Proces-verbaal"
    , "Strafbaar feit" : procesVerbaal;strafbaar -- ;(I[Feit]/\tav;strafbepaling;strafbepaling~;tav~)
      BOX [ omschrijving : omschrijving                                       -- Artikel 7 lid 1 sub d BJG
          , kwalificatie : kwalificatie                                       -- Artikel 7 lid 1 sub d BJG
          , "maatschappelijke classificatie" : maatschappelijkeClassificatie  -- Artikel 7 lid 1 sub e BJG
          , datum : voorgedaan[Gebeurtenis*Tijdstip]                          -- Artikel 7 lid 1 sub f BJG
          , periode : voorgedaan[Feit*Periode]                                -- Artikel 7 lid 1 sub f BJG
          , sepot : sepot                                                     -- Artikel 7 lid 1 sub g BJG
            BOX [ datum : voorgedaan[Gebeurtenis*Tijdstip]
                , sepotcode : sepotcode
                , grond : grond
                , "gestelde voorwaarden" : voorwaarde
                , "aan alles voldaan per (datum)" : voldaan
                ]
          , "bij beschikking" : tav;I[Beschikking]                            -- Artikel 7 lid 1 sub h BJG
            BOX [ datum : voorgedaan[Gebeurtenis*Tijdstip]
                , "opgelegde straffen, maatregelen en aanwijzingen" : strafbepaling
                , "onherroepelijk per (datum)" : dt_onherroepelijk
                , "uitgevoerd per (datum)" : uitgevoerd
                , "Art. 10, 11, of 12 tot (datum)" : gemerkt101112
                ]
          ]
    , "voorlopige maatregel op grond van de WED" : zak_id~;(I[Beslissing]/\ogv;regeling;bwb;"BWBR0002063";(ogv;regeling;bwb)~);maatregel                           -- Artikel 7 lid 1 sub i BJG
      BOX [ datum : maatregel~;voorgedaan[Gebeurtenis*Tijdstip]
          , aanduiding : aanduiding
          , beëindiging : beeindiging
          , verlenging : verlenging
          , wijziging : wijziging
          , intrekking : intrekking
          , opheffing : opheffing
          ]
    , "rechterlijke uitspraak" : zak_id~;I[RechterlijkeUitspraak]          -- Artikel 7 lid 1 sub j BJG
      BOX [ gerecht : gerecht
          , datum : voorgedaan[Gebeurtenis*Tijdstip]
          , kwalificatie : tav~;kwalificatie
          , strafbepalingen : strafbepaling
          , voorwaarden : voorwaarde
          , "onherroepelijk per (datum)" : dt_onherroepelijk
          , "vermoedelijke einde van een proeftijd" : maatregel;eindeProeftijd
          , "Art. 10, 11, of 12 tot (datum)" : gemerkt101112
          ]
    , "ten uitvoer is gelegd" : zak_id~;(I[RechterlijkeUitspraak]/\uitgevoerd;uitgevoerd~);maatregel  -- Artikel 7 lid 1 sub k BJG
      BOX [ "uitgevoerd per (datum)" : maatregel~;uitgevoerd
          , wijze : aanduiding
          , aangevangen : aanvang
          , beëindigd : beeindiging
          , invrijheidstelling : invrijheidstelling                           -- Artikel 7 lid 1 sub l BJG
          ]
    ]

ENDCONTEXT