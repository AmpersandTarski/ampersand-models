CONTEXT JustGegevens
INCLUDE "Verstrekkingen.xlsx"        -- Hierin zijn de regels over het verstrekken uit het Besluit justitiële en strafvorderlijke gegevens gecodeerd.

PURPOSE PATTERN Verstrekkingen
{+ In het Besluit justitiële en strafvorderlijke gegevens (BJG) is gespecificeerd welke gegevens aan welke organen voor welke doelen verstrekt mogen worden.
Dit besluit dient onder meer tot regeling van de verstrekking van deze gegevens (aanhef BJG).
De relaties in dit pattern zijn door een taalkundige analyse van de BJG tot stand gekomen.
+}

PATTERN Verstrekkingen
RELATION aan[Verstrekkingsgrond*Rol] [TOT]
MEANING "Een verstrekkingsgrond maakt duidelijk aan welke functionaris(sen) een verstrekking plaatsvindt."
RELATION objecttype[Verstrekkingsgrond*GegevenSoort]
MEANING "Een verstrekkingsgrond specificeert welke gegevens mogen worden verstrekt."
RELATION wanneer[Verstrekkingsgrond*Tekst] [UNI]
MEANING "Een verstrekkingsgrond specificeert wanneer gegevens mogen worden verstrekt."
RELATION doel[Verstrekkingsgrond*Doel]
MEANING "Een verstrekkingsgrond specificeert de doelen waarvoor gegevens mogen worden verstrekt."
RELATION grondslag[Verstrekkingsgrond*RechtsGrond] [UNI,TOT,INJ]
MEANING "Een verstrekkingsgrond specificeert op welke titel gegevens mogen worden verstrekt."
RELATION titel[Verstrekkingsgrond*Titel] [UNI]
MEANING "Een titel specificeert in tekst op welke titel gegevens mogen worden verstrekt."

RELATION aanduiding[Doel*TEKST] [UNI,TOT]
MEANING "Het doel duidt aan waartoe gegevens mogen worden verstrekt."
RELATION grondslag[Doel*RechtsGrond] [UNI,TOT,INJ]
MEANING "De grondslag van een doel is de titel waarin dit doel is geformuleerd."
RELATION titel[Doel*Titel] [UNI]
MEANING "Een titel specificeert in tekst op welke titel het doel is gebaseerd."

RELATION rolnaam[Rol*Functionaris] [UNI]
MEANING "Een verstrekkingsgrond maakt duidelijk aan welke functionaris(sen) een verstrekking plaatsvindt."
RELATION verbijzonderd[Rol*Rol] [UNI]
PRAGMA "Rol " " is een generieke benaming van " "."
MEANING "Een rol kan nader worden gepreciseerd, waardoor een specialisatie van rollen ontstaat."
RELATION orgaan[Rol*Orgaan] [UNI]
MEANING "Het orgaan beschrijft namens welk orgaan de bedoelde functionaris handelt."
RELATION alsbedoeld[Rol*RechtsGrond] [UNI]
MEANING "Een rechtsgrond maakt eenduidig bepaalbaar welke functionaris bedoeld wordt."
RELATION titel[Rol*Titel] [UNI]
MEANING "Een titel specificeert in tekst op welke titel de Rol is gebaseerd."
RELATION gegevensoort[GegevenSoort*Tekst] [UNI]
MEANING "Een gegevensoort specificeert welke gegevens mogen worden verstrekt."
RELATION objecttype[GegevenSoort*Object] [UNI,TOT]
MEANING "Een objecttype specificeert van welk soort object gegevens mogen worden verstrekt."
RELATION alsbedoeld[GegevenSoort*RechtsGrond] [UNI,INJ]
MEANING "De rechtsgrond maakt traceerbaar welke gegevens bedoeld worden."
RELATION titel[GegevenSoort*Titel] [UNI]
MEANING "Een titel specificeert in tekst op welke titel de GegevenSoort is gebaseerd."
ENDPATTERN

{-Als gebruiker wil ik een rechtsgrond zien in de vorm van een hyperlink naar wetten.nl
Om die reden moet `grondslag` injectief zijn en `titel` univalent.
-}
VIEW RechtsGrondDoel :       RechtsGrond {itemName : grondslag~;titel[Doel*Titel], url : url} HTML TEMPLATE "View-Hyperlink.html" ENDVIEW
VIEW Verstrekkingsgrond :    RechtsGrond {itemName : grondslag~;titel[Verstrekkingsgrond*Titel], url : url} HTML TEMPLATE "View-Hyperlink.html" ENDVIEW
VIEW RechtsGrondAlsbedoeld : RechtsGrond {itemName : alsbedoeld~;titel[GegevenSoort*Titel]\/grondslag~;titel[Doel*Titel], url : url} HTML TEMPLATE "View-Hyperlink.html" ENDVIEW
VIEW RechtsGrondRol :        RechtsGrond {itemName : alsbedoeld~;titel[Rol*Titel], url : url} HTML TEMPLATE "View-Hyperlink.html" ENDVIEW

INTERFACE Verstrekkingsgronden : "_SESSION" cRud BOX
    [ Verstrekkingsgronden : V[SESSION*Verstrekkingsgrond] INTERFACE "Verstrekkingsgrond"
    ]

INTERFACE "Verstrekkingsgrond": I[Verstrekkingsgrond] cRud BOX
         [ grondslag : grondslag cRud <Verstrekkingsgrond>
         , "verstrekken aan" : aan INTERFACE Rol
         , gegevens : objecttype;(I/\gegevensoort;gegevensoort~) cRud BOX
             [ "object type" : objecttype[GegevenSoort*Object]
             , gegevensoort :  gegevensoort
             , "als bedoeld in" : alsbedoeld cRud <RechtsGrondAlsbedoeld>
             ]
         , wanneer : wanneer
         , "doel(en)" : doel INTERFACE Doel
         ]

INTERFACE Doelen : "_SESSION" cRud BOX
    [ Doelen : V[SESSION*Doel] INTERFACE Doel
    ]

INTERFACE Doel: I[Doel] cRud BOX
         [ doel : aanduiding
         , grondslag :  grondslag cRud <RechtsGrondDoel>
         ]

INTERFACE Rollen : "_SESSION" cRud BOX
    [ Rollen : V[SESSION*Rol] INTERFACE Rol
    ]

INTERFACE Rol: I[Rol] cRud BOX
         [ rol : rolnaam
         , orgaan : orgaan
         , "als bedoeld in" :  alsbedoeld cRud <RechtsGrondRol>
         ]

ENDCONTEXT