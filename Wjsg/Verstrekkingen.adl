CONTEXT JustGegevens
INCLUDE "Verstrekkingen.xlsx"        -- Hierin zijn de regels over het verstrekken uit het Besluit justitiële en strafvorderlijke gegevens gecodeerd.

PURPOSE PATTERN Verstrekkingen
{+ In het Besluit justitiële en strafvorderlijke gegevens (BJG) is gespecificeerd welke gegevens aan welke organen voor welke doelen verstrekt mogen worden.
Dit besluit dient onder meer tot regeling van de verstrekking van deze gegevens (aanhef BJG).
De relaties in dit pattern zijn door een taalkundige analyse van de BJG tot stand gekomen.
+}

PATTERN Verstrekkingen
RELATION aan[Verstrekkingsgrond*Rol] [TOT]
MEANING "Een verstrekkingsgrond maakt duidelijk aan welke functionaris(sen) een verstrekking plaatsvindt."
RELATION object[Verstrekkingsgrond*GegevenSoort]
MEANING "Een verstrekkingsgrond specificeert welke gegevens mogen worden verstrekt."
RELATION wanneer[Verstrekkingsgrond*Tekst] [UNI]
MEANING "Een verstrekkingsgrond specificeert wanneer gegevens mogen worden verstrekt."
RELATION doel[Verstrekkingsgrond*Doel]
MEANING "Een verstrekkingsgrond specificeert de doelen waarvoor gegevens mogen worden verstrekt."
RELATION grondslag[Verstrekkingsgrond*RechtsGrond] [UNI,TOT,INJ]
MEANING "Een verstrekkingsgrond specificeert op welke titel gegevens mogen worden verstrekt."
RELATION titel[Verstrekkingsgrond*Titel] [UNI]
MEANING "Een titel specificeert in tekst op welke titel gegevens mogen worden verstrekt."

RELATION aanduiding[Doel*Tekst] [UNI,TOT]
MEANING "Een verstrekkingsgrond specificeert op welke titel gegevens mogen worden verstrekt."
RELATION grondslag[Doel*RechtsGrond] [UNI]
MEANING "De grondslag van een doel is de titel waarin dit doel is geformuleerd."
RELATION titel[Doel*Titel] [UNI]
MEANING "Een titel specificeert in tekst op welke titel het doel is gebaseerd."

RELATION rolnaam[Rol*Functionaris] [UNI]
MEANING "Een verstrekkingsgrond maakt duidelijk aan welke functionaris(sen) een verstrekking plaatsvindt."
RELATION verbijzonderd[Rol*Rol] [UNI]
PRAGMA "Rol " " is een generieke benaming van " "."
MEANING "Een rol kan nader worden gepreciseerd, waardoor een specialisatie van rollen ontstaat."
RELATION alsbedoeld[Rol*RechtsGrond]
MEANING "Een rechtsgrond maakt eenduidig bepaalbaar welke functionaris bedoeld wordt."

RELATION object[GegevenSoort*Object] [UNI,TOT]
MEANING "Een verstrekkingsgrond specificeert welke gegevens mogen worden verstrekt."
RELATION alsbedoeld[GegevenSoort*RechtsGrond] [UNI,TOT]
MEANING "De rechtsgrond maakt traceerbaar welke gegevens bedoeld worden."
ENDPATTERN

INTERFACE Verstrekkingsgronden : "_SESSION" cRud BOX
    [ VERSTREKKINGEN : V[SESSION*Verstrekkingsgrond] INTERFACE "Verstrekkingsgrond"
    ]

INTERFACE "Verstrekkingsgrond": I[Verstrekkingsgrond] cRud BOX
         [ aan :        aan;rolnaam
         , object :     object;object[GegevenSoort*Object]
         , wanneer :    wanneer
         , doel :       doel cRud BOX
             [ aanduiding : aanduiding
             , grondslag :  grondslag
             , titel :      titel
             ]
         , grondslag :  grondslag
         , titel :      titel
         ]

INTERFACE Doelen : "_SESSION" cRud BOX
    [ VERSTREKKINGEN : V[SESSION*Doel] INTERFACE "Doel"
    ]

INTERFACE Doel: I[Doel] cRud BOX
         [ aanduiding : aanduiding
         , grondslag :  grondslag
         , titel :      titel
         ]

ENDCONTEXT