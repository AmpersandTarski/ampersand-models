CONTEXT JustGegevens
PURPOSE CONTEXT JustGegevens
{+
+}
INCLUDE "Wetten.adl"        -- omdat we de structuur van de Nederlandse wetgeving nodig hebben.

RELATION justitieel[Zaak] [PROP]  -- Artt. 2-4 Bjsg

ROLE ExecEngine MAINTAINS Art2BjsgIns, Art3aBjsgIns, Art3bBjsgIns, Art4BjsgIns, JustitiëleGegevensIns
                        , Art2BjsgDel, Art3aBjsgDel, Art3bBjsgDel, Art4BjsgDel, JustitiëleGegevensDel

RELATION art2Bjsg[Zaak] [PROP]  -- Art. 2 Bjsg
PURPOSE RULE Art2BjsgIns
{+Met betrekking tot misdrijven worden als justitiële gegevens aangemerkt de in de artikelen 6 en 7 vermelde gegevens van zaken waarvan het proces-verbaal door het openbaar ministerie of de procureur-generaal bij de Hoge Raad op grond van artikel 76 van de Wet op de rechterlijke organisatie in behandeling is genomen.
+}
RULE Art2BjsgIns : procesVerbaal;(inBehandeling\/inBehandelingPG);I[Misdrijf];V |- art2Bjsg
VIOLATION (TXT "{EX} InsPair;art2Bjsg;Zaak;", SRC I, TXT ";Zaak;", TGT I)
RULE Art2BjsgDel : art2Bjsg |- procesVerbaal;(inBehandeling\/inBehandelingPG);I[Misdrijf];V
VIOLATION (TXT "{EX} DelPair;art2Bjsg;Zaak;", SRC I, TXT ";Zaak;", TGT I)

PURPOSE RULE Art3aBjsgIns
{+Met betrekking tot overtredingen worden als justitiële gegevens aangemerkt:
a.	de in de artikelen 6 en 7, eerste lid, vermelde gegevens van zaken waarin het openbaar ministerie een beslissing tot afdoening van de zaak heeft genomen met uitzondering van de beslissing tot uitvaardiging van een strafbeschikking waarin uitsluitend een geldboete wordt opgelegd die minder dan € 100,– beloopt alsmede de beslissing tot niet verdere vervolging van de zaak, tenzij voorwaarden zijn gesteld aan laatstgenoemde beslissing;
+}
RELATION art3aBjsg[Zaak] [PROP]  -- Art. 3 sub a Bjsg
RULE Art3aBjsgIns : procesVerbaal;strafbaar;I[Overtreding];(afdoeningOM-(lageBoete\/sepot));V[Beslissing*Zaak] |- art3aBjsg
VIOLATION (TXT "{EX} InsPair;art3aBjsg;Zaak;", SRC I, TXT ";Zaak;", TGT I)
RULE Art3aBjsgDel : art3aBjsg |- procesVerbaal;strafbaar;I[Overtreding];(afdoeningOM-(lageBoete\/sepot));V[Beslissing*Zaak]
VIOLATION (TXT "{EX} DelPair;art3aBjsg;Zaak;", SRC I, TXT ";Zaak;", TGT I)

PURPOSE RULE Art3bBjsgIns
{+Met betrekking tot overtredingen worden als justitiële gegevens aangemerkt:
b.	de in de artikelen 6 en 7, eerste lid, vermelde gegevens van zaken waarin de rechter een al dan niet herroepelijke beslissing heeft genomen voorzover een taakstraf of een vrijheidsstraf, anders dan vervangende, is opgelegd of een geldboete van minimaal € 100,– alsmede de zaken waarin een bijkomende straf is opgelegd.
+}
RELATION art3bBjsg[Zaak] [PROP]  -- Art. 3 sub b Bjsg
RULE Art3bBjsgIns : procesVerbaal;strafbaar;I[Overtreding];tav;(gerecht;V/\(taakStraf\/(vrijheidsStraf-vervangendeStraf)\/hogeBoete\/bijkomendeStraf));V |- art3bBjsg
VIOLATION (TXT "{EX} InsPair;art3bBjsg;Zaak;", SRC I, TXT ";Zaak;", TGT I)
RULE Art3bBjsgDel : art3bBjsg |- procesVerbaal;strafbaar;I[Overtreding];tav;(gerecht;V/\(taakStraf\/(vrijheidsStraf-vervangendeStraf)\/hogeBoete\/bijkomendeStraf));V
VIOLATION (TXT "{EX} DelPair;art3bBjsg;Zaak;", SRC I, TXT ";Zaak;", TGT I)

RELATION art4Bjsg[Zaak] [PROP]  -- Art. 4 Bjsg
PURPOSE RULE Art4BjsgIns
{+In afwijking van artikel 3 worden met betrekking tot de in het tweede lid genoemde overtredingen als justitiële gegevens aangemerkt de in de artikelen 6 en 7 vermelde gegevens van zaken waarvan het proces-verbaal door het openbaar ministerie in behandeling is genomen.
+}
RULE Art4BjsgIns : procesVerbaal;(inBehandeling;V/\strafbaar;I[Overtreding];V) |- art4Bjsg
VIOLATION (TXT "{EX} InsPair;art4Bjsg;Zaak;", SRC I, TXT ";Zaak;", TGT I)
RULE Art4BjsgDel : art4Bjsg |- procesVerbaal;(inBehandeling;V/\strafbaar;I[Overtreding];V)
VIOLATION (TXT "{EX} DelPair;art4Bjsg;Zaak;", SRC I, TXT ";Zaak;", TGT I)

PURPOSE RULE JustitiëleGegevensIns
{+Artikelen 2 t/m 4 Bjsg bepalen welke zaken worden aangemerkt als justitiele gegevens.
Uit deze zaken zijn slechts die gegevens justitieel, die in artt. 6 en 7 limitatief zijn opgesomd. +}
RULE JustitiëleGegevensIns : art2Bjsg\/art3aBjsg\/art3bBjsg\/art4Bjsg |- justitieelVIOLATION (TXT "{EX} InsPair;justitieel;Zaak;", SRC I, TXT ";Zaak;", TGT I)
RULE JustitiëleGegevensDel : justitieel |- art2Bjsg\/art3aBjsg\/art3bBjsg\/art4BjsgVIOLATION (TXT "{EX} DelPair;justitieel;Zaak;", SRC I, TXT ";Zaak;", TGT I)

-- De volgende regels gaan over Artikel 5, die bepaalt welke strafbeschikkingen als justitieel gegeven worden aangemerkt.
RELATION justitieel[Beschikking] [PROP]  -- Artt. 5 Bjsg
ROLE ExecEngine MAINTAINS Art5lid1BjsgIns, Art5lid2BjsgIns, JustitiëleBeschikkingenIns
                        , Art5lid1BjsgDel, Art5lid2BjsgDel, JustitiëleBeschikkingenDel

RELATION art5lid1Bjsg[Beschikking] [PROP]  -- Art. 5 lid 1 Bjsg
PURPOSE RULE Art5lid1BjsgIns
{+Als justitiële gegevens worden aangemerkt de strafbeschikkingen uitgevaardigd op grond van de artikelen 257b en 257ba van het Wetboek van Strafvordering, met uitzondering van de met betrekking tot overtredingen uitgevaardigde strafbeschikkingen waarin een geldboete wordt opgelegd die minder dan € 100 beloopt.
+}
RULE Art5lid1BjsgIns : (I[Beschikking]/\ogv;(regeling;bwb;'Wetboek van Strafvordering';V[BasisWettenBestand*Beschikking]/\(artikel;'257b'\/artikel;'257ba');V[Artikel*Beschikking])) - lageBoete~;tav |- art5lid1Bjsg
VIOLATION (TXT "{EX} InsPair;art5lid1Bjsg;Beschikking;", SRC I, TXT ";Beschikking;", TGT I)
RULE Art5lid1BjsgDel : art5lid1Bjsg |- (I[Beschikking]/\ogv;(regeling;bwb;'Wetboek van Strafvordering';V[BasisWettenBestand*Beschikking]/\(artikel;'257b'\/artikel;'257ba');V[Artikel*Beschikking])) - lageBoete~;tav
VIOLATION (TXT "{EX} DelPair;art5lid1Bjsg;Beschikking;", SRC I, TXT ";Beschikking;", TGT I)

RELATION art5lid2Bjsg[Beschikking] [PROP]  -- Art. 5 lid 2 Bjsg
PURPOSE RULE Art5lid2BjsgIns
{+Als justitiële gegevens worden aangemerkt de strafbeschikkingen terzake van misdrijven, uitgevaardigd krachtens de artikelen 76 van de Algemene wet inzake rijksbelastingen en 10:15 van de Algemene douanewet.
+}
RULE Art5lid2BjsgIns :    (I[Beschikking]/\ogv;(regeling;bwb;'Algemene wet inzake rijksbelastingen';V[BasisWettenBestand*Beschikking]/\artikel;'76';V[Artikel*Beschikking]))
                       \/ (I[Beschikking]/\ogv;(regeling;bwb;'Algemene douanewet'                  ;V[BasisWettenBestand*Beschikking]/\artikel;'10:15';V[Artikel*Beschikking]))
                     |- art5lid2Bjsg
VIOLATION (TXT "{EX} InsPair;art5lid2Bjsg;Beschikking;", SRC I, TXT ";Beschikking;", TGT I)
RULE Art5lid2BjsgDel : art5lid2Bjsg |-
                          (I[Beschikking]/\ogv;(regeling;bwb;'Algemene wet inzake rijksbelastingen';V[BasisWettenBestand*Beschikking]/\artikel;'76';V[Artikel*Beschikking]))
                       \/ (I[Beschikking]/\ogv;(regeling;bwb;'Algemene douanewet'                  ;V[BasisWettenBestand*Beschikking]/\artikel;'10:15';V[Artikel*Beschikking]))
VIOLATION (TXT "{EX} DelPair;art5lid2Bjsg;Beschikking;", SRC I, TXT ";Beschikking;", TGT I)

PURPOSE RULE JustitiëleGegevensIns
{+Artikelen 2 t/m 4 Bjsg bepalen welke zaken worden aangemerkt als justitiele gegevens.
Uit deze zaken zijn slechts die gegevens justitieel, die in artt. 6 en 7 limitatief zijn opgesomd. +}
RULE JustitiëleBeschikkingenIns : art5lid1Bjsg\/art5lid2Bjsg |- justitieelVIOLATION (TXT "{EX} InsPair;justitieel;Beschikking;", SRC I, TXT ";Beschikking;", TGT I)
RULE JustitiëleBeschikkingenDel : justitieel |- art5lid1Bjsg\/art5lid2BjsgVIOLATION (TXT "{EX} DelPair;justitieel;Beschikking;", SRC I, TXT ";Beschikking;", TGT I)

ENDCONTEXT