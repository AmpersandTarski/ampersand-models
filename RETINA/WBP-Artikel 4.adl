CONTEXT WBP
-----------------------------------------------------------
PATTERN "WBP - artikel 4"

wbpIsVanToepassing :: Verwerking * Verwerking [PROP] PRAGMA "De WBP is van toepassing op "
PURPOSE RELATION wbpIsVanToepassing REF "artikel 4 lid 1-2 WBP" {++} 

verantwoordelijke :: Verwerking * Verantwoordelijke PRAGMA "" " geschiedt door of ten behoeve van "
PURPOSE RELATION verantwoordelijke REF "artikel 4 lid 1-2 WBP" {++}

--[artikel 4 lid 1 WBP]--

RULE "artikel 4 lid 1 WBP": I /\ verantwoordelijke;heeftVestigingIn;'Nederland';V |- wbpIsVanToepassing
MEANING "De WBP is van toepassing op de VERWERKING in het kader van activiteiten van een vestiging van een VERANTWOORDELIJKE in Nederland (artikel 4 lid 1 WBP)"

--[artikel 4 lid 2 WBP]--

RULE "artikel 4 lid 2 WBP": I[Verwerking] /\ verantwoordelijke;-(heeftVestigingIn;landBinnenEU);V /\ verwerkingsmiddelen;(bevindenZichIn;'Nederland';V \/ (-verwerkingsfunctie!'doorvoeren van persoonsgegevens');V ) |- wbpIsVanToepassing
MEANING "Deze wet is van toepassing op de VERWERKING door of ten behoeve van een VERANTWOORDELIJKE die geen vestiging heeft in de Europese Unie, waarbij gebruik wordt gemaakt van al dan niet geautomatiseerde middelen die zich in Nederland bevinden, tenzij deze middelen slechts worden gebruikt voor de doorvoer van PERSOONSGEGEVENs (artikel 4 lid 2 WBP)"

heeftVestigingIn :: Verantwoordelijke * Land [TOT] PRAGMA "" " heeft een vestiging in "
PURPOSE RELATION heeftVestigingIn REF "artikel 4 lid 1-2 WBP" {++}

landBinnenEU :: Land * Land [PROP] PRAGMA "" " is een land binnen de Europese Unie"
PURPOSE RELATION heeftVestigingIn REF "artikel 4 lid 2 WBP" {++}

CONCEPT Verwerkingsmiddelen "al dan niet geautomatiseerde middelen die gebruikt (kunnen) worden voor een Verwerking" "artikel 4 lid 2 WBP"

verwerkingsmiddelen :: Verwerking -> Verwerkingsmiddelen PRAGMA "" " geschiedt door middel van "
PURPOSE RELATION verwerkingsmiddelen REF "artikel 4 lid 2 WBP" {++}

bevindenZichIn :: Verwerkingsmiddelen -> Land PRAGMA "" " bevinden zich in "
PURPOSE RELATION bevindenZichIn REF "artikel 4 lid 2 WBP" {++}

CONCEPT Verwerkingsfunctie "een functie die, als deze op PERSOONSGEGEVENs wordt uitgevoerd, aangemerkt kan worden als een VERWERKING in de zin van artikel 1 sub b WBP"

verwerkingsfunctie :: Verwerkingsmiddelen * Verwerkingsfunctie PRAGMA "Een of meer middelen uit " " wordt, of kan worden gebruikt, voor "
PURPOSE RELATION verwerkingsfunctie REF "artikel 1 lid b, artikel 4 lid 2" {++}

--[artikel 4 lid 3 WBP]--

RULE "artikel 4 lid 3": externVerantwoordelijke |- verantwoordelijke;(I /\ heeftVestigingIn;'Nederland';heeftVestigingIn~ /\ handeltNamens;handeltNamens~)
MEANING "Een VERWERKING door een VERANTWOORDELIJKE als bedoeld in artikel 4 lid 2 WBP vindt alleen plaats als hij in Nederland een persoon of instantie heeft aangewezen die namens hem handelt overeenkomstig de bepalingen van deze wet. Voor de toepassing van deze wet en de daarop berustende bepalingen, wordt hij aangemerkt als de VERANTWOORDELIJKE (artikel 4 lid 3)"

handeltNamens :: Verantwoordelijke * ExternVerantwoordelijke PRAGMA "" " handelt namens " " overeenkomstig de bepalingen van de WBP"
PURPOSE RELATION handeltNamens REF "artikel 4 lid 3 WBP" {+Een buiten de EU gevestigde VERANTWOORDELIJKE moet volgens artikel 4 lid 3 WBP een in Nederland gevestigde persoon of instantie aanwijzen die namens hem de rol van VERANTWOORDELIJKE op zich neemt. Deze wordt dan, voor de WBP, de VERANTWOORDELIJKE en neemt daarmee de plaats in van de buiten de EU gevestigde partij. Om deze partijen te kunnen onderscheiden noemen we elke buiten de EU gevestigde VERANTWOORDELIJKEN een 'ExternVerantwoordelijke'. Gevolg hiervan is dan dat elke VERWERKING plaatsvindt door of namens een VERANTWOORDELIJKE met een vestiging binnen de EU.+}

PURPOSE RULE "Verantwoordelijken"
{+Volgens artikel 4 lid 3 WBP moet elke VERANTWOORDELIJKE die niet in de EU is gevestigd, zich laten vertegenwoordigen door een partij binnen Nederland die door de WBP vervolgens als VERANTWOORDELIJKE wordt aangemerkt. Volgens artikel 4 lid 2 WBP is het wel mogelijk dat een VERANTWOORDELIJKE een vestiging binnen de EU heeft, buiten Nederland. Daarmee moet elke VERANTWOORDELIJKE dus binnen de EU zitten.+}
RULE "Verantwoordelijken": I[Verantwoordelijke] |- heeftVestigingIn; landBinnenEU; heeftVestigingIn~
MEANING "Elke VERANTWOORDELIJKE heeft een vestiging binnen de EU (artikel 4 lid 2-3 WBP)"

RULE "ExternVerantwoordelijken": I[ExternVerantwoordelijke] = I[Verantwoordelijke] /\ -(heeftVestigingIn;landBinnenEU;heeftVestigingIn~)
MEANING "Een 'ExterneVerantwoordelijke' is een Verantwoordelijke die geen vestiging heeft in de EU"

CONCEPT ExternVerantwoordelijke "een VERANTWOORDELIJKE die geen vestiging heeft in de Europese Unie" "artikel 4 lid 2 WBP"
SPEC ExternVerantwoordelijke ISA Verantwoordelijke

externVerantwoordelijke :: Verwerking * ExternVerantwoordelijke [UNI] PRAGMA "" " geschiedt door of ten behoeve van"
PURPOSE RELATION externVerantwoordelijke REF "artikel 4 lid 3 WBP" {++}

ENDPATTERN
-----------------------------------------------------------
POPULATION landBinnenEU CONTAINS
 [ ("BelgiÃ«", "BelgiÃ«")
 ; ("Belgie", "Belgie")
 ; ("Bulgarije", "Bulgarije")
 ; ("Cyprus", "Cyprus")
 ; ("Denemarken", "Denemarken")
 ; ("Duitsland", "Duitsland")
 ; ("Estland", "Estland")
 ; ("Finland", "Finland")
 ; ("Frankrijk", "Frankrijk")
 ; ("Griekenland", "Griekenland")
 ; ("Hongarije", "Hongarije")
 ; ("Ierland", "Ierland")
 ; ("ItaliÃ«", "ItaliÃ«")
 ; ("Italie", "Italie")
 ; ("Letland", "Letland")
 ; ("Litouwen", "Litouwen")
 ; ("Luxemburg", "Luxemburg")
 ; ("Malta", "Malta")
 ; ("Nederland", "Nederland")
 ; ("Oostenrijk", "Oostenrijk")
 ; ("Polen", "Polen")
 ; ("Portugal", "Portugal")
 ; ("RoemeniÃ«", "RoemeniÃ«")
 ; ("Roemenie", "Roemenie")
 ; ("SloveniÃ«", "SloveniÃ«")
 ; ("Slovenie", "Slovenie")
 ; ("Slowakije", "Slowakije")
 ; ("Spanje", "Spanje")
 ; ("TsjechiÃ«", "TsjechiÃ«")
 ; ("Tsjechie", "Tsjechie")
 ; ("Verenigd Koninkrijk", "Verenigd Koninkrijk")
 ; ("Engeland", "Engeland")
 ; ("Scholtland", "Scholtland")
 ; ("Wales", "Wales")
 ; ("Zweden", "Zweden")
 ]

ENDCONTEXT
