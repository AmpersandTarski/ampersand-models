CONTEXT Computations IN ENGLISH
{-TTexts can be used in different ways. 

One particular view is that a TText is a variable, where
- `ttName` is the name of the variable;
- `ttValue` is its value, and 
- `ttTemplate` is the abstract specification by which the value must be computed;
- `ttInstance` is the concrete instruction for computing the value.

This service supports this way of looking at TTexts.
In order to compute a value for some TText, a `Computation` is created for that TText.
The arguments of that computation are the latest assignments (traces) of the TTexts
that are mentioned in the abstract specification of the computation,(i.e.: mentioned 
in the `ttTemplate` of the TText for which the computation will provide a value).
Whenever a computation produces a result, it is assigned as the value of the TText
for which it was computed.

Currently, the actual computations are to be done manually. Extensions to this 
file may provide ways for automatically doing such computations.
-}
CONCEPT Computation "(for a TText T): a set of Assignments for all TTexts T[i] that are mentioned in the ttTemplate of T, for the purpose of computing a ttValue for T"

IDENT Computations: Computation (compArg,compVar) -- a Computation is identified by its arguments and the TText for which it produces a value.
RELATION compVar[Computation*TText] [INJ,UNI,TOT] -- TText, whose value will be set to compRes. INJ ensures result assignment is unambiguous.
RELATION compArg[Computation*Assignment] -- Arguments that the computation will use, or has used, to produce its result
RELATION compRes[Computation*TTValue] [UNI] -- the result of the computation, to be assigned to its compVar

RULE "Different computation arguments (assignments) must refer to different TTexts": (compArg~;compArg)-I |- asmVar;-I;asmVar~

-- (1) A Computation may only exist while there is a TText for which it produces (or has produced) a value.
ROLE ExecEngine MAINTAINS "Delete obsolete Computations"
RULE "Delete obsolete Computations": I[Computation] |- compVar;compVar~
VIOLATION (TXT "{EX} DelAtom;Computation;", SRC I)

-- (2) A computation must exist for every TText T that uses at least one TText T'; the value of other TTexts can be assigned/changed indiscriminately.
ROLE ExecEngine MAINTAINS "Create computations"
RULE "Create computations": I[TText] /\ ttIsUsedBy~;ttIsUsedBy |- compVar~;compVar
VIOLATION (TXT "{EX} InsAtom;Computation"
                ,TXT ";InsPair;compVar;Computation;_NEW;TText;", SRC I[TText]
          )

-- (3) The arguments of a computation for a TText V consist of Assignments (traces) of all TTexts V' that are referred to in the specification of V.
ROLE ExecEngine MAINTAINS "Add arguments to computations"
RULE "Add arguments to computations": 
   compVar;ttIsUsedBy~;ttTrace /\ -(compArg;asmVar;asmVar~) |- compArg
MEANING "If the computation should uses (the value of) a TText as an argument (because it is used by the TText for which the computation computes its result), then the latest assignment of that TText should become an argument unless there is an ambiguity (i.e. the computation already has an argument that is an assignment for that TText)."
VIOLATION (TXT "{EX} InsPair;compArg;Computation;", SRC I, TXT ";Assignment;", TGT I)

-- (4) Arguments for a computation that has no result, shall be updated when needed.
ROLE ExecEngine MAINTAINS "Update arguments of computations that do not have a result"
RULE "Update arguments of computations that do not have a result": 
  (I-compRes;compRes~);compArg;(I-asmVar;ttTrace) |- -V
VIOLATION (TXT "{EX} DelPair;compArg;Computation;", SRC I, TXT ";Assignment;", TGT I
          ,TXT "{EX} InsPair;compArg;Computation;", SRC I, TXT ";Assignment;", TGT asmVar;ttTrace
          )

-- (5) A Computation that has a result shall be discarded when one or more of its arguments has/have changed.
ROLE ExecEngine MAINTAINS "Delete computations (that have results) of which one or more arguments have changed"
RULE "Delete computations (that have results) of which one or more arguments have changed": 
  (I /\ compRes;compRes~);compArg;(I-asmVar;ttTrace) |- -V
VIOLATION (TXT "{EX} DelAtom;Computation;", SRC I)

-- (6) Whenever a computation for TText V has produced a result, this result is assigned as the value of V
ROLE ExecEngine MAINTAINS "Assign computation result to TText (provided actual arguments were used)"
RULE "Assign computation result to TText (provided actual arguments were used)":
  compVar~;compRes |- ttValue
VIOLATION (TXT "{EX} InsPair;ttValue;TText;", SRC I, TXT ";TTValue;", TGT I)

ENDCONTEXT