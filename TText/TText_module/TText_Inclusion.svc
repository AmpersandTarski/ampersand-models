CONTEXT TText_InclusionService IN ENGLISH
{- TTexts can be copied from one Scope to another, 
as part of Scope copying (see file `Scope_Inclusion.svc`).

It is rather difficult to precisely define what 'inclusion' of a scope, or being a copy of a TText means,
specifically in cases where changes are made. For example, consider:
- renaming a TText in a scope that is included in another scope that contains a TText with the new name
- specifying a value in a TText in a scope that is included in another scope that contains a TText with (another) value
We may need to rethink this inclusion service...

This service creates and maintains copies of TTexts within Scopes
in which other Scopes have been included. This entails two steps:

1. Maintaining the existence of TTexts in a target Scope St (i.e. Scope_target)
   that are copies of the TTexts in a source Scope Ss (i.e. Scope_source).
   This can mean two things:
   1) If target scope St is a (carbon or non-carbon) copy of source scope Ss
      (i.e. (Ss, St) is a pair in `scopeIII`),
      then for every TText Ts in the source scope Ss,
      a corresponding TText Tt must exist in the target scope St
      where either
      - Tt and Ts have the same TTNames (in `ttName`), or
      - the pair (Tt, Ts) is registerd in `ttICO`, thereby stating that
        Tt is (a possibly modified version of) a copy of Ts.
      The effect of this definition of 'correspondence' is that 
      modification of Ts (in source scope) does not overwrite the
      corresponding Tt (in the target scope), and also that 
      changes in Tt do not get 'undone' because it no longer is
      a carbon copy of Ts.
   2) If target scope St is a carbon copy of a source scope Ss,
      then for every TText Ts in the source scope Ss
      a TText Tt must exist in the target scope St
      which has the property of being a carbon copy
      (i.e. (Tt, Tt) is a pair in `ttIsaCC`)
      and the pair (Tt, Ts) is registered in `ttICO`.
      The effect of this is that if Tt has the same name as Ts,
      then every attribute of Ts is copied to the 
      corresponding attribute of Tt.
      This feature is defined in file "../TText/TText_CarbonCopy.svc"
    
2. Maintaining the (values of) attributes of copied TTexts.
   This can mean two things:
   1) If TText Tt is a (non-carbon) copy of TText Ts
      (meaning that (Tt, Ts) is registered in `ttICO`),
      and Tt does not have an attribute that Ts has,
      then Tt is given (a copy of) that attribute of Ts.
   2) If TText Tt is a carbon copy of TText Ts
      (meaning that (Tt, Ts) is registered in `ttICO`
       AND that (Tt,Tt) is registered in `ttIsaCC`),
      then Tt has (a copy of) any attribute that Ts has,
      and has no attribute where Ts does not have them.
      This feature is defined in file "../TText/TText_CarbonCopy.svc"

This comes down to the following:
- Every attribute of a TText Tt is initialized with 
  the value of the corresponding attribute in a TText Ts, 
  for as long as Tt is a (non-carbon) copy of Ts.
- Every attribute of a TText Tt is nonexistent or the same as
  the value of the corresponding attribute in a TText Ts,
  for as long as Tt is a carbon copy of Ts.
  This feature is defined in file "../TText/TText_CarbonCopy.svc"

Note that while this module creates TTexts as necessary, it does NOT
destroy them, as this is considered too application specific.
-}

INCLUDE "./Scope_Inclusion.svc" -- Service that enables various ways of copying Scopes and their contents

{--The below rule will only fire after execution has terminated with an error 'Maximum reruns exceeded for ExecEngine'. To fix this, we need the ability to limit the execution of ExecEngine rules to Services, and the ability to specify preconditions (of which the below rule would then be one).
RULE "Scopes with TTexts with the same name cannot be included into the same target Scope":
  ttScope;(scopeIII;scopeIII~ - I);ttScope~;ttName |- -ttName
VIOLATION (TXT "TText ", SRC ttName, TXT " in Scope ", SRC ttScope, TXT " is included in a Scope that also includes another scope that has a TText with the same name") --}

--[Maintaining the existence of copied TTexts in the target Scope (see introduction, part 1)]--
-- For every TText in the source scope, a corresponding TText must exist in the target scope (a registration of which is found in the relation `ttICO`)
ttICO :: TText * TText [UNI,IRF,ASY] -- SRC TText Is a Copy Of (ICO) TGT TText. 
RULE "A TText can only be a copy of a TText from another scope": ttICO |- ttScope;-I;ttScope~

ROLE ExecEngine MAINTAINS "Create TText copies for named TTexts"
RULE "Create TText copies for named TTexts":
   (scopeIII~;ttScope~)-(ttScope~;ttName;ttName~) |- ttScope~;ttICO
--   (ttScope;scopeIII - ttName;ttName~;ttScope)~ |- (ttICO~;ttScope)~ is equivalent
-- Rule `ttScope;scopeIII |- ttScope` would lead to violations of IDENT(TText) and UNI(ttName) when the target scope already contains a TText with the copied/same TTName
-- Rule `scopeIII~;ttScope~;ttName |- ttScope~;ttICO;ttName` would not copy nameless TTexts (note that UNI(ttName))
MEANING "A Scope that includes a (source) Scope must have a copy of all (named) TTexts of the source Scope"
VIOLATION (TXT "{EX} InsAtom;TText"
              ,TXT "{EX} InsPair;ttScope;TText;_NEW;Scope;", SRC I
              ,TXT "{EX} InsPair;ttICO;TText;_NEW;TText;", TGT I[TText]
              ,TXT "{EX} InsPair;ttName;TText;_NEW;TTName;", TGT ttName
          )

ROLE ExecEngine MAINTAINS "Auto link TTexts in included scopes if they have the same name"
RULE "Auto link TTexts in included scopes if they have the same name":
   ttScope;scopeIII~;ttScope~ /\ ttName;ttName~ |- ttICO
MEANING "A TText in a carbon-copied scope that has the name of a TText in the template-scope, is its TText-copy"
VIOLATION (TXT "{EX} InsPair;ttICO;TText;", SRC I, TXT ";TText;", TGT I)

ROLE ExecEngine MAINTAINS "Automatically depopulate ttICO" -- allows for `scopeIII` to be depopulated.
RULE "Automatically depopulate ttICO": ttICO |- ttScope;scopeIII~;ttScope~
MEANING "A TText can only be a copy of a (source) TText if its scope has included the scope of the (source) TText"
VIOLATION (TXT "{EX} DelPair;ttICO;TText;", SRC I, TXT ";TText;", TGT I)

--[Maintaining the (values of) attributes of copied TTexts - regular attributes (see introduction, part 2)]--
--**Note that if TText-attributes are defined/deleted, then this must be reflected in the rules below (or elsewhere)**
{-- Here is an example of how to copy TText specialiazations, i.e. for `CLASSIFY Objective ISA TText`:
ROLE ExecEngine MAINTAINS "Copy 'Objective' property"
RULE "Copy 'Objective' property": ttIsaCC;I[Objective] |- I[Objective];ttIsaCC
VIOLATION (TXT "{EX} SetConcept;TText;Objective;", SRC I[TText]) --}

-- TText: including `ttName[TText*TTName]`:
ROLE ExecEngine MAINTAINS "Initialize copied ttName"
RULE "Initialize copied ttName": (I-ttName;ttName~);ttICO;ttName |- ttName
VIOLATION (TXT "{EX}_;InsPair_;ttName_;TText_;", SRC I[TText], TXT "_;TTName_;", TGT I)

-- TText: including `ttValue[TText*TTValue]`:
ROLE ExecEngine MAINTAINS "Initialize copied ttValue"
RULE "Initialize copied ttValue": (I-ttValue;ttValue~);ttICO;ttValue |- ttValue
VIOLATION (TXT "{EX}_;InsPair_;ttValue_;TText_;", SRC I[TText], TXT "_;TTValue_;", TGT I)

-- TText: including `ttTemplate[TText*TTPhrase]`:
ROLE ExecEngine MAINTAINS "Initialize copied ttTemplate"
RULE "Initialize copied ttTemplate": (I-ttTemplate;ttTemplate~);ttICO;ttTemplate |- ttTemplate
VIOLATION (TXT "{EX}_;InsPair_;ttTemplate_;TText_;", SRC I[TText], TXT "_;TTPhrase_;", TGT I)

-- TText: including `ttInstance[TText*TTPhrase]` is not done as `ttInstance` is computed, internal to the Scope

ENDCONTEXT