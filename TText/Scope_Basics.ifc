CONTEXT Scope_BasicsIfc IN ENGLISH

--[Copying Scopes (and trigger Scope copying)]--
RELATION scopeCreateCopyReq[Scope*ScopeID] [UNI] -- Create a copy of an existing scope, (with the newly specified ScopeID)
ROLE ExecEngine MAINTAINS "Create a copy of the Scope"
RULE "Create a copy of the Scope": scopeCreateCopyReq |- scopeIII;scopeID
VIOLATION (TXT "{EX} InsAtom;Scope"
              ,TXT "{EX} InsPair;scopeID;Scope;_NEW;ScopeID;", TGT I
              ,TXT "{EX} InsPair;scopeIII;Scope;", SRC I, TXT ";Scope;_NEW"
          ,TXT "{EX} DelPair;scopeCreateCopyReq;Scope;", SRC I, TXT ";ScopeID;", TGT I
          )

--[Views]--
VIEW ScopeCheckbox: Scope HTML TEMPLATE "View-PROPERTY.html" ENDVIEW -- Enable Scope-properties to be shown as checkboxes
VIEW Scopes: Scope(scopeID)

--[Interfaces]--
INTERFACE "[Scopes]": V[SESSION*Scope] CRuD BOX <SHCOLS>
   [ " ": I cRud
   , "ID": scopeID cRUd
   , "Description": scopeDescr cRUd
   , "CC?" : scopeIsaCC cRUd -- Carbon Copy?
   , "includes" : scopeIII~ cRud
   , "incl. by" : scopeIII cRud
   , "Copy": scopeCreateCopyReq cRUd
   ]

INTERFACE "Scope": I[Scope] CRuD BOX <DIV>
   [ "Header" : I cRud COLS
      [ "ID": scopeID cRUd
      , "Description": scopeDescr cRUd
      , "CC?" : scopeIsaCC cRUd -- Carbon Copy?
      , "includes" : scopeIII~ cRUd
      , "incl. by" : scopeIII cRud
      ]
   , "Structural contents": ttScope~ CRuD BOX <SHCOLS>
      [ " ": I LINKTO INTERFACE "TText"
      , "Name": ttName cRUd
      , "Value": ttValue cRUd
--    , "-" : compVar~ cRud
      ]
   ]

ENDCONTEXT