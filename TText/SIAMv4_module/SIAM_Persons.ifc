CONTEXT "Person Registration Interfaces" IN ENGLISH
-----------------------------------------------------------

VIEW Persons : Person DEFAULT { "name": personRef } ENDVIEW
VIEW PersonAsActor :  Person  { "name": actorRef  } ENDVIEW
VIEW PersonAsParty :  Person  { "name": partyRef  } ENDVIEW

POPULATION Role CONTAINS [ "PersonMgr" ]

personUseFullNameAsRef :: Person * Person [PROP] -- property that a Person will be referred to by its full name
ROLE ExecEngine MAINTAINS "InsPair personRef from personFullName"
RULE "InsPair personRef from personFullName": personUseFullNameAsRef;personFullName |- personRef
VIOLATION (TXT "{EX} InsPair;personRef;Person;", SRC I, TXT ";PersonRef;", TGT I[PersonRef])

INTERFACE "PersonAdmin" FOR "PersonMgr": I[Person] cRud BOX <RAW  table>
   [ "Short name"  : personRef cRUd
   , "First name"  : personFirstName cRUd
   , "Middle name" : personMiddleName cRUd
   , "Last name"   : personLastName cRUd
   , "Initials"    : personInitials cRUd
   , "Full Name"   : personFullName cRud
   , "Full->-short name": personUseFullNameAsRef cRUd
   ]

INTERFACE "People" FOR "PersonMgr": V[SESSION*Person]  cRuD BOX <TABLE>
   [ "Short name"  : I LINKTO INTERFACE "PersonAdmin"
   , "Full name"   : personFullName cRud
   , "Initials"    : personInitials cRUd
   ]

INTERFACE "Me" FOR User: "_SESSION";sessionActor;I[Person] cRud BOX <FORM>
   [ "Short name"  : personRef cRUd
   , "First name"  : personFirstName cRUd
   , "Middle name" : personMiddleName cRUd
   , "Last name"   : personLastName cRUd
   , "Initials"    : personInitials cRUd
   , "Full Name"   : personFullName cRud
   , "Full->-short name": personUseFullNameAsRef cRUd
   ]

-----------------------------------------------------------
ENDCONTEXT