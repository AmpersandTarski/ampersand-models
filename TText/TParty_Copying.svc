CONTEXT TParty_CopyingService IN ENGLISH
{- Tparties, which are specializations of TTexts,
can be copied from one Scope to another, 
as part of Scope copying (see file `Scope_Copying.svc`)
and by extending the `TText_Copying.svc`
and using the same principles.

So, this service creates and maintains copies of Tparties within Scopes
that include other Scopes. 
-}

-- Make sure the `scopeIII` relation is available
INCLUDE "../TText/TText_Copying.svc"                 -- Service that enables various ways of copying Scopes and their contents

--[Copying TParty extensions]--
-- TParty: copying `ttIsaTParty[TText*TText]` is not done, as it is automatically maintained.

-- TParty: copying `tPartyReqdOrg[TText*Organization]`:
ROLE ExecEngine MAINTAINS "Initialize copied tPartyReqdOrg"
RULE "Initialize copied tPartyReqdOrg": (I-tPartyReqdOrg;tPartyReqdOrg~);ttICO;tPartyReqdOrg |- tPartyReqdOrg
VIOLATION (TXT "{EX}_;InsPair_;tPartyReqdOrg_;TText_;", SRC ttIsaTParty, TXT "_;Organization_;", TGT I)
ROLE ExecEngine MAINTAINS "Create/Update copied tPartyReqdOrg"
RULE "Create/Update copied tPartyReqdOrg": ttICCO;tPartyReqdOrg |- tPartyReqdOrg
VIOLATION (TXT "{EX}_;InsPair_;tPartyReqdOrg_;TText_;", SRC ttIsaTParty, TXT "_;Organization_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted tPartyReqdOrg"
RULE "Update deleted tPartyReqdOrg": ttICCO;(ttIsaTParty-tPartyReqdOrg;tPartyReqdOrg~) |- (ttIsaTParty-tPartyReqdOrg;tPartyReqdOrg~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;tPartyReqdOrg_;TText_;", SRC ttIsaTParty, TXT "_;Organization_;", SRC tPartyReqdOrg)

-- TParty: copying `tPartyAcc[TText*Account]`:
ROLE ExecEngine MAINTAINS "Initialize copied tPartyAcc"
RULE "Initialize copied tPartyAcc": (I-tPartyAcc;tPartyAcc~);ttICO;tPartyAcc |- tPartyAcc
VIOLATION (TXT "{EX}_;InsPair_;tPartyAcc_;TText_;", SRC ttIsaTParty, TXT "_;Account_;", TGT I)
ROLE ExecEngine MAINTAINS "Create/Update copied tPartyAcc"
RULE "Create/Update copied tPartyAcc": ttICCO;tPartyAcc |- tPartyAcc
VIOLATION (TXT "{EX}_;InsPair_;tPartyAcc_;TText_;", SRC ttIsaTParty, TXT "_;Account_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted tPartyAcc"
RULE "Update deleted tPartyAcc": ttICCO;(ttIsaTParty-tPartyAcc;tPartyAcc~) |- (ttIsaTParty-tPartyAcc;tPartyAcc~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;tPartyAcc_;TText_;", SRC ttIsaTParty, TXT "_;Account_;", SRC tPartyAcc)

--[Copying TText extensions]--
-- TText: copying `ttValSupName[TText*TTName]`:
ROLE ExecEngine MAINTAINS "Initialize copied ttValSupName"
RULE "Initialize copied ttValSupName": (I-ttValSupName;ttValSupName~);ttICO;ttValSupName |- ttValSupName
VIOLATION (TXT "{EX}_;InsPair_;ttValSupName_;TText_;", SRC I[TText], TXT "_;TTName_;", TGT I)
ROLE ExecEngine MAINTAINS "Create/Update copied ttValSupName"
RULE "Create/Update copied ttValSupName": ttICCO;ttValSupName |- ttValSupName
VIOLATION (TXT "{EX}_;InsPair_;ttValSupName_;TText_;", SRC I[TText], TXT "_;TTName_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted ttValSupName"
RULE "Update deleted ttValSupName": ttICCO;(I-ttValSupName;ttValSupName~) |- (I-ttValSupName;ttValSupName~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;ttValSupName_;TText_;", SRC I[TText], TXT "_;TTName_;", SRC ttValSupName)

-- TText: copying copied `ttValSupplier[TText*TText]`:
ROLE ExecEngine MAINTAINS "Initialize copied ttValSupplier (from the same scope)"
RULE "Initialize copied ttValSupplier (from the same scope)":
   ((I-ttValSupplier;ttValSupplier~);ttICO;ttValSupplier;ttICO~ /\ ttScope;ttScope~);ttIsaTParty |- ttValSupplier
VIOLATION (TXT "{EX}_;InsPair_;ttValSupplier_;TText_;", SRC I[TText], TXT "_;TText_;", TGT I[TText])
ROLE ExecEngine MAINTAINS "Create/Update copied ttValSupplier"
RULE "Create/Update copied ttValSupplier": (ttICCO;ttValSupplier;ttICCO~ /\ ttScope;ttScope~);ttIsaTParty |- ttValSupplier
VIOLATION (TXT "{EX}_;InsPair_;ttValSupplier_;TText_;", SRC I[TText], TXT "_;TText_;", TGT I[TText])
ROLE ExecEngine MAINTAINS "Update deleted ttValSupplier"
RULE "Update deleted ttValSupplier": ttICCO;(I-ttValSupplier;ttValSupplier~) |- (I-ttValSupplier;ttValSupplier~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;ttValSupplier_;TText_;", SRC I[TText], TXT "_;TText_;", SRC I[TText])

ENDCONTEXT