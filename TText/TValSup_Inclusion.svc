CONTEXT TValSup_InclusionService IN ENGLISH
{- Tparties, which are specializations of TTexts,
can be copied from one Scope to another, 
as part of Scope copying (see file `Scope_Inclusion.svc`)
and by extending the `TText_Copying.svc`
and using the same principles.

So, this service creates and maintains copies of Tparties within Scopes
that include other Scopes. 
-}

-- Make sure the `scopeIII` relation is available
INCLUDE "../TText/TText_Inclusion.svc" -- Service that enables various ways of copying Scopes and their contents

--[Including TParty extensions]--
-- TParty: including `ttIsaTParty[TText*TText]` is not done, as it is automatically maintained.

-- TParty: including `tPartyReqdOrgRef[TText*Organization]`:
ROLE ExecEngine MAINTAINS "Initialize copied tPartyReqdOrgRef"
RULE "Initialize copied tPartyReqdOrgRef": (I-tPartyReqdOrgRef;tPartyReqdOrgRef~);ttICO;tPartyReqdOrgRef |- tPartyReqdOrgRef
VIOLATION (TXT "{EX}_;InsPair_;tPartyReqdOrgRef_;TText_;", SRC ttIsaTParty, TXT "_;OrgRef_;", TGT I)

-- TParty: including `tPartyAcc[TText*Account]`:
ROLE ExecEngine MAINTAINS "Initialize copied tPartyAcc"
RULE "Initialize copied tPartyAcc": (I-tPartyAcc;tPartyAcc~);ttICO;tPartyAcc |- tPartyAcc
VIOLATION (TXT "{EX}_;InsPair_;tPartyAcc_;TText_;", SRC ttIsaTParty, TXT "_;Account_;", TGT I)

--[Including TText extensions]--
-- TText: including `ttValSupName[TText*TTName]`:
ROLE ExecEngine MAINTAINS "Initialize copied ttValSupName"
RULE "Initialize copied ttValSupName": (I-ttValSupName;ttValSupName~);ttICO;ttValSupName |- ttValSupName
VIOLATION (TXT "{EX}_;InsPair_;ttValSupName_;TText_;", SRC I[TText], TXT "_;TTName_;", TGT I)

-- TText: including copied `ttValSupplier[TText*TText]`:
ROLE ExecEngine MAINTAINS "Initialize copied ttValSupplier (from the same scope)"
RULE "Initialize copied ttValSupplier (from the same scope)":
   ((I-ttValSupplier;ttValSupplier~);ttICO;ttValSupplier;ttICO~ /\ ttScope;ttScope~);ttIsaTParty |- ttValSupplier
VIOLATION (TXT "{EX}_;InsPair_;ttValSupplier_;TText_;", SRC I[TText], TXT "_;TText_;", TGT I[TText])

ENDCONTEXT