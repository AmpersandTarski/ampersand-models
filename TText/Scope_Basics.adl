CONTEXT Scopes IN ENGLISH
{-There are many use cases where all sorts of structured contexts is to be scoped. 
Examples include case-management, judicial cases, a reasoning argument,
the setting up of contracts (transactions), and more.

This module provides a mechanism for supporting such kinds of groupings
This support is based around the concept of `Scope`. 
Basically, a `Scope` is a set of 'structured contents' that we assign some attributes to:
- naming the scope (`scopeID`)
- distinguishing between scopes whose TTexts are templates (define-time) or cases (run-time) (`scopeIsaCasus`)
- documenting the purpose of this grouping (`scopeDescr`)

The structured contents of a Scope can be Atoms of any kind.
Prerequisite for associating such Atoms to a Scope is
the existence of a relation that links such Atoms to a Scope.
An example for `TText`s is the existence of the relation `ttScope`.
-}
META "authors" "Rieks Joosten (rieks.joosten@tno.nl)"

--[TText Scopes]--
-- We do not specify (e.g.) IDENT Scopes: Scope(scopeOwner,scopeID) so as to enable other uses, that require a different IDENT 
RELATION scopeID[Scope*ScopeID] [UNI] -- a short name that helps to refer to the Scope
RELATION scopeOwner[Scope*Account] [UNI] -- The Account that is authorized to change and/or delete the Scope. We define it here becuase it is in the IDENT statement, which cannot be updated easily...
--**NOTE that adding/changing relations here below requires adjustment of the ScopeCopying service.**--
RELATION scopeDescr[Scope*ScopeDescr] [UNI] -- descriptive text or purpose of the Scope (may be used e.g. in (popup) helptexts)

--[Declarations for relations used in extensions]--
-- Because we have no conditional source file texts (e.g. as in #IF ($scopeinclusion) <some lines of source code> #ENDIF), we supply the declaration of some relations here unconditionally 
RELATION scopeIII[Scope*Scope] [IRF,ASY] -- SRC Scope 'Is Included In' the TGT Scope (see Scope_Inclusion.svc, TText_Inclusion.svc for details).
MEANING "The structured content of a (SRC) Scope may be included in that of a (TGT) Scope"
RELATION scopeIsaCC[Scope*Scope] [PROP] -- A Scope may have the property of being a Carbon Copy (CC) - see the comments at the beginning of the file.
MEANING "A Scope may have the property of being a carbon copy of another Scope"


--[[Glossary]]----------------------------------------

CONCEPT Scope "a set of TTexts that are controlled by a single authority and (together) serve a particular purpose"

CONCEPT ScopeID "a short name that helps to refer to the Scope"
REPRESENT ScopeID TYPE ALPHANUMERIC

CONCEPT ScopeDescr "descriptive text or purpose of the Scope"
REPRESENT ScopeDescr TYPE ALPHANUMERIC

ENDCONTEXT