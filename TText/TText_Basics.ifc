CONTEXT "TText_BasicIfc" IN ENGLISH

VIEW ScopeCheckbox: Scope HTML TEMPLATE "View-PROPERTY.html" ENDVIEW -- Enable Scope-properties to be shown as checkboxes
VIEW TTexts: TText( TXT "TText Details" )  -- Replace the dirty TText-identifier with the text 'Details' (only works if there is a single INTERFACE with I[TText])
VIEW Scopes: Scope( TXT "Scope Details" )  -- Replace the dirty Scope-identifier with the text 'Details' (only works if there is a single INTERFACE with I[Scope])
{- BOX <PropertyButton> -- is a custom HTML Template (i.e. it is in the local templates/views directory)
   [ disabled : <expr1> -- The button is disabled when `expr1` has one or more target atoms.
   , property : <expr2> -- `expr2` **must be a [PROP]-type relation**; it gets populated whenever the button is clicked. 
   ]
-}

INTERFACE "Scopes": V[SESSION*Scope] CRuD BOX <SHCOLS>
   [ " ": I cRud
   , "ID": scopeID cRUd
   , "Casus?": scopeIsaCasus cRUd <ScopeCheckbox>
   , "Description": scopeDescription cRUd
   , "Subject/Object": scopeObject cRUd
-- , "Owner": scopeOwner cRud
   , "CC?" : scopeIsaCC cRud -- Carbon Copy?
   , "includes" : scopeIncludes;scopeID cRud
   , "incl. by" : scopeIncludes~;scopeID cRud
   , "New template ID": scopeCreateTemplateReq cRUd
   , "New casus ID": scopeCreateCasusReq cRUd
   , "CarbonCopy ID": scopeCarbonCopyReq cRUd
   ]

INTERFACE "Scope": I[Scope] CRuD BOX <ROWSNL>
   [ "Header" : I cRud COLS
      [ "ID": scopeID cRUd
      , "Casus?": scopeIsaCasus cRUd <ScopeCheckbox>
      , "Description": scopeDescription cRUd
      , "Subject/Object": scopeObject cRUd
--    , "Owner": scopeOwner cRud
      , "CC?" : scopeIsaCC cRud -- Carbon Copy?
      , "includes" : scopeIncludes;scopeID cRud
      , "incl. by" : scopeIncludes~;scopeID cRud
      ]
   , "Structural contents": ttScope~ CRuD BOX <SHCOLS>
      [ " ": I cRud LINKTO INTERFACE "TText"
      , "Name": ttName cRUd
      , "Value": ttValue cRUd
      , "-" : compVar~ cRud
      , "Template": ttTemplate cRUd
      ]
   ]

INTERFACE "TTexts": V[SESSION*TText] CRuD BOX <SHCOLS>
   [ " ": I cRud
   , "Name": ttName cRUd
   , "Template": ttTemplate cRUd
   , "Value": ttValue cRUd
   , "-" : compVar~ cRud
   , "Instance": ttInstance cRud
   ]

INTERFACE "TText": I[TText] CRud BOX
   [ "Name": ttName cRUd
   , "Template": ttTemplate cRUd
   , "Value": ttValue cRUd
   , "-" : compVar~ cRud
   , "Instance": ttInstance cRud
   , "Scope": ttScope LINKTO INTERFACE "Scope"
   ]

{-- The below interface is meant to be used for importing data from Excel sheets
INTERFACE "#TTexts" {-FOR ExcelImporter-}: I[TText] CRud BOX
   [ "I": I cRud
   , "Scope": ttScope cRUd
   , "Name": ttName cRUd
   , "Value": ttValue cRUd
   , "Template": ttTemplate cRUd
   , "TopLevel?": ttTopLevel cRUd
   ] --}

ENDCONTEXT