CONTEXT TText_SIAM_Extensions IN ENGLISH

INCLUDE "../TText/SIAM_Module.adl" -- Sessions, Identity & Access Management facilities

--[TText Ownership]--
ttOwner :: TText * Account [UNI] -- The Account that is authorized to change and/or delete the TText.
ROLE ExecEngine MAINTAINS "Set default TText ownership"
RULE "Set default TText ownership": 
   (I[TText]-ttOwner;ttOwner~);V;"_SESSION";sessionAccount |- ttOwner
MEANING "Ownership of a TText is automatically assigned to the Account that has created the TText"
VIOLATION (TXT "{EX} InsPair;ttOwner;TText;", SRC I, TXT ";Account;", TGT I)

ROLE ExecEngine MAINTAINS "Unowned TTexts that are used by an owned TText, will be assigned the same owner"
RULE "Unowned TTexts that are used by an owned TText, will be assigned the same owner":
   (I[TText]-ttOwner;ttOwner~);ttIsUsedBy;ttOwner |- ttOwner
VIOLATION (TXT "{EX} InsPair;ttOwner;TText;", SRC I[TText], TXT ";Account;", TGT I)

-- TText: copying `ttOwner[TText*Account]`:
-- The assumption that the Owner controls the values of `ttTemplate`, `ttName` and `ttDescr` implies that `ttOwner` must also be copied (so that you can make sure that (s)he also controls this stuff in the copied versions. You should use `ttValSupplier`s for the Accounts that do the runtime filling in of `ttValue`s
ROLE ExecEngine MAINTAINS "Initialize copied ttOwner"
RULE "Initialize copied ttOwner": (I-ttOwner;ttOwner~);ttICO;ttOwner |- ttOwner
VIOLATION (TXT "{EX}_;InsPair_;ttOwner_;TText_;", SRC I[TText], TXT "_;Account_;", TGT I)
ROLE ExecEngine MAINTAINS "Create/Update copied ttOwner"
RULE "Create/Update copied ttOwner": ttICCO;ttOwner |- ttOwner
VIOLATION (TXT "{EX}_;InsPair_;ttOwner_;TText_;", SRC I[TText], TXT "_;Account_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted ttOwner"
RULE "Update deleted ttOwner": ttICCO;(I[TText]-ttOwner;ttOwner~) |- (I[TText]-ttOwner;ttOwner~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;ttOwner_;TText_;", SRC I[TText], TXT "_;Account_;", SRC ttOwner)

ENDCONTEXT