CONTEXT TValSup_CarbonCopyService IN ENGLISH

INCLUDE "../TText/TValSup_Inclusion.svc" -- include necessary relations/declarations

--[Carbon Copying TParty extensions]--
-- TParty: carbon copying `ttIsaTParty[TText*TText]` is not done, as it is automatically maintained.

-- TParty: carbon copying `tPartyReqdOrgRef[TText*Organization]`:
ROLE ExecEngine MAINTAINS "Create/Update copied tPartyReqdOrgRef"
RULE "Create/Update copied tPartyReqdOrgRef": ttICCO;tPartyReqdOrgRef |- tPartyReqdOrgRef
VIOLATION (TXT "{EX}_;InsPair_;tPartyReqdOrgRef_;TText_;", SRC ttIsaTParty, TXT "_;OrgRef_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted tPartyReqdOrgRef"
RULE "Update deleted tPartyReqdOrgRef": ttICCO;(ttIsaTParty-tPartyReqdOrgRef;tPartyReqdOrgRef~) |- (ttIsaTParty-tPartyReqdOrgRef;tPartyReqdOrgRef~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;tPartyReqdOrgRef_;TText_;", SRC ttIsaTParty, TXT "_;OrgRef_;", SRC tPartyReqdOrgRef)

-- TParty: carbon copying `tPartyAcc[TText*Account]`:
ROLE ExecEngine MAINTAINS "Create/Update copied tPartyAcc"
RULE "Create/Update copied tPartyAcc": ttICCO;tPartyAcc |- tPartyAcc
VIOLATION (TXT "{EX}_;InsPair_;tPartyAcc_;TText_;", SRC ttIsaTParty, TXT "_;Account_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted tPartyAcc"
RULE "Update deleted tPartyAcc": ttICCO;(ttIsaTParty-tPartyAcc;tPartyAcc~) |- (ttIsaTParty-tPartyAcc;tPartyAcc~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;tPartyAcc_;TText_;", SRC ttIsaTParty, TXT "_;Account_;", SRC tPartyAcc)

--[Carbon Copying TText extensions]--
-- TText: carbon copying `ttValSupName[TText*TTName]`:
ROLE ExecEngine MAINTAINS "Create/Update copied ttValSupName"
RULE "Create/Update copied ttValSupName": ttICCO;ttValSupName |- ttValSupName
VIOLATION (TXT "{EX}_;InsPair_;ttValSupName_;TText_;", SRC I[TText], TXT "_;TTName_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted ttValSupName"
RULE "Update deleted ttValSupName": ttICCO;(I-ttValSupName;ttValSupName~) |- (I-ttValSupName;ttValSupName~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;ttValSupName_;TText_;", SRC I[TText], TXT "_;TTName_;", SRC ttValSupName)

-- TText: carbon copying copied `ttValSupplier[TText*TText]`:
ROLE ExecEngine MAINTAINS "Create/Update copied ttValSupplier"
RULE "Create/Update copied ttValSupplier": (ttICCO;ttValSupplier;ttICCO~ /\ ttScope;ttScope~);ttIsaTParty |- ttValSupplier
VIOLATION (TXT "{EX}_;InsPair_;ttValSupplier_;TText_;", SRC I[TText], TXT "_;TText_;", TGT I[TText])
ROLE ExecEngine MAINTAINS "Update deleted ttValSupplier"
RULE "Update deleted ttValSupplier": ttICCO;(I-ttValSupplier;ttValSupplier~) |- (I-ttValSupplier;ttValSupplier~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;ttValSupplier_;TText_;", SRC I[TText], TXT "_;TText_;", SRC I[TText])

ENDCONTEXT