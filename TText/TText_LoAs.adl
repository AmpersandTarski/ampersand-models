CONTEXT "TText_LoAs.adl" IN ENGLISH
--[TText authentication- and validation levels]--
{- ISO/IEC 29115 or ITU-T X.1254 framework for entity authentication assurance; 
LoA = Level of Assurance (confidence) in the asserted identity
LoA = 1: little confidence in the asserted identity
LoA = 2: some confidence in the asserted identity
LoA = 3: high confidence in the asserted identity
LoA = 4: very high confidence in the asserted identity
Analogously, we use the term 'LoV' (Level of Validity) for the level of assurance (confidence) in the validity (of the value of a TText).
-}
ttReqdLoA :: TText * ISOLevel [UNI] -- Minimum level at which the supplier of the TText's value must be authenticated.
ttReqdLoV :: TText * ISOLevel [UNI] -- Minimum level at which the value of the TText must have been validated.
ttActualLoV :: TText * ISOLevel [UNI] -- Level at which the value of the TText has been validated by its supplier.

RULE "Actual validation of TTexts must be at least at the required validation level": ttActualLoV~;ttReqdLoV |- isoLevelGTE

ROLE ExecEngine MAINTAINS "The default value for ttReqdLoA = 1"
RULE "The default value for ttReqdLoA = 1":
   I[TText] -(ttReqdLoA;ttReqdLoA~) |- ttReqdLoA;ttReqdLoA~
VIOLATION (TXT "{EX} InsPair;ttReqdLoA;TText;", SRC I[TText], TXT ";ISOLevel;1")

ROLE ExecEngine MAINTAINS "The default value for ttReqdLoV = 1"
RULE "The default value for ttReqdLoV = 1":
   I[TText] -(ttReqdLoV;ttReqdLoV~) |- ttReqdLoV;ttReqdLoV~
VIOLATION (TXT "{EX} InsPair;ttReqdLoV;TText;", SRC I[TText], TXT ";ISOLevel;1")

ROLE ExecEngine MAINTAINS "TTexts that have a value have at least actual LoV=1"
RULE "TTexts that have a value have at least actual LoV=1":
   (I[TText]-(ttActualLoV;ttActualLoV~)) /\ ttValue;ttValue~
|- ttActualLoV;ttActualLoV~
VIOLATION (TXT "{EX} InsPair;ttActualLoV;TText;", SRC I[TText], TXT ";ISOLevel;1")

{-- **ISOLevel inheritance must be thought about carefully before using it**
ROLE ExecEngine MAINTAINS "TTexts that are used by other TTexts initially inherit ttReqdLoA"
RULE "TTexts that are used by other TTexts initially inherit ttReqdLoA":
   (I[TText]-(ttReqdLoA;ttReqdLoA~));ttIsUsedBy;ttReqdLoA |- ttReqdLoA
VIOLATION (TXT "{EX} InsPair;ttReqdLoA;TText;", SRC I[TText], TXT ";ISOLevel;", TGT I)

ROLE ExecEngine MAINTAINS "TTexts that are used by other TTexts initially inherit ttReqdLoV"
RULE "TTexts that are used by other TTexts initially inherit ttReqdLoV":
   (I[TText]-(ttReqdLoV;ttReqdLoV~));ttIsUsedBy;ttReqdLoV |- ttReqdLoV
VIOLATION (TXT "{EX} InsPair;ttReqdLoV;TText;", SRC I[TText], TXT ";ISOLevel;", TGT I) --}

--[Extension to TText copying facility]--

INCLUDE "../TText/TText_Copying.svc" -- Ensure the relation `ttICO` and `ttICCO` are defined

-- TText: copying `ttReqdLoA[TText*ISOLevel]`:
ROLE ExecEngine MAINTAINS "Initialize copied ttReqdLoA"
RULE "Initialize copied ttReqdLoA": (I-ttReqdLoA;ttReqdLoA~);ttICO;ttReqdLoA |- ttReqdLoA
VIOLATION (TXT "{EX}_;InsPair_;ttReqdLoA_;TText_;", SRC I[TText], TXT "_;ISOLevel_;", TGT I)
ROLE ExecEngine MAINTAINS "Create/Update copied ttReqdLoA"
RULE "Create/Update copied ttReqdLoA": ttICCO;ttReqdLoA |- ttReqdLoA
VIOLATION (TXT "{EX}_;InsPair_;ttReqdLoA_;TText_;", SRC I[TText], TXT "_;ISOLevel_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted ttReqdLoA"
RULE "Update deleted ttReqdLoA": ttICCO;(I-ttReqdLoA;ttReqdLoA~) |- (I-ttReqdLoA;ttReqdLoA~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;ttReqdLoA_;TText_;", SRC I[TText], TXT "_;ISOLevel_;", SRC ttReqdLoA)

-- TText: copying `ttReqdLoV[TText*ISOLevel]`:
ROLE ExecEngine MAINTAINS "Initialize copied ttReqdLoV"
RULE "Initialize copied ttReqdLoV": (I-ttReqdLoV;ttReqdLoV~);ttICO;ttReqdLoV |- ttReqdLoV
VIOLATION (TXT "{EX}_;InsPair_;ttReqdLoV_;TText_;", SRC I[TText], TXT "_;ISOLevel_;", TGT I)
ROLE ExecEngine MAINTAINS "Create/Update copied ttReqdLoV"
RULE "Create/Update copied ttReqdLoV": ttICCO;ttReqdLoV |- ttReqdLoV
VIOLATION (TXT "{EX}_;InsPair_;ttReqdLoV_;TText_;", SRC I[TText], TXT "_;ISOLevel_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted ttReqdLoV"
RULE "Update deleted ttReqdLoV": ttICCO;(I-ttReqdLoV;ttReqdLoV~) |- (I-ttReqdLoV;ttReqdLoV~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;ttReqdLoV_;TText_;", SRC I[TText], TXT "_;ISOLevel_;", SRC ttReqdLoV)

-- TText: copying `ttActualLoV[TText*ISOLevel]`:
ROLE ExecEngine MAINTAINS "Initialize copied ttActualLoV"
RULE "Initialize copied ttActualLoV": (I-ttActualLoV;ttActualLoV~);ttICO;ttActualLoV |- ttActualLoV
VIOLATION (TXT "{EX}_;InsPair_;ttActualLoV_;TText_;", SRC I[TText], TXT "_;ISOLevel_;", TGT I)
ROLE ExecEngine MAINTAINS "Create/Update copied ttActualLoV"
RULE "Create/Update copied ttActualLoV": ttICCO;ttActualLoV |- ttActualLoV
VIOLATION (TXT "{EX}_;InsPair_;ttActualLoV_;TText_;", SRC I[TText], TXT "_;ISOLevel_;", TGT I)
ROLE ExecEngine MAINTAINS "Update deleted ttActualLoV"
RULE "Update deleted ttActualLoV": ttICCO;(I-ttActualLoV;ttActualLoV~) |- (I-ttActualLoV;ttActualLoV~);ttICCO
VIOLATION (TXT "{EX}_;DelPair_;ttActualLoV_;TText_;", SRC I[TText], TXT "_;ISOLevel_;", SRC ttActualLoV)

ENDCONTEXT