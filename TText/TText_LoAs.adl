CONTEXT "TText_LoAs.adl" IN ENGLISH
--[TText authentication- and validation levels]--
{- ISO/IEC 29115 or ITU-T X.1254 framework for entity authentication assurance; 
LoA = Level of Assurance (confidence) in the asserted identity
LoA = 1: little confidence in the asserted identity
LoA = 2: some confidence in the asserted identity
LoA = 3: high confidence in the asserted identity
LoA = 4: very high confidence in the asserted identity
Analogously, we use the term 'LoV' (Level of Validity) for the level of assurance (confidence) in the validity (of the value of a TText).
-}
ttReqdLoA :: TText * ISOLevel [UNI] -- Minimum level at which the supplier of the TText's value must be authenticated.
ttReqdLoV :: TText * ISOLevel [UNI] -- Minimum level at which the value of the TText must have been validated.
ttActualLoV :: TText * ISOLevel [UNI] -- Level at which the value of the TText has been validated by its supplier.

RULE "Actual validation of TTexts must be at least at the required validation level": ttActualLoV~;ttReqdLoV |- isoLevelGTE

ROLE ExecEngine MAINTAINS "The default value for ttReqdLoA = 1"
RULE "The default value for ttReqdLoA = 1":
   I[TText] - ttReqdLoA;ttReqdLoA~ |- ttReqdLoA;ttReqdLoA~
VIOLATION (TXT "{EX} InsPair;ttReqdLoA;TText;", SRC I[TText], TXT ";ISOLevel;1")

ROLE ExecEngine MAINTAINS "The default value for ttReqdLoV = 1"
RULE "The default value for ttReqdLoV = 1":
   I[TText] - ttReqdLoV;ttReqdLoV~ |- ttReqdLoV;ttReqdLoV~
VIOLATION (TXT "{EX} InsPair;ttReqdLoV;TText;", SRC I[TText], TXT ";ISOLevel;1")

ROLE ExecEngine MAINTAINS "TTexts that have a value have at least actual LoV=1"
RULE "TTexts that have a value have at least actual LoV=1":
   (I[TText] - ttActualLoV;ttActualLoV~) /\ ttValue;ttValue~
|- ttActualLoV;ttActualLoV~
VIOLATION (TXT "{EX} InsPair;ttActualLoV;TText;", SRC I[TText], TXT ";ISOLevel;1")

{-- **ISOLevel inheritance must be thought about carefully before using it**
ROLE ExecEngine MAINTAINS "TTexts that are used by other TTexts initially inherit ttReqdLoA"
RULE "TTexts that are used by other TTexts initially inherit ttReqdLoA":
   (I[TText]-ttReqdLoA;ttReqdLoA~);ttIsUsedBy;ttReqdLoA |- ttReqdLoA
VIOLATION (TXT "{EX} InsPair;ttReqdLoA;TText;", SRC I[TText], TXT ";ISOLevel;", TGT I)

ROLE ExecEngine MAINTAINS "TTexts that are used by other TTexts initially inherit ttReqdLoV"
RULE "TTexts that are used by other TTexts initially inherit ttReqdLoV":
   (I[TText]-ttReqdLoV;ttReqdLoV~);ttIsUsedBy;ttReqdLoV |- ttReqdLoV
VIOLATION (TXT "{EX} InsPair;ttReqdLoV;TText;", SRC I[TText], TXT ";ISOLevel;", TGT I) --}

ENDCONTEXT