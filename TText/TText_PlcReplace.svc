CONTEXT ReplacementServices IN ENGLISH
--RJ: For increased readability of expressions, and to allow for future extensions, I sometimes chose to use long descriptive relation names over short ones.

--[[Placeholder Replace-By-TTValue service]]--
PATTERN "Placeholder Replace-By-TTValue Service"
PURPOSE PATTERN "Placeholder Replace-By-TTValue Service"
{+This service ensures that the `ttInstance` phrase of a TText
is the `ttTemplate` phrase of that TText, where
every occurrence of a reference to another TText in that template 
(i.e. occurences of `[<TTName>]`, called 'placeholder's)
**is replaced by the `ttValue`** of that other TText,
provided that the `ttValue` is populated for that TText.
+}

--[Replacing placeholders by TTValues]--
-- The following relation is needed to detect changes in phrases that are used to replace placeholders. When detected, such changes can then propagate further.
RELATION ttValueUsedToReplacePlaceholders[TText*TTValue] [UNI] -- The ttValue (TTValue) that has been used to replace placeholders (i.e. references to the SRC TText) with in (instance phrases of) (some other) TTexts
MEANING "References to this TText (i.e.: placeholders) have been replaced with the specified TTValue"
RULE "ttValueUsedToReplacePlaceholders integrity": ttValueUsedToReplacePlaceholders |- ttValue

ROLE ExecEngine MAINTAINS "Register that the value of a TText has been used to replace placeholders"
RULE "Register that the value of a TText has been used to replace placeholders": 
   (I /\ ttValue;ttValue~);ttIsUsedBy;(I-ttInstanceResetReq) -- When SERVICEs are implemented, the `I-ttInstanceResetReq` is no longer needed
|- ttValueIsUsedInInstanceOfTText
MEANING "If a TText has a value, and is used in a TText that is not being re-initialized, then it must appear in the `ttValueIsUsedInInstanceOfTText` of the TText."
VIOLATION (TXT "{EX} InsPair;ttValueIsUsedInInstanceOfTText;TText;", SRC I[TText], TXT ";TText;", TGT I[TText]
          ,TXT "{EX} InsPair;ttValueUsedToReplacePlaceholders;TText;", SRC I[TText], TXT ";TTValue;", SRC ttValue
          ,TXT "{EX}_;ReplacePlaceholdersInTTextInstance"
                     ,TXT "_;", TGT I           -- TText source atom for ttInstance that is in need of replacements
                     ,TXT "_;", TGT ttInstance  -- string in which the replacements should take place
                     ,TXT "_;", SRC ttName      -- placeholder that needs to be replaced
                     ,TXT "_;", SRC ttValue     -- value by which to replace the placeholders
          )

--[Accommodating changed TTValues]--
-- When the value of a TText differs from the value used in replacements, or has become void, then the instance-texts of all affected TTexts (i.e. TTexts in which the replacements took place) must be discarded and recreated.
ROLE ExecEngine MAINTAINS "After a value update, all TTexts that used the value must be reset"
RULE "After a value update, all TTexts that used the value must be reset": -- Don't rewrite the below rule as `(I-(ttValueUsedToReplacePlaceholders;ttValue~)) |- ttValueIsUsedInInstanceOfTText~~;ttInstanceResetReq` because that can also fire when `ttValueUsedToReplacePlaceholders` does not exist, in which case the VIOLATION will result in an error (because of the `SRC ttValueUsedToReplacePlaceholders`)
   ( I /\ ( (ttValueUsedToReplacePlaceholders;-I;ttValue~) 
           \/ -(ttValue;ttValue~)
          )
   );ttValueIsUsedInInstanceOfTText
|- ttValueIsUsedInInstanceOfTText;ttInstanceResetReq
MEANING "When the value of a TText differs from the value used in replacements, or has become void, then the instance-texts of all TTexts in which the replacements took place must be discarded and recreated."
VIOLATION (TXT "{EX} DelPair;ttValueUsedToReplacePlaceholders;TText;", SRC I, TXT ";TTValue;", SRC ttValueUsedToReplacePlaceholders
          ,TXT "{EX} InsPair;ttInstanceResetReq;TText;", TGT I, TXT ";TText;", TGT I
          )

ENDPATTERN
ENDCONTEXT