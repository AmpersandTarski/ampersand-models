CONTEXT TText_SIAM_Extensions IN ENGLISH

  INCLUDE "./SIAM_Module.adl"                 -- Sessions, Identity & Access Management facilities

--[TText Ownership]--
ttOwner :: TText * Account [UNI] -- The Account that is authorized to change and/or delete the TText.

ROLE ExecEngine MAINTAINS "Automatically assign TText ownership"
RULE "Automatically assign TText ownership": 
   (I[TText]-ttOwner;ttOwner~);V;'_SESSION';sessionAccount |- ttOwner
MEANING "Ownership of a TText is automatically assigned to the Account that has created the TText"
VIOLATION (TXT "{EX} InsPair;ttOwner;TText;", SRC I, TXT ";Account;", TGT I)

ROLE ExecEngine MAINTAINS "Unowned TTexts that are used by an owned TText, will be assigned the same owner"
RULE "Unowned TTexts that are used by an owned TText, will be assigned the same owner":
   (I[TText]-(ttOwner;ttOwner~));ttIsUsedBy;ttOwner |- ttOwner
VIOLATION (TXT "{EX} InsPair;ttOwner;TText;", SRC I[TText], TXT ";Account;", TGT I)

ENDCONTEXT