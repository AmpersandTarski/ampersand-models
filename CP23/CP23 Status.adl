CONTEXT HRMStatus IN ENGLISH
-----------------------------------------------------------
PROCESS HRMStatus
--[Employee Status]--
{-
-----------------------------------------------------------------------------------------------------------
Employee needs to return one or more equipment            |           N           |           Y           |
Employee has been issued all equipment that (s)he needs   |     Y     |     N     |     Y     |     N     |
Employee has been assigned no equipment that it needs     |  Y  |  N  |  Y  |  N  |  Y  |  N  |  Y  |  N  |
-----------------------------------------------------------------------------------------------------------
Statuscolor:                                              |Black|Green| Red |Yello|Grey |Blue | Red |Orang|  
-----------------------------------------------------------------------------------------------------------
-}
needsToReturnEqt :: Employee * Employee --[PROP] $BUGGY!!!!
ROLE ExecEngine MAINTAINS insneedsToReturnEqt
RULE insneedsToReturnEqt: I /\ (emplIssuedEqt;eqtKind /\ -(emplOrgRole;stdIssueEqtKind));V |- needsToReturnEqt
VIOLATION (TXT "{EX} InsPair;needsToReturnEqt;Employee;", SRC I, TXT ";Employee;", TGT I)
ROLE ExecEngine MAINTAINS delneedsToReturnEqt
RULE delneedsToReturnEqt: needsToReturnEqt |- I /\ (emplIssuedEqt;eqtKind /\ -(emplOrgRole;stdIssueEqtKind));V
VIOLATION (TXT "{EX} DelPair;needsToReturnEqt;Employee;", SRC I, TXT ";Employee;", TGT I)

allNecessaryEqtHasBeenIssued :: Employee * Employee --[PROP] $BUGGY!!!!
ROLE ExecEngine MAINTAINS insallNecessaryEqtHasBeenIssued
RULE insallNecessaryEqtHasBeenIssued: I /\ (-(emplOrgRole;stdIssueEqtKind) ! (emplIssuedEqt;eqtKind)~) |- allNecessaryEqtHasBeenIssued
VIOLATION (TXT "{EX} InsPair;allNecessaryEqtHasBeenIssued;Employee;", SRC I, TXT ";Employee;", TGT I)
ROLE ExecEngine MAINTAINS delallNecessaryEqtHasBeenIssued
RULE delallNecessaryEqtHasBeenIssued: allNecessaryEqtHasBeenIssued |- I /\ (-(emplOrgRole;stdIssueEqtKind) ! (emplIssuedEqt;eqtKind)~)
VIOLATION (TXT "{EX} DelPair;allNecessaryEqtHasBeenIssued;Employee;", SRC I, TXT ";Employee;", TGT I)

noNecessaryEqtHasBeenIssued :: Employee * Employee --[PROP] $BUGGY!!!!
ROLE ExecEngine MAINTAINS insnoNecessaryEqtHasBeenIssued
RULE insnoNecessaryEqtHasBeenIssued: I /\ (-(emplOrgRole;stdIssueEqtKind) ! -(emplIssuedEqt;eqtKind)~) |- noNecessaryEqtHasBeenIssued
VIOLATION (TXT "{EX} InsPair;noNecessaryEqtHasBeenIssued;Employee;", SRC I, TXT ";Employee;", TGT I)
ROLE ExecEngine MAINTAINS delnoNecessaryEqtHasBeenIssued
RULE delnoNecessaryEqtHasBeenIssued: noNecessaryEqtHasBeenIssued |- I /\ (-(emplOrgRole;stdIssueEqtKind) ! -(emplIssuedEqt;eqtKind)~)
VIOLATION (TXT "{EX} DelPair;noNecessaryEqtHasBeenIssued;Employee;", SRC I, TXT ";Employee;", TGT I)

emplStatus :: Employee * Status [UNI]

ROLE ExecEngine MAINTAINS setemplStatusBlack
RULE setemplStatusBlack: I /\ -needsToReturnEqt /\ allNecessaryEqtHasBeenIssued /\ noNecessaryEqtHasBeenIssued |- emplStatus;'Black';emplStatus~
VIOLATION (TXT "{EX} InsPair;emplStatus;Employee;", SRC I, TXT ";Status;Black")

ROLE ExecEngine MAINTAINS setemplStatusGreen
RULE setemplStatusGreen: I /\ -needsToReturnEqt /\ allNecessaryEqtHasBeenIssued /\ -noNecessaryEqtHasBeenIssued |- emplStatus;'Green';emplStatus~
VIOLATION (TXT "{EX} InsPair;emplStatus;Employee;", SRC I, TXT ";Status;Green")

ROLE ExecEngine MAINTAINS setemplStatusRed
RULE setemplStatusRed: I /\ -allNecessaryEqtHasBeenIssued /\ noNecessaryEqtHasBeenIssued |- emplStatus;'Red';emplStatus~
VIOLATION (TXT "{EX} InsPair;emplStatus;Employee;", SRC I, TXT ";Status;Red")

ROLE ExecEngine MAINTAINS setemplStatusYellow
RULE setemplStatusYellow: I /\ -needsToReturnEqt /\ -allNecessaryEqtHasBeenIssued /\ -noNecessaryEqtHasBeenIssued |- emplStatus;'Yellow';emplStatus~
VIOLATION (TXT "{EX} InsPair;emplStatus;Employee;", SRC I, TXT ";Status;Yellow")

ROLE ExecEngine MAINTAINS setemplStatusGrey
RULE setemplStatusGrey: I /\ needsToReturnEqt /\ allNecessaryEqtHasBeenIssued /\ noNecessaryEqtHasBeenIssued |- emplStatus;'Grey';emplStatus~
VIOLATION (TXT "{EX} InsPair;emplStatus;Employee;", SRC I, TXT ";Status;Grey")

ROLE ExecEngine MAINTAINS setemplStatusBlue
RULE setemplStatusBlue: I /\ needsToReturnEqt /\ allNecessaryEqtHasBeenIssued /\ -noNecessaryEqtHasBeenIssued |- emplStatus;'Blue';emplStatus~
VIOLATION (TXT "{EX} InsPair;emplStatus;Employee;", SRC I, TXT ";Status;Blue")

ROLE ExecEngine MAINTAINS setemplStatusOrange
RULE setemplStatusOrange: I /\ needsToReturnEqt /\ -allNecessaryEqtHasBeenIssued /\ -noNecessaryEqtHasBeenIssued |- emplStatus;'Orange';emplStatus~
VIOLATION (TXT "{EX} InsPair;emplStatus;Employee;", SRC I, TXT ";Status;Orange")

ENDPROCESS
-----------------------------------------------------------
ENDCONTEXT