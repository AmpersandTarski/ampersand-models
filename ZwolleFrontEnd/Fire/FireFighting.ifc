CONTEXT "Fire Fighting Interfaces" IN ENGLISH

INTERFACE "OVERVIEW" (fdetDetectsFire) : "_SESSION"[SESSION]
TABS[ "Fire detectors" : V[SESSION*FireDetector]
    COLS[ "Fdet ID" : fdetID
        , "FireDetected?" : fdetDetectsFire -- do the default thing
        , "BoolFireOrNot" : fdetDetectsFire <BoolFireOrNot> 
        , "YesNoBool"     : fdetDetectsFire <YesNoBool>
        , "Coordinate pair" : I <ShowOnMap>
        ]
    , "Fire Doors" : V[SESSION*FireDoor]
    COLS[ "Fdoor ID" : fdoorID
        , "Locations" : fdoorIsRelevantForLocation;locationID
        , "Can close itself?" : fdoorCanCloseItself;V;'TRUE'[BOOL] \/ (I /\ -fdoorCanCloseItself);V;'FALSE'[BOOL]
        , "Door status" : fdoorStatus
        , "Coordinate pair" : I <ShowOnMap>
        ]
    , "Locations" : V[SESSION*Location]
    COLS[ "ID" : locationID
        , "Adjacent locations" : (-I /\ cpairSeparatesLocation~;cpairSeparatesLocation);locationID
        , "Fire detectors" : cpairLocation~;I[FireDetector];fdetID
        , "Fire doors" : fdoorIsRelevantForLocation~;fdoorID
        ]
    , "Operational Pictures" : V[SESSION*Map] -- <CommonOperationalPictureMap>
    ]
{- VIEW CommonOperationalPictureMap: Map
    { mapimage  : I
    , firedoors : cpairMap~ <ShowOnMap>
    }
HTML TEMPLATE "CommonOperationalPictureMap.html"
ENDVIEW -}

-- VIEW CoordinatePairs: CoordinatePair(TXT "(X=", cpairX ,TXT ", Y=", cpairX ,TXT ", Z=", cpairDeck ,TXT ")")
INTERFACE "CoordinatePair" : I[CoordinatePair]
COLS[ "X (integer)" : cpairX
    , "Y (integer)" : cpairY
    , "Deck" : cpairDeck
    , "in location" : cpairLocation;locationID
    , "between locations" : cpairSeparatesLocation;locationID
    , "on map" : cpairMap
    , "use icon" : cpairIcon
    ]

VIEW ShowOnMap : CoordinatePair
    { cpairX    : cpairX
    , cpairY    : cpairY
    , cpairDeck : cpairDeck
    , imgfile   : cpairMap
    , iconfile  : cpairIcon
    }
HTML TEMPLATE "ShowOnMap.html"
ENDVIEW

VIEW ShowMap: Map DEFAULT { imgfile : I }
HTML TEMPLATE "ShowMap.html"
ENDVIEW

VIEW ShowIcon: Icon DEFAULT { iconfile : I }
HTML TEMPLATE "ShowIcon.html"
ENDVIEW

VIEW BoolFireOrNot: BOOL { bool : I }
HTML TEMPLATE "BoolFireOrNot.html"
ENDVIEW

VIEW YesNoBool: BOOL { bool : I }
HTML TEMPLATE "YesNoBool.html"
ENDVIEW
ENDCONTEXT 