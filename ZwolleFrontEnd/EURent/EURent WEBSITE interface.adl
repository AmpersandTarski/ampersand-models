CONTEXT EURentWEBSITEInterfaces IN ENGLISH
------------------------------------------------------------
sessionRC  :: SESSION * RentalCase [UNI] -- Current rental case in a session

INTERFACE "EU-Rent Website" FOR Developer,User : '_SESSION'[SESSION] cRud BOX
   [ "Create a new rental" : sessionRC CRud
   , "or select your case" : sessionRC cRUd -- to be expanded with the possibility to search after a login
   ]

INTERFACE "New Car Rental" FOR Developer,User : I[RentalCase] CRud BOX
   [ "Customer details" : I cRud INTERFACE "Edit Customer Details"
   , "Contract details" : I cRud INTERFACE "Edit Contract Details"
   , "Projected costs"  : I cRud INTERFACE "Show Projected Regular Costs"
   , "New rental request" : I cRud BOX
      [ "Submit this rental request?" : rcUserRequestedBOOL cRUd
      , "Has rental been promised?" : rentalHasBeenPromised;V;'TRUE'[BOOL] \/ (I /\ -rentalHasBeenPromised);V;'FALSE'[BOOL] cRud
      ]
   ]
-----------------------------------------------------------
PROCESS "EU-Rent: New User Rental Request - Signaling"
ROLE User MAINTAINS "Required field: Driver"
RULE "Required field: Driver"          : I /\ sessionRC~;'_SESSION';sessionRC |- rcDriver;rcDriver~
VIOLATION (TXT "Required field 'Driver' must be filled in")

ROLE User MAINTAINS "Required field: Pick-up branch"
RULE "Required field: Pick-up branch"  : I /\ sessionRC~;'_SESSION';sessionRC |- contractedPickupBranch;contractedPickupBranch~
VIOLATION (TXT "Required field 'Pick-up branch' must be filled in")

ROLE User MAINTAINS "Required field: Drop-off branch"
RULE "Required field: Drop-off branch" : I /\ sessionRC~;'_SESSION';sessionRC |- contractedDropOffBranch;contractedDropOffBranch~
VIOLATION (TXT "Required field 'Drop-off branch' must be filled in")

ROLE User MAINTAINS "Required field: Start date"
RULE "Required field: Start date"      : I /\ sessionRC~;'_SESSION';sessionRC |- contractedStartDate;contractedStartDate~
VIOLATION (TXT "Required field 'Start date' must be filled in")

ROLE User MAINTAINS "Required field: End date"
RULE "Required field: End date"        : I /\ sessionRC~;'_SESSION';sessionRC |- contractedEndDate;contractedEndDate~
VIOLATION (TXT "Required field 'End date' must be filled in")

ROLE User MAINTAINS "Required field: Car type"
RULE "Required field: Car type"        : I /\ sessionRC~;'_SESSION';sessionRC |- contractedCarType;contractedCarType~
VIOLATION (TXT "Required field 'Car type' must be filled in")

ROLE User MAINTAINS "Submitting rental request"
RULE "Submitting rental request":
 I /\ sessionRC~;'_SESSION';sessionRC 
   /\ rcDriver;rcDriver~
   /\ contractedPickupBranch;contractedPickupBranch~
   /\ contractedDropOffBranch;contractedDropOffBranch~
   /\ contractedStartDate;contractedStartDate~
   /\ contractedEndDate;contractedEndDate~
   /\ contractedCarType;contractedCarType~
|- rcUserRequestedBOOL;'TRUE';rcUserRequestedBOOL~
VIOLATION (TXT "Please submit the request (by answering 'Submit this rental request?' with 'TRUE').")
ENDPROCESS
-----------------------------------------------------------
PROCESS "EU-Rent website: New Rental Requests"
PURPOSE PROCESS "EU-Rent website: New Rental Requests" REF "P2:1"
{+The user interface "New User Rental" provides some automated functionality. This section describes the features for filling in or changing the contents of forms that are presented in that interface. The assumption is that this interface is provided over the Internet, allowing users to request a rental in advance (see P2:1) from any location of their choosing (e.g. at home).-}

PURPOSE RULE "Car availability"
{+When a contract is being created, cars and/or branches may only be selected if such cars are available at these branches.-}
RULE "Car availability":  contractedCarType~;(I /\ sessionRC~;'_SESSION';sessionRC);contractedPickupBranch |- carType~;carAvailableAt
MEANING "When creating a new rental contract, car types and pick-up branch must be selected such that the branch has cars of that type available."
VIOLATION (TXT "Cars of type ", SRC I, TXT " are not available at ", TGT I)

PURPOSE RULE "Submitting user rental requests"
{+When a submitted rental case has been promised, it will no longer be a new rental request.-}
ROLE ExecEngine MAINTAINS "Submitting user rental requests"
RULE "Submitting user rental requests": sessionRC;rentalHasBeenPromised |- sessionRC;-(rcUserRequestedBOOL;'TRUE';rcUserRequestedBOOL~)
MEANING "User initiated rental requests must be submitted in order for them to be handled by EU-Rent."
VIOLATION (TXT "{EX} DelPair;sessionRC;SESSION;", SRC I, TXT ";RentalCase;", TGT I)

ENDPROCESS
-----------------------------------------------------------
ENDCONTEXT