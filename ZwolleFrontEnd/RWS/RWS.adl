CONTEXT RWS IN DUTCH
{- RWS is een proof-of-principle  voor het opschonen van gegevensbestanden.
CONTEXT: 
Installaties kunnen onderling zijn verbonden via kabels.
Dit moet worden geadministreerd.
Een dergelijke administratie kan fouten bevatten - zeker als hij groot is.

DEMO:
Voorbereiden:
  - Vertaal dit ADL bestand naar een prototype, start hem op en reset 
    de database (die is dan leeg).
  - Als er al een bestand "Bron.xlsx" bestaat, gooi hem dan weg.
  - Kopieer het bestand "Kabeladministratie (fout).xlsx" naar "Bron.xlsx"
Stap 1: 
  - Importeer "Bron.xlsx".
  - Klik op 'Overzicht' in de menubalk, en laat zien dat je een overzicht 
    hebt van de kabels en installaties, en dat de fouten daar zijn opgemerkt.
  - Klik op 'Issues' in de menubalk, en 
    laat zien dat je hier alleen die dingen ziet waar problemen mee zijn.
Stap 2:
  - Herstel alle fouten in "Bron.xlsx"
    hiermee simuleer je het op orde brengen van de administratie.
    (ter info: het bestand "Kabeladministratie (goed).xlsx" bevat geen fouten
               en zou je dus ook kunnen gebruiken)
  - Reset de database (dan is-ie weer leeg).
  - Importeer "Bron.xlsx" en kijk (als in stap 1) of er geen issues meer zijn.
Herhaal stap 2 tot er geen issues meer zijn.
-}
--[Ontologie]--

kabelNummer :: Kabel * KabelNummer [INJ,UNI,TOT] -- Elke kabel wordt geidentificeerd door zijn kabel nummer
kabelSoort :: Kabel * KabelSoort -- Elke kabel kan van een zeker soort zijn
vanInst :: Kabel * Installatie -- Een kabel kan van een installatie uit gaan
naarInst:: Kabel * Installatie -- Een kabel kan naar een installatie toe gaan

ROLE USER MAINTAINS "Kabelsoort [uni]"
RULE "Kabelsoort [uni]": kabelSoort |- -(kabelSoort;-I)
MEANING "Elke kabel kan van hoogstens 1 soort zijn" 
VIOLATION (TXT "Voor kabel ", SRC I, TXT " zijn meerdere kabelsoorten gespecificeerd, waaronder ", TGT I)

ROLE USER MAINTAINS "Kabel beginpunt [uni]"
RULE "Kabel beginpunt [uni]": vanInst |- -(vanInst;-I)
MEANING "Elke kabel kan van hoogstens 1 installatie uit gaan" 
VIOLATION (TXT "Kabel ", SRC I, TXT " heeft meerdere BEGINpunten, waaronder ", TGT I)

ROLE USER MAINTAINS "Kabel eindpunt [uni]"
RULE "Kabel eindpunt [uni]": naarInst |- -(naarInst;-I)
MEANING "Elke kabel kan naar maximaal 1 installatie lopen" 
VIOLATION (TXT "Kabel ", SRC I, TXT " heeft meerdere EINDpunten, waaronder ", TGT I)

instSoort :: Installatie * InstallatieSoort [UNI]
MEANING "Elke installatie is van een zeker type, die bekend kan zijn."

ROLE USER MAINTAINS "InstallatieSoort [uni]"
RULE "InstallatieSoort [uni]": instSoort |- -(instSoort;-I)
MEANING "Elke installatie kan van hoogstens 1 soort zijn" 
VIOLATION (TXT "Voor installatie ", SRC I, TXT " zijn meerdere installatiesoorten gespecificeerd, waaronder ", TGT I)

--[Interfaces]--
INTERFACE "Overzicht" : "_SESSION"[SESSION] cRud
TABS[ "Kabels"         : V[SESSION*Kabel] cRud INTERFACE "[Kabels]"
    , "Installaties"   : V[SESSION*Installatie] cRud INTERFACE "[Installaties]"
    , "Aansluitpunten" : V[SESSION*Aansluitpunt] cRud
    ]

INTERFACE "Issues" : "_SESSION"[SESSION] cRud
TABS[ "Kabel issues" : V[SESSION*Kabel];
    (  (I /\ vanInst;-I;vanInst~) -- meerdere beginpunten
    \/ (I /\ naarInst;-I;naarInst~) -- meerdere eindpunten
    \/ (I /\  naarInst;naarInst~ /\ -(vanInst;vanInst~)) -- alleen agsl op eindpunt
    \/ (I /\ -(naarInst;naarInst~) /\ vanInst;vanInst~) -- alleen agsl op beginpunt
    ) cRud INTERFACE "[Kabels]"

    , "Installatie issues" : V[SESSION*Installatie];
    (  (I /\ vanInst~;(I /\ -(naarInst;naarInst~));vanInst) -- kabel gaat nergens heen
    \/ (I /\ naarInst~;(I /\ -(vanInst;vanInst~));naarInst) -- kabel komt nergens vandaan
    ) cRud INTERFACE "[Installaties]"
    ]

INTERFACE "[Kabels]" : I[Kabel] cRud
COLS[ "Kabelnr" : I cRud
    , "Soort " : kabelSoort cRud
    , "Van (installatie)" : vanInst cRud
    , "Naar (installatie)" : naarInst cRud
    , "Problemen" : (I /\ vanInst;-I;vanInst~);V;'Kabel heeft meerdere installaties als beginpunt'[TEXT]
                 \/ (I /\ naarInst;-I;naarInst~);V;'Kabel heeft meerdere installaties als eindpunt'[TEXT]
                 \/ (I /\  naarInst;naarInst~ /\ -(vanInst;vanInst~));V;'Kabel is eenzijdig op eindpunt aangesloten'[TEXT]
                 \/ (I /\ -(naarInst;naarInst~) /\ vanInst;vanInst~);V;'Kabel is eenzijdig op beginpunt aangesloten'[TEXT]
                 cRud
    ]

INTERFACE "[Installaties]" : I[Installatie] cRud
COLS[ "Installatienummer " : I cRud
    , "Soort installatie" : instSoort cRud
    , "Kabel problemen" : vanInst~;(I /\ -(naarInst;naarInst~))
                       \/ naarInst~;(I /\ -(vanInst;vanInst~))
                       cRud
    COLS[ "Kabel" : I cRud
        , "Probleem" : (I /\ -(naarInst;naarInst~));V;'gaat nergens heen.'[TEXT]
                    \/ (I /\ -(vanInst;vanInst~));V;'komt nergens vandaan.'[TEXT]
                    cRud
        ]
    ]
-----------------------------------------------------------
ENDCONTEXT