CONTEXT FPE_Deelprocessen

--[Define-time: Activiteiten]
-- Activiteiten zijn specificaties van werk, voor de uitvoering waarvan 1 partij verantwoordelijk is
-- aan wie een bij de Activiteit horende rol is toegekend (dat regelen we later middels SIAMv4)
-- zodra aan de preconditie EN NIET aan de postconditie van de Activiteit is voldaan.
actNaam    :: Activiteit * ActNaam [INJ,UNI,TOT] -- Naam van de Activiteit (deze is identificerend)
actPrecdx  :: Activiteit * Expressie [UNI,TOT]
actPostcdx :: Activiteit * Expressie [UNI,TOT]
actRandcdx :: Activiteit * Expressie
actVoorschrift :: Activiteit * Voorschrift [UNI,TOT]
actRole :: Activiteit * Role -- Rol(len) die het Handelingsvoorschrift van de Activiteit uit mogen/moeten voeren.

--[Expressie evaluatie]
-- Het resultaat van evaluatie van een Expressie hangt af van de ToestandsRuimte waarin hij wordt geevalueerd.
-- Daarom specificeren we een structuur `TRExpr` die een Expressie en ToestandsRuimte koppelt aan het evaluatie-resultaat.
treRuimte :: TRExpr * ToestandsRuimte [UNI,TOT] -- ToestandsRuimte waar de TRExpr bij hoort
treExpr   :: TRExpr * Expressie [UNI,TOT] -- expressie die in deze ToestandsRuimte moet worden geevalueerd
treIsWaar :: TRExpr * TRExpr [PROP] -- eigenschap dat de Expressie van TRExpr 'waar' is
treOnWaar :: TRExpr * TRExpr [PROP] -- eigenschap dat de Expressie van TRExpr 'onwaar' is

RULE "Een expressie kan niet tegelijkertijd 'waar' en 'onwaar' zijn": treIsWaar /\ treOnWaar |- -V

-- We willen voor elk paar (ToestandsRuimte,Expressie) een evaluatieresultaat kunnen maken
ROLE ExecEngine MAINTAINS "Maak een TRExpr"
RULE "Maak een TRExpr": V[ToestandsRuimte*Activiteit];(actPrecdx \/ actPostcdx) |- treRuimte~;treExpr
VIOLATION (TXT "{EX} InsAtom;TRExpr"
          ,TXT "{EX} InsPair;treRuimte;TRExpr;_NEW;ToestandsRuimte;", SRC I
          ,TXT "{EX} InsPair;treExpr;TRExpr;_NEW;Expressie;", TGT I
          )

--[Interfaces - de Proces Motor]
{- De procesmotor kent twee fasen:
   1. In de eerste fase worden de pre- en postcondities van de activiteiten geevalueerd, 
      en vervolgens van elke Activiteit bepaald of deze al dan niet uitvoerbaar is.
      In deze fase blijft de ToestandsRuimte onveranderd.
   2. In de tweede fase wordt tenminste 1 van de uitvoerbare Activiteiten uitgevoerd.
      Dit leidt doorgaans tot een verandering in de ToestandsRuimte.
      Deze fase eindigt als de ontstane ToestandsRuimte wordt ge-commit.
-}

INTERFACE "Proces Motor": V[SESSION*ToestandsRuimte];(I-trOpvolger;trOpvolger~) cRud BOX <DIV>
    [ "Fase 1": trFaseIsEvalueren-trFaseIsUitvoeren INTERFACE "Evalueer Expressies"
    , "Fase 2": trFaseIsUitvoeren-trFaseIsCommitted INTERFACE "Voer Activiteit(s) uit"
    ]

INTERFACE "Evalueer Expressies": I[ToestandsRuimte]-trOpvolger;trOpvolger~ cRud BOX <HROWS>
    [ "ToestandsRuimte": I cRud BOX <CDIV>
        [ "Volgnummer": trVolgnr cRud
        , ":": TXT ":"
        , "Inhoud": trInhoud cRud
        ]
    , "Te evalueren expressies": treRuimte~ cRud BOX <SHCOLS>
        [ "Waar?": treIsWaar cRUd
        , "Expressie": treExpr cRud
        ]
    , " ": I cRud BOX <PROPBUTTON>
        [ label: TXT "Alle expressies zijn geevalueerd"
        , property: trFaseIsUitvoeren cRUd
        , popovertext: TXT "Als alle expressies zijn geevalueerd, klik dan op deze knop"
        ]
    , "Uitvoerbare acties": treRuimte~;treIsWaar;treExpr;actPrecdx~ /\ treRuimte~;(I-treIsWaar);treExpr;actPostcdx~ cRud BOX <SHCOLS>
        [ "Activiteit": actNaam cRud
        , "Role": actRole cRud
        , "Handeling": actVoorschrift cRud
        ]
    ]

INTERFACE "Voer Activiteit(s) uit": I[ToestandsRuimte]-trOpvolger;trOpvolger~ cRud BOX <HROWS>
    [ "Nieuwe ToestandsRuimte": I cRud BOX <CDIV>
        [ "Volgnummer": trVolgnr cRud
        , ":": TXT ":"
        , "Inhoud": trInhoud CRUd -- geen 'D' maar 'd', omdat Uitspraken onderdeel van eerdere ToestandsRuimtes uitmaken
        ]
    , "Uit te voeren acties": treRuimte~;treIsWaar;treExpr;actPrecdx~ /\ treRuimte~;(I-treIsWaar);treExpr;actPostcdx~ cRud BOX <SHCOLS>
        [ "Activiteit": actNaam cRud
        , "Role": actRole cRud
        , "Handeling": actVoorschrift cRud
        ]
    , " ": I cRud BOX <PROPBUTTON>
        [ label: TXT "Committeren aan nieuwe ToestandsRuimte"
        , property: trFaseIsCommitted cRUd
        , popovertext: TXT "Als alle veranderingen in de ToestandsRuimte zijn aangebracht, klik dan op deze knop"
        ]
    ]

--[Interfaces - Definitie van Activiteiten]
VIEW "Activiteiten": Activiteit(actNaam)

INTERFACE Activiteiten: V[SESSION*ToestandsRuimte];(I-trOpvolger;trOpvolger~) BOX <DIV>
    [ "Evaluatiefase": trFaseIsEvalueren-trFaseIsUitvoeren cRud BOX <DIV>
        [ "horizontal line 1": TXT "<hr>"
        , "Mededeling": TXT "Als de Procesmotor in de evaluatie-fase is, mogen Activiteiten niet aangemaakt of verwijderd worden"
        , "horizontal line 2": TXT "<hr>"
        , "Tabel": V cRud BOX <SHCOLS> -- In deze fase mogen Activiteiten niet aangemaakt of verwijderd worden
            [ "Naam": actNaam cRud
            , "Rol": actRole cRud
            , "Preconditie": actPrecdx cRud
            , "Postconditie": actPostcdx cRud
            , "Handelingsvoorschrift": actVoorschrift cRud
            ]
        ]
    , "Uitvoeringsfase": trFaseIsUitvoeren;V CRuD BOX <SHCOLS> -- Nu mogen Activiteiten wel aangemaakt of verwijderd worden
        [ "Naam": I cRud
        , "Rol": actRole cRud
        , "Preconditie": actPrecdx cRud
        , "Postconditie": actPostcdx cRud
        , "Handelingsvoorschrift": actVoorschrift cRud
        ]
    ]

-- De onderstaande INTERFACE moet nog bijgewerkt worden om te zorgen dat er geen pre- c.q. postcondities
-- worden gewijzigd als de procesmotor in de evaluatiefase zit.
INTERFACE Activiteit: I[Activiteit] cRud BOX <SHCOLS>
    [ "Naam": actNaam cRUd
    , "Rol": actRole cRUd
    , "Preconditie": actPrecdx cRUd
    , "Postconditie": actPostcdx cRUd
    , "Handelingsvoorschrift": actVoorschrift cRUd
    ]

--[Representaties]
REPRESENT Voorschrift TYPE BIGALPHANUMERIC
REPRESENT Uitspraak, ActNaam, Expressie TYPE ALPHANUMERIC
REPRESENT Volgnummer TYPE INTEGER

ENDCONTEXT