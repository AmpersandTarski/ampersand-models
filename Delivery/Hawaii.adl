CONTEXT Hawaii

RELATION required[Subject*Destination]
MEANING "A subject that you must have passed to qualify for the school trip to a destination"

POPULATION required CONTAINS
   [ ("Surfing", "Hawaii")
   ; ("Latin", "Rome")
   ; ("World Religions", "Rome")
   ]

RELATION pass[Subject*Student]
MEANING "The subjects that have been passed by specific students"

POPULATION pass CONTAINS
   [ ("Surfing", "Brown")
   ; ("Surfing", "Conway")
   ; ("Latin", "Brown")
   ; ("World Religions", "Applegate")
   ; ("World Religions", "Brown")
   ]


INTERFACE Overview : '_SESSION'
TABS [ Destinations : V[SESSION*Destination]
       COLS [ "Destination" : I
            , "required subject" : required~
            , "Eligible students" : required\pass cRud
            ]
     , Subjects     : V[SESSION*Subject]
       COLS [ "Subject" : I
            , "required for trip" : required
            , "students that passed" : pass
            ]
     , Students     : V[SESSION*Student]
       COLS [ "Student" : I[Student]
            , "passed" : pass~
            , "Eligible for trip" : pass~/required~ cRud
            ]
     ]

ENDCONTEXT