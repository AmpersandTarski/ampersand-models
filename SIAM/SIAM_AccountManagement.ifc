CONTEXT "AccountAndRoleMgt" IN ENGLISH
-- No support yet for Persona's.
-----------------------------------------------------------

VIEW Accounts: Account(accUserid)

POPULATION Role CONTAINS [ "AccountMgr" ]

accNewPassword :: Account * Password [UNI]
ROLE ExecEngine MAINTAINS "(Re)set the password of an account"
RULE "(Re)set the password of an account": 
   accNewPassword /\ (sessionAccount~ \/ V;'AccountMgr';sessionActiveRoles~);'_SESSION';V |- -V
MEANING "(Re)setting the password for an account can only be done by an AccountMgr or the account user."
VIOLATION (TXT "{EX} InsPair;accPassWord;Account;", SRC I, TXT ";Password;", TGT I
          ,TXT "{EX} DelPair;accNewPassword;Account;", SRC I, TXT ";Password;", TGT I
          )
RULE "(Re)setting the password for an account can only be done by an AccountMgr or the account user":
   accNewPassword |- (sessionAccount~ \/ V;'AccountMgr';sessionActiveRoles~);'_SESSION';V

INTERFACE "Accounts" 
   (accUserid, accNewPassword, accAllowedRoles, accDefaultRoles
   ) FOR "AccountMgr": '_SESSION';V[SESSION*Account]  cRud
   BOX <SCOLS>
   [ "Userid": I cRUd LINKTO INTERFACE "Account"
-- , "Organization" : accOrg cRUd
-- , "Person" : accPerson cRUd
   , "Allowed roles": accAllowedRoles cRUd
   , "Default roles": accDefaultRoles cRUd
   ]
           
INTERFACE "Account" 
   (accUserid, accNewPassword, accPerson, accOrg, accAllowedRoles, accDefaultRoles
   ) FOR "AccountMgr": I[Account] cRUd ROWS
   [ "Userid": accUserid cRUd
   , "(Re)set password": accNewPassword cRUd
   , "Organization": accOrg cRUd
   , "Person": accPerson cRUd
   , "Allowed roles": accAllowedRoles cRUd
   , "Default roles": accDefaultRoles cRUd
   ]

INTERFACE "My Account" (accUserid, accNewPassword, accDefaultRoles): '_SESSION';sessionAccount cRUd ROWS
   [ "Userid": accUserid cRUd
   , "New password": accNewPassword cRUd
   , "Organization": accOrg cRud
   , "Person": accPerson cRud
   , "Allowed roles": accAllowedRoles cRud
   , "Default roles": accDefaultRoles cRUd
   ]

--[Roles]--

INTERFACE "Roles" (accAllowedRoles, accDefaultRoles) FOR "AccountMgr": '_SESSION';V[SESSION*Role] CRUd BOX <SCOLS>
   [ "Role": I cRud
   , "Assigned to": accAllowedRoles~ cRUd
   , "Default for": accDefaultRoles~ cRUd
   ]

ENDCONTEXT