CONTEXT "AutoLogin" IN ENGLISH
-- This file implements the auto-login feature, i.e. it defines the property `autoLoginAccount`.

RELATION autoLoginAccount[Account*Account] [PROP] -- An account that has this property is automatically logged in.

RULE "AutoLoginAccounts must be active": autoLoginAccount |- accIsActive

ROLE ExecEngine MAINTAINS "Auto login" -- When multiple accounts have this property, only one will be selected.
RULE "Auto login": autoLoginAccount /\ accIsActive /\ V;("_SESSION"-(sessionAccount;sessionAccount~));V |- -V
VIOLATION (TXT "{EX} InsPair;sessionAccount;SESSION;", SRC V;"_SESSION"[SESSION], TXT ";Account;", TGT I
          ,TXT "{EX} DelPair;autoLoginAccount;Account;", SRC I, TXT ";Account;", TGT I
          )

ROLE ExecEngine MAINTAINS "Auto logout (when other account requests an auto login)"
RULE "Auto logout (when other account requests an auto login)":
   "_SESSION";sessionAccount /\ V;autoLoginAccount;-I |- -V
VIOLATION (TXT "{EX} DelAtom;SESSION;", SRC I)

ENDCONTEXT