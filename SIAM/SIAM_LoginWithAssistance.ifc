CONTEXT "LoginWithAssistance" IN ENGLISH LATEX
{-This script is intended to be reusable, and for INCLUDEsion in your own scripts.
This interface provides you with LOGIN functionality.
It is particularly suited for developers, since it allows you to select an account
by which you then will be logged in, rather than that you have to type the actual
username and password.
-}
-----------------------------------------------------------

INTERFACE "AssistedLogin" 
   (loginUserid, loginPassword, sessionAccount, logoutRequest
   ,sessionLoginAssist, autoLoginAccount
   ): '_SESSION'[SESSION] cRud BOX <ROWSNL>
   [ "Login": I-(sessionAccount;sessionAccount~) cRud BOX <HCOLS>
      [ "Userid": loginUserid cRUd
      , "Password": loginPassword cRUd
--$The following two lines are meant for demonstration contexts only
      , "Please help": sessionLoginAssist cRUd
      , " ": sessionLoginAssist;(V-(V;I[Account];V));'Cannot help - there are no accounts'[Message] cRud
      ]
--$This is for an easy login, which is just for testing purposes
   , "LoginAssist ": sessionLoginAssist;V[SESSION*Account] cRud BOX <SHCOLS>
      [ "Login?": autoLoginAccount cRUd
      , "Userid": accUserid cRud
      , "Organization": accOrg cRud
      , "Person": accPerson cRud
--    , "Persona" : accPersona cRud
      , "Allowed roles": accAllowedRoles cRUd
      , "Default roles": accDefaultRoles cRUd
      ]
   , "Logout": I /\ sessionAccount;sessionAccount~ cRud BOX <ROWSNL>
      [ "Logout": I cRud BOX <HCOLS>
         [ "Logout?": logoutRequest cRUd
         , "UserID": sessionUserid cRud
         , "Organization": sessionOrg cRud
         , "Person": sessionPerson cRud
--       , "Persona" : sessionPersona cRud
--       , "Allowed roles": sessionAllowedRoles cRUd
--       , "Active roles": sessionActiveRoles cRUd
         ]
      ]
   ]

--[Assisted Login rules]-----------------------------------

autoLoginAccount :: Account * Account [PROP]
ROLE ExecEngine MAINTAINS "Automatically login"
RULE "Automatically login": autoLoginAccount /\ V;('_SESSION'-(sessionAccount;sessionAccount~));V |- -V
VIOLATION (TXT "{EX} InsPair;sessionAccount;SESSION;", SRC V;'_SESSION'[SESSION], TXT ";Account;", TGT I)
ROLE ExecEngine MAINTAINS "Disable automated login"
RULE "Disable automated login": autoLoginAccount /\ V;('_SESSION' /\ sessionAccount;sessionAccount~);V |- -V
VIOLATION (TXT "{EX} DelPair;autoLoginAccount;Account;", SRC I, TXT ";Account;", TGT I)

sessionLoginAssist :: SESSION * SESSION [PROP]
ROLE ExecEngine MAINTAINS "Reset login help"
RULE "Reset login help": sessionLoginAssist /\ sessionAccount;sessionAccount~ |- -V
VIOLATION (TXT "{EX} DelPair;sessionLoginAssist;SESSION;", SRC I, TXT ";SESSION;", TGT I)

-----------------------------------------------------------
ENDCONTEXT