CONTEXT TaskList IN ENGLISH
PURPOSE CONTEXT TaskList
{+-}
-----------------------------------------------------------
#define useTestPopulations 1
-----------------------------------------------------------
PATTERN Tasks
PURPOSE PATTERN Tasks IN ENGLISH
{+In order to keep track of the work to be done, we need an overview of tasks-}
 
CONCEPT Task "a piece of work"

VIEW "Tasks": Task(goal, TXT " (by ", taskOwner, TXT ")")

taskOwner :: Task -> Userid
#if useTestPopulations
POPULATION taskOwner[Task*Userid] CONTAINS
  [ ("Task_001", "Piet")
  ; ("Task_002", "Michiel")
  ; ("Task_003", "Michiel")
  ]
#endif

PURPOSE RELATION complete
{+We need to be able to distinguish tasks that still need work to be done from those
that do not. We say that tasks that do not need any (additional) work are 'complete'.-}
complete :: Task * Boolean [UNI]
PRAGMA "" " does not need any (additional) work."
#if useTestPopulations
POPULATION complete[Task*Boolean] CONTAINS
  [ ("Task_001", "True")
  ; ("Task_003", "True")
  ]
#endif

PURPOSE RELATION goal
{+-}
goal :: Task * Text [UNI]
PRAGMA "" " serves the fulfillment of "
#if useTestPopulations
POPULATION goal CONTAINS
  [ ("Task_001", "Goal of task 1")
  ; ("Task_002", "Be happy, for the rest of my life")
  ; ("Task_003", "Remain alive as long as I can")
  ]
#endif

ENDPATTERN
-----------------------------------------------------------
PROCESS TaskManagement
PURPOSE PROCESS TaskManagement IN ENGLISH
{+-}

RULE "Specify the goal": I[Task] |- goal;V
MEANING "Every task should have its goal specified"
MESSAGE "This task has no goal specified"
ROLE TaskOwner MAINTAINS "Specify the goal"

RULE "All work is done": I[Task] |- complete;'True';V
MEANING "All work is done means that all tasks are complete"
MESSAGE "This task is incomplete"
ROLE TaskOwner MAINTAINS "All work is done"

ENDPROCESS
-----------------------------------------------------------
INTERFACE Overview : I[ONE]
BOX[ "Tasks" : V[ONE*Task]
   ]

INTERFACE Task (taskOwner,goal,complete) FOR TaskOwner: I[Task]
BOX[ "Task"      : I
   , "Task Owner": taskOwner
   , "goal"      : goal
   , "completed" : complete
   ]
-----------------------------------------------------------
PATTERN Booleans

CONCEPT Boolean "a type that can only have values 'True' or 'False'"

RULE "Boolean values": I[Boolean] = 'True' \/ 'False' 
MEANING "Every boolean can only have the value 'True' or 'False'"
MESSAGE "You should enter either 'True' or 'False' (for booleans)."

ENDPATTERN
-----------------------------------------------------------
ENDCONTEXT
