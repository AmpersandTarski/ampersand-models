CONTEXT COVOG IN DUTCH
------------------------------------------------------------
--[Beslissen over een VOG aanvraag]-------------------------
{-Naast de WJG en het BJG is een en ander gebaseerd op de BeleidsregelBeleidsregels 2008 voor het beoordelen van aanvragen ter verkrijging van een verklaring omtrent het gedrag van natuurlijke personen en rechtspersonen [Beleidsregels VOG-NP-RP 2008] 
Het proces loopt grofweg als volgt. Nadat is vastgesteld dat er een vergoeding (leges) zijn betaald voor het behandelen van de VOG aanvraag (en dat de aanvraag volledig is en de identiteit is vastgesteld), moet worden besloten of de aanvraag ontvankelijk is (zie beleidsregels). Mocht dat zo zijn, dan volgt een onderzoek in de justitiele documentatie (zie beleidsregels), op basis waarvan wordt besloten tot het al dan niet afgeven van een VOG.
-}
------------------------------------------------------------
INTERFACE "Overzicht (COVOG)" FOR COVOG : I[ONE]
BOX[ "COVOG medewerker" : V[ONE*SESSION];"_SESSION";
   (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
         ;(I /\ idmSoort;'Rijkspas';idmSoort~)                 -- met een Rijkspas
         ;idmDisplayName                                         -- dan laten we de displayName zien
   \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
         ;(I /\ -(idmSoort;'Rijkspas';idmSoort~))              -- niet met een Rijkspas
         ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
   \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
         ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
   )
   , "In behandeling te nemen aanvragen" : V[ONE*VOGAanvraag];
      (I /\ (  aanvragerHandtekening;aanvragerHandtekening~
            \/ gemVOGAanvraag~;gemAmbtenaarHandtekening;gemAmbtenaarHandtekening~;gemVOGAanvraag
            )
         /\ -(covogZaakNummer;covogZaakNummer~)
      )
   , "Ontvankelijkheid van de aanvraag toetsen c.q. vaststellen": V[ONE*VOGAanvraag];
      (I /\ (  (aanvragerHandtekening;aanvragerHandtekening~)
            \/ (gemVOGAanvraag~;gemAmbtenaarHandtekening;gemAmbtenaarHandtekening~;gemVOGAanvraag)
            )
         /\ (  (zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~)
            \/ (gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag)
            )
         /\ -(isAanvraagOntvankelijk;isAanvraagOntvankelijk~)
      );covogZaakNummer
   , "Te beoordelen aanvragen" : V[ONE*VOGAanvraag];
      (I /\ (isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~) -- preconditie moet gelden
         /\ -(vogAfgifteBesluit;vogAfgifteBesluit~) -- postconditie mag niet gelden
      );covogZaakNummer
   , "Aanvragen waarop een VOG is afgegeven" : V[ONE*VOGAanvraag];
      (I /\ (vogAfgifteBesluit;'Ja';vogAfgifteBesluit~));covogZaakNummer
   , "Aanvragen waarop geen VOG is afgegeven": V[ONE*VOGAanvraag];
      (I /\ (vogAfgifteBesluit;'Nee';vogAfgifteBesluit~));covogZaakNummer
   , "Aanvragen die niet ontvankelijk zijn" : V[ONE*VOGAanvraag];
      (I /\ (
              (aanvragerHandtekening;aanvragerHandtekening~)
             \/
              (gemVOGAanvraag~;gemAmbtenaarHandtekening;gemAmbtenaarHandtekening~;gemVOGAanvraag)
            )
         /\ (
              (zijnDeLegesBetaald;'Nee';zijnDeLegesBetaald~)
             \/
              (isAanvraagOntvankelijk;'Nee';isAanvraagOntvankelijk~)
            )
      );covogZaakNummer
   ]
------------------------------------------------------------
--[Aanvraag in behandeling nemen]--
INTERFACE "Aanvraag in behandeling nemen" (covogZaakNummer) FOR COVOG : I[VOGAanvraag];
      (I /\ (  aanvragerHandtekening;aanvragerHandtekening~
            \/ gemVOGAanvraag~
              ;({-gemAmbtenaarHandtekening-}
                 (  gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~
                 /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~
                 );idmDisplayHandtekening
               )
              ;({-gemAmbtenaarHandtekening-}
                 (  gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~
                 /\ gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~
                 );idmDisplayHandtekening
               )~
              ;gemVOGAanvraag
            )
         /\ -(covogZaakNummer;covogZaakNummer~)
      )
BOX[ "COVOG zaaknummer" : covogZaakNummer
   , "Aanvrager" : ((vogAanvragerUserid;idmUserid~ /\ vogAanvragerPassword;idmPassword~) \/ (vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmDisplayName
   , "Leges zijn betaald door aanvrager" : zijnDeLegesBetaald
   , "Leges zijn betaald via de gemeente" : gemVOGAanvraag~;gemLegesBetaald
   , "Ingelogde COVOG ambtenaar" : V[VOGAanvraag*SESSION];"_SESSION";
     (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
           ;(I /\ idmSoort;'Rijkspas';idmSoort~)                   -- met een Rijkspas
           ;idmDisplayName                                         -- dan laten we de displayName zien
     \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
           ;(I /\ -(idmSoort;'Rijkspas';idmSoort~))                -- niet met een Rijkspas
           ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
     \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
           ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
     ) 
   ]
------------------------------------------------------------
PROCESS "Aanvraag in behandeling nemen"

RULE "Vergoeding voor het behandelen van een VOG aanvraag": I[VOGAanvraag] /\
   (  (aanvragerHandtekening;aanvragerHandtekening~)
   \/ (gemVOGAanvraag~;gemAmbtenaarHandtekening;gemAmbtenaarHandtekening~;gemVOGAanvraag)
   )
 |-  gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag
   \/ zijnDeLegesBetaald;zijnDeLegesBetaald~ -- hier geen 'Ja' tussen zetten. Als er 'Nee' komt, dan verdwijnen deze zaken naar 'uitgevallen zaken'.
MEANING "Voor elke VOGAanvraag zijn leges verschuldigd"
MESSAGE "Artikel 36 tweede lid BJG"
VIOLATION (TXT "Aanvraag ", SRC I, TXT " kan pas in behandeling worden genomen als de verschuldigde leges zijn ontvangen")
ROLE COVOG MAINTAINS "Vergoeding voor het behandelen van een VOG aanvraag"
PURPOSE RULE "Vergoeding voor het behandelen van een VOG aanvraag" REF "Artikel 36 tweede lid BJG"
{+Onze Minister neemt een aanvraag tot afgifte van een verklaring omtrent het gedrag eerst in behandeling nadat de bij ministeriële regeling vastgestelde vergoeding voor de kosten van deze behandeling is ontvangen (artikel 36 tweede lid BJG)+}

RULE "COVOG Zaaknummer toekennen": I[VOGAanvraag]
/\ (  (aanvragerHandtekening;aanvragerHandtekening~)
   \/ (gemVOGAanvraag~;gemAmbtenaarHandtekening;gemAmbtenaarHandtekening~;gemVOGAanvraag)
   )
/\ ( gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag
   \/ zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~
   )
|- covogZaakNummer;covogZaakNummer~
MEANING "Voor elke VOG aanvraag die in behandeling genomen kan worden, moet een zaaknummer zijn toegekend"
MESSAGE "U moet nog een zaaknummer toekennen aan de VOG aanvraag voor:"
VIOLATION (SRC ( (vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~)
               \/(vogAanvragerUserid;idmUserid~ /\ vogAanvragerPassword;idmPassword~)
               );idmDisplayName
          ,TXT " t.b.v. ", SRC vogAanvraagOrganisatieTemplate
          )
ROLE COVOG MAINTAINS "COVOG Zaaknummer toekennen"

ENDPROCESS
------------------------------------------------------------
--[Paragraaf 2. Ontvankelijkheid van de aanvraag]--
{- Samenvatting: Het COVOG toetst of een aanvraag voor een VOG ontvankelijk is. Hierbij wordt getoetst of een onderzoek naar het gedrag van de aanvrager noodzakelijk is om, gelet op het doel van de aanvraag, een risico voor de samenleving te beperken.-}
------------------------------------------------------------
INTERFACE "Ontvankelijkheid van de aanvraag toetsen c.q. vaststellen" 
(covogZaakNummer,
 isVOGWettelijkVerplicht,
 isAanvraagVereistDoorBuitenlandseWet,
 isAanvraagNoodzakelijkOmRisicoVoorSamenlevingTeBeperken,
 isErEenAanwijsbareBelanghebbendeDerde,
 zijnDeTeBeschermenBelangenVanPriveAard,
 isAanvraagOntvankelijk,
 covogOntvankelijkheidMotivatie
) FOR COVOG : I[COVOGZaakNummer];covogZaakNummer~;
      (I /\ (
              (aanvragerHandtekening;aanvragerHandtekening~)
             \/
              (gemVOGAanvraag~;gemAmbtenaarHandtekening;gemAmbtenaarHandtekening~;gemVOGAanvraag)
            )
         /\ ( gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag
              \/ zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~
            )
         /\ -(isAanvraagOntvankelijk;isAanvraagOntvankelijk~)
      )
BOX[ "COVOG zaaknummer" : covogZaakNummer
-- Identiteit is ofwel middels DigiD, ofwel door de gemeente vastgesteld 
-- Toets op ontvankelijkheid:
   , "Is de VOG wettelijk verplicht?" : 
      isVOGWettelijkVerplicht
-- De toets op een werkrelatie wordt automatisch gedaan, de rest doen we 'met de hand'.
   , "Betreft de aanvraag het aangaan dan wel bestendigen van een werkrelatie?" :
      vogAanvraagOrganisatieTemplate;werkrelatie
   , "Ziet de aanvraag op een doel en/of belanghebbende derde in het buitenland, EN bestaat daarvoor een wettelijke grondslag bestaan in dat buitenland?" :
      isAanvraagVereistDoorBuitenlandseWet
   , "Is een verklaring noodzakelijk om, gelet op het doel van de aanvraag, een risico voor de samenleving te beperken?" :
      isAanvraagNoodzakelijkOmRisicoVoorSamenlevingTeBeperken
   , "Is er een aanwijsbare belanghebbende derde?" :
      isErEenAanwijsbareBelanghebbendeDerde
   , "Zijn de te beschermen belangen van privé-aard?" :
      zijnDeTeBeschermenBelangenVanPriveAard
-- Toetsen moeten leiden tot een al dan niet ontvankelijk verklaren van de aanvraag
   , "Is, gezien het voorafgaande, de aanvraag ontvankelijk?" :
      isAanvraagOntvankelijk
   , "Motivatie (indien wordt afgeweken van het beleid)" : covogOntvankelijkheidMotivatie
   , "Ingelogde COVOG ambtenaar" : V[VOGAanvraag*SESSION];"_SESSION";
     (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
           ;(I /\ idmSoort;'Rijkspas';idmSoort~)                   -- met een Rijkspas
           ;idmDisplayName                                         -- dan laten we de displayName zien
     \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
           ;(I /\ -(idmSoort;'Rijkspas';idmSoort~))                -- niet met een Rijkspas
           ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
     \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
           ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
     ) 
   ]
------------------------------------------------------------
PROCESS "Ontvankelijkheid van de aanvraag toetsen" --Paragraaf 2 [Beleidsregels VOG-NP-RP 2008] 

RULE "Ontvankelijkheid vaststellen": covogZaakNummer~;
   (  I[VOGAanvraag]
   /\ zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~
   /\ gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag
   )
 |- covogZaakNummer~;(I /\ isAanvraagOntvankelijk;isAanvraagOntvankelijk~)
MEANING "Van elke aanvraag moet de ontvankelijkheid zijn vastgesteld"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Van VOG aanvraag ", SRC I, TXT " moet de ontvankelijkheid worden vastgesteld")
ROLE COVOG MAINTAINS "Ontvankelijkheid vaststellen"

RULE "Ontvankelijkheid op basis van wettelijke verplichting": 
 I /\ isVOGWettelijkVerplicht;'Ja';isVOGWettelijkVerplicht~
   /\ -(covogOntvankelijkheidMotivatie;covogOntvankelijkheidMotivatie~ )
|- isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~ 
MEANING "De aanvraag voor een VOG is ontvankelijk wanneer de VOG wettelijk verplicht is (Paragraaf 2 Beleidsregels VOG-NP-RP 2008)"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC covogZaakNummer, TXT " is ontvankelijk op basis van een wettelijke verplichting")
PURPOSE RULE "Ontvankelijkheid op basis van wettelijke verplichting" REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+De aanvraag voor een VOG is ontvankelijk wanneer de VOG wettelijk verplicht is+} 
ROLE COVOG MAINTAINS "Ontvankelijkheid op basis van wettelijke verplichting"

RULE "Integriteit van ontvankelijkheid op basis van wettelijke verplichting": 
 I /\ isVOGWettelijkVerplicht;'Ja';isVOGWettelijkVerplicht~
   /\ isAanvraagOntvankelijk;'Nee';isAanvraagOntvankelijk~ 
   /\ -(covogOntvankelijkheidMotivatie;covogOntvankelijkheidMotivatie~)
|- -I
MEANING "De aanvraag voor een VOG is ontvankelijk wanneer de VOG wettelijk verplicht is (Paragraaf 2 Beleidsregels VOG-NP-RP 2008)"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC covogZaakNummer, TXT " is ontvankelijk op basis van een wettelijke verplichting")
PURPOSE RULE "Integriteit van ontvankelijkheid op basis van wettelijke verplichting" REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+De aanvraag voor een VOG is ontvankelijk wanneer de VOG wettelijk verplicht is+} 

RULE "Ontvankelijkheid op basis van werkrelatie": covogZaakNummer~;
(I[VOGAanvraag]
   /\ ( gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag
      \/ zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~
      )
   /\ (vogAanvraagOrganisatieTemplate;werkrelatie)
      ;'Ja';
      (vogAanvraagOrganisatieTemplate;werkrelatie)~
   /\ -(covogOntvankelijkheidMotivatie;covogOntvankelijkheidMotivatie~ )
)
|- covogZaakNummer~;(I /\ isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~)
MEANING "Elke aanvraag voor het aangaan dan wel bestendigen van een werkrelatie is ontvankelijk"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC I, TXT " is ontvankelijk op grond van het aangaan dan wel bestendigen van een werkrelatie")
PURPOSE RULE "Ontvankelijkheid op basis van werkrelatie" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+de aanvraag voor een VOG-NP is in elk geval ontvankelijk wanneer deze wordt aangevraagd voor het aangaan dan wel bestendigen van een werkrelatie. Onder een werkrelatie wordt mede verstaan het verrichten van werkzaamheden voor een vereniging of een vrijwilligersorganisatie. (**Dit laatste is niet meegemodelleerd**)+}
ROLE COVOG MAINTAINS "Ontvankelijkheid op basis van werkrelatie"

RULE "Integriteit van ontvankelijkheid op basis van werkrelatie": 
(I /\ covogZaakNummer;covogZaakNummer~ 
   /\ ( gemVOGAanvraag~;gemLegesBetaald;'Ja';gemLegesBetaald~;gemVOGAanvraag
      \/ zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~
      )
   /\ (vogAanvraagOrganisatieTemplate;werkrelatie)
      ;'Ja';
      (vogAanvraagOrganisatieTemplate;werkrelatie)~
   /\ isAanvraagOntvankelijk;'Nee';isAanvraagOntvankelijk~
   /\ -(covogOntvankelijkheidMotivatie;covogOntvankelijkheidMotivatie~)
) 
|- -I
MEANING "Elke aanvraag voor het aangaan dan wel bestendigen van een werkrelatie is ontvankelijk"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC covogZaakNummer, TXT " is ontvankelijk op grond van het aangaan dan wel bestendigen van een werkrelatie")
PURPOSE RULE "Integriteit van ontvankelijkheid op basis van werkrelatie" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+de aanvraag voor een VOG-NP is in elk geval ontvankelijk wanneer deze wordt aangevraagd voor het aangaan dan wel bestendigen van een werkrelatie. Onder een werkrelatie wordt mede verstaan het verrichten van werkzaamheden voor een vereniging of een vrijwilligersorganisatie. (**Dit laatste is niet meegemodelleerd**)+}

RULE "Ontvankelijkheid op basis van een buitenlandse wet": 
covogZaakNummer~;
(I /\ isAanvraagVereistDoorBuitenlandseWet;'Ja';isAanvraagVereistDoorBuitenlandseWet~
   /\ -(covogOntvankelijkheidMotivatie;covogOntvankelijkheidMotivatie~ )
)
|- covogZaakNummer~;(I /\ isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~)
MEANING "Als een aanvraag ziet op een doel en/of belanghebbende derde in het buitenland, moet daarvoor een wettelijke grondslag bestaan in dat buitenland (Paragraaf 2 Beleidsregels VOG-NP-RP 2008)"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC I, TXT " is ontvankelijk omdat de aanvraag ziet op een doel en/of belanghebbende derde in het buitenland, en daarvoor in dat buitenland een wettelijke grondslag bestaat")
PURPOSE RULE "Ontvankelijkheid op basis van een buitenlandse wet" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+Indien een aanvraag ziet op een doel en/of belanghebbende derde in het buitenland, wordt een aanvraag voor een VOG-NP of VOG-RP alleen dan in behandeling genomen indien de verklaring in een buitenlandse wet als vereiste is opgenomen, ten behoeve van een in die wet duidelijk omschreven doel. De aanvrager dient dit wettelijke vereiste aan te tonen en indien mogelijk het in het betreffende land gehanteerde screeningsprofiel bij te voegen. Indien er geen buitenlands screeningsprofiel bestaat wordt aangesloten bij een vergelijkbaar Nederlands screeningsprofiel.+}
ROLE COVOG MAINTAINS "Ontvankelijkheid op basis van een buitenlandse wet"

RULE "Integriteit van ontvankelijkheid op basis van een buitenlandse wet": 
(I /\ covogZaakNummer;covogZaakNummer~
   /\ isAanvraagVereistDoorBuitenlandseWet;'Ja';isAanvraagVereistDoorBuitenlandseWet~
   /\ isAanvraagOntvankelijk;'Nee';isAanvraagOntvankelijk~
   /\ -(covogOntvankelijkheidMotivatie;covogOntvankelijkheidMotivatie~)
)
|- -I
MEANING "Als een aanvraag ziet op een doel en/of belanghebbende derde in het buitenland, moet daarvoor een wettelijke grondslag bestaan in dat buitenland (Paragraaf 2 Beleidsregels VOG-NP-RP 2008)"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC covogZaakNummer, TXT " is ontvankelijk omdat de aanvraag ziet op een doel en/of belanghebbende derde in het buitenland, en daarvoor in dat buitenland een wettelijke grondslag bestaat")
PURPOSE RULE "Integriteit van ontvankelijkheid op basis van een buitenlandse wet" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+Indien een aanvraag ziet op een doel en/of belanghebbende derde in het buitenland, wordt een aanvraag voor een VOG-NP of VOG-RP alleen dan in behandeling genomen indien de verklaring in een buitenlandse wet als vereiste is opgenomen, ten behoeve van een in die wet duidelijk omschreven doel. De aanvrager dient dit wettelijke vereiste aan te tonen en indien mogelijk het in het betreffende land gehanteerde screeningsprofiel bij te voegen. Indien er geen buitenlands screeningsprofiel bestaat wordt aangesloten bij een vergelijkbaar Nederlands screeningsprofiel.+}

RULE "Niet-ontvankelijkheid door gebrek aan noodzaak":
covogZaakNummer~;
(I /\ isAanvraagNoodzakelijkOmRisicoVoorSamenlevingTeBeperken
      ;'Nee';isAanvraagNoodzakelijkOmRisicoVoorSamenlevingTeBeperken~
   /\ -(covogOntvankelijkheidMotivatie;covogOntvankelijkheidMotivatie~ )
)
|- covogZaakNummer~;(I /\ isAanvraagOntvankelijk;'Nee';isAanvraagOntvankelijk~ )
MEANING "Als een onderzoek naar het gedrag van de aanvrager kennelijk niet noodzakelijk is om, gelet op het doel van de aanvraag, een risico voor de samenleving te beperken, is de aanvraag niet ontvankelijk (Paragraaf 2 Beleidsregels VOG-NP-RP 2008)"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC I, TXT " is NIET ontvankelijk omdat het kennelijk niet noodzakelijk is een risico voor de samenleving te beperken")
PURPOSE RULE "Niet-ontvankelijkheid door gebrek aan noodzaak" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+Een aanvraag voor een VOG-NP of VOG-RP is niet ontvankelijk wanneer een onderzoek naar het gedrag van de aanvrager kennelijk niet noodzakelijk is om, gelet op het doel van de aanvraag, een risico voor de samenleving te beperken.+}
ROLE COVOG MAINTAINS "Niet-ontvankelijkheid door gebrek aan noodzaak"

RULE "Integriteit van niet-ontvankelijkheid door gebrek aan noodzaak":
(I /\ covogZaakNummer;covogZaakNummer~
   /\ isAanvraagNoodzakelijkOmRisicoVoorSamenlevingTeBeperken;'Nee';isAanvraagNoodzakelijkOmRisicoVoorSamenlevingTeBeperken~
   /\ isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~
   /\ -(covogOntvankelijkheidMotivatie;covogOntvankelijkheidMotivatie~)
)
|- -I
MEANING "Als een onderzoek naar het gedrag van de aanvrager kennelijk niet noodzakelijk is om, gelet op het doel van de aanvraag, een risico voor de samenleving te beperken, is de aanvraag niet ontvankelijk (Paragraaf 2 Beleidsregels VOG-NP-RP 2008)"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC covogZaakNummer, TXT " is NIET ontvankelijk omdat het kennelijk niet noodzakelijk is een risico voor de samenleving te beperken")
PURPOSE RULE "Integriteit van niet-ontvankelijkheid door gebrek aan noodzaak" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+Een aanvraag voor een VOG-NP of VOG-RP is niet ontvankelijk wanneer een onderzoek naar het gedrag van de aanvrager kennelijk niet noodzakelijk is om, gelet op het doel van de aanvraag, een risico voor de samenleving te beperken.+}

RULE "Niet-ontvankelijkheid door gebrek aan belanghebbende derde":
covogZaakNummer~;
(I /\ isErEenAanwijsbareBelanghebbendeDerde;'Nee';isErEenAanwijsbareBelanghebbendeDerde~
   /\ -(covogOntvankelijkheidMotivatie;covogOntvankelijkheidMotivatie~ )
)
|-covogZaakNummer~;(I /\  isAanvraagOntvankelijk;'Nee';isAanvraagOntvankelijk~)
MEANING "Als er geen aanwijsbare belanghebbende derde is, dan is de aanvraag niet ontvankelijk (Paragraaf 2 Beleidsregels VOG-NP-RP 2008)"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC I, TXT " is NIET ontvankelijk omdat er geen aanwijsbare belanghebbende derde is")
PURPOSE RULE "Niet-ontvankelijkheid door gebrek aan belanghebbende derde" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+Een aanvraag voor een VOG-NP of VOG-RP is niet ontvankelijk wanneer er geen aanwijsbare belanghebbende derde is+}
ROLE COVOG MAINTAINS "Niet-ontvankelijkheid door gebrek aan belanghebbende derde"

RULE "Integriteit niet-ontvankelijkheid door gebrek aan belanghebbende derde":
(I /\ covogZaakNummer;covogZaakNummer~
   /\ isErEenAanwijsbareBelanghebbendeDerde;'Nee';isErEenAanwijsbareBelanghebbendeDerde~
   /\ isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~
   /\ -(covogOntvankelijkheidMotivatie;covogOntvankelijkheidMotivatie~)
)
|- -I
MEANING "Als er geen aanwijsbare belanghebbende derde is, dan is de aanvraag niet ontvankelijk (Paragraaf 2 Beleidsregels VOG-NP-RP 2008)"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC covogZaakNummer, TXT " is NIET ontvankelijk omdat er geen aanwijsbare belanghebbende derde is")
PURPOSE RULE "Integriteit niet-ontvankelijkheid door gebrek aan belanghebbende derde" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+Een aanvraag voor een VOG-NP of VOG-RP is niet ontvankelijk wanneer er geen aanwijsbare belanghebbende derde is+}

RULE "Niet-ontvankelijkheid omdat belangen van privé-aard zijn":
covogZaakNummer~;
(I /\ zijnDeTeBeschermenBelangenVanPriveAard;'Ja';zijnDeTeBeschermenBelangenVanPriveAard~
   /\ -(covogOntvankelijkheidMotivatie;covogOntvankelijkheidMotivatie~ )
)
|- covogZaakNummer~;(I /\ isAanvraagOntvankelijk;'Nee';isAanvraagOntvankelijk~)
MEANING "Als de te beschermen belangen van privé-aard zijn, dan is de aanvraag niet ontvankelijk (Paragraaf 2 Beleidsregels VOG-NP-RP 2008)"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC I, TXT " is NIET ontvankelijk omdat de te beschermen belangen van privé-aard zijn")
PURPOSE RULE "Niet-ontvankelijkheid omdat belangen van privé-aard zijn" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+Een aanvraag voor een VOG-NP of VOG-RP is niet ontvankelijk wanneer de te beschermen belangen van privé-aard zijn+}
ROLE COVOG MAINTAINS "Niet-ontvankelijkheid omdat belangen van privé-aard zijn"

RULE "Integriteit niet-ontvankelijkheid omdat belangen van privé-aard zijn":
(I /\ covogZaakNummer;covogZaakNummer~
   /\ zijnDeTeBeschermenBelangenVanPriveAard;'Ja';zijnDeTeBeschermenBelangenVanPriveAard~
   /\ isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~
   /\ -(covogOntvankelijkheidMotivatie;covogOntvankelijkheidMotivatie~)
)
|- -I
MEANING "Als de te beschermen belangen van privé-aard zijn, dan is de aanvraag niet ontvankelijk (Paragraaf 2 Beleidsregels VOG-NP-RP 2008)"
MESSAGE "Paragraaf 2 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC covogZaakNummer, TXT " is NIET ontvankelijk omdat de te beschermen belangen van privé-aard zijn")
PURPOSE RULE "Integriteit niet-ontvankelijkheid omdat belangen van privé-aard zijn" IN DUTCH REF "Paragraaf 2 Beleidsregels VOG-NP-RP 2008"
{+Een aanvraag voor een VOG-NP of VOG-RP is niet ontvankelijk wanneer de te beschermen belangen van privé-aard zijn+}

ENDPROCESS
------------------------------------------------------------
--[Paragraaf 3. Beoordeling van de aanvraag]--
{-Samenvatting: Bij de beoordeling van de aanvraag wordt in beginsel gekeken naar de justitiële gegevens die zijn opgenomen in de justitiële documentatie in de voor het doel van de aanvraag relevante termijn.
Aan de aanvrager die in het geheel niet voorkomt in de justitiële documentatie wordt zonder meer een VOG afgegeven.
Wanneer de aanvrager wel voorkomt in de justitiële documentatie wordt de vraag of een VOG kan worden afgegeven beoordeeld aan de hand van een objectief criterium en een subjectief criterium.-}
------------------------------------------------------------
INTERFACE "Te beoordelen aanvraag"
(vogAfgifteBesluit
) FOR COVOG : I[COVOGZaakNummer];covogZaakNummer~;
      (I /\ (isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~) -- preconditie moet gelden
         /\ -(vogAfgifteBesluit;vogAfgifteBesluit~) -- postconditie mag niet gelden
      )
BOX[ "VOG Zaaknummer" : covogZaakNummer
   , "Strafblad" : 
     ({-vogAaanvragerGevalideerdeIdentiteit-} 
         (  (  vogAanvragerUserid;idmUserid~ 
            /\ vogAanvragerPassword;idmPassword~
            );(I /\  idmSoort;'DigiD';idmSoort~)
         \/  (I /\ vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~)
            ;(  vogAanvragerIDMSoort;idmSoort~
             /\ vogAanvragerIDMNummer;idmNummer~
             )
         )
       )                       -- I[Identificatiemiddel]
      ;idmHouder;gbaBSN~       -- I[NatuurlijkPersoon]
      ;(   strafrechtsketennummer~ -- Strafblad
{- Onderstaand spul geeft teveel neveneffecten voor het beoogde effect, waaronder het moeten maken van een NatuurlijkPersoon 'Dummy', die dan in allerlei overzichten terugkomt.
       \/ -- maar als die niet bestaat drukken we een tekst af
         (I /\ -(strafrechtsketennummer~;strafrechtsketennummer))
         ;V;dezePersoonHeeftGeenStrafblad
-}
        )
   , "Wordt de VOG afgegeven?" : vogAfgifteBesluit
   , "Ingelogde COVOG ambtenaar" : V[VOGAanvraag*SESSION];"_SESSION";
     (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
           ;(I /\ idmSoort;'Rijkspas';idmSoort~)                   -- met een Rijkspas
           ;idmDisplayName                                         -- dan laten we de displayName zien
     \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
           ;(I /\ -(idmSoort;'Rijkspas';idmSoort~))                -- niet met een Rijkspas
           ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
     \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
           ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
     ) 
   ]
------------------------------------------------------------
PROCESS "Te beoordelen aanvragen"

RULE "Aanvraag beoordelen integriteit": I /\ vogAfgifteBesluit;vogAfgifteBesluit~ |- isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~
MEANING "Een aanvraag mag alleen worden beoordeeld als deze ontvankelijk is."
MESSAGE "Beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Zaak ", SRC I, TXT " wordt eerst beoordeeld nadat is vastgesteld dat deze ontvankelijk is")

RULE "VOG toekennen als de aanvrager geen strafblad heeft": 
I /\ isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~  -- Als de aanvraag ontvankelijk is..
  /\ ( ({-vogAaanvragerGevalideerdeIdentiteit-}           -- en de aanvrager heeft geen strafblad
         (  (  vogAanvragerUserid;idmUserid~ 
            /\ vogAanvragerPassword;idmPassword~
            );(I /\  idmSoort;'DigiD';idmSoort~)
         \/  (I /\ vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~)
            ;(  vogAanvragerIDMSoort;idmSoort~
             /\ vogAanvragerIDMNummer;idmNummer~
             )
         )
       )
      ;idmHouder;gbaBSN~
      ;-(strafrechtsketennummer~;strafrechtsketennummer) -- strafrechtsketennummer :: Strafrechtsketennummer -> NatuurlijkPersoon
      ;gbaBSN;idmHouder~
      ;({-vogAaanvragerGevalideerdeIdentiteit-}
         (  (  vogAanvragerUserid;idmUserid~ 
            /\ vogAanvragerPassword;idmPassword~
            );(I /\  idmSoort;'DigiD';idmSoort~)
         \/  (I /\ vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~)
            ;(  vogAanvragerIDMSoort;idmSoort~
             /\ vogAanvragerIDMNummer;idmNummer~
             )
         )
       )~
     )
|- vogAfgifteBesluit;'Ja'; vogAfgifteBesluit~              -- dan moet de VOG worden afgegeven
MEANING "Aan de aanvrager die in het geheel niet voorkomt in de justitiële documentatie wordt zonder meer een VOG afgegeven (Paragraaf 3 beleidsregels VOG-NP-RP 2008)"
MESSAGE "Paragraaf 3 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Voor ", SRC covogZaakNummer, TXT " kan zonder meer een VOG worden afgegeven omdat de aanvrager niet voorkomt in de justitiële documentatie")
ROLE COVOG MAINTAINS "VOG toekennen als de aanvrager geen strafblad heeft"

RULE "Integriteit van VOG toekenningen als de aanvrager geen strafblad heeft": 
I /\ isAanvraagOntvankelijk;'Ja';isAanvraagOntvankelijk~  -- Als de aanvraag ontvankelijk is..
  /\ ( ({-vogAaanvragerGevalideerdeIdentiteit-}           -- en de aanvrager heeft geen strafblad
         (  (  vogAanvragerUserid;idmUserid~ 
            /\ vogAanvragerPassword;idmPassword~
            );(I /\  idmSoort;'DigiD';idmSoort~)
         \/  (I /\ vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~)
            ;(  vogAanvragerIDMSoort;idmSoort~
             /\ vogAanvragerIDMNummer;idmNummer~
             )
         )
       )
      ;idmHouder;gbaBSN~
      ;-(strafrechtsketennummer~;strafrechtsketennummer) -- strafrechtsketennummer :: Strafrechtsketennummer -> NatuurlijkPersoon
      ;gbaBSN;idmHouder~
      ;({-vogAaanvragerGevalideerdeIdentiteit-}
         (  (  vogAanvragerUserid;idmUserid~ 
            /\ vogAanvragerPassword;idmPassword~
            );(I /\  idmSoort;'DigiD';idmSoort~)
         \/  (I /\ vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~)
            ;(  vogAanvragerIDMSoort;idmSoort~
             /\ vogAanvragerIDMNummer;idmNummer~
             )
         )
       )~
     )
  /\ vogAfgifteBesluit;'Nee'; vogAfgifteBesluit~              -- en de VOG wordt niet afgegeven
|- -I -- Dan zijn we niet integer bezig
MEANING "Voor elke ontvankelijke aanvraag waarvan de aanvrager die in het geheel niet voorkomt in de justitiële documentatie wordt zonder meer een VOG afgegeven (Paragraaf 3 beleidsregels VOG-NP-RP 2008)"
MESSAGE "Paragraaf 3 beleidsregels VOG-NP-RP 2008:"
VIOLATION (TXT "Voor VOG aanvraag ", SRC covogZaakNummer, TXT " moet een VOG worden afgegeven omdat de aanvrager niet voorkomt in de justitiële documentatie (en de aanvraag ontvankelijk is)")
--ROLE COVOG MAINTAINS "VOG toekennen als de aanvrager geen strafblad heeft"

ENDPROCESS
------------------------------------------------------------
INTERFACE "VOG aanvraag (Aanvrager deel)" FOR COVOG : I[COVOGZaakNummer];covogZaakNummer~
BOX[ "VOG Zaaknummer" : covogZaakNummer
   , "Contactgegevens": I
   BOX[ "Telefoonnummer" : aanvragerTelefoonnummer
      , "E-mailadres" : aanvragerEmailAddress
      ]
   , "1 Gegevens Aanvrager": vogBSNAanvrager
   BOX[ "Geslacht (M/V)"  : gbaGeslacht
      , "(Geslachts)Naam" : gbaGeslachtsnaam
   -- , "Tussenvoegsel(s)": gbaTussenvoegsels
      , "Voornamen" : gbaVoornamen -- art 32 eerste lid WJG
      , "Geboortedatum" : gbaGeboortedatum -- art 32 eerste lid WJG
      , "Geboortegemeente" : gbaGeboorteplaats
      , "Geboorteland" : gbaGeboorteland
      , "Woonadres (postadres)" : I -- adresHaakje~
      BOX[ "Straat" : gbaStraatnaam
         , "Huisnummer" : gbaHuisnummer
         , "Postcode" : gbaPostcode
         , "Plaats" : gbaGemeentedeel
         , "Land" : V;'Nederland'[Land]
         ]
      , " " : I
      BOX[ "Nationaliteit(en)" : gbaNationaliteit
        , "Burgerservicenummer" : gbaBSN
      ]  ]
   , "2 - Ondertekening door aanvrager" : I -- ondertekeningHaakje~
   BOX[ "De aanvrager verklaart dit formulier volledig en naar waarheid te hebben ingevuld" : I
      BOX[ "Plaats" : aanvragerOndertekeningPlaats
         , "Datum" : aanvragerOndertekeningDatum
         , "Handtekening" : aanvragerHandtekening
      ]  ]
   , "Ingelogde COVOG ambtenaar" : V[VOGAanvraag*SESSION];"_SESSION";
     (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
           ;(I /\ idmSoort;'Rijkspas';idmSoort~)                   -- met een Rijkspas
           ;idmDisplayName                                         -- dan laten we de displayName zien
     \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
           ;(I /\ -(idmSoort;'Rijkspas';idmSoort~))                -- niet met een Rijkspas
           ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
     \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
           ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
     ) 
   ]
-----------------------------------------------------------
INTERFACE "VOG aanvraag (Organisatie deel)" FOR COVOG :
  I[COVOGZaakNummer];covogZaakNummer~;vogAanvraagOrganisatieTemplate
BOX[ "2. VOG aanvraag (Organisatie template)" : I 
   , "2.1 - Gegevens organisatie-instelling" : {-organisatieOrganisatie-}
          ( ( (organisatieUserid;idmUserid~ /\ organisatiePassword;idmPassword~)
              ;(I /\ idmSoort;'EHvB';idmSoort~) 
            /\ organisatieHandtekening;idmDisplayHandtekening~
            );idmOrganisatie
          )
   BOX[ "Naam organisatie/instelling" : hrOrganisatieNaam
      , "Vestigingsadres" : hrVestigingsAdres
      ]
   , "Contactpersoon" : I
   BOX[ "Naam vertegenwoordiger" : {-organisatieVertegenwoordiger-}
          ( (organisatieUserid;idmUserid~ /\ organisatiePassword;idmPassword~)
            ;(I /\ idmSoort;'EHvB';idmSoort~);idmDisplayName
          )
      , "Telefoonnummer" : orgTelefoonnummer
      ]
   , "2.2 - Doel van de aanvraag" : I
   BOX[ "Werkrelatie (Ja/Nee)" : werkrelatie
      , "Functie (waarop de aanvraag betrekking heeft)" : functie
      , "Taakomschrijving (bij voorkeur functiebeschrijving bijvoegen)" : taakomschrijving
      , "Overig (Ja/Nee)" : overig
      , "Omschrijving (bijv. Chaufferskaart, Visum en emigratie, Exploitatievergunning)" : overigOmschrijving
      ]
   , "2.3 - Specifiek screeningsprofiel" : I
   BOX[ "Heeft de aanvraag betrekking op één van de hieronder genoemde functies of doelen?" : specifiekScreeningsProfielRelevant
      , "Ja, namelijk op: (u kunt slechts één vakje aankruisen!)" : specifiekScreeningsProfiel
      ]
   , "2.4 - Algemeen screeningsprofiel" : I
   BOX[ " ": algemeenScreeningsProfiel
      ]
   , "2.5 - Bijzondere omstandigheden m.b.t. de aanvraag" : I
   BOX[ "Zijn er bijzondere omstandigheden (Ja/Nee)?" : orgErZijnBijzondereOmstandigheden
      , "Ja, toelichting" : orgErZijnBijzondereOmstandighedenToelichting
      ]
   , "2.6 - Ondertekening door organisatie/instelling" : I
   BOX[ "Plaats" : orgOndertekeningPlaats
      , "Datum" : orgOndertekeningDatum
      , "Handtekening" : organisatieHandtekening
      ]
   , "Ingelogde COVOG ambtenaar" : V[VOGAanvraagOrganisatieTemplate*SESSION];"_SESSION";
     (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
           ;(I /\ idmSoort;'Rijkspas';idmSoort~)                   -- met een Rijkspas
           ;idmDisplayName                                         -- dan laten we de displayName zien
     \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
           ;(I /\ -(idmSoort;'Rijkspas';idmSoort~))                -- niet met een Rijkspas
           ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
     \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
           ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
     ) 
   ]
-----------------------------------------------------------
INTERFACE "VOG aanvraag (Gemeentelijk deel)" FOR COVOG :
  I[COVOGZaakNummer];covogZaakNummer~;gemVOGAanvraag~
BOX[ "COVOG Zaaknummer" : gemVOGAanvraag;covogZaakNummer
   , "Gemeentelijk Aanvraagnummer" : gemeenteAanvraagnummer
   , "VOG aanvraag" : gemVOGAanvraag
   BOX[ "Doel van de aanvraag" : vogAanvraagOrganisatieTemplate
      , "Soort identiteitsbewijs (paspoort, rijbewijs)" : vogAanvragerIDMSoort
      , "Serienummer" : vogAanvragerIDMNummer
      , "Komen deze gegevens overeen met de persoon die u voor zich heeft?" : vogAanvragerIDVastgesteld
      , "DigiD" : vogAanvragerUserid
      , "Wachtwoord" : vogAanvragerPassword
      , "GBA gegevens van aanvrager" : vogBSNAanvrager
      BOX[ "BSN" : I
         , "Geslachtsnaam" : gbaGeslachtsnaam
         , "Voornamen" : gbaVoornamen -- art 32 eerste lid WJG
         , "Geboortedatum" : gbaGeboortedatum -- art 32 eerste lid WJG
         , "Gemeente van inschrijving": gbaGemeenteVanInschrijving
         ]
      ]
   , "Bijzonderheden" : I
   BOX[ "Bijzonderheden geconstateerd (Ja/Nee)?" : bijzonderhedenGeconstateerd
      , "Ja, toelichting" : bijzonderhedenGeconstateerdToelichting
      ]
   , "Persisteren" : I
   BOX[ "Persisteren in de aanvraag (Ja/Nee)?" : persisterenInDeAanvraag
      , "Ja, toelichting" : persisterenInDeAanvraagToelichting
      ]
   , "COVOG advies" : I
   BOX[ "COVOG om advies gevraagd omtrent ontvankelijkheid (Ja/Nee)?" : covogOmAdviesGevraagd
      , "Ja, toelichting" : covogOmAdviesGevraagdToelichting
      ]
   , "Aanvraagformulier is gecontroleerd op volledigheid en de identiteit van de aanvrager is vastgesteld" : I
   BOX[ "Datum" : gemOndertekeningDatum
      , "Plaats" : (gemAmbtenaarUserid;idmUserid~ /\ gemAmbtenaarPassword;idmPassword~);(I /\ idmSoort;'GemeenteID';idmSoort~)
                   ;idmGemeentePlaats
      , "Handtekening" : 
                  ({-gemAmbtenaarHandtekening-}
                    (gemAmbtenaarDigitaleHandtekening;idmDigitaleHandtekening~ /\
                     gemAmbtenaarPasswordHandtekening;idmPasswordHandtekening~
                    );idmDisplayHandtekening
                  )
      ]
   , "Ingelogde COVOG ambtenaar" : V[GemeentelijkeVOGZaak*SESSION];"_SESSION";
     (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
           ;(I /\ idmSoort;'Rijkspas';idmSoort~)                   -- met een Rijkspas
           ;idmDisplayName                                         -- dan laten we de displayName zien
     \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
           ;(I /\ -(idmSoort;'Rijkspas';idmSoort~))                -- niet met een Rijkspas
           ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
     \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
           ;V;'Rijkspas'[Berichtje];loginMet -- dan zeggen we dat je met een Rijkspas moet inloggen
     ) 
   ]
-----------------------------------------------------------
ENDCONTEXT