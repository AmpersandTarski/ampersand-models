CONTEXT VOGAanvraagGemeentebalie
-----------------------------------------------------------
INTERFACE "Overzicht (Gemeente)" FOR Gemeente : I[ONE]
BOX[ "Gemeente ambtenaar" : V[ONE*SESSION];'_SESSION';
   (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
         ;(I /\ idmSoort;'GemeenteID';idmSoort~)                 -- met een GemeenteID
         ;idmDisplayName                                         -- dan laten we de displayName zien
   \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
         ;(I /\ -(idmSoort;'GemeenteID';idmSoort~))              -- niet met een GemeenteID
         ;V;'GemeenteID'[Berichtje];loginMet -- dan zeggen we dat je met een GemeenteID moet inloggen
   \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
         ;V;'GemeenteID'[Berichtje];loginMet -- dan zeggen we dat je met een GemeenteID moet inloggen
   )
   , "In te vullen aanvragen" :V[ONE*GemeentelijkeVOGZaak];
   (I /\ -sigGemeenteHeeftVolledigheidEnIdentiteitVastgesteld
   )
   BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
      , "Behandelend ambtenaar" : (gemAmbtenaarUserid;idmUserid~ /\ gemAmbtenaarPassword;idmPassword~);idmDisplayName
      ]
   , "Uitstaande aanvragen" : V[ONE*GemeentelijkeVOGZaak];
   ( sigGemeenteHeeftVolledigheidEnIdentiteitVastgesteld
     /\ gemVOGAanvraag;-(vogAfgifteBesluit;vogAfgifteBesluit~);gemVOGAanvraag~
   )
   BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
      , "Voor" : gemVOGAanvraag;vogAanvraagOrganisatieTemplate
      , "Betrokkene" : gemVOGAanvraag;vogBSNAanvrager
      , "Behandelend ambtenaar" : (gemAmbtenaarUserid;idmUserid~ /\ gemAmbtenaarPassword;idmPassword~);idmDisplayName
      ]
   , "Ontvangen VOGs"       : V[ONE*GemeentelijkeVOGZaak];
   (I /\ gemVOGAanvraag;vogAfgifteBesluit;vogAfgifteBesluit~;gemVOGAanvraag~)
   BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
      , "Voor" : gemVOGAanvraag;vogAanvraagOrganisatieTemplate
      , "Betrokkene" : gemVOGAanvraag;vogBSNAanvrager;gbaDisplayNaam
--    , "Behandelend door" : (gemAmbtenaarUserid;idmUserid~ /\ gemAmbtenaarPassword;idmPassword~);idmDisplayName
      , "Is de VOG afgegeven?" : gemVOGAanvraag;vogAfgifteBesluit
      ]
   ]
-----------------------------------------------------------
PROCESS "VOG aanvraag (Gemeente)"

RULE "Gemeentegegevens invullen": -- dus exclusief het gedeelte voor controle op volledigheid en ondertekening
(I[GemeentelijkeVOGZaak]
  /\ gemeenteAanvraagnummer;gemeenteAanvraagnummer~
  /\ gemLegesBetaald;'Ja';gemLegesBetaald~
  /\ gemAmbtenaarUserid;gemAmbtenaarUserid~
  /\ gemAmbtenaarPassword;gemAmbtenaarPassword~
)
|- sigGemeentegegevensZijnIngevuld
MEANING "Alle gegevens op de aanvraag zijn volledig ingevuld"
MESSAGE "Gemeentelijke aanvraag invullen en ondertekenen"
ROLE Gemeente MAINTAINS "Gemeentegegevens invullen"

RULE "Gemeentegegevens ondertekenen":
sigGemeentegegevensZijnIngevuld
  |-    gemOndertekeningDatum;gemOndertekeningDatum~ 
     /\ gemAmbtenaarHandtekening;gemAmbtenaarHandtekening~
MEANING "Elke aanvraag die volledig is ingevuld en waarvan de identiteit van de aanvrager is vastgesteld, moet worden ondertekend (artikel 30 WJG)"
MESSAGE "Te ondertekenen aanvragen"
ROLE Gemeente MAINTAINS "Gemeentegegevens ondertekenen"
PURPOSE RULE "Gemeentegegevens ondertekenen" REF "artikel 30" {+De aanvraag om afgifte van de verklaring omtrent het gedrag van een natuurlijk persoon wordt ingediend bij de burgemeester van de gemeente, waar de aanvrager op het tijdstip van de aanvraag als ingezetene is ingeschreven in de basisadministratie persoonsgegevens. De burgemeester onderzoekt de volledigheid van de bij de aanvraag verstrekte gegevens en verschaft zich de nodige zekerheid over de identiteit van de aanvrager. De burgemeester zendt de aanvraag terstond door aan Onze Minister-}

ENDPROCESS
-----------------------------------------------------------
INTERFACE "Nieuwe VOG aanvraag"
( gemeenteAanvraagnummer, gemLegesBetaald
, gemAmbtenaarUserid, gemAmbtenaarPassword
) FOR Gemeente : I[GemeentelijkeVOGZaak] 
BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
   , "Zijn de leges betaald?" : gemLegesBetaald
   , "Behandelend ambtenaar" : I
   BOX[ "GemeenteID" : gemAmbtenaarUserid
      , "Wachtwoord" : gemAmbtenaarPassword
      ]
   , "Ingelogde Gemeente ambtenaar" : V[GemeentelijkeVOGZaak*SESSION];'_SESSION';
   (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
         ;(I /\ idmSoort;'GemeenteID';idmSoort~)                 -- met een GemeenteID
         ;idmDisplayName                                         -- dan laten we de displayName zien
   \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
         ;(I /\ -(idmSoort;'GemeenteID';idmSoort~))              -- niet met een GemeenteID
         ;V;'GemeenteID'[Berichtje];loginMet -- dan zeggen we dat je met een GemeenteID moet inloggen
   \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
         ;V;'GemeenteID'[Berichtje];loginMet -- dan zeggen we dat je met een GemeenteID moet inloggen
   )
   ]
-----------------------------------------------------------
INTERFACE "VOG aanvraag invullen en ondertekenen"
( gemAmbtenaarUserid, gemAmbtenaarPassword
, vogAanvraagOrganisatieTemplate
, gemVOGAanvraag
  , vogAanvragerIDMSoort, vogAanvragerIDMNummer, vogAanvragerIDVastgesteld
  , vogAanvragerUserid, vogAanvragerPassword
, bijzonderhedenGeconstateerd, bijzonderhedenGeconstateerdToelichting
, persisterenInDeAanvraag, persisterenInDeAanvraagToelichting
, covogOmAdviesGevraagd, covogOmAdviesGevraagdToelichting
, gemOndertekeningDatum
, gemAmbtenaarDigitaleHandtekening, gemAmbtenaarPasswordHandtekening
) FOR Gemeente : 
(I[GemeentelijkeVOGZaak] 
  /\ (  gemVOGAanvraag;zijnDeLegesBetaald;'Ja';zijnDeLegesBetaald~;gemVOGAanvraag~
     \/ gemLegesBetaald;'Ja';gemLegesBetaald~
     )
  /\ gemeenteAanvraagnummer;gemeenteAanvraagnummer~
)
/\ {-userIsBehandelendGemeenteambtenaar-}
   (I /\ V;'_SESSION'[SESSION];sessionUserid;gemAmbtenaarUserid~)
/\ -sigGemeenteHeeftVolledigheidEnIdentiteitVastgesteld
BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
   , "Behandelend ambtenaar" : (gemAmbtenaarUserid;idmUserid~ /\gemAmbtenaarPassword;idmPassword~);idmDisplayName
   , "VOG aanvraag" : gemVOGAanvraag
   BOX[ "Doel van de aanvraag" : vogAanvraagOrganisatieTemplate
      , "Soort identiteitsbewijs (paspoort, rijbewijs)" : vogAanvragerIDMSoort
      , "Serienummer" : vogAanvragerIDMNummer
      , "Komen deze gegevens overeen met de persoon die u voor zich heeft?" : vogAanvragerIDVastgesteld
      , "DigiD" : vogAanvragerUserid
      , "Wachtwoord" : vogAanvragerPassword
      , "GBA gegevens van aanvrager" : vogBSNAanvrager
      BOX[ "BSN" : I
         , "Geslachtsnaam" : gbaGeslachtsnaam
         , "Voornamen" : gbaVoornamen -- art 32 eerste lid WJG
         , "Geboortedatum" : gbaGeboortedatum -- art 32 eerste lid WJG
         , "Gemeente van inschrijving": gbaGemeenteVanInschrijving
         ]
      ]
   , "Bijzonderheden" : I[GemeentelijkeVOGZaak] 
   BOX[ "Bijzonderheden geconstateerd (Ja/Nee)?" : bijzonderhedenGeconstateerd
      , "Ja, toelichting" : bijzonderhedenGeconstateerdToelichting
      ]
   , "Persisteren" : I[GemeentelijkeVOGZaak] 
   BOX[ "Persisteren in de aanvraag (Ja/Nee)?" : persisterenInDeAanvraag
      , "Ja, toelichting" : persisterenInDeAanvraagToelichting
      ]
   , "COVOG advies" : I[GemeentelijkeVOGZaak] 
   BOX[ "COVOG om advies gevraagd omtrent ontvankelijkheid (Ja/Nee)?" : covogOmAdviesGevraagd
      , "Ja, toelichting" : covogOmAdviesGevraagdToelichting
      ]
   , "Kan VOG meteen worden afgegeven?" :
-- Dat kan voor elke gemeentelijke VOG zaak waarvoor
     (I[GemeentelijkeVOGZaak] 
-- Waarvoor geen bijzonderheden zijn
     /\ bijzonderhedenGeconstateerd;'Nee';bijzonderhedenGeconstateerd~
-- de aanvraag is voor het aangaan of bestendigen van een werkrelatie
-- (dan immers is de aanvraag ontvankelijk)
     /\ gemVOGAanvraag;vogAanvraagOrganisatieTemplate
        ;werkrelatie;'Ja';werkrelatie~
        ;vogAanvraagOrganisatieTemplate~;gemVOGAanvraag~
-- en de identiteit van aanvrager is vastgesteld 
-- en die aanvrager heeft geen strafblad
     /\ gemVOGAanvraag
        ;({-vogAaanvragerGevalideerdeIdentiteit-} 
            (  (  vogAanvragerUserid;idmUserid~ 
               /\ vogAanvragerPassword;idmPassword~
               );(I /\  idmSoort;'DigiD';idmSoort~)
            \/  (I /\ vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~)
               ;(  vogAanvragerIDMSoort;idmSoort~
                /\ vogAanvragerIDMNummer;idmNummer~
                )
            )
         )                       -- I[Identificatiemiddel]
        ;idmHouder;gbaBSN~       -- I[NatuurlijkPersoon]
        ;-(strafrechtsketennummer~;strafrechtsketennummer) -- heeft geen strafblad
        ;gbaBSN;idmHouder~       -- I[Identificatiemiddel]
        ;({-vogAaanvragerGevalideerdeIdentiteit-} 
            (  (  vogAanvragerUserid;idmUserid~ 
               /\ vogAanvragerPassword;idmPassword~
               );(I /\  idmSoort;'DigiD';idmSoort~)
            \/  (I /\ vogAanvragerIDVastgesteld;'Ja';vogAanvragerIDVastgesteld~)
               ;(  vogAanvragerIDMSoort;idmSoort~
                /\ vogAanvragerIDMNummer;idmNummer~
                )
            )
         )~
        ;gemVOGAanvraag~
-- en de gemeentelijke leges zijn betaald
     );gemLegesBetaald -- die is dan immers ook 'Ja'
   , "Aanvraagformulier is gecontroleerd op volledigheid en de identiteit van de aanvrager is vastgesteld" : I
   BOX[ "Datum" : gemOndertekeningDatum
      , "Digitale handtekening" : gemAmbtenaarDigitaleHandtekening
      , "Password handtekening" : gemAmbtenaarPasswordHandtekening
      , "Ondertekening" : (I /\ gemOndertekeningDatum;gemOndertekeningDatum~); (gemAmbtenaarUserid;idmUserid~ /\ gemAmbtenaarPassword;idmPassword~ /\ gemAmbtenaarHandtekening;idmDisplayHandtekening~);(I /\ idmSoort;'GemeenteID';idmSoort~)
      BOX[ "Naam gemeenteambtenaar" : idmDisplayName
         , "Gemeente" : idmGemeente
         , "Plaats" : idmGemeentePlaats
      ]  ]
   , "Ingelogde Gemeente ambtenaar" : V[GemeentelijkeVOGZaak*SESSION];'_SESSION';
   (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
         ;(I /\ idmSoort;'GemeenteID';idmSoort~)                 -- met een GemeenteID
         ;idmDisplayName                                         -- dan laten we de displayName zien
   \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
         ;(I /\ -(idmSoort;'GemeenteID';idmSoort~))              -- niet met een GemeenteID
         ;V;'GemeenteID'[Berichtje];loginMet -- dan zeggen we dat je met een GemeenteID moet inloggen
   \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
         ;V;'GemeenteID'[Berichtje];loginMet -- dan zeggen we dat je met een GemeenteID moet inloggen
   )
   ]
-----------------------------------------------------------
INTERFACE "Opgestuurde VOG aanvragen" 
FOR Gemeente : I[GemeentelijkeVOGZaak]
;{-userIsBehandelendGemeenteambtenaar-}(I /\ V;'_SESSION'[SESSION];sessionUserid;gemAmbtenaarUserid~)
;sigGemeenteHeeftVolledigheidEnIdentiteitVastgesteld
BOX[ "Aanvraagnummer" : gemeenteAanvraagnummer
   , "Behandelend ambtenaar" : (gemAmbtenaarUserid;idmUserid~ /\gemAmbtenaarPassword;idmPassword~);idmDisplayName
   , "VOG aanvraag" : gemVOGAanvraag
   BOX[ "Doel van de aanvraag" : vogAanvraagOrganisatieTemplate
      , "Aanvrager" : vogBSNAanvrager
      BOX[ "Geslachtsnaam" : gbaGeslachtsnaam
         , "Voornamen" : gbaVoornamen -- art 32 eerste lid WJG
         , "Geboortedatum" : gbaGeboortedatum -- art 32 eerste lid WJG
         , "BSN" : I
         , "Gemeente van inschrijving": gbaGemeenteVanInschrijving
         ]
      , "Aanvrager is geauthenticeerd m.b.v." : ((vogAanvragerUserid;idmUserid~ /\ vogAanvragerPassword;idmPassword~) \/ (vogAanvragerIDMSoort;idmSoort~ /\ vogAanvragerIDMNummer;idmNummer~));idmSoort
      ]
   , "Bijzonderheden" : I
   BOX[ "Bijzonderheden geconstateerd (Ja/Nee)?" : bijzonderhedenGeconstateerd
      , "Ja, toelichting" : bijzonderhedenGeconstateerdToelichting
      ]
   , "Persisteren" : I
   BOX[ "Persisteren in de aanvraag (Ja/Nee)?" : persisterenInDeAanvraag
      , "Ja, toelichting" : persisterenInDeAanvraagToelichting
      ]
   , "COVOG advies" : I
   BOX[ "COVOG om advies gevraagd omtrent ontvankelijkheid (Ja/Nee)?" : covogOmAdviesGevraagd
      , "Ja, toelichting" : covogOmAdviesGevraagdToelichting
      ]
   , "Aanvraagformulier is gecontroleerd op volledigheid en de identiteit van de aanvrager is vastgesteld" : I
   BOX[ "Datum" : gemOndertekeningDatum
      , "Ondertekening" : (I /\ gemOndertekeningDatum;gemOndertekeningDatum~); (gemAmbtenaarUserid;idmUserid~ /\ gemAmbtenaarPassword;idmPassword~ /\ gemAmbtenaarHandtekening;idmDisplayHandtekening~);(I /\ idmSoort;'GemeenteID';idmSoort~)
      BOX[ "Naam gemeenteambtenaar" : idmDisplayName
         , "Gemeente" : idmGemeente
         , "Plaats" : idmGemeentePlaats
      ]  ]
   , "Ingelogde Gemeente ambtenaar" : V[GemeentelijkeVOGZaak*SESSION];'_SESSION';
   (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
         ;(I /\ idmSoort;'GemeenteID';idmSoort~)                 -- met een GemeenteID
         ;idmDisplayName                                         -- dan laten we de displayName zien
   \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
         ;(I /\ -(idmSoort;'GemeenteID';idmSoort~))              -- niet met een GemeenteID
         ;V;'GemeenteID'[Berichtje];loginMet -- dan zeggen we dat je met een GemeenteID moet inloggen
   \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
         ;V;'GemeenteID'[Berichtje];loginMet -- dan zeggen we dat je met een GemeenteID moet inloggen
   )
   ]
-----------------------------------------------------------
INTERFACE "VOG aanvraag (organisatie deel)" FOR Gemeente : I[VOGAanvraagOrganisatieTemplate] /\ (organisatieHandtekening;organisatieHandtekening~)
BOX[ "VOG aanvraag (Organisatie template)" : I[VOGAanvraagOrganisatieTemplate] 
   , "1 - Gegevens organisatie-instelling" : {-organisatieOrganisatie-}
          ( ( (organisatieUserid;idmUserid~ /\ organisatiePassword;idmPassword~)
              ;(I /\ idmSoort;'EHvB';idmSoort~) 
            /\ organisatieHandtekening;idmDisplayHandtekening~
            );idmOrganisatie
          )
   BOX[ "Naam organisatie/instelling" : hrOrganisatieNaam
      , "Vestigingsadres" : hrVestigingsAdres
      ]
   , "Contactpersoon" : I
   BOX[ "Naam vertegenwoordiger" : {-organisatieVertegenwoordiger-}
          ( (organisatieUserid;idmUserid~ /\ organisatiePassword;idmPassword~)
            ;(I /\ idmSoort;'EHvB';idmSoort~);idmDisplayName
          )
      , "Telefoonnummer" : orgTelefoonnummer
      ]
   , "2 - Doel van de aanvraag" : I
   BOX[ "Werkrelatie (Ja/Nee)" : werkrelatie
      , "Functie (waarop de aanvraag betrekking heeft)" : functie
      , "Taakomschrijving (bij voorkeur functiebeschrijving bijvoegen)" : taakomschrijving
      , "Overig (Ja/Nee)" : overig
      , "Omschrijving (bijv. Chaufferskaart, Visum en emigratie, Exploitatievergunning)" : overigOmschrijving
      ]
   , "3 - Specifiek screeningsprofiel" : I
   BOX[ "Heeft de aanvraag betrekking op één van de hieronder genoemde functies of doelen?" : specifiekScreeningsProfielRelevant
      , "Ja, namelijk op: (u kunt slechts één vakje aankruisen!)" : specifiekScreeningsProfiel
      ]
   , "4 - Algemeen screeningsprofiel" : I
   BOX[ " ": algemeenScreeningsProfiel
      ]
   , "5 - Bijzondere omstandigheden m.b.t. de aanvraag" : I
   BOX[ "Zijn er bijzondere omstandigheden (Ja/Nee)?" : orgErZijnBijzondereOmstandigheden
      , "Ja, toelichting" : orgErZijnBijzondereOmstandighedenToelichting
      ]
   , "6 - Ondertekening door organisatie/instelling" : I
   BOX[ "Plaats" : orgOndertekeningPlaats
      , "Datum" : orgOndertekeningDatum
      , "Handtekening" : organisatieHandtekening
      ]
   , "Ingelogde Gemeente ambtenaar" : V[VOGAanvraagOrganisatieTemplate*SESSION];'_SESSION';
   (  (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd
         ;(I /\ idmSoort;'GemeenteID';idmSoort~)                 -- met een GemeenteID
         ;idmDisplayName                                         -- dan laten we de displayName zien
   \/ (sessionUserid;idmUserid~ /\ sessionPassword;idmPassword~) -- als er is ingelogd, maar 
         ;(I /\ -(idmSoort;'GemeenteID';idmSoort~))              -- niet met een GemeenteID
         ;V;'GemeenteID'[Berichtje];loginMet -- dan zeggen we dat je met een GemeenteID moet inloggen
   \/ (I/\ -(sessionUserid;sessionUserid~))                      -- als er niet is ingelogd
         ;V;'GemeenteID'[Berichtje];loginMet -- dan zeggen we dat je met een GemeenteID moet inloggen
   )
   ]
-----------------------------------------------------------
ENDCONTEXT
