CONTEXT Arbeidsduur IN DUTCH LATEX
META "authors" "Stef Joosten"
PURPOSE CONTEXT Arbeidsduur
{+
De Wet aanpassing arbeidsduur regelt het recht van werknemers om te vragen om aanpassing van de arbeidsduur.
Op deze wet is een Ampersand analyse gedaan,
om inzicht te krijgen in de wijze van modelleren aan de hand van een klein voorbeeld.
Daar waar in deze tekst wordt gerefereerd naar een wetsartikel, zonder de naam van de wet erbij te noemen,
wordt de Wet aanpassing arbeidsduur bedoeld.
-}

PROCESS Arbeidsduur

RELATION tot[Verzoek*Aanpassing] [UNI] -- is niet totaal vanwege de demo
PRAGMA "" "tot"
MEANING "Elk verzoek beschrijft de gewenste aanpassing van de uit zijn arbeidsovereenkomst of publiekrechtelijke aanstelling voortvloeiende arbeidsduur."
PURPOSE RELATION tot[Verzoek*Aanpassing] LATEX REF "Artikel 2 lid 1"
{+Omdat het gaat om verzoeken tot aanpassing van de arbeidsduur, gedaan vanuit de werknemer,
is het beschrijven van de aanpassing verplicht.
-}
RELATION van[Verzoek*Persoon] [UNI,TOT]
PRAGMA "" "van"
MEANING "Elk verzoek is van een persoon afkomstig."
PURPOSE RELATION van[Verzoek*Persoon] LATEX
{+Omdat een verzoek door een persoon wordt gedaan, moet op elk moment duidelijk zijn wie de verzoekende partij is.
-}
RELATION aan[Verzoek*Persoon] [UNI,TOT]
PRAGMA "" "aan"
MEANING "Elk verzoek dient zich aan een (rechts)persoon te richten."
PURPOSE RELATION aan LATEX
{+Omdat een verzoek aan een persoon is gericht (natuurlijke persoon dan wel rechtspersoon), moet op elk moment duidelijk zijn wie de partij is van wie een beslissing wordt gevraagd.
-}
RELATION ingediend[Verzoek*Tijdstip] [UNI]
PRAGMA "" "is ingediend"
MEANING "Van elk verzoek is het tijdstip van indienen geregistreerd."
PURPOSE RELATION ingediend LATEX REF "Artikel 2 lid 3"
{+Omdat het tijdstip van indienen van het verzoek relevant is voor de ontvankelijkheid ervan, moeten we het registreren.
-}
RELATION op[Beslissing*Verzoek] [UNI,TOT]
PRAGMA "" "op"
MEANING "Elke beslissing verwijst naar het verzoek waarop de beslissing genomen is."
PURPOSE RELATION op LATEX REF "Artikel 2 lid 7"
{+Duidelijkheid is nodig over de vraag op welk verzoek een beslissing gevraagd wordt.
De tekst van de wet rept van ``De beslissing op het verzoek ...''
Daarom wordt voor elke beslissing bijgehouden op welk verzoek de betreffende beslissing genomen wordt.
-}
RELATION aanp[Beslissing*Aanpassing] [UNI,TOT]
PRAGMA "" "tot"
MEANING "Een beslissing beschrijft de aanpassing van de uit de betreffende arbeidsovereenkomst of publiekrechtelijke aanstelling voortvloeiende arbeidsduur."
PURPOSE RELATION aanp[Beslissing*Aanpassing] LATEX REF "Artikel 2 lid 7"
{+De aanpassing waartoe een werkgever besluit kan afwijken van de aanpassing die door de werknemer is verzocht.
Daarom wordt aan het besluit nadrukkelijk de aanpassing verbonden die door de werkgever is besloten.
Onder inwilligen zullen we dan ook verstaan dat de aanpassing waarom verzocht is gelijk is aan de aanpassing die in het besluit is vermeld.
-}
RELATION door[Beslissing*Persoon] [UNI,TOT]
PRAGMA "" "door"
MEANING "Elke beslissing wordt door een (rechts)persoon genomen."
PURPOSE RELATION door LATEX
{+Het moet duidelijk zijn door welke (rechts)persoon een beslissing genomen is.
Het optreden van natuurlijke personen als tekenbevoegde voor een rechtspersoon wordt buiten deze wet geregeld en valt dan ook buiten de scope van dit model.
Hier volstaan we met de juridische entiteit, ofwel de werkgever die de beslissing neemt.
-}
RELATION medegedeeldAan[Beslissing*Persoon] [UNI]
PRAGMA "" "is medegedeeld aan"
MEANING "Beslissingen kunnen worden medegedeeld aan personen. Dat gebeurt schriftelijk."
PURPOSE RELATION medegedeeldAan LATEX REF "Artikel 2 lid 7"
{+De beslissing op het verzoek om aanpassing van de arbeidsduur wordt door de werkgever schriftelijk aan de werknemer meegedeeld.
Daarom wordt het mededelen vastgelegd.
-}
RELATION reden[Reden*Beslissing] [UNI,TOT]
PRAGMA "" "is een motief voor"
MEANING "Beslissingen kunnen met redenen worden omkleed."
PURPOSE RELATION reden LATEX REF "Artikel 2 lid 7"
{+Een beslissing, die afwijkt van de wensen van de werknemer, dient onder schriftelijke opgave van de redenen te worden meegedeeld.
Hier scheppen we de ruimte om redenen voor de beslissing vast te leggen.
De eis zelf, namelijk dat er redenen moeten zijn voor een afwijkend besluit, is elders in deze analyse geformuleerd.
-}
RELATION werkgever[Arbeidsrelatie*Persoon] [UNI,TOT]
PRAGMA "In" "is" "de werkgever"
MEANING "Elke arbeidsrelatie benoemt expliciet welke (rechts)persoon de rol van werkgever vervult."
PURPOSE RELATION werkgever LATEX
{+Om de werkgever te kunnen bepalen gaan we ervan uit dat elke arbeidsrelatie precies Ã©Ã©n werkgever heeft.
-}
RELATION werknemer[Arbeidsrelatie*Persoon] [UNI,TOT]
PRAGMA "In" "is" "de werknemer"
MEANING "Elke arbeidsrelatie benoemt expliciet welke persoon de rol van werknemer vervult."
PURPOSE RELATION werknemer LATEX
{+We gaan ervan uit dat elke arbeidsrelatie precies Ã©Ã©n werknemer heeft.
-}
RELATION arbeidsduur[Arbeidsrelatie*Arbeidsduur] [UNI,TOT]
PRAGMA "In" "is" "als arbeidsduur afgesproken"
MEANING "Elke arbeidsrelatie vermeldt de afgesproken arbeidsduur."
PURPOSE RELATION arbeidsduur LATEX REF "Artikel 2 lid 1"
{+De Wet aanpassing arbeidsduur spreekt van ``de uit zijn arbeidsovereenkomst of publiekrechtelijke aanstelling voortvloeiende arbeidsduur''.
Dat impliceert dat er voor elke arbeidsovereenkomst of publiekrechtelijke aanstelling precies Ã©Ã©n arbeidsduur is.
Er zijn ongetwijfeld andere wetten en regelingen die dat garanderen, maar hier nemen we dit als een gegeven (uitgangspunt) over.
-}
RELATION inDienst[Arbeidsrelatie*Tijdstip] [UNI,TOT]
PRAGMA "" "vermeldt" "als tijdstip van indiensttreding"
MEANING "Elke arbeidsrelatie kent een tijdstip van indiensttreding."
PURPOSE RELATION inDienst LATEX REF "Artikel 2 lid 3"
{+Om een verzoek te mogen indienen dient de werknemer ten minste een jaar voorafgaand aan het beoogde tijdstip van ingang van de aanpassing in dienst te zijn bij de werkgever.
Daarom moet de tijdstip van indiensttreding bekend zijn.
Dit artikel bevat nuanceringen ten aanzien van de berekening van het tijdvak. Deze details worden om praktische redenen buiten scope gehouden.
-}
RELATION nieuw[Aanpassing*Arbeidsduur] [UNI,TOT]
PRAGMA "" "leidt tot een nieuwe arbeidsduur van"
MEANING "De arbeidsduur na aanpassing moet in de beslissing worden vermeld."
PURPOSE RELATION nieuw LATEX REF "Artikel 2 lid 3"
{+De nieuwe arbeidsduur moet eenduidig uit de aanpassing blijken.
-}
RELATION arbeidsrelatie[Verzoek*Arbeidsrelatie] [UNI,TOT]
PRAGMA "" "betreft"
MEANING "Het verzoek vermeldt de arbeidsrelatie waarop de aanpassing van toepassing is."
PURPOSE RELATION arbeidsrelatie LATEX
{+Het verzoek moet duidelijk vermelden welke arbeidsrelatie onderwerp is van het verzoek.
Dat is immers niet vanzelfsprekend in die gevallen waar een werknemer meerdere arbeidsrelaties over verschillende periodes heeft.
De onderzochte wet sluit zelfs niet uit dat Ã©Ã©n werkgever en Ã©Ã©n werknemer op hetzelfde moment verschillende arbeidsrelaties onderhouden.
-}
RELATION ingang[Aanpassing*Tijdstip] [UNI,TOT]
PRAGMA "" "gaat in op"
MEANING "Het tijdstip van ingang moet in de aanpassing worden vermeld."
PURPOSE RELATION ingang LATEX REF "Artikel 2 lid 3"
{+De aanpassing van de arbeidsduur moet worden ingediend onder opgave van een beoogd tijdstip van ingang.
-}
RELATION omvang[Aanpassing*Omvang] [UNI,TOT]
PRAGMA "" "betreft een omvang van"
MEANING "De omvang van de arbeidsduur moet in de aanpassing worden vermeld."
PURPOSE RELATION omvang LATEX REF "Artikel 2 lid 3"
{+De aanpassing moet worden ingediend onder opgave van de omvang van de aanpassing van de arbeidsduur per week of, als de arbeidsduur over een ander tijdvak is overeengekomen over dat tijdvak en de gewenste spreiding van de uren over de week of het anderszins overeengekomen tijdvak.
-}

PURPOSE RULE "arbeidsrelatie werkgever" LATEX REF "Artikel 1"
{+Omdat het mogelijk is dat mensen elkaar verzoeken sturen, terwijl er geen sprake is van een arbeidsrelatie,
is een regel nodig die garandeert dat het verzoek over een bestaande arbeidsrelatie handelt.
-}
RULE "arbeidsrelatie werkgever": arbeidsrelatie |- aan;werkgever~
MEANING "Elk verzoek moet gericht zijn aan de (rechts)persoon die in de arbeidsrelatie als werkgever staat vermeld."
MESSAGE "Artikel 1 Wet aanpassing arbeidsduur zegt dat elk verzoek moet worden gericht aan de werkgever."
VIOLATION (TXT "Dit verzoek is gericht aan ", SRC aan, TXT " in plaats van aan werkgever ", SRC arbeidsrelatie;werkgever, TXT ".")

PURPOSE RULE "arbeidsrelatie werknemer" LATEX REF "Artikel 1"
{+Hetzelfde artikel heeft een regel nodig die garandeert dat het verzoek over een bestaande arbeidsrelatie handelt.
-}
RULE "arbeidsrelatie werknemer": arbeidsrelatie |- van;werknemer~
MEANING "De arbeidsrelatie die in het verzoek wordt genoemd is een arbeidsrelatie tussen de verzoeker als werknemer en de geaddresseerde als werkgever. Dit impliceert dat werknemer en werkgever een arbeidsrelatie hebben."
MESSAGE "Uit Artikel 1 Wet aanpassing arbeidsduur volgt dat de werknemer een verzoek mag indienen."
VIOLATION (TXT "Dit verzoek is niet afkomstig van werknemer ", SRC arbeidsrelatie;werknemer, TXT " maar van ", SRC van, TXT ".")

PURPOSE RULE beslisser LATEX REF "Artikel 2 lid 1"
{+We spreken af dat de beslissing genomen wordt door de werkgever, en door niemand anders.
-}
RULE beslisser : op~;door |- aan
MEANING "Een beslissing wordt genomen door de (rechts)persoon aan wie het verzoek is gericht."
MESSAGE "Artikel 2 lid 1 Wet aanpassing arbeidsduur:"
VIOLATION (TXT "Deze beslissing dient genomen te worden door", SRC arbeidsrelatie;werkgever, TXT ".")

PURPOSE RULE beslissen LATEX REF "Artikel 2 lid 10"
{+We spreken af dat er een beslissing genomen moet worden.
De wet stelt namelijk dat indien de werkgever niet een maand voor het beoogde tijdstip van ingang van de aanpassing op het verzoek heeft beslist, de arbeidsduur wordt aangepast overeenkomstig het verzoek van de werknemer.
-}
RULE beslissen : aan |- op~;door
MEANING "Degene aan wie een verzoek gericht is dient daarop een beslissing te nemen."
MESSAGE "Nog beslissen:"
VIOLATION (SRC aan, TXT " moet nog beslissen over ", SRC I, TXT ".")

PURPOSE RULE beslissen LATEX REF "Artikel 2 lid 7"
{+De beslissing op het verzoek om aanpassing van de arbeidsduur wordt door de werkgever schriftelijk aan de werknemer meegedeeld.
-}
RULE mededelen : op |- medegedeeldAan;van~ 
MEANING "De beslissing op een verzoek dient medegedeeld te worden aan degene die het verzoek heeft ingediend"
MESSAGE "Nog te verzenden besluiten:"
VIOLATION (TXT "verzoek van ", SRC op;van, TXT ", die op ", SRC aanp[Beslissing*Aanpassing];ingang, TXT " in effect treedt.")

PURPOSE RULE "gemotiveerd afwijzen" LATEX REF "Artikel 2 lid 7"
{+Indien de werkgever het verzoek niet inwilligt of de spreiding van de uren vaststelt in afwijking van de wensen van de werknemer, wordt dit onder schriftelijke opgave van de redenen meegedeeld.
Hieruit valt op te maken dat elke afwijkende arbeidsduur vraagt om opgave van redenen.-}
RULE "gemotiveerd afwijzen" : op;tot;-I;aanp~ /\ I  |- reden~;reden
MEANING "Wanneer de arbeidsduur uit de beslissing afwijkt van de arbeidsduur uit het verzoek, dan dient er een reden voor te zijn, die in de beslissing wordt opgenomen.."
MESSAGE "Artikel 2 lid 7 Wet aanpassing arbeidsduur."
VIOLATION (TXT "Beslissing van ", SRC door, TXT ", op ", SRC op, TXT " vereist een schriftelijke opgave van de redenen.")

ROLE Werkgever MAINTAINS beslissen, mededelen

PURPOSE RULE "een jaar in dienst" LATEX REF "Artikel 2 lid 1"
{+Omdat werknemers niet direct na indiensttreding een verzoek tot aanpassing mogen doen, geldt in de wet een termijn van een jaar na indiensttreding.
-}
RULE "een jaar in dienst" : arbeidsrelatie~;tot |- minstenseenjaar
MEANING "De werknemer, die een verzoek indient, dient ten minste een jaar voorafgaand aan het beoogde tijdstip van ingang van die aanpassing in dienst te zijn bij die werkgever."
MESSAGE "Wet op Aanpassing van de Arbeidsduur, Artikel 2 lid 1:"
VIOLATION (SRC werknemer, TXT " is minder dan een jaar voorafgaand aan ingangstijdstip ", TGT ingang, TXT " in dienst.")
PURPOSE RELATION minstenseenjaar LATEX REF "Artikel 2 lid 1"
{+Omdat werknemers niet direct na indiensttreding een verzoek tot aanpassing mogen doen, geldt in de wet een termijn van een jaar na indiensttreding.
Omdat we in Ampersand (nog) niet kunnen rekenen, voeren we een relatie ``minstenseenjaar'' in. Deze dient als stub voor het rekenwerk.
-}
RELATION minstenseenjaar[Arbeidsrelatie*Aanpassing]
PRAGMA "het tijdstip van indiensttreding in" "ligt minstens een jaar voor het tijdstip van ingang van aanpassing"
MEANING "Deze relatie geeft weer of er minstens een jaar is verstreken tussen het tijdstip van indiensttreding en het tijdstip van ingang van de aanpassing."

PURPOSE RULE "tijdig indienen" LATEX REF "Artikel 2 lid 3"
{+Omdat werkgevers voldoende tijd moeten krijgen voor het beslissen, schrijft de wet voor dat een aanvraag vier maanden voor de ingangsdatum voor de aanpassing moet worden ingediend.
Omdat we in Ampersand (nog) niet kunnen rekenen, voeren we een relatie ``minstensviermaanden'' in. Deze dient als stub voor het rekenwerk.
-}
ROLE Werknemer MAINTAINS "tijdig indienen"
RULE "tijdig indienen" : tot |- ingediend;minstensviermaanden;ingang~
MEANING "Deze regel bewaakt dat er minstens vier maanden zijn verstreken tussen het tijdstip van aanvragen en het tijdstip van ingang van de aanpassing."
MESSAGE "Wet op Aanpassing van de Arbeidsduur, Artikel 2 lid 3"
VIOLATION (TXT "Het ", SRC I, TXT " is later dan vier maanden voor de ingangsdatum ", TGT ingang, TXT " ingediend.")

PURPOSE RELATION minstensviermaanden LATEX REF "Artikel 2 lid 3"
{+Omdat de wet een termijn van vier maanden noemt, en omdat we in Ampersand (nog) niet kunnen rekenen, voeren we een relatie ``minstensviermaanden'' in.
Deze dient als stub voor het rekenwerk.
-}
RELATION minstensviermaanden[Tijdstip*Tijdstip]
PRAGMA "" "ligt minstens vier maanden voor"
MEANING "Deze relatie geeft weer of er minstens vier maanden zijn verstreken tussen twee tijdstippen."

VIEW verzoek : Verzoek(TXT "verzoek van ", van, TXT " aan ", aan, TXT " over het aanpassen van ", arbeidsrelatie)
VIEW Arbeidsrelatie : Arbeidsrelatie (werknemer, TXT " in dienst bij ", werkgever, TXT " vanaf ", inDienst )
ENDPROCESS

INTERFACE Overzicht : I[ONE]
 BOX [ Verzoeken : V[ONE*Verzoek]
       BOX [ "nummer" : I
           , indiener : van
           , werkgever : aan
           ]
     , Beslissingen : V[ONE*Beslissing]
       BOX [ beslissing : I
           , door : door
           , medegedeeld : medegedeeldAan
           , verzoek : op
           ]
     , Arbeidsrelaties : V[ONE*Arbeidsrelatie]
       BOX [ werknemer : werknemer
           , werkgever : werkgever
           , arbeidsduur : arbeidsduur
           , "in dienst sinds" : inDienst
           ]
     , Werknemers : V[ONE*Persoon]; (I /\ werknemer~;werknemer)
     , Werkgevers : V[ONE*Persoon]; (I /\ werkgever~;werkgever)
     ]

INTERFACE Arbeidsrelatie (arbeidsrelatie, werknemer, werkgever, arbeidsduur, inDienst, van, aan, arbeidsrelatie, tot[Verzoek*Aanpassing], omvang, ingang, nieuw) FOR Werknemer, Werkgever : I[Arbeidsrelatie]
       BOX [ werkgever : werkgever
           , werknemer : werknemer
           , arbeidsduur : arbeidsduur
           , "in dienst sinds" : inDienst
           , verzoeken : arbeidsrelatie~
             BOX [ aan : aan
                 , van : van
                 , "aanpassing" : tot
                    BOX [ aanpassing : I INTERFACE Aanpassing
                    , omvang : omvang
                    , "gaat in op" : ingang
                    , "voorstel voor de nieuwe arbeidsduur" : nieuw
                    ]
                 ]
           ]

INTERFACE "Verzoek indienen" (van, aan, arbeidsrelatie, tot[Verzoek*Aanpassing], omvang, ingang, nieuw) FOR Werknemer : I[Verzoek]
BOX [ indiener : van
    , werkgever : aan
    , arbeidsrelatie : arbeidsrelatie
    , aanpassing : tot
      BOX [ aanpassing : I INTERFACE Aanpassing
          , omvang : omvang
          , "gaat in op" : ingang
          , "voorstel voor de nieuwe arbeidsduur" : nieuw
          ]
    ]

INTERFACE "Gewijzigde beslissing nemen" (op, aanp, medegedeeldAan, door, reden, omvang, ingang, nieuw) FOR Werkgever : I[Verzoek]
BOX [ indiener : van
    , werkgever : aan
    , arbeidsrelatie : arbeidsrelatie
    , "voorgestelde aanpassing" : tot[Verzoek*Aanpassing] INTERFACE Aanpassing
    , beslissingen : op~
      BOX [ "medegedeeld aan" : medegedeeldAan
          , door : door
          , aanpassing : aanp
            BOX [ besloten : I
                , omvang : omvang
                , "gaat in op" : ingang
                , "voorstel voor de nieuwe arbeidsduur" : nieuw
                ]
          , "motivatie (elke reden afzonderlijk)" : reden~
          ]
    ]

INTERFACE "Voorstel ongewijzigd accorderen" (op, medegedeeldAan, door, aanp, reden) FOR Werkgever : I[Verzoek]
BOX [ indiener : van
    , werkgever : aan
    , arbeidsrelatie : arbeidsrelatie
    , "voorgestelde aanpassing" : tot[Verzoek*Aanpassing]
      BOX [ aanpassing : I
          , omvang : omvang
          , "gaat in op" : ingang
          , "voorstel voor de nieuwe arbeidsduur" : nieuw
          ]
    , beslissingen : op~
      BOX [ "medegedeeld aan" : medegedeeldAan
          , door : door
          , aanpassing : aanp
          , "motivatie (elke reden afzonderlijk)" : reden~
          ]
    ]

INTERFACE Aanpassing (omvang, ingang, nieuw) FOR Werknemer, Werkgever : I[Aanpassing]
BOX [ aanpassing : I
    , omvang : omvang
    , "gaat in op" : ingang
    , "voorstel voor de nieuwe arbeidsduur" : nieuw
    ]

INTERFACE Beslissing (door, op, aanp, reden, medegedeeldAan, omvang, ingang, nieuw) FOR Werkgever : I[Beslissing]
BOX [ door : door
    , verzoek : op
    , aanpassing : aanp[Beslissing*Aanpassing]
          BOX [ besloten : I[Aanpassing]
                , omvang : omvang
                , "gaat in op" : ingang
                , "voorstel voor de nieuwe arbeidsduur" : nieuw
                ] 
    , "motivatie (elke reden afzonderlijk)" : reden~
    , "medegedeeld aan" : medegedeeldAan
    ]

INTERFACE "persoon" : I[Persoon]
BOX [ "unieke naam" : I
    , "werkgever in:" : werkgever~ INTERFACE Arbeidsrelatie
    , "werknemer in:" : werknemer~ INTERFACE Arbeidsrelatie
    ]


CONCEPT Verzoek  "Een verzoek is een schrijven waarin een werknemer verzoekt om de arbeidsduur van een arbeidsrelatie aan te passen."
PURPOSE CONCEPT Verzoek
{+De wet geeft geen definitie van ``verzoek'' en gaat kennelijk uit van de betekenis algemeen gebruikt Nederlands.
Wel stelt de wet de eis van een schriftelijk verzoek, waardoor het verzoek het karakter van een document krijgt.
-}
CONCEPT Aanpassing "Een aanpassing is de gebeurtenis waarbij de arbeidsduur van een arbeidsrelatie verandert."
PURPOSE CONCEPT Aanpassing LATEX
{+Het begrip aanpassing is nodig om de gebeurtenis van het aanpassen te objectiveren.
Het gaat in deze wet uitsluitend om het aanpassen van de arbeidsduur, die uit een arbeidsrelatie voortvloeit.
-}
CONCEPT Persoon "Een persoon is een natuurlijke persoon of een rechtspersoon, naar geldend Nederlands recht."
PURPOSE CONCEPT Persoon LATEX
{+Er is sprake van werkgever en werknemer, waarbij de werkgever (naar geldend recht) zowel een natuurlijke persoon als een rechtspersoon kan zijn.
In deze analyse wordt daarvan geabstraheerd, en noemen we alle natuurlijke- en rechtspersonen gewoon ``Persoon''.
-}
CONCEPT Beslissing "Een beslissing is een schrijven, waarin een werkgever mededeelt wat hij op een in de beslissing vermeld verzoek heeft besloten."
PURPOSE CONCEPT Beslissing LATEX
{+Omdat de beslissing (net als het verzoek) schriftelijk wordt meegedeeld, heeft ook de beslissing een documentair karakter.
Het onderscheid tussen besluit en beslissing, wat we kennen uit de Algemene Wet Bestuursrecht, wordt hier niet gemaakt.
Wellicht omdat het hier niet alleen over ambtelijke aanstellingen, maar ook over arbeidsovereenkomsten naar burgerlijk recht gaat.
-}
CONCEPT Reden "Een reden is een omschrijving van de grond van een beslissing."
PURPOSE CONCEPT Reden LATEX
{+Omdat de Wet aanpassing arbeidsduur expliciet om redenen vraagt, is hiervoor een concept nodig.
De aard van de redenen kan worden gecategoriseerd conform artikel 1 lid 8-9.
-}
CONCEPT Arbeidsrelatie "Onder arbeidsrelatie verstaan we een arbeidsovereenkomst naar burgerlijk recht of publiekrechtelijke aanstelling." "Artikel 1 sub a"
PURPOSE CONCEPT Arbeidsrelatie LATEX REF "Artikel 1 sub a"
{+De wet spreekt van ``arbeidsovereenkomst naar burgerlijk recht of publiekrechtelijke aanstelling''.
Kennelijk zijn er twee manieren om een arbeidsrelatie te hebben.
In de onderhavige analyse spreken we van een arbeidsrelatie, maar bedoelen daarmee zoals in dit artikel staat omschreven.
-}
CONCEPT Arbeidsduur "Onder arbeidsduur verstaan we een afspraak over de omvang en het tijdstip van ingang van die afspraak."
PURPOSE CONCEPT Arbeidsduur LATEX
{+Als je de arbeidsduur wilt aanpassen moet je wel weten wat het betekent.
Een beetje curieus is dan ook dat de Wet aanpassing arbeidsduur ``arbeidsduur'' noemt zonder dat begrip nader te duiden.
Wel doet deze wet een aantal aannames, zoals: Arbeidsduur heeft een omvang, die je kunt aanpassen (specifiek door te verminderen of vermeerderen).
Arbeidsduur heeft ook een tijdstip van ingang.
Deze aannames zijn in het model verwerkt, zonder te kijken naar andere wetten of regelingen die het begrip verder inkleuren.
De definitie die we hier hanteren voor Arbeidsduur gaat dan ook wat verder dan strikt uit deze wet kan worden opgemaakt (uitzoeken).
-}
CONCEPT Omvang "Onder de omvang verstaan we het aantal uren dat een werknemer per periode voor een werkgever arbeid verricht en de verdeling van die uren over de dag/week/periode."
PURPOSE CONCEPT Omvang LATEX
{+Niet helemaal duidelijk is wat deze wet verstaat onder de omvang van de arbeidsduur.
Het aantal uren per week (of tijdvak) valt onder de omvang, maar er is ook een vage aanwijzing over de verdeling van die uren over de week.
Deze onduidelijkheid wordt niet verder geanalyseerd, omdat we dan naar andere wetten en regelingen moeten kijken.
-}


POPULATION tot[Verzoek*Aanpassing] CONTAINS
   [ ("Verzoek 392a", "1 dag meer werken op vrijdag")
   ; ("Verzoek 18", "Peter wil vrijdag vrij")
   ]
POPULATION van[Verzoek*Persoon] CONTAINS
   [ ("Verzoek 392a", "Willem Alexander")
   ; ("Verzoek 18", "Peter van Tienhoven")
   ]
POPULATION aan CONTAINS
   [ ("Verzoek 392a", "Minister President")
   ; ("Verzoek 18", "ten Hoopen BV")
   ]
POPULATION ingediend CONTAINS
   [ ("Verzoek 392a", "10 Mei 2013")
   ; ("Verzoek 18", "10 Mei 2013")
   ]
POPULATION op CONTAINS
   [ ("Beslissing b342", "Verzoek 392a")
   ]
POPULATION aanp[Beslissing*Aanpassing] CONTAINS
   [ ("Beslissing b342", "1 dag meer werken op vrijdag")
   ]
POPULATION door CONTAINS
   [ ("Beslissing b342", "Minister President")
   ]
POPULATION medegedeeldAan CONTAINS
   [ ("Beslissing b342", "Willem Alexander")
   ]
POPULATION werkgever CONTAINS
   [ ("Koningschap", "Minister President")
   ; ("Contract 00577", "ten Hoopen BV")
   ]
POPULATION werknemer CONTAINS
   [ ("Koningschap", "Willem Alexander")
   ; ("Contract 00577", "Peter van Tienhoven")
   ]
POPULATION arbeidsduur CONTAINS
   [ ("Koningschap", "87upw, naar eigen inzicht in te vullen")
   ; ("Contract 00577", "36upw, op werkdagen van 9-5")
   ]
POPULATION inDienst CONTAINS
   [ ("Koningschap", "30 April 2013")
   ; ("Contract 00577", "1 Augustus 1996")
   ]
POPULATION nieuw CONTAINS
   [ ("1 dag meer werken op vrijdag", "95 upw, en in ieder geval op vrijdag")
   ; ("Peter wil vrijdag vrij", "32upw, op ma,di,wo,do van 9-5")
   ]
POPULATION arbeidsrelatie CONTAINS
   [ ("Verzoek 392a", "Koningschap")
   ; ("Verzoek 18", "Contract 00577")
   ]
POPULATION ingang CONTAINS
   [ ("1 dag meer werken op vrijdag", "1 Mei 2014")
   ; ("Peter wil vrijdag vrij", "1 Oktober 2013")
   ]
POPULATION omvang CONTAINS
   [ ("1 dag meer werken op vrijdag", "8u")
   ; ("Peter wil vrijdag vrij", "4u")
   ]
POPULATION minstenseenjaar CONTAINS
   [ ("Koningschap", "1 dag meer werken op vrijdag")
   ; ("Contract 00577", "Peter wil vrijdag vrij")
   ]
POPULATION minstensviermaanden CONTAINS
   [ ("1 Augustus 1996", "30 April 2013")
   ; ("1 Augustus 1996", "10 Mei 2013")
   ; ("1 Augustus 1996", "1 Oktober 2013")
   ; ("1 Augustus 1996", "1 Mei 2014")
   ; ("30 April 2013", "1 Oktober 2013")
   ; ("30 April 2013", "1 Mei 2014")
   ; ("10 Mei 2013", "1 Oktober 2013")
   ; ("10 Mei 2013", "1 Mei 2014")
   ; ("1 Oktober 2013", "1 Mei 2014")
   ]


ENDCONTEXT
