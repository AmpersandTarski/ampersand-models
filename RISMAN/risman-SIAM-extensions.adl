CONTEXT "RISMAN_SIAM_Extensions" IN ENGLISH

--!Note: SIAM also defines the concept 'Person', and naming relations. These are different from what is defined here. This may cause interference-issues.

PURPOSE CONCEPT Person
{+In order to administer project participants, the system must register information about them.
For that reason, we introduce the concept Person.
+}
CONCEPT Person "A person is any human being"

RELATION personName[Person*PersonName] [UNI]
MEANING "A person can have (at most) one name"
RELATION personStatus[Person*PersonStatus] [UNI]
MEANING "A person can have a status"
RELATION personEmail[Person*Email] [UNI,TOT]
MEANING "A person can have an email-address"

REPRESENT PersonName, PersonStatus, Email TYPE ALPHANUMERIC

PURPOSE RULE "People are identifiable by their email-address"
{+In order to refer to a person (in the system), (s)he must be identifiable, which means that it must be possible to select him or her from the set of registered people. We choose to use the person's email-address for that.+}
RULE "People are identifiable by their email-address" : personEmail;personEmail~ |- I[Person]

VIEW People: Person(personName)
VIEW PersonEmail: Person { email : personEmail } HTML TEMPLATE "PersonEmail.html" ENDVIEW

ENDCONTEXT