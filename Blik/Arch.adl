CONTEXT Architectuur IN DUTCH
{-
   INCLUDE "../SIAM/SIAM_PersonReg.adl"
   INCLUDE "../SIAM/SIAM_OrgReg.adl"
-- INCLUDE "../SIAM/SIAM_Persona.adl"
   INCLUDE "../SIAM/SIAM_Roles.adl"
   INCLUDE "../SIAM/SIAM_LoginLogging.adl"
   INCLUDE "../SIAM/SIAM_Login.adl"

   INCLUDE "../SIAM/SIAM_PersonReg.ifc"
   INCLUDE "../SIAM/SIAM_OrgReg.ifc"
-- INCLUDE "../SIAM/SIAM_Persona.ifc"
   INCLUDE "../SIAM/SIAM_AccountManagement.ifc"
   INCLUDE "../SIAM/SIAM_Login.ifc"
--   INCLUDE "../SIAM/SIAM_LoginWithAssistance.ifc"
--INCLUDE "SIAMpop.xlsx"
-}

INCLUDE "Domeinen.xlsx"
INCLUDE "ProductCodes.xlsx"  -- If you want to run with the population of ProductCodes.xlsx only, this requires loading Domeinen.xlsx as well.
INCLUDE "grondslagen.xlsx"   -- If you want to run with the population of ProductCodes.xlsx only, this requires loading Domeinen.xlsx as well.
INCLUDE "Processen.xlsx"     -- If you want to run with the population of processen.xlsx only, this requires loading Domeinen.xlsx and grondslagen.xlsx as well.
INCLUDE "Medewerkers.xlsx"   -- If you want to run with the population of Medewerkers.xlsx only, this requires loading Domeinen.xlsx as well.
INCLUDE "Requirements.xlsx"  -- If you want, you can run with the population of Medewerkers.xlsx only.
INCLUDE "Projecten.xlsx"     -- If you want, you can run with the population of Projecten.xlsx only.
INCLUDE "Interventiebeleid tabak.xlsx"  -- If you want, you can run with the population of "Interventiebeleid tabak.xlsx" only.

REPRESENT Tekst TYPE BIGALPHANUMERIC

PATTERN Organisatie
VIEW Eenheid : Eenheid (afkorting)
RELATION naam[Eenheid*Tekst] [UNI]
RELATION grond[Eenheid*RechtsGrond]
RELATION afkorting[Eenheid*Tekst] [UNI]
RELATION divisienaam[Divisie*Tekst] [UNI,TOT]
RELATION eenheidnr[Divisie*Eenheid] [UNI,TOT]
ENDPATTERN

PATTERN Projecten
RELATION projectnaam[Project*Tekst] [UNI]
VIEW Project : Project(projectnaam)
RELATION omschrijving[Project*Tekst] [UNI]
RELATION resultaat[Project*Tekst] [UNI]  -- Bijdrage aan resultaat
RELATION verbetering[Project*Tekst] [UNI]  -- Bijdrage aan C&V
RELATION inhoudKaderbrief[Project*Tekst] [UNI]
RELATION onderwerpKaderbrief[Project*Tekst] [UNI]
RELATION doelgroep[Project*Doelgroep]
RELATION eenheid[Project*Tekst] [UNI]
RELATION wanneer[Project*Tekst] [UNI]
RELATION werkinstructie[Project*Bool] [UNI,TOT]
REPRESENT Bool TYPE BOOLEAN
RELATION evaluatiemoment[Project*Tekst] [UNI]
RELATION rapportagemoment[Project*Tekst] [UNI]
RELATION projectleider[Project*Persoon]
RELATION contact[Project*Persoon]
RELATION protocol[Project*Protocol] [UNI]
RELATION tijdschrijfcode[Project*Tijdschrijfcode]
RELATION werkpakketcode[Project*WerkpakketCode] [UNI]
ENDPATTERN


PATTERN Kwaliteitssystemen
RELATION naam[Kwaliteitssysteem*Tekst] [UNI]
RELATION type[Kwaliteitssysteem*Kstype] [UNI]
RELATION schemabeheerder[Kwaliteitssysteem*Rechtspersoon] [UNI]
RELATION convenant[Kwaliteitssysteem*Document] [UNI]
RELATION contactNVWA[Kwaliteitssysteem*Persoon] [UNI]
RELATION contactSB[Kwaliteitssysteem*Persoon] [UNI]
RELATION ingang[Kwaliteitssysteem*Datum] [UNI]
RELATION opgezegd[Kwaliteitssysteem*Datum] [UNI]

RELATION datum[Certificaat*Datum] [UNI]
RELATION geldigVan[Certificaat*Datum] [UNI]
RELATION geldigTot[Certificaat*Datum] [UNI]
RELATION subject[Certificaat*Rechtspersoon] [UNI]
RELATION lv[Certificaat*Rechtspersoon] [UNI]
RELATION conform[Certificaat*Kwaliteitssysteem] [UNI]
RELATION toetsRapportage[Certificaat*Document] [UNI]

RELATION voor[Erkenning*Kwaliteitssysteem] [UNI]
RELATION subject[Erkenning*Rechtspersoon] [UNI]
RELATION lv[Erkenning*Rechtspersoon] [UNI]
ENDPATTERN

PATTERN Interventies
RELATION titel[Interventieregel*Titel] [UNI]
REPRESENT Titel TYPE BIGALPHANUMERIC
RELATION rechtsgrond[Interventieregel*RechtsGrond] [UNI]
RELATION onderwerp[Interventieregel*Rechtspersoonsoort]
RELATION regelsoort[Interventieregel*Regelsoort] [UNI]
RELATION gezegde[Interventieregel*Praedicaat] [UNI]
RELATION lv[Interventieregel*Concept] [UNI]
RELATION mv[Interventieregel*Concept] [UNI]
RELATION norm[Interventieregel*Tekst] [UNI]
RELATION controleItem[Interventieregel*Concept] [UNI]
RELATION typeInspecteur[Interventieregel*Tekst] [UNI]
RELATION overtreding[Interventieregel*Tekst] [UNI]
RELATION overschrijding[Interventieregel*Tekst] [UNI]
RELATION ernst[OvertredingsKlasse*OvertredingsErnst] [UNI,TOT]
RELATION klasse[Interventieregel*OvertredingsKlasse]
RELATION motivatie[Interventieregel*Tekst] [UNI]
RELATION interventie[Interventieregel*InterventieSoort] [UNI]
RELATION followup[Interventieregel*Tekst] [UNI]
RELATION opmerking[Interventieregel*Tekst] [UNI]
-- RULE rechtsgrond~;titel |- rechtsgrond~;regeling;titel -- Aan deze regel is op 15 febr. voldaan geweest.
ENDPATTERN

PATTERN Zaken
RELATION zaaknummer[Zaak*Zaaknummer] [TOT,INJ]  -- Een zaak kan meerdere zaaknummers hebben. Hiermee kan het samenvoegen van zaken plaatsvinden.
REPRESENT Zaaknummer TYPE INTEGER
RELATION zaaktype[Zaak*Zaaktype] [UNI]
RELATION domein[Zaak*Domein] [UNI,TOT]
RELATION behandelaar[Zaak*Persoon] [UNI,TOT]
RELATION start[Zaak*Datum] [UNI,TOT]
RELATION project[Zaak*Project] [UNI] -- De taken van de NVWA staan in Art XVII lid 2 Besluit mandaat, volmacht en machtiging van het Ministerie van Economische Zaken 2015
RELATION dossier[Zaak*Document]
RELATION afgedaan[Zaak*Datum] [UNI]
RULE "Domein van een zaak" : project;werkpakketcode;domein |- domein
MEANING "Als een zaak uit een project voorkomt, dan is het domein van die zaak gelijk aan het domein van dat project."
VIOLATION (TXT "{EX} InsPair;domein;Zaak;", SRC I, TXT ";Domein;", SRC I)
ROLE ExecEngine MAINTAINS "Domein van een zaak"
ENDPATTERN

PATTERN Personen
VIEW Persoon : Persoon (volledigenaam)
RELATION naam[Rol*Tekst] [UNI]
RELATION grond[Rol*RechtsGrond]
RELATION persoon[Dienstverband*Persoon] [UNI]
--REPRESENT Dienstverband TYPE ALPHANUMERIC
RELATION team[Dienstverband*Eenheid] [UNI]
RELATION functie[Dienstverband*Functie] [UNI]
RELATION contract[Dienstverband*Contractsoort] [UNI]
RELATION wtrCategorie[Dienstverband*WTRcat] [UNI]
RELATION wtr4[Dienstverband*WTRcat] [UNI]
RELATION volledigenaam[Persoon*Tekst] [UNI]
RELATION naam[Eenheid*Tekst] [UNI]
RELATION sub[Eenheid*Eenheid] [UNI]
RELATION divisie[Eenheid*Divisie] [UNI]
VIEW Functie : Functie (functienaam)
RELATION functienaam[Functie*Functienaam] [UNI]
RELATION functiekwal[Functie*Kwalificatie] [UNI]
RELATION functie[Functie*Functienaam] [UNI]
RELATION functiegebied[Functie*Functiegebied] [UNI]
ENDPATTERN

PATTERN Zaaktypen
RELATION grond[Zaaktype*RechtsGrond]
RELATION naam[Zaaktype*Tekst] [UNI,TOT]
VIEW Zaaktype : Zaaktype (naam)
RELATION definitie[Zaaktype*Tekst] [UNI]
RELATION pf[Zaaktype*Procesflow]
RELATION grond[Taak*RechtsGrond] -- De taken van de NVWA staan in Art XVII lid 2 Besluit mandaat, volmacht en machtiging van het Ministerie van Economische Zaken 2015
RELATION naam[Taak*Tekst] [UNI,TOT]
ENDPATTERN

PATTERN Overtredingen
RELATION omschrijving [Overtreding*Tekst] [UNI]       -- als bedoeld in Art. 5:9 sub a. Awb
RELATION voorschrift [Overtreding*RechtsGrond]        -- als bedoeld in Art. 5:9 sub a. Awb
RELATION plaats [Overtreding*Plaatsaanduiding] [UNI]  -- als bedoeld in Art. 5:9 sub b. Awb
RELATION tijdstip [Overtreding*Tijdsaanduiding] [UNI] -- als bedoeld in Art. 5:9 sub b. Awb
RELATION geconstateerd [Overtreding*Persoon]          -- als bedoeld in Art. 5:9 Awb
RELATION overtreder [Overtreding*Persoon] [UNI]       -- als bedoeld in Art. 5:1 lid 2 Awb
RELATION uitgereikt [Overtreding*Datum] [UNI]         -- als bedoeld in Art. 5:48 lid 3 Awb
ENDPATTERN

PATTERN Klachten
CLASSIFY Klacht ISA Zaak
RELATION indiener [Klacht*Persoon]             -- als bedoeld in Art. 9:4 lid 2 sub a. AwB
RELATION dagtekening [Klacht*Datum] [UNI]      -- als bedoeld in Art. 9:4 lid 2 sub b. AwB
RELATION omschrijving [Klacht*Tekst] [UNI]     -- als bedoeld in Art. 9:4 lid 2 sub c. AwB
RELATION klaagschrift [Klacht*Document] [UNI]  -- als bedoeld in Art. 5:9 sub a. Awb
RELATION stuk [Klacht*Document]                -- als bedoeld in Art. 5:9 sub b. Awb
RELATION beklaagde [Klacht*Persoon]            -- 
RELATION klager [Klacht*Persoon]               -- 
RELATION hoorzitting [Klacht*Zitting]          -- 
ENDPATTERN

PATTERN Inspecties
CLASSIFY Inspectie ISA Zaak
RELATION object [Inspectie*Object]
RELATION overtreding[Inspectie*Overtreding]
ENDPATTERN

PATTERN Requirements
RELATION subject [Requirement*Rol]
RELATION gezegde [Requirement*Praedicaat] [UNI]
RELATION lv [Requirement*Concept] [UNI]
RELATION omschrijving [Requirement*Tekst] [UNI]
RELATION rol [Requirement*Rol]
RELATION wens [Requirement*Tekst] [UNI]
RELATION doel [Requirement*Tekst] [UNI]
RELATION auteur [Requirement*Tekst]
RELATION processtap [Requirement*Processtap] [UNI]
RELATION opmerking [Requirement*Tekst] [UNI]
RELATION procesflow [Requirement*Procesflow] [UNI]
RELATION url[RechtsGrond*URL]
ENDPATTERN

PATTERN Wetsartikelen
VIEW RechtsGrond : RechtsGrond DEFAULT {url:url, regelingTitel:regeling;titel, artikel:artikel, lid:lid} HTML TEMPLATE "Rechtsgrondlink.html" ENDVIEW
RELATION url[RechtsGrond*URL]
RELATION regeling[RechtsGrond*Regeling] [UNI]
RELATION bijlage[RechtsGrond*Bijlage] [UNI]
RELATION aanwijzing[RechtsGrond*Aanwijzing] [UNI]
RELATION artikel[RechtsGrond*Artikel]
RELATION lid[RechtsGrond*Lid]
RELATION sub[RechtsGrond*Sub]
RELATION bwb[Regeling*BasisWettenBestand] [UNI]
RELATION titel[Regeling*Titel] [UNI,TOT]
RELATION artikelen[Regeling*Aantal] [UNI]
REPRESENT Aantal TYPE INTEGER
RELATION url[Regeling*URL]
RELATION gedelegeerdUit[Regeling*Regeling] [UNI]
RELATION regelgeving[Domein*Regeling]
RELATION soort[Regeling*Regelingsoort] [UNI]
RELATION onderwerp[Regeling*Concept]
RELATION procesflow [Regeling*Procesflow] [UNI]
RELATION afkorting[Regeling*Tekst] [UNI]
VIEW Domein : Domein (domeinnaam)
ENDPATTERN

PATTERN Procesflows
RELATION naam[Procesflow*Tekst] [UNI] -- INTERFACE "Procesflows zonder naam"  signaleert het overtreden van totaliteit.
RELATION niveau[Procesflow*Tekst] [UNI] -- INTERFACE "Procesflows zonder niveau" signaleert het overtreden van totaliteit.
RELATION uren[Procesflow*Aantal] [UNI]
RELATION fte[Procesflow*Aantal] [UNI]
RELATION voorziening[Procesflow*Voorziening]
RELATION taak[Procesflow*Taak]
ENDPATTERN

PATTERN Handelingen
RELATION omschrijving[Handeling*Tekst] [UNI]
RELATION domein[Handeling*Domein]
RELATION bron[Handeling*Regeling] [UNI]
RELATION gezegde[Handeling*Praedicaat]
RELATION lv[Handeling*Concept]
RELATION regeling[Handeling*Regeling]
RELATION grondslag[Handeling*RechtsGrond]
RELATION periode[Handeling*Periode] [UNI]
RELATION opmerking[Handeling*Tekst] [UNI]
RELATION producttype[Handeling*Producttype]
RELATION waardering[Handeling*Waardering] [UNI]
ENDPATTERN

PATTERN Applicaties
RELATION onderdeel[Component*Voorziening]
RELATION ondersteunt[Component*RechtsGrond]
RELATION componentnaam[Component*Tekst] [UNI,TOT]
RELATION regelingen[Component*Regeling]
RULE Regelingenscope : ondersteunt;regeling |- regelingen
VIOLATION (TXT "Scope fout! De component ", SRC componentnaam, TXT " kan ", SRC ondersteunt, TXT " niet ondersteunen omdat \"", TGT titel, TXT "\" niet in de regelingen van deze component is opgenomen.")
ENDPATTERN

PATTERN Producten
RELATION werkpakketcode[ProductDienstCode*WerkpakketCode] [UNI,TOT]
RELATION productcode[ProductDienstCode*ProductCode] [UNI,TOT]
RELATION opdrachtgever[ProductDienstCode*Opdrachtgever] [UNI,TOT]

RELATION werkpakketnaam[WerkpakketCode*Tekst] [UNI,TOT]
RELATION domein[WerkpakketCode*Domein] [UNI,TOT]
RELATION grondslag[WerkpakketCode*RechtsGrond] [UNI]
RELATION werkpakketproces[WerkpakketCode*Procesflow] [UNI]

RELATION domeinnaam[Domein*Tekst] [UNI,TOT]
RELATION divisie[Domein*Divisie] [UNI,TOT]

RELATION pdc[Doelstelling*ProductDienstCode] [UNI,TOT]
RELATION jaar[Doelstelling*Jaartal] [UNI,TOT]
RELATION gepland[Doelstelling*Uren] [UNI]
REPRESENT Uren TYPE INTEGER

RELATION productnaam[ProductCode*Tekst] [UNI,TOT]
VIEW WerkpakketCode : WerkpakketCode(I, TXT ": ", werkpakketnaam)

RELATION productnaam[ProductCode*Tekst] [UNI,TOT]
ENDPATTERN

VIEW Component : Component(componentnaam)

INTERFACE Component {-(componentnaam, ondersteunt, regelingen)-} FOR Architect : I[Component] BOX
    [ naam : componentnaam
    , ondersteunt : ondersteunt
    , "regeling(en)" : regelingen
    ]

INTERFACE Werkpakket {-(werkpakketnaam)-} : I[WerkpakketCode] BOX
    [ werkpakket : werkpakketnaam
    , domein : domein
    , divisie : domein;divisie
    , grondslag : grondslag
    , werkpakketproces : werkpakketproces
    , "PDC's" : werkpakketcode[ProductDienstCode*WerkpakketCode]~
    , productcode : werkpakketcode~;productcode
    , opdrachtgever : werkpakketcode~;opdrachtgever
    , doelstelling : werkpakketcode~;pdc~ BOX<SCOLS>
        [ doelstelling : I
        , "geplande uren" : gepland
        ]
    ]

INTERFACE Voorziening {-(onderdeel, ondersteunt, regelingen)-} FOR Architect : I[Voorziening] BOX
            [ Componenten : onderdeel~
            , "regeling(en)" : onderdeel~;regelingen
            , "te onderhouden" : onderdeel~ BOX<SCOLS>
                [ regels : ondersteunt
                ]
            ]

INTERFACE VoorzieningUser {-(onderdeel)-} FOR User : I[Voorziening] BOX
            [ "Voorziening" : I
            , Componenten : onderdeel~
            , grondslagen : onderdeel~;ondersteunt
            ]

VIEW Regeling : Regeling(titel)

INTERFACE Regeling {-(titel[Regeling*Titel], regeling[RechtsGrond*Regeling], regelgeving, url[Regeling*URL])-} FOR User : I[Regeling] ROWS
    [ Regeling : titel
    , afkorting : afkorting
    , domein : regelgeving~
    , URL : url
    ]

INTERFACE "Regeling details" {-(titel[Regeling*Titel], regeling[RechtsGrond*Regeling], regelgeving, artikel[RechtsGrond*Artikel], url[Regeling*URL])-} FOR Jurist : I[Regeling] ROWS
    [ Regeling : titel
    , Artikel  : regeling[RechtsGrond*Regeling]~ COLS [ artikel : artikel]
    , URL      : url
    , domein : regelgeving~
    , "voorziening(en)" : regelingen~;onderdeel
    , "component(en)" : regelingen~;ondersteunt
    ]

INTERFACE Interventieregel {-(rechtsgrond, onderwerp[Interventieregel*Rechtspersoonsoort], regelsoort, gezegde[Interventieregel*Praedicaat], lv[Interventieregel*Concept], mv, norm, overtreding[Interventieregel*Tekst], overschrijding, klasse, motivatie, interventie, followup, opmerking[Interventieregel*Tekst])-}
FOR TO : I[Interventieregel] ROWS
    [ rechtsgrond	    : rechtsgrond
    , regelsoort	    : regelsoort
    , onderwerp	        : onderwerp
    , gezegde	        : gezegde
    , lv	            : lv
    , mv	            : mv
    , norm	            : norm
    , overtreding	    : overtreding
    , overschrijding	: overschrijding
    , klasse	        : klasse
    , motivatie	        : motivatie
    , interventie	    : interventie
    , followup	        : followup
    , opmerking	        : opmerking
    ]

INTERFACE Interventiebeleid FOR TO : '_SESSION';V[SESSION*RechtsGrond];(I/\rechtsgrond~;I[Interventieregel];rechtsgrond) BOX<SCOLS>
    [ rechtsgrond	        : I
    , ""                    : rechtsgrond[Interventieregel*RechtsGrond]~ BOX<SCOLS>
        [ interventieregel  : I
        , norm	            : norm
        , overtreding	    : overtreding
        , klasse	        : klasse
        , opmerking	        : opmerking
        ]
    ]

INTERFACE InterventiebeleidArch FOR Architect : '_SESSION';V[SESSION*RechtsGrond];(I/\rechtsgrond~;I[Interventieregel];rechtsgrond) BOX<SCOLS>
    [ rechtsgrond	        : I
    , ""                    : rechtsgrond[Interventieregel*RechtsGrond]~ BOX<SCOLS>
        [ interventieregel  : I
        , norm	            : norm
        , overtreding	    : overtreding
        , klasse	        : klasse
        , onderwerp	        : onderwerp
        , gezegde	        : gezegde
        , lv        	    : lv  -- concepten, later uit te breiden met requirements:   `;lv[Requirement*Concept]~`
        , mv	            : mv
        ]
    ]

INTERFACE Handelingen {-(gezegde[Handeling*Praedicaat], lv[Handeling*Concept], opmerking[Handeling*Tekst])-}
FOR ProcesModelleur : V[SESSION*Handeling] BOX<SCOLS>
            [ naam         : omschrijving[Handeling*Tekst]
            , domein       : domein
--            , bron         : bron
            , periode      : periode[Handeling*Periode]
            , opmerking    : opmerking[Handeling*Tekst]
            , documenttype : producttype[Handeling*Producttype]
            , waardering   : waardering[Handeling*Waardering]
            , gezegde      : gezegde[Handeling*Praedicaat]
            , voorwerp     : lv[Handeling*Concept]
            ]

INTERFACE HandelingenPerDomein FOR ProcesModelleur : '_SESSION';V[SESSION*Domein] BOX<SCOLS>
      [ Domein : domeinnaam
      , "" : domein~ BOX<SCOLS>
            [ omschrijving : omschrijving[Handeling*Tekst]
            , bron         : bron
--            , gezegde     : gezegde[Handeling*Praedicaat]
--            , voorwerp    : lv[Handeling*Concept]
--            , periode     : periode[Handeling*Periode]
--            , opmerking   : opmerking[Handeling*Tekst]
--            , producttype : producttype[Handeling*Producttype]
--            , waardering  : waardering[Handeling*Waardering]
            ]
      ]

INTERFACE RegelingenPerDomein FOR TO : I[Domein]/\regelgeving;regelgeving~ BOX<SCOLS>
      [ Domein : I
      , "" : regelgeving BOX<SCOLS>
            [ Regeling        : I
            , ""  : regeling~ BOX<SCOLS>
                  [ Bepaling        : I[RechtsGrond]
                  , beleidsregels   : rechtsgrond~
                  ]
            ]
      ]

INTERFACE Regelingen FOR User : '_SESSION';V[SESSION*Regeling] BOX<SCOLS>
    [ Regeling : titel
    , soort : soort
    , handelingen : regeling~;grondslag~ BOX<SCOLS>
            [ naam        : omschrijving[Handeling*Tekst]
            , gezegde     : gezegde[Handeling*Praedicaat]
            , voorwerp    : lv[Handeling*Concept]
            , periode     : periode[Handeling*Periode]
            , opmerking   : opmerking[Handeling*Tekst]
            , producttype : producttype[Handeling*Producttype]
            , waardering  : waardering[Handeling*Waardering]
            ]
    , afkorting : afkorting
    , domein : regelgeving~
    , URL : url
    ]

INTERFACE Zaken FOR User : '_SESSION';V[SESSION*Zaak] BOX<SCOLS>
            [ zaaknummer : zaaknummer
            , zaaktype : zaaktype
            , domein : domein
            , behandelaar : behandelaar
            , startdatum : start
            , project : project
            ]

INTERFACE Zaak {-(zaaknummer, zaaktype, domein[Zaak*Domein], behandelaar, start, afgedaan, project)-} FOR User: I[Zaak] BOX<SCOLS>
            [ zaaknummer : zaaknummer
            , zaaktype : zaaktype
            , domein : domein
            , behandelaar : behandelaar
            , startdatum : start
            , project : project
            , afgedaan : afgedaan
            ]

INTERFACE Rollen {-(naam[Rol*Tekst], omschrijving[Requirement*Tekst], wens, doel, opmerking[Requirement*Tekst])-} FOR Architect : '_SESSION';V[SESSION*Rol] ROWS
      [ rol : naam
      , rechtsgrond : grond
      , eisen : rol~ BOX<SCOLS>
            [ activiteit : omschrijving
            , "ik wil"     : wens
            , opdat         : doel
            , opmerking    : opmerking
            ]
      ]

INTERFACE Zaaktypes {-(naam[Zaaktype*Tekst], definitie)-} FOR Architect : '_SESSION' BOX<SCOLS>
      [ gedefinieerd : V[SESSION*Zaaktype];(I/\definitie;definitie~) BOX<SCOLS>
            [ Zaaktype : naam
            , definitie : definitie
            , grond : grond
            ]
      , ongedefinieerd : V[SESSION*Zaaktype];(I-definitie;definitie~) BOX<SCOLS>
            [ Zaaktype : naam
            , definitie : definitie
            , grond : grond
            ]
      ]

INTERFACE Procesflow {-(naam[Procesflow*Tekst],niveau,voorziening)-} FOR Architect : I[Procesflow] BOX<SCOLS>
            [ Procesflow : I
            , naam : naam
            , niveau : niveau
            , stappen : procesflow~;processtap
            , "Rechtsgronden" : werkpakketproces~;grondslag
            , "uren 2013" : uren
            , voorzieningen : voorziening
            ]

INTERFACE Procesflows FOR Architect : '_SESSION';V[SESSION*Procesflow] BOX<SCOLS>
            [ Procesflow : I
            , naam : naam
            , niveau : niveau
            , stappen : procesflow~;processtap
            , "uren 2013" : uren
            , voorzieningen : voorziening
            ]

INTERFACE Bronnen FOR Jurist : '_SESSION';V[SESSION*Regeling] BOX<SCOLS>
            [ Regeling : titel
            , domein : regelgeving~
            , Artikel : regeling~;I[RechtsGrond];artikel
            , URL : url
            ]

INTERFACE Rechtsgrond FOR TO: I[RechtsGrond] BOX
    [ regeling   : regeling
    , artikel    : artikel
    , lid        : lid
    , sub        : sub
    , url        : url
    , bijlage    : bijlage
    , aanwijzing : aanwijzing
    ]

INTERFACE Wetsartikelen FOR Jurist : '_SESSION';V[SESSION*Domein] BOX<SCOLS>
      [ domeinnaam : domeinnaam
      , "" : regelgeving BOX<SCOLS>
            [ Regeling : titel
            , Artikel : regeling~;I[RechtsGrond];artikel
            , URL : url
            ]
      ]

INTERFACE Requirements FOR Architect : '_SESSION';V[SESSION*Requirement] BOX<SCOLS>
            [ requirement  : I
            , omschrijving : omschrijving
            , rol          : rol
            , wens         : wens
            , doel         : doel
            , handelingen  : (gezegde;gezegde~ /\ lv;lv~);omschrijving[Handeling*Tekst]
            , processtap   : processtap
            , opmerking    : opmerking
            , procesflow   : procesflow
            , subject      : subject
            , gezegde      : gezegde
            , lv           : lv
            ]

INTERFACE Componenten FOR Architect : '_SESSION';V[SESSION*Voorziening] BOX<SCOLS>
            [ "Voorziening" : I
            , Componenten : onderdeel~
            , grondslagen : onderdeel~;ondersteunt
            ]

INTERFACE Medewerkers FOR User : '_SESSION';V[SESSION*Dienstverband] BOX<SCOLS>
            [ naam : persoon;volledigenaam
            , functie : functie
            , team : team
            , divisie : team;divisie;divisienaam
            ]

INTERFACE Projecten FOR TO : '_SESSION';V[SESSION*Domein];(I/\domein~;werkpakketcode~;V[Project*Domein]) BOX<SCOLS>
     [ domein : I
     , "" : domein~;werkpakketcode~ BOX<SCOLS>
            [ project	            : I
            , omschrijving	        : omschrijving
            , doelgroep	            : doelgroep
            , eenheid	            : eenheid
            , projectleider	        : projectleider
            , contact	            : contact
            , protocol	            : protocol
            , tijdschrijfcode	    : tijdschrijfcode
            ]
     ]

INTERFACE "Maak Project"
      {-( projectnaam
      , omschrijving[Project*Tekst]
      , resultaat
      , verbetering
      , inhoudKaderbrief
      , onderwerpKaderbrief
      , doelgroep
      , eenheid
      , wanneer
      , werkinstructie
      , evaluatiemoment
      , rapportagemoment
      , projectleider
      , contact
      , protocol
      , tijdschrijfcode
      , werkpakketcode[Project*WerkpakketCode]
      ) -}FOR Projectinrichter : I[Project] ROWS
            [ projectnaam	        : projectnaam
            , omschrijving	        : omschrijving
            , resultaat	            : resultaat
            , verbetering	        : verbetering
            , inhoudKaderbrief	    : inhoudKaderbrief
            , onderwerpKaderbrief	: onderwerpKaderbrief
            , doelgroep	            : doelgroep
            , eenheid	            : eenheid
            , wanneer	            : wanneer
            , werkinstructie	    : werkinstructie
            , evaluatiemoment	    : evaluatiemoment
            , rapportagemoment	    : rapportagemoment
            , projectleider	        : projectleider
            , contact	            : contact
            , protocol	            : protocol
            , tijdschrijfcode	    : tijdschrijfcode
            , werkpakketcode	    : werkpakketcode
            , domein	            : werkpakketcode;domein
            , zaken : project~
            , zaaktypen : project~;zaaktype
            , behandelaars : project~;behandelaar
            , startdatum : project~;start
            , afgedaan : project~;afgedaan
            ]

INTERFACE Project FOR TO : I[Project] ROWS
            [ project	        : I
            , omschrijving	        : omschrijving
            , werkpakketcode	    : werkpakketcode
            , domein	            : werkpakketcode;domein
            , regelgeving	            : werkpakketcode;domein;regelgeving
            , doelgroep	            : doelgroep
            , eenheid	            : eenheid
            , projectleider	        : projectleider
            , contact	            : contact
            , zaaktypen : project~;zaaktype
            , behandelaars : project~;behandelaar
            , startdatum : project~;start
            , afgedaan : project~;afgedaan
            ]

INTERFACE "Requirements zonder handelingen" FOR Controle : '_SESSION' BOX<SCOLS>
    [ "" : V[SESSION*Requirement]-V[SESSION*Handeling];(gezegde;gezegde~ /\ lv;lv~) BOX<SCOLS>
            [ requirement  : I
            , omschrijving : omschrijving
            , rol          : rol
            , wens         : wens
            , doel         : doel
            , handelingen  : (gezegde;gezegde~ /\ lv;lv~);omschrijving[Handeling*Tekst]
            , processtap   : processtap
            , procesflow   : procesflow
            , subject      : subject
            , gezegde      : gezegde
            , lv           : lv
            ]
    ]

INTERFACE "Handelingen zonder requirements" FOR Controle : '_SESSION' BOX<SCOLS>
    [ "" : V[SESSION*Handeling]-V[SESSION*Requirement];(gezegde;gezegde~ /\ lv;lv~) BOX<SCOLS>
            [ naam        : omschrijving[Handeling*Tekst]
            , gezegde     : gezegde[Handeling*Praedicaat]
            , voorwerp    : lv[Handeling*Concept]
            , producttype : producttype[Handeling*Producttype]
            ] 
    ]

--RELATION naam[Procesflow*Tekst] [UNI,TOT]
INTERFACE "Procesflows zonder naam" FOR Controle : '_SESSION' BOX<SCOLS>
    [ "" : V[SESSION*Procesflow]-V[SESSION*Tekst];naam~ ]

--RELATION niveau[Procesflow*Tekst] [UNI,TOT]
INTERFACE "Procesflows zonder niveau" FOR Controle : '_SESSION' BOX<SCOLS>
    [ "" : V[SESSION*Procesflow]-V[SESSION*Tekst];niveau~ ]

INTERFACE "Dienstverbanden zonder team" FOR Controle : '_SESSION' BOX<SCOLS>
    [ "" : V[SESSION*Dienstverband]-V[SESSION*Eenheid];team~ ]

--RELATION werkinstructie[Project*Bool] [UNI,TOT]
INTERFACE "Projecten zonder werkinstructie" FOR Controle : '_SESSION' BOX<SCOLS>
    [ "" : V[SESSION*Project]/\V[SESSION*Bool];'FALSE';werkinstructie~ ]

INTERFACE "Dienstverbanden zonder functie" FOR Controle : '_SESSION' BOX<SCOLS>
    [ "" : V[SESSION*Dienstverband]-V[SESSION*Functie];functie~ ]

INTERFACE "Externen" FOR Controle : '_SESSION' BOX<SCOLS>
    [ "" : V[SESSION*Dienstverband]-V[SESSION*Contractsoort];contract~ ]

INTERFACE "Dienstverbanden zonder wtrCategorie" FOR Controle : '_SESSION' BOX<SCOLS>
    [ "" : V[SESSION*Contractsoort];contract~ - V[SESSION*WTRcat];wtrCategorie~ ]

ENDCONTEXT