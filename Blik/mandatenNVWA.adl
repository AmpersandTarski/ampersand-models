 CONTEXT Mandatering IN DUTCH
 INCLUDE "mandatenNVWA.xlsx"
  META "authors" "Stef Joosten"


 PURPOSE CONTEXT Mandatering LATEX
 {+ In Nederland heeft elke minister de bevoegdheid om alles te doen en laten wat in zijn of haar portefeuille passend acht, en verantwoordt zich daarvoor in de Tweede Kamer.
    Echter, elke minister heeft slechts twee handen, maar gelukkig wel een aantal ambtenaren die namens de minister kunnen handelen.
    Welke ambtenaar waartoe bevoegd is wordt geregeld met mandaten, die verankerd zijn in in Afdeling 10:1.1. Awb.
    Maar hoe weet de burger of een ambtenaar heeft gehandeld binnen of buiten zijn mandaat?
    Voor dat doel worden mandaten gepubliceerd in de Staatscourant (op grond van Artikel 3:42 lid 1 Awb).
    Omdat elk bestuursorgaan zijn eigen mandaatregeling kent, kan de burger precies nagaan langs welk pad van mandaten de rechtmatigheid van een willekeurige overheidsbeslissing tot stand komt.
-}

PATTERN Wetsartikelen
RELATION url[RechtsGrond*URL] [UNI]
RELATION regeling[RechtsGrond*Regeling] [UNI]
RELATION titel[Regeling*Titel] [UNI,TOT]
REPRESENT Artikel TYPE ALPHANUMERIC
RELATION artikel[RechtsGrond*Artikel] [UNI]
RELATION lid[RechtsGrond*Lid] [UNI]
RELATION bijlage[RechtsGrond*Bijlage] [UNI]
RELATION aanwijzing[RechtsGrond*Aanwijzing] [UNI]
RELATION sub[RechtsGrond*Sub] [UNI]
ENDPATTERN

  PURPOSE PATTERN Mandaten
 {+
 Deze analyse beschrijft de taal van de Mandaatregeling VWS en het Besluit mandaat, volmacht en machtiging EZ 2015, voor zover van toepassing op de NVWA.
 Deze regelingen zijn verankerd in Afdeling 10:1.1. Awb.
 Aan de orde is de vraag of alle mandaten, die uit deze regelingen voortvloeien, zijn verwerkt in het ontwerp van "Blik".

 Waar mogelijk verwijst de tekst naar de bron, zodat de lezer afwijkingen die hij of zij constateert kan traceren.
 Daar waar de tekst afwijkt van de bron is die bron uiteraard meer gezaghebbend dan deze tekst.
 -}
   
  PATTERN Mandaten

  -- Concepten --

   PURPOSE CONCEPT Mandaat
   {+Een door de gemandateerde binnen de grenzen van zijn bevoegdheid genomen besluit geldt als een besluit van de mandaatgever (artikel 10:2 Awb, Artikel 4 Mandaatregeling VWS). Mandaat heeft altijd betrekking op publiekrechtelijke rechtshandelingen, bijvoorbeeld het verlenen van een vergunning of het verlenen van een subsidie. Bij mandaat gaat het dus om bevoegdheidsuitoefening, waarvoor het mandaterende bestuursorgaan verantwoordelijk blijft en waarover het ook de zeggenschap blijft houden. Er vindt dus geen verschuiving van bevoegdheden en verantwoordelijkheden plaats. (zie http://decentrale.regelgeving.overheid.nl/)-}

   CONCEPT Mandaat "Een mandaat is de bevoegdheid om in naam van een bestuursorgaan besluiten te nemen." "Art 10:1 Awb"
   CONCEPT Mandaat "Een mandaat is de bevoegdheid om in naam van de Minister van Volksgezondheid, Welzijn en Sport besluiten te nemen." "art. 1 Mandaatregeling VWS"

   PURPOSE CONCEPT Volmacht
   {+Volgens het Burgerlijk wetboek  (). Een door de gevolmachtigde binnen de grenzen van zijn bevoegdheid in naam van de volmachtgever verrichte rechtshandeling treft in haar gevolgen de volmachtgever (artikel 3:66 lid 1 Burgerlijk wetboek). Volmacht heeft altijd betrekking op privaatrechtelijke rechtshandelingen, bijvoorbeeld het sluiten van een onderhoudsovereenkomst, het aanschaffen van boeken of het kopen van grond. Op grond van artikel 160 lid 1 sub e is het college bevoegd tot het verrichten van privaatrechtelijke rechtshandelingen. (zie http://decentrale.regelgeving.overheid.nl/)-}

   CONCEPT Volmacht "Onder volmacht wordt verstaan: de bevoegdheid die een volmachtgever verleent aan een ander, de gevolmachtigde, om in zijn naam rechtshandelingen te verrichten." "Art. 3:60 lid 1 Burgerlijk wetboek"

   PURPOSE CONCEPT Machtiging
   {+Van machtiging is sprake bij het verrichten van feitelijke handelingen. Feitelijke handelingen zijn geen privaatrechtelijke rechtshandelingen of besluiten als bedoeld in artikel 1:3 van de Awb. Feitelijke handelingen zijn bijvoorbeeld het geven van informatie, het versturen van een ontvangstbevestiging, de gemeente vertegenwoordigen bij de rechtbank, het bijhouden van de administratie of het blussen van een brand. (zie http://decentrale.regelgeving.overheid.nl/)-}

   CONCEPT Machtiging "Onder machtiging wordt verstaan de bevoegdheid om in naam van de Minister handelingen te verrichten die noch een besluit, noch een privaatrechtelijke rechtshandeling zijn." "Artikel 1 Mandaatregeling VWS"

   CONCEPT Functie "Een Functie is een plek in een organisatie die ingevuld kan worden in dienstverband door een natuurlijke persoon."
   PURPOSE CONCEPT Functie LATEX
   {+   De wet en regelgeving noemt ambtenaren bij functie, zoals ``directeur-generaal Jeugd en Sanctietoepassing van het Ministerie van Veiligheid en Justitie''.
   Door de functie te noemen in plaats van de persoon kan een eventuele opvolger van deze ambtenaar het werk eenvoudig overnemen en de consequenties van besluiten van zijn voorganger op zich nemen.  -}

   CONCEPT Organisatie "Een organisatie is een dienstverband waartoe een bepaalde functie behoort."

   CONCEPT Bevoegdheid "De Awb spreekt van een bevoegdheid om in naam van een bestuursorgaan besluiten te kunnen nemen." "Art 10:1 Awb"

   CONCEPT Beperking "Uit een wettelijk voorschrift kan een beperking in de bevoegdheid voortvloeien" "Art 3:4 Awb"

   CONCEPT Bestuursorgaan "Een bestuursorgaan kan mandaten verlenen" "Art 10:3 Awb"

   PURPOSE CONCEPT RechtsGrond
   {+Om te verwijzen naar wet- en regelgeving gebruiken we het begrip rechtsgrond. We maken gebruik van digitale verwijs-standaarden, zoals juriconnect, Eur-lex en ECLI. -}
   CONCEPT RechtsGrond "Met 'RechtsGrond' wordt bedoeld: een duurzame verwijzing naar een wettelijke bepaling of regeling."

   VIEW RechtsGrond : RechtsGrond DEFAULT {url: url, regelingTitel: regeling;titel, artikel: artikel, lid: lid, sub: sub} HTML TEMPLATE "Rechtsgrondlink.html"
   ENDVIEW


  -- SPECIALIZATIONS --
   PURPOSE RELATION isa[Functie*Functie]
   {+  Om functies als specialisatie te kunnen benoemden, registreert het systeem de generieke en specifieke functies. -}
   RELATION isa[Functie*Functie] [IRF,ASY]
   PRAGMA "Een " " is een "
   MEANING "Van elke functie registreert het systeem de generieke benaming, indien van toepassing."

    RELATION isaStar[Functie*Functie] -- Transitive, reflexive closure of isa, aka isa*
    MEANING "s isa g  means that  s  is  g  or  each element of concept s  is an element of concept  g  as well."
    RELATION isaPlus[Functie*Functie] -- Transitive closure of isa, aka isa+
    MEANING "s isa g  means that each element of concept  s  is an element of concept  g  as well."

    RELATION isaCopy[Functie*Functie] -- necessary only for calling the Transitive closure function.
    ROLE ExecEngine MAINTAINS "Compute transitive closure of isa"
    RULE "Compute transitive closure of isa" : isa = isaCopy
    VIOLATION (TXT "{EX} TransitiveClosure;isa;Functie;isaCopy;isaPlus")

    ROLE ExecEngine MAINTAINS "Compute transitive closure of isa by Ins", "Compute transitive closure of isa by Del"
    RULE "Compute transitive closure of isa by Ins" : isaPlus\/I |- isaStar
    VIOLATION (TXT "{EX} InsPair;isaStar;Functie;", SRC I, TXT ";Functie;", TGT I)
    RULE "Compute transitive closure of isa by Del" : isaStar |- isaPlus\/I
    VIOLATION (TXT "{EX} DelPair;isaStar;Functie;", SRC I, TXT ";Functie;", TGT I)



  -- RELATIONS --

   PURPOSE RELATION bevoegdheid[Bestuursorgaan*Bevoegdheid] LATEX REF "Art 1:3 lid 4 Awb"
   {+  De Awb spreekt van ``een bevoegdheid van een bestuursorgaan.''. Kennelijk kan een bestuursorgaan bevoegdheden hebben. -}
   RELATION bevoegdheid[Bestuursorgaan*Bevoegdheid]
   PRAGMA "" " heeft " " als bevoegdheid"
   MEANING LATEX "De uitspraak ``Bestuursorgaan $b$ heeft $x$ als bevoegdheid.'' behoort tot de gemeenschappelijke taal."

   REPRESENT BigText TYPE BIGALPHANUMERIC
   PURPOSE RELATION omschrijving[Bevoegdheid*BigText]
   {+  Om de juridische omschrijving van een bevoegdheid te kunnen registreren en zonodig aanpassen, is een relatie "omschrijving" nodig. -}
   RELATION omschrijving[Bevoegdheid*BigText] [UNI,TOT]
   PRAGMA "" " heeft " " als omschrijving"
   MEANING "Van elke bevoegdheid registreert het systeem de omschrijving waaronder deze bevoegdheid bij de NVWA geregistreerd is."

   PURPOSE RELATION functienaam[Functie*Tekst]
   {+  Om over functies te kunnen spreken wordt elke functie met één naam aangeduid. -}
   RELATION functienaam[Functie*Tekst] [UNI,TOT]
   PRAGMA "" " heeft " " als naam"
   MEANING "Van elke functie registreert het systeem de naam waaronder deze functie bij de NVWA bekend is."

   PURPOSE RELATION functiebenaming[Functie*Tekst]
   {+  In de geldende regelgeving wordt elke functie met één naam aangeduid. -}
   RELATION functiebenaming[Functie*Tekst] [UNI,TOT]
   PRAGMA "" " heeft " " als naam"
   MEANING "Van elke functie registreert het systeem de naam waaronder deze functie in de regelgeving bekend is."

   PURPOSE RELATION grondURL[Functie*RechtsGrond] LATEX
   {+  Om aan te geven dat het bestaan van een functie wettelijk is verankerd, kan de juridische grondslag ervan worden vastgelegd. -}
   RELATION grondURL[Functie*RechtsGrond]
   PRAGMA "De grondslag voor functie "" is vindbaar via "
   MEANING LATEX "Van functies kan een URL naar de juridische grondslag worden geregistreerd."
   RELATION grond[Functie*Wetsartikel]
   PRAGMA "Functie "" bestaat op grond van "
   MEANING LATEX "Van functies kan de juridische grondslag worden geregistreerd."

   RELATION formatie[Organisatie*Functie] [SUR,INJ]
   PRAGMA "" " heeft de " " in dienst"
   MEANING LATEX "De uitspraak ``Organisatie $b$ heeft de (functie) $a$ in dienst.'' behoort tot de gemeenschappelijke taal."

   PURPOSE RELATION namens[Mandaat*Bestuursorgaan] LATEX REF "Art 10:10 Awb"
   {+  De Awb zegt dat een krachtens mandaat genomen besluit vermeldt namens welk bestuursorgaan het besluit is genomen. Dat impliceert dat een besluit altijd in naam van een bestuursorgaan wordt genomen.  -}
   RELATION namens[Mandaat*Bestuursorgaan] [UNI,TOT]  -- ontleend aan Art 10:10 Awb
   PRAGMA "Een "" is verleend namens de "
   MEANING LATEX "De zin ``Mandaat $m$ is verleend in naam van bestuursorgaan $b$.'' behoort tot de gemeenschappelijke taal."

   PURPOSE RELATION mandaatverlener[Mandaat*Bestuursorgaan] LATEX REF "Art 10:3, 10:5 Awb"
   {+De Awb zegt dat een bestuursorgaan mandaat kan verlenen. Daarbij kan worden opgemerkt dat ``De Minister van OCW'' door de AwB ook als bestuursorgaan wordt aangemerkt. -}
   RELATION mandaatverlener[Mandaat*Bestuursorgaan] [UNI]  -- ontleend aan Art 10:3 en 10:5 Awb
   PRAGMA "Een "" is verleend door de "
   MEANING LATEX "De zin ``Mandaat $m$ is verleend door bestuursorgaan $b$.'' behoort tot de gemeenschappelijke taal."

   PURPOSE RELATION mandaatgever[Mandaat*Functie] LATEX REF "Artikelen 10:6-9 Awb"
   {+  De Awb spreekt van een mandaatgever: de ambtenaar die (een deel van) zijn bevoegdheden mandateert aan ondergeschikte ambtenaren. De Mandaatregeling VWS definieert dit als degene die mandaat verleent.-}
   RELATION mandaatgever[Mandaat*Functie] [UNI]
   PRAGMA "Een "" is verleend door de "
   MEANING LATEX "De zin ``Ondermandaat $m$ is verleend door (mandaatgever) $f$.'' behoort tot de gemeenschappelijke taal."

   PURPOSE RELATION gemandateerde[Mandaat*Functie] LATEX REF "Art 10:2 Awb"
   {+De Awb spreekt van een gemandateerde: de ambtenaar die bevoegdheden krijgt van hogerhand. De Mandaatregeling VWS definieert dit als degene aan wie mandaat is verleend. -}
   RELATION gemandateerde[Mandaat*Functie] [TOT]
   PRAGMA "Een "" is verleend aan de "
   MEANING LATEX "Als een mandaat is verleend aan iemand, dan noemen we deze persoon de gemandateerde."

   PURPOSE RELATION mandaatbevoegdheid[Mandaat*Bevoegdheid] LATEX REF "Art 10:6 lid 1 Awb"
   {+  Een bevoegdheid is in de Awb het object (lijdend voorwerp) van mandatering.
   De wetstekst spreekt van ``de gemandateerde bevoegdheid''.
   Kennelijk bestaat er voor elk mandaat een bevoegdheid die gemandateerd wordt.
   In de gemeenschappelijke taal wordt het woord ``volgt'' gebruikt om deze bevoegdheid aan te duiden.  -}
   RELATION mandaatbevoegdheid[Mandaat*Bevoegdheid] [TOT]
   PRAGMA "Uit het "" volgt de bevoegdheid betreffende "
   MEANING LATEX "De bevoegdheid die wordt verleend wordt in de naam van een mandaat expliciet benoemd."

   PURPOSE RELATION mandaatbeperking[Mandaat*Beperking] LATEX REF "Art 10:2 Awb"
   {+  De Awb spreekt van ``een door de gemandateerde binnen de grenzen van zijn bevoegdheid genomen besluit''.
   Dit impliceert dat er beperkingen kunnen gelden voor een mandaat.  -}
   RELATION mandaatbeperking[Mandaat*Beperking]
   PRAGMA "Het "" kent als beperking:  "
   MEANING LATEX "Beperkingen aan een mandaat gesteld worden in de benaming van het mandaat verwoord."

   PURPOSE RELATION voorbehoud[Bevoegdheid*Functie] LATEX REF "Art 4, 6 Organisatie- en Mandaatbesluit IvhO"
   {+  De Awb kent zelf geen voorbehouden in mandaat, maar er is in het Organisatie- en Mandaatbesluit IvhO wél sprake van. Daarom registreren we welke bevoegdheden aan welke functie zijn voorbehouden.  -}
   RELATION voorbehoud[Bevoegdheid*Functie]
   PRAGMA "" " is voorbehouden aan de "
   MEANING LATEX "Sommige bevoegdheden zijn voorbehouden aan iemand, wat betekent dat de bijbehorende bevoegdheden niet verder kunnen worden gemandateerd."

   PURPOSE RELATION vervangt[Functie*Functie] LATEX
   {+ Vervangingsregels behoren ook tot mandaatregelingen. Een organisatiebesluit kan voorschriften bevatten over het vervangen van functionarissen. Dat is ook het geval in het Organisatie- en Mandaatbesluit IvhO. -}
   RELATION vervangt[Functie*Functie]
   PRAGMA "" " mag " " vervangen"
   MEANING LATEX "Het mogen vervangen van elkaar geldt alleen als daar noodzaak toe bestaat, zoals bij verlet of ziekte. Aan vervanging kunnen aanvullende regels zijn gesteld."

   PURPOSE RELATION grondURL[Mandaat*RechtsGrond] LATEX
   {+  Om aan te geven dat het bestaan van een mandaat wettelijk is verankerd, kan de juridische grondslag ervan worden vastgelegd. -}
   RELATION grondURL[Mandaat*RechtsGrond]
   PRAGMA "De grondslag voor mandaat "" is vindbaar via "
   MEANING LATEX "Van mandaten kan een URL naar de juridische grondslag worden geregistreerd."
   RELATION grond[Mandaat*Wetsartikel]
   PRAGMA "Mandaat "" bestaat op grond van "
   MEANING LATEX "Van mandaten kan de juridische grondslag worden geregistreerd."

   PURPOSE RELATION grondURL[Bevoegdheid*RechtsGrond] LATEX
   {+  Om aan te geven dat er voorbehouden aan een bevoegdheid zijn gemaakt, kan de juridische grondslag ervan worden vastgelegd. -}
   RELATION grondURL[Bevoegdheid*RechtsGrond]
   PRAGMA "De grondslag voor bevoegdheid \"" "\" is vindbaar via "
   MEANING LATEX "Van bevoegdheden kan een URL naar de juridische grondslag worden geregistreerd."
   RELATION grond[Bevoegdheid*Wetsartikel]
   PRAGMA "Bevoegdheid \"" "\" bestaat op grond van "
   MEANING LATEX "Van bevoegdheden kan de juridische grondslag worden geregistreerd."

   RELATION onder[Functie*Functie]
   PRAGMA "" " valt onder de verantwoordelijkheid van "
   MEANING LATEX "In het organogram van een bestuursorgaan is geregeld welke functies onder verantwoordelijkheid van andere functies vallen."

   PURPOSE RELATION ondermandaat LATEX REF "Art 10:9 Awb"
   {+  De mandaatgever kan toestaan dat ondermandaat wordt verleend. Het begrip ondermandaat impliceert dat er eerst een mandaat moet bestaan om een ondermandaat te kunnen hebben.
   Tevens gelden voor een ondermandaat dezelfde regels als voor een mandaat. Dit impliceert dat ook een ondermandaat weer een ondermandaat kan hebben.''.  -}
   ondermandaat :: Mandaat * Mandaat [UNI, ASY]
   PRAGMA "Mandaat "" is een ondermandaat van "
   MEANING LATEX "De uitspraak ``Mandaat $om$ is een ondermandaat van mandaat $m$.'' behoort tot de gemeenschappelijke taal."

  -- RULES --

PURPOSE RULE Voorbehoud LATEX
{+Mandaatregelingen bevatten vaak voorbehouden, in de zin dat bepaalde bevoegdheden niet naar lagere ambtenaren worden doorgemandateerd.
In een voorbehoud wordt dus een bepaalde bevoegdheid voorbehouden aan een functie, waardoor er geen mandaat kan bestaan waarin deze mandaatbevoegdheid door deze functie in mandaat wordt gegeven.
-}

   RULE Voorbehoud : voorbehoud |- -(mandaatbevoegdheid~;mandaatgever)
   MEANING LATEX "Als een bevoegdheid is voorbehouden aan een functie, kan deze bevoegdheid niet door deze functie in mandaat worden doorgegeven."

   RULE "Formatie" : onder |- formatie~;formatie
   MEANING LATEX "Een functie (ambtenaar) kan alleen onder de verantwoordelijkheid van een andere vallen wanneer zij tot dezelfde organisatie behoren."

   -- RULE HB  
   RULE "Mandaat namens bestuursorgaan" : mandaatverlener |- namens
   MEANING LATEX "Een mandaat gegeven door een bestuursorgaan geldt als een mandaat namens het bestuursorgaan"
   PURPOSE RULE "Mandaat namens bestuursorgaan" LATEX REF "Art 10:10 Awb"
   {+ Het Awb stelt: Een krachtens mandaat genomen besluit vermeldt namens welk bestuursorgaan het besluit is genomen. 
   Dat impliceert dat van elk mandaat bekend moet zijn door welk bestuursorgaan het is verleend. -}

   -- RULE HB
   RULE "Mandaatgever van ondermandaat is gemandateerde" : ondermandaat |- mandaatgever;gemandateerde~
   MEANING LATEX "De mandaatgever van een ondermandaat moet dezelfde functie zijn als gemandateerde van het oorspronkelijke (onder)mandaat."
   PURPOSE RULE "Mandaatgever van ondermandaat is gemandateerde" LATEX REF "Art 10:9 Awb"
   {+ Het begrip 'ondermandaat' impliceert dat alleen de functie waaraan mandaat verleend is, een ondermandaat kan verlenen van dat mandaat. -}





   ENDPATTERN

 -- PROCESSES --


--   PURPOSE PROCESS ToevoegenFunctie LATEX {+ test -}
   PROCESS ToevoegenFunctie 
   ROLE Bestuurder MAINTAINS "Formatie"
   ENDPROCESS  

--   PURPOSE PROCESS ToevoegenMandaat LATEX {+ test -}
--   PROCESS ToevoegenMandaat {- HB -}
--   ROLE Bestuurder MAINTAINS "Bevoegdheid alleen door mandaat"
--   ENDPROCESS
 


 -- INTERFACES --

   INTERFACE Overview : '_SESSION'
   BOX[ "Bestuursorganen" : V[SESSION*Bestuursorgaan] cRud
      , "Functies" : V[SESSION*Functie] cRud
      , "Mandaten" : V[SESSION*Mandaat] cRud
     
      ]


   PURPOSE INTERFACE Functie LATEX
   {+ HB -}
   INTERFACE Functie (functienaam, formatie, onder) FOR Bestuurder: I[Functie] 
   BOX[ "Functienaam" : functienaam cRud
      , "Op grond van": grondURL cRud
      , "Organisatie": formatie~ cRud
      , "Leidinggevende": onder cRud
      , "Hiërarchie": I cRud
        BOX[ "Boven leidinggevende": onder;onder cRud
           , "Leidinggevende": onder cRud
           , "Geeft leiding aan": onder~ cRud
           ]
      ]

   VIEW "Unieke functie" : Functie(functienaam, TXT " van ",  formatie~)
   VIEW "Bevoegdheid" : Bevoegdheid(omschrijving)

   VIEW "Mandaatreferentie" : Mandaat(TXT "Mandaat betreffende ", mandaatbevoegdheid, TXT " ", mandaatbeperking, TXT " op grond van " , grond)

   PURPOSE INTERFACE Mandaat LATEX
   {+ HB -}
   INTERFACE Mandaat FOR Bestuurder: I[Mandaat] 
   BOX[ "Mandaat betreffende" : mandaatbevoegdheid cRud
      , "Op grond van": grondURL cRud
      , "Ondermandaat van": ondermandaat cRud
      , "Mandaatgever": mandaatgever cRud
      , "Gemandateerde": gemandateerde cRud
      , "Namens": namens cRud
      , "mandaatbeperkingen" : mandaatbeperking cRud
      ]

   PURPOSE INTERFACE Bevoegdheid LATEX
   {+ HB -}
   INTERFACE Bevoegdheid FOR Bestuurder: I[Bevoegdheid]
   BOX[ "Mandaat betreffende" : mandaatbevoegdheid~ cRud
        BOX[ "Op grond van": grondURL cRud
           , "Ondermandaat van": ondermandaat cRud
           , "Mandaatgever": mandaatgever cRud
           , "Gemandateerde": gemandateerde cRud
           , "Namens": namens cRud
           , "mandaatbeperkingen" : mandaatbeperking cRud
           ]
      ]

   PURPOSE INTERFACE Functiebevoegd LATEX
   {+ HB -}
   INTERFACE Functiebevoegd FOR Bestuurder: I[Functie]  cRud
   BOX[ "Functie"      : I cRud
      , "Gemandateerde bevoegdheid": isaStar;gemandateerde~ cRud
        COLS[ "Bevoegdheid": mandaatbevoegdheid cRud
            , grond : grondURL
            , "Voorbehoud": mandaatbeperking cRud
           ]
      ]

   PURPOSE INTERFACE Bestuursorgaan LATEX
   {+ HB -}
   INTERFACE Bestuursorgaan FOR Bestuurder: I[Bestuursorgaan]  cRud
   BOX[ "Bestuursorgaan"      : I cRud
      , "Mandaten namens dit bestuursorgaan": namens~ cRud
        BOX<SCOLS>[ "Bevoegdheid" : mandaatbevoegdheid cRud
           , "Mandaatgever"       : mandaatgever    cRud
           , "Gemandateerde"      : gemandateerde;(isaStar~ - isa~;isa)   cRud
           ]
      
      ]

ENDCONTEXT