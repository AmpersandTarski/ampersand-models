CONTEXT "Private Kwaliteitssystemen" IN DUTCH
PURPOSE CONTEXT "Private Kwaliteitssystemen"
{+In Verordening (EG) nr. 852/2004 en Verordening (EG) nr. 853/2004 wordt, in het kader van algemene hygiënevoorschriften, o.a. gesproken over ‘algemene toepassing van procedures die gebaseerd zijn op de beginselen van het HACCP-systeem, wat, samen met goede hygiënepraktijken, resulteert in een grotere verantwoordelijkheid voor de exploitanten van levensmiddelenbedrijven’.
Bij een goed functionerend kwaliteitssysteem kan certificatie in de sfeer van het nalevingstoezicht een rol van betekenis spelen. De NVWA kan dan immers bij de prioritering van haar toezichtstaken rekening houden met resultaten van certificatie en/of accreditatie. De NVWA toetst daarbij alleen inhoudelijk of en in welke mate een specifiek kwaliteitssysteem en/of certificaat bijdraagt aan naleving van wetgeving.
-}

PURPOSE CONCEPT PKS
{+De NVWA is geïnteresseerd in private kwaliteitssystemen om sectoren, die hun eigen verantwoordelijkheid nemen, te kunnen belonen met verminderd toezicht.
Het feitelijke toezicht kan worden uitgevoerd door een branche- of beroepsorganisatie, of extern privaat door certificerende instellingen.
De NVWA houdt dan toezicht op de toezichthouder.
-}
CONCEPT PKS "Een PKS (Privaat Kwaliteitssysteem) is een vrijwillige zelfcontrolesystemen van privaatrechtelijke organisaties, niet zijnde privaatrechtelijke organisaties met een aangewezen taak, dat toezicht houdt op (aangesloten) individuele bedrijven in een bepaalde branche of sector." "Criteria voor acceptatie van kwaliteitssystemen door de NVWA, vastgesteld 4 juni 2014"

PATTERN "Private Kwaliteitssystemen"
PURPOSE RELATION schemaEigenaar[PKS*Orgaan]
{+De schema-eigenaar (schemabeheerder; regeling-houder) is in principe het eerste aanspreekpunt voor de NVWA als het gaat om de inhoud en het beheer van het schema.
De schema-eigenaar bepaalt welke CI’s de certificatie mogen uitvoeren en onder welke voorwaarden dat kan gebeuren.
Indien de schema-eigenaar in het buitenland gevestigd is kan daarnaast communicatie met in Nederland gevestigde CI’s aan de orde zijn, maar alleen als daarvoor valide argumenten zijn.
De schema-eigenaar wordt hierover wel ingelicht.-}
RELATION schemaEigenaar[PKS*Orgaan] [UNI,TOT]
PRAGMA "De schema-eigenaar van " " is "
MEANING "Van elk PKS registreert INSPECT het orgaan die het schema beheert."
RELATION pks[Toelating*PKS] [UNI,TOT]
MEANING "Van elke toelating registreert INSPECT tot welk systeem/schema er is toegelaten."
RELATION naam[PKS*Tekst] [UNI,TOT,INJ]
MEANING "Van elk privaat kwaliteitssysteem registreert INSPECT de naam."
RELATION toetst[PKS*Criterium]
PRAGMA "PKS " " toetst "
MEANING "De normen, die onder een PKS worden getoetst, worden in INSPECT geregistreerd."
PURPOSE RELATION niveau[PKS*Niveau]
{+Een aantal bestaande schema’s kan momenteel niet aan alle in dit document genoemde richtlijnen/criteria voldoen,
omdat de schema-eigenaren niet gekozen hebben voor ISO/RvA accreditatie en/of omdat gekozen is voor zogenaamde ‘second party auditing’
(auditors komen uit eigen geleding, meestal wel van andere bedrijven).
Een systeem/schema kan op het zogenaamde eerste niveau of op het tweede niveau acteren.
Om op het eerste niveau te acteren moet een systeem voldoen aan de ‘Richtlijnen/criteria voor een goed kwaliteitssysteem’.
Om op het tweede niveau te acteren moet een systeem minimaal voldoen aan de voorwaarden, opgenomen in bijlage 1 van de criteria voor acceptatie.
-}
RELATION niveau[PKS*Niveau] [UNI,TOT]

PURPOSE RELATION geaccepteerd[PKS*Bool]
{+Systemen, die voldoen aan de criteria voor acceptatie van kwaliteitssystemen, vastgesteld op 4 juni 2014, worden door de NVWA
geaccepteerd als ondersteunend aan de NVWA-taken in het kader van nalevingstoezicht.
Acceptatie van een systeem c.q. certificatieschema betekent dat het PKS door de NVWA, middels verificatie in de praktijk, getoetst is aan de uitgangspunten van dit document en daaraan voldoet.
Het is geen formele goedkeuring of erkenning en de NVWA maakt een eigen afweging over de mate van toezicht die noodzakelijk is.
Deelname aan geaccepteerde systemen speelt daarbij een rol maar is niet het enige criterium.
Deelnemers aan geaccepteerde systemen krijgen in principe een lagere prioritering bij de invulling van de toezichtstaken door de NVWA.
-}
RELATION geaccepteerd[PKS*Bool] [UNI,TOT]
MEANING "INSPECT registreert of de NVWA een PKS heeft geaccepteerd als ondersteunend aan de NVWA-taken in het kader van nalevingstoezicht."
REPRESENT Bool TYPE BOOLEAN
PURPOSE RELATION certificerend[PKS*Orgaan]
{+De schema-eigenaar garandeert dat de deelnemende CI’s volgens een gepaste accreditatie standaard zijn geaccrediteerd: de accreditatiestandaard moet zijn erkend door een accreditatie-organisatie, die lid is van het International Accreditation en ondertekenaar van het IAF Multilateral Recognition Arrangement (MLA). Dit houdt in dat de CI moet zijn toegelaten door de schema-eigenaar en zijn geaccrediteerd tegen de ISO normen: ISO/IEC Guide 170652 of ISO/IEC 17021:20113 (aangevuld met ISO/TS 22003).
-}
RELATION certificerend[PKS*Orgaan]
PRAGMA "Het certificeren van PKS " " mag door " " worden uitgevoerd."
MEANING "De certificerende instellingen voor elk PKS worden door INSPECT geregistreerd."

RELATION betrokkenheid[PKS*Orgaan]
PRAGMA "In een governance orgaan van PKS " " is betrokkenheid van "
MEANING "INSPECT dient elke betrokkenheid van de overheid bij een governance orgaan van een PKS te registreren."

PURPOSE RULE Verificatie
{+Een verificatie in de praktijk is een onderzoek of een systeem c.q. certificatieschema voldoet aan de criteria voor acceptatie van kwaliteitssystemen.
Acceptatie van een systeem c.q. certificatieschema betekent dat de werking van het PKS door de NVWA,
middels verificatie in de praktijk, regelmatig wordt getoetst.
-}
RULE Verificatie : verificatie~;schema |- toegelaten~;pks
MEANING "Verificatie in de praktijk van een PKS vindt plaats bij een bedrijf dat deelneemt aan dat PKS."
ENDPATTERN

PATTERN Toelatingen
CLASSIFY Toelating ISA Bericht
PURPOSE RELATION door[Toelating*Orgaan]
{+De schema-eigenaar is degene die de norm/scope en werkwijze van het keurmerk of certificaat (onafhankelijk, kwaliteit, breedte) bepaalt en de voorwaarden waaronder bedrijven worden toegelaten.
-}
RELATION door[Toelating*Orgaan] [UNI,TOT]
MEANING "De NVWA registreert van elke toelating de schema-beheerder."

PURPOSE RELATION toegelaten[Toelating*Orgaan]
{+De schema-eigenaar is degene die de norm/scope en werkwijze van het keurmerk of certificaat (onafhankelijk, kwaliteit, breedte) bepaalt en de voorwaarden waaronder bedrijven worden toegelaten.
Er is dus niet altijd sprake van een fysiek certificaat. Desondanks registreert de NVWA de toelating van een bedrijf door een daartoe bevoegde instantie alsof er een certificaat is verleend.
-}
RELATION gecertificeerd[Toelating*Toelating] [PROP]
MEANING "INSPECT registreert of de toelating van  vastgelegd is door middel van een certificaat."
RELATION toegelaten[Toelating*Orgaan] [UNI,TOT]
MEANING "De NVWA registreert de toelating van een bedrijf op basis van een toelatingsbericht van de schema-beheerder."
RELATION geldigVanaf[Toelating*Datum] [UNI,TOT]
PRAGMA "" ""
MEANING "Van elke toelating registreert INSPECT vanaf welke datum het geldt."
RELATION geldigTot[Toelating*Datum] [UNI,TOT]
PRAGMA "" ""
MEANING "Van elke toelating registreert INSPECT tot welke datum het geldt."
ENDPATTERN

PATTERN Onderzoeken
RELATION schema[Onderzoek*PKS] [UNI]
PRAGMA "" ""
MEANING "Het schema, dat in een onderzoek wordt toegepast, wordt in INSPECT geregistreerd."
RELATION onderzoeker[Onderzoek*Orgaan] [UNI,TOT]
PRAGMA "Onderzoek " " is uitgevoerd door "
MEANING "Van elk onderzoek registreert INSPECT welk orgaan verantwoordelijk is voor de uitvoering ervan."
PURPOSE RELATION realityCheck[Onderzoek*PKS]
{+Verificatie door de NVWA in de praktijk vindt plaats door middel van audits en inspecties in bedrijven, zogenaamde reality checks. Dat de NVWA daarnaast dieper in kan gaan op het systeem (door het houden van een audit) kan onderdeel uitmaken van de afspraken tussen partijen (NVWA en schema-eigenaar).
-}
RELATION realityCheck[Onderzoek*PKS]
PRAGMA "Onderzoek " " is een reality check op PKS "
MEANING "INSPECT registreert elke reality check op een geaccepteerd PKS."
RELATION grond[Criterium*JurGrond]
PRAGMA "Criterium " " is gegrond in "
MEANING "Van elk criterium registreert INSPECT de juridische grondslag."
RELATION concordantie[Criterium*Criterium]
PRAGMA "" ""
MEANING ""
RELATION verificatie[Onderzoek*Orgaan]
PRAGMA "" ""
MEANING "Van elke verificatie registreert INSPECT welk orgaan (bedrijf) is geïnspecteerd."
RELATION object[Onderzoek*Onderzoeksobject] [UNI]
PRAGMA "" ""
MEANING "Van elke verificatie kan INSPECT registreren bij welk orgaan (bedrijf) de verificatie is uitgevoerd."
RELATION objecttype[Onderzoek*ObjectType] [UNI,TOT]
PRAGMA "Onderzoek " " is van toepassing op een object van type "
MEANING "Van elk onderzoek registreert INSPECT over de soort (objecttype) van het onderzoeksobject."
RELATION criterium[Bevinding*Criterium] [UNI]
PRAGMA "" ""
MEANING ""
RELATION onderzoek[Bevinding*Onderzoek] [UNI,TOT]
PRAGMA "" ""
MEANING ""
RELATION vraag[Bevinding*Vraag] [UNI]
PRAGMA "" ""
MEANING ""
RELATION antwoord[Bevinding*Antwoord] [UNI]
PRAGMA "" ""
MEANING ""
ENDPATTERN

INCLUDE "Bedrijfsregels PKS.xlsx"
PATTERN Criteria
RELATION objecttype[Criterium*ObjectType] [UNI]
PRAGMA "Criterium " " is van toepassing op objecten van type "
MEANING "Van elk criterium kan INSPECT registreren over welk soort dingen (objecttype) deze regel handelt."
RELATION formulering[Criterium*Tekst] [UNI,TOT]
PRAGMA "De formulering van criterium " "  luidt: \"" "\""
MEANING "Van elk criterium registreert INSPECT de formulering in juridische taal."
RELATION controlevraag[Criterium*Tekst] [UNI]
PRAGMA "De controlevraag van criterium " "  luidt: \"" "\""
MEANING "Van elk criterium kan INSPECT controlevragen registreren."
RELATION toelichting[Criterium*Tekst] [UNI]
PRAGMA "De toelichting op criterium " " luidt: \"" "\""
MEANING "Van elk criterium kan INSPECT een toelichting registreren."
RELATION grond[Criterium*JurGrond]
PRAGMA "Een juridische grondslag voor criterium " " is "
MEANING "Van elk criterium dient INSPECT de juridische grondslag te registreren."
ENDPATTERN

PURPOSE PATTERN Aanvraagproces
{+Om een privaat kwaliteitssysteem te laten accepteren neemt de eigenaar ervan contact op met de NVWA voor het indienen van een aanvraag.
De NVWA start vervolgens een procedure, die wordt afgerond met een besluit tot acceptatie dan wel afwijzing van de aanvraag.
(bron: http://ketenborging.nl/kwaliteitsschemas-en-status/)
-}
PATTERN Aanvraagproces
RELATION aanvrager[PKSaanvraag*Orgaan] [UNI,TOT]
PRAGMA "Aanvraag " " is afkomstig van "
MEANING "Van elke aanvraag registreert INSPECT de aanvrager."
RELATION zelfbeoordeling[Document*PKSaanvraag]
PRAGMA "Document " " is de zelfbeoordeling, die hoort bij aanvraag "
MEANING "De aanvrager dient een zelfbeoordeling in op basis waarvan de NVWA beslist om de aanvraag in behandeling te nemen."
RELATION inBehandeling[PKS*Bool] [UNI,TOT]
MEANING "INSPECT registreert of de NVWA een PKS in behandeling heeft genomen ter toetsing voor acceptatie."
PURPOSE RELATION inBehandeling[PKSaanvraag*Besluit] LATEX
{+Fase 1 betreft het in behandeling nemen van een nieuwe aanvraag.
\begin{itemize}
\item   De NVWA stuurt een zelfbeoordelingsformulier aan de aanvrager/schemabeheerder;
\item   De NVWA beoordeelt na ontvangst het zelfbeoordelingsformulier en bepaalt of de aanvraag voldoende basis biedt om het kwaliteitssysteem verder te behandelen;
\item   Als er voldoende basis is, heeft de NVWA een intakegesprek met de schemabeheerder om de aanvraag in detail te bespreken en in te schatten of het kwaliteitssysteem (op termijn) mogelijk zal voldoen aan de gestelde criteria;
\item   Op basis van het gesprek neemt de NVWA uiterlijk 6 weken na ontvangst van het zelfbeoordelingsformulier een besluit tot verdere behandeling van de aanvraag.(bron: http://ketenborging.nl/kwaliteitsschemas-en-status/)
\end{itemize}
(bron: http://ketenborging.nl/kwaliteitsschemas-en-status/)-}
RELATION inBehandeling[PKSaanvraag*Besluit]
PRAGMA "Aanvraag " " is op basis van besluit " " in behandeling genomen."
MEANING "Om een aanvraag in behandeling te nemen, neemt de NVWA een besluit. INSPECT registreert dit."
RELATION afgewezen[PKSaanvraag*Besluit]
PRAGMA "Aanvraag " " is op basis van besluit " " afgewezen."
MEANING "Het afwijzen van een aanvraag is een besluit van de NVWA. INSPECT registreert dit."
RELATION geaccepteerd[PKSaanvraag*Besluit]
PRAGMA "Aanvraag " " is op basis van besluit " " geaccepteerd."
MEANING "Het accepteren van een aanvraag is een besluit van de NVWA. INSPECT registreert dit."
PURPOSE RELATION verificatie[PKSaanvraag*Onderzoek]
{+Op basis van de verificatie in de praktijk beslist de NVWA over de acceptatie van het kwaliteitssysteem, stelt de voorwaarden vast waaronder de acceptatie plaatsvindt en stelt aangepast toezicht vast.
De IG NVWA neemt de beslissing na advies van de daartoe ingestelde Toetsingscommissie.
Deze procedure zal doorgaans een half tot 1 jaar in beslag nemen.
(bron: http://ketenborging.nl/kwaliteitsschemas-en-status/)
-}
RELATION verificatie[PKSaanvraag*Onderzoek]
PRAGMA "Om aanvraag " " te verifieren in de praktijk voert de NVWA onderzoek " " uit"
MEANING "INSPECT registreert van elke aanvraag voor het accepteren van een PKS welk onderzoek ter verificatie in de praktijk is uitgevoerd."
RELATION pks[PKSaanvraag*PKS] [UNI,TOT]
PRAGMA "Aanvraag " " is een verzoek tot acceptatie van kwaliteitssysteem "
MEANING "Van elke aanvraag registreert INSPECT het te accepteren kwaliteitssysteem."
--RELATION [*]
--PRAGMA "" ""
--MEANING ""

CLASSIFY PKS ISA Onderzoeksobject
PURPOSE RULE Onderzoeksobject
{+Wanneer de NVWA een verificatie-onderzoek van een PKS in gang zet,
wordt een aantal velden van dat onderzoek automatisch ingevuld.
INSPECT registreert De NVWA als uitvoerder en het te accepteren PKS als onderzoeksobject.
De NVWA hanteert voor het verificatie-onderzoek het schema "PKS Acceptatie".
-}
ROLE ExecEngine MAINTAINS Onderzoeksobject
RULE Onderzoeksobject : verificatie~;pks[PKSaanvraag*PKS] |- object
MEANING "Het private kwaliteitssysteem, dat op de aanvraag is vermeld, is het onderwerp van een verificatie-onderzoek voor die aanvraag."
VIOLATION ( TXT "{EX} InsPair;object;Onderzoek;", SRC I[Onderzoek], TXT ";Onderzoeksobject;", TGT I[Onderzoeksobject]
          , TXT "{EX} InsPair;;Onderzoek;", SRC I[Onderzoek], TXT ";Onderzoeksobject;"
          , TXT "{EX} InsPair;objecttype;Onderzoek;", SRC I[Onderzoek], TXT ";ObjectType;PKS"
          , TXT "{EX} InsPair;onderzoeker;Onderzoek;", SRC I[Onderzoek], TXT ";Onderzoeksobject;NVWA"
          , TXT "{EX} InsPair;schema;Onderzoek;", SRC I[Onderzoek], TXT ";PKS;PKS Acceptatie"
          )

PURPOSE RULE "Start PKS verificatie onderzoek"
{+Wanneer de NVWA een besluit heeft genomen om een PKS-aanvraag in behandeling te nemen,
wordt een zogenaamd verificatie-onderzoek gestart.
Dit onderzoek bestaat uit een bureau-onderzoek en een aantal (één of meer) verificaties-in-de-praktijk.
INSPECT maakt hiervoor automatisch een onderzoek aan en registreert dat als verificatie-onderzoek.
-}
RULE "Start PKS verificatie onderzoek" : (I/\inBehandeling;inBehandeling~);pks |- verificatie;V
VIOLATION (TXT "{EX} NewStruct;Onderzoek"
              ,TXT ";verificatie;PKSaanvraag;", SRC I ,TXT ";Onderzoek;_NEW"
          )
ENDPATTERN

INTERFACE Overzicht : '_SESSION'[SESSION]
TABS [ kwaliteitssystemen : V[SESSION*PKS]
     , onderzoeken :        V[SESSION*Onderzoek]
     , toelatingen :        V[SESSION*Toelating]
     , aanvragen :          V[SESSION*PKSaanvraag]
     , besluiten :          V[SESSION*Besluit]
     ]

ENDCONTEXT
