CONTEXT MessagingService IN ENGLISH
PURPOSE CONTEXT MessagingService
{+This service allows applications to send messages to a MsgEndpoint, provided that 
the MsgEndpoint has a (valid) ContactEndpoint that is suitable for sending automated 
messages to. This is the case when that MsgEndpoint has a ContactEndpoint with the 
property `cepAppUseProp` set. 
If the application wants to send a message to a MsgEndpoint using a validated 
ContactEndpoint, then it should ALSO check the property `cepAppIsValid`.

An application can send a message to a MsgEndpoint by doing the following: 
1) Create a `MsgSendReq`,
2) Populate the relations `msgRecipient` and `msgMsgText`,
3) Optionally: populate any other `MsgSendRea`-relation.
4) Set the property `msgSendReq` (this will trigger an auto-transmit service
   so if you create your own transmission records, you should not set it)
-}

RELATION cepmeansImplemented[CEPMeans*CEPMeans] [PROP]
MEANING "A CEPMeans may have the property that the application can use it for sending messages."

ROLE ExecEngine MAINTAINS "This CEPMeans cannot be used by the application for sending messages"
RULE "This CEPMeans cannot be used by the application for sending messages":
  cepAppUseProp;cepMeans;(I-cepmeansImplemented) |- -V
MEANING "Applications can only send messages to a CEP using a CEPMeans that has been implemented"
VIOLATION (TXT "{EX} DelPair;cepAppUseProp;ContactEndpoint;", SRC I, TXT ";ContactEndpoint;", TGT I)

--[Message Send Requests]--
msgSender    :: MsgSendReq * MsgEndpoint [UNI] -- Endpoint where the message originated from.
msgRecipient :: MsgSendReq * MsgEndpoint [UNI] -- Endpoint that is intended to receive the message.
msgMsgTitle  :: MsgSendReq * MsgTitle    [UNI]   REPRESENT MsgTitle  TYPE ALPHANUMERIC
msgMsgText   :: MsgSendReq * MsgText     [UNI]   REPRESENT MsgText   TYPE ALPHANUMERIC
msgURLTitle  :: MsgSendReq * URLTitle    [UNI]   REPRESENT URLTitle  TYPE ALPHANUMERIC -- representation of URL
msgURLText   :: MsgSendReq * URLText     [UNI]   REPRESENT URLText   TYPE ALPHANUMERIC -- actual URL
msgSendReq   :: MsgSendReq * MsgSendReq [PROP] -- Request the message to be actually sent.

RULE "Unsent messages can only be sent (from this Application) to people that have registered a method for doing so":
  (I-(trxMsgSR~;trxMsgSR));msgRecipient |- V;cepAppUseProp;cepConnectsTo
VIOLATION (TGT I, TXT " has not specified a CEPMeans for receiving messages from this application")

RULE "Message send requests may only be done if there is a message and a recipient":
  msgSendReq |- msgRecipient;msgRecipient~ /\ msgMsgText;msgMsgText~
--VIOLATION (TXT "DelPair;msgSendReq;MsgSendReq;", SRC I, TXT ";MsgSendReq;", TGT I)
--ROLE ExecEngine MAINTAINS "Message send requests may only be done if there is a message and a recipient"

--[Message TrxRecord records]--
trxDateTime  :: TrxRecord * DateTime        [UNI]  REPRESENT DateTime  TYPE DATETIME -- the time at which the transmission took place
trxMsgSR     :: TrxRecord * MsgSendReq      [UNI,TOT] -- the message to be transmitted
trxCEP       :: TrxRecord * ContactEndpoint [UNI,TOT] -- the endpoint to which to transmit the message
trxCEPMeans  :: TrxRecord * CEPMeans        [UNI,TOT] -- we do this only for efficient processing by message transmission services

ROLE ExecEngine MAINTAINS "Delete transmission record when message is discarded"
RULE "Delete transmission record when message is discarded": I[TrxRecord] |- trxMsgSR;trxMsgSR~
VIOLATION (TXT "{EX} DelAtom;TrxRecord;", SRC I)

ROLE ExecEngine MAINTAINS "Create transmission records for messages that must be transmitted"
RULE "Create transmission records for messages that must be transmitted": 
  msgSendReq;msgRecipient;cepConnectsTo~;cepAppUseProp |- trxMsgSR~;V
VIOLATION (TXT "{EX} NewStruct;TrxRecord"
              ,TXT ";trxMsgSR;TrxRecord;_NEW;MsgSendReq;", SRC I
              ,TXT ";trxCEP;TrxRecord;_NEW;ContactEndpoint;", TGT I
              ,TXT ";trxCEPMeans;TrxRecord;_NEW;CEPMeans;", TGT cepMeans -- N.B.: cepMeans is [FUN]
          )

ENDCONTEXT