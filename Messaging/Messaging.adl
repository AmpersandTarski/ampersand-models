CONTEXT "Messaging" IN ENGLISH
{- This is a stand-alone context used for demonstrating and testing messaging services 
   To use the messaging module, INCLUDE "../Messaging/MSG_Module.adl".
   This module does not provide any (user)interfacing facilities.

Make sure that for testing and login, you check the following settings in `localSettings.php`:
-**set `loginEnabled`**to `false` or `true`, depending on whether you do or do not use the Login stub
  Config::set('loginEnabled', 'global', true);
-**ensure that the ExcelImport function works**, by commenting out the line that starts with:
  Config::set('allowedRolesForExcelImport', 'excelImport', (etc.)
-**ensure you can run the ExecEngine from the menu bar**, by commenting out the line that has:
  Config::set('allowedRolesForRunFunction', 'ExecEngine', (etc.)
-}

   INCLUDE "../Messaging/MSG_ContactEndpoints.adl" 
   INCLUDE "../Messaging/MSG_CEPValidation.adl"
   INCLUDE "../Messaging/MSG_Transmission.adl"
   INCLUDE "../Messaging/MSG_Pushover.adl"
   INCLUDE "../Messaging/MSG_Pushalot.adl"
   INCLUDE "../Messaging/MSG_Email.adl"
-- INCLUDE "../Messaging/MSG_SMS.adl"

   INCLUDE "../Messaging/MSG_Endpoints.ifc"

--[Testing interface]--

   INCLUDE "SIAM_Module.adl" -- Include Login, and simple person/org registrations
   INCLUDE "SIAM_pop.xlsx"   -- Include some test accounts
   INCLUDE "MSG_Test.xlsx"   -- Population for testing

CLASSIFY Person ISA MsgEndpoint -- This allows us to send messages to a Person

RELATION sessionMsgRecipient[SESSION*MsgEndpoint] [UNI]
RELATION sessionMsg[SESSION*MsgSendReq] [UNI]

INTERFACE "Send Message" 
   (sessionMsgRecipient, msgDelReq, msgSendReq, msgSMSReq
   , msgSender, msgRecipient
   , msgMsgTitle, msgMsgText
   , msgURLTitle, msgURLText
   ): '_SESSION'[SESSION] cRud
BOX[ "New message to (Person)" : sessionMsgRecipient cRUd
   , "Message details" : sessionMsg;(I-msgSendReq) CRud COLS
     [ "Recipient": msgRecipient cRUd
     , "Title"    : msgMsgTitle cRUd
     , "Message"  : msgMsgText cRUd
     , "URLTitle" : msgURLTitle cRUd
     , "URL"      : msgURLText cRUd
     , "Send?"    : msgSendReq cRUd
     , "SMS?"     : msgSMSReq cRUd
     ]
   , "Messages" : V[SESSION*MsgSendReq] cRUd COLS
     [ "Del?"     : msgDelReq cRUd
     , "Recipient": msgSendReq;msgRecipient cRud
     , "Title"    : msgSendReq;msgMsgTitle cRud
     , "Message"  : msgSendReq;msgMsgText cRud
     , "Delivery" : trxMsgSR~ cRud BOX <COLSNL>
       [ "Means"    : trxCEPMeans cRud
       , "Address"  : trxCEP;cepAddress cRud
       , "Sent at"  : trxDateTime cRud
       ]
     ]
   ]

--[Message manipulation rules]--
ROLE ExecEngine MAINTAINS "Create new message"
RULE "Create new message": '_SESSION';sessionMsgRecipient |- -V
VIOLATION (TXT "{EX}_;NewStruct_;MsgSendReq"
              ,TXT "_;sessionMsg_;SESSION_;", SRC I, TXT "_;MsgSendReq_;_NEW"
              ,TXT "_;msgRecipient_;MsgSendReq_;_NEW_;MsgEndpoint_;", TGT I
          ,TXT "{EX} DelPair;sessionMsgRecipient;SESSION;", SRC I, TXT ";MsgEndpoint;", TGT I
          )

RELATION msgDelReq[MsgSendReq*MsgSendReq] [PROP] -- Request the message to be deleted after sending.
ROLE ExecEngine MAINTAINS "Deleting message"
RULE "Deleting message": msgDelReq |- -V
VIOLATION (TXT "{EX} DelAtom;MsgSendReq;", SRC I)

RELATION msgSMSReq[MsgSendReq*MsgSendReq] [PROP] -- Request the message to be deleted after sending.
ROLE ExecEngine MAINTAINS "Sending SMS message"
RULE "Sending SMS message": msgSMSReq;msgRecipient;cepConnectsTo~;(I /\ cepMeans;'SMS';cepMeans~) |- -V
VIOLATION (TXT "{EX} NewStruct;TrxRecord"
              ,TXT ";trxMsgSR;TrxRecord;_NEW;MsgSendReq;", SRC I
              ,TXT ";trxCEP;TrxRecord;_NEW;ContactEndpoint;", TGT I
              ,TXT ";trxCEPMeans;TrxRecord;_NEW;CEPMeans;SMS"
          ,TXT "{EX} DelPair;msgSMSReq;MsgSendReq;", SRC I, TXT ";MsgSendReq;", SRC I
          )

RULE "SMS can only be sent to a people that have specified a phone number for SMS":
  msgSMSReq;msgRecipient |- V;'SMS';cepMeans~;cepConnectsTo

--[Use a simple person registration]--

INTERFACE "Me"
   (personFirstName, personLastName
   ,cepConnectsTo, cepMeans, cepAddress, cepNote
   ,cepAppUseProp, cvrResponse
   ): '_SESSION';sessionPerson cRud BOX <ROWSNL>
   [ "Who I am": I cRud COLS
      [ "First name": personFirstName cRUd
      , "Last name" : personLastName cRUd
      , "Account (Userid)" : accPerson~;accUserid cRud
      ]
   , "Ways of contacting" : I cRud INTERFACE "MessageEndpoint" 
   ]

--[Rules for validation testing]--

ROLE ExecEngine MAINTAINS "Test - invalidating CEPs"
RULE "Test - invalidating CEPs": cepAppIsValid |- cepAppUseProp
VIOLATION (TXT "{EX} DelPair;cepAppIsValid;ContactEndpoint;", SRC I, TXT ";ContactEndpoint;", TGT I)

ENDCONTEXT