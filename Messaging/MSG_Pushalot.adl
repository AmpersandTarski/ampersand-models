CONTEXT Pushalot IN ENGLISH
PURPOSE CONTEXT Pushalot
{+Sometimes, there an application may be programmed to send a message to one or more users. This context specifies a way to send messages to registered users that can be reached through a communication means called `Pushalot`.

In order to start using the Pushalot notifications extension within a prototype, you must execute the following steps:
1) Enable the Pushalot extension in your localsettings file.
   This is done by adding the following lines to your 'localSettings.php' file:

 	 // Enable Pushalot notification extension
	  require_once(__DIR__ . '/extensions/Messaging/Pushalot/Pushalot.php');

2) Include the Pushalot messaging module in your script:

   INCLUDE "../Messaging/MSG-Pushalot.adl"

3) Send messages by a VIOLATION statement such as

	  VIOLATION(TXT "{EX}PushalotNotifications::execEnginePushNotificationOnCommit;<authorizationtokens>;<message>;<title>;<url>")

   Notes:
   - Multiple <authorizationtokens> must be seperated with _AND. This is atomatically done when using an ampersand expression to fill the userKeys.
   - The message is sent as html content. You can use a limited set of html tags (see https://pushalot.com/api)
   - The title and url are optional

4) Users that want to receive messages through the Pushalot notification service must register an account at https://pushalot.com/ (free of charge). This will provide them with a so-called 'authorization token' (for the 'API' app). An authorization token is similar to a phone number or an email-address (it is a CEPEndpoint). Users can create additional authorization tokens at https://pushalot.com/manager/authorizations
-}

INCLUDE "../Messaging/MSG_Transmission.adl"

POPULATION cepmeansImplemented CONTAINS [ ("Pushalot", "Pushalot") ]

--[Pushalot Messaging Service]--
ROLE ExecEngine MAINTAINS "Service Pushalot Message Send Requests"
RULE "Service Pushalot Message Send Requests":
  (I[TrxRecord]-(trxDateTime;trxDateTime~));trxMsgSR /\ trxCEPMeans;'Pushalot';V |- -V
VIOLATION(TXT "{EX}_;PushalotNotifications::execEnginePushNotificationOnCommit" -- Function call
              ,TXT "_;", SRC trxCEP;cepAddress  -- $userKeys: array of <userkey>s
              ,TXT "_;", TGT msgMsgText  -- $message: message text
              ,TXT "_;", TGT msgMsgTitle -- $title: message-title>
              ,TXT "_;", TGT msgURLText  -- $url: actual URL 
              ,TXT "_;", TGT msgURLTitle -- $urltitle: representation of URL to the user
         ,TXT "{EX} InsPair;trxDateTime;TrxRecord;", SRC I, TXT ";DateTime;{php}gmdate('d-m-Y H:i T')" -- Set the DateTime
         ,TXT "{EX}_;Notifications::addLog_;"
              ,TXT "MSG_Pushalot Service: message sent to ", TGT msgRecipient
              ,TXT " (addr='", SRC trxCEP;cepAddress, TXT "')"
              ,TXT "_;MESSAGING"
         )

ENDCONTEXT