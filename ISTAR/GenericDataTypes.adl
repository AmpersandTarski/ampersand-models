CONTEXT GenericDataTypes IN ENGLISH
------------------------------------------------------------

--[Yes/No Answer]--

CONCEPT "Yes/No answer" "an answer that can be either 'Yes' or 'No'"
PURPOSE CONCEPT "Yes/No answer" IN ENGLISH
{+In general, we need the ability to distinguish between information objects based on a property. For example, we need to distinguish warning orders from non-warning orders. In order to accommodate this, we use questions (such as: "Is this (order) a warning order?" that can only be answered with 'Yes' and 'No'."
-}
RULE "Yes/No answer integrity": I["Yes/No answer"] |- 'Yes' \/ 'No'
MEANING "A Yes/No answer can only have the values 'Yes' or 'No'"
MESSAGE "You may only specify 'Yes' or 'No' as an answer."

--[Session]--

CONCEPT SESSION "a representation of the webbrowser/user that uses an application through some communications link."
PURPOSE CONCEPT SESSION IN ENGLISH 
{+Whenever multiple users want to access a single application, the communication that such users (webclients) have with the application must be kept apart from one another. The singleton '_SESSION' is used for that; this singleton is defined (and implemented) by the prototype generator.-}

sessionUserid :: SESSION * Userid [UNI]
sessionPassword :: SESSION * Password [UNI]

RULE "Login": '_SESSION'[SESSION];sessionUserid;accUserid~ |- '_SESSION';sessionPassword;accPassword~
MEANING "A registered (Userid,Password) combination must be provided when logging in."
MESSAGE "Incorrect (or empty) password"

RULE "Session login integrity": I /\ sessionUserid;sessionUserid~ /\ sessionPassword;sessionPassword~
|- (sessionUserid;accUserid~ /\ sessionPassword;accPassword~);V
MEANING "Logging in is only allowed using a registered account."
MESSAGE "Logging in is only allowed using a registered account."

--[Account]--

CONCEPT Account "a registration (of attributes) of an actor that is capable of fulfilling roles in the ISTAR processes"

KEY accIDs: Account(accID)
--IDENT accIDs: Account(accID) -- lokaal in de DB
--VIEW  accIDs: Account(accID) -- zoals 'KEY' in het verleden, met TXT, en relaties.
accID          :: Account -> AccountID
accDisplayName :: Account -> DisplayName
accUserid      :: Account -> Userid
accPassword    :: Account -> Password
accUnit        :: Account -> Unit

RULE "Account identifiability": accUserid;accUserid~ /\ accPassword;accPassword~ |- I[Account]
MEANING "Every account is identifiable by its userid and its password"

--[Units]--
CONCEPT Unit "an organizational unit, the head of which is its commander"
PURPOSE CONCEPT Unit
{+The Defense organisation is organized in units consisting of a commander (who is in charge), other employees and materials for which the commander is accountable to his direct superior in the command-chain.-}

RULE "operational units": I[Unit] |- unitID;unitID~
unitID :: Unit * UnitID -- REF See http://nl.wikipedia.org

------------------------------------------------------------
ENDCONTEXT