CONTEXT RAP IN DUTCH

INTERFACE "Context" FOR Student,Tutor : I[Context] cRud 
  BOX [ "Terug naar script" : context[ScriptVersion*Context]~;version~ cRud
      , ""     : I[Context] TABS
         [ "Concepten" : (context[Pattern*Context]~;concepts[Pattern*Concept]) \/ context[Concept*Context]~ cRud LINKTO INTERFACE "Concept"
         , "Relaties"  : declaredIn[Relation*Context]~  cRud LINKTO INTERFACE "Relation"
         , "Regels"    : I
            TABS [ "gewone regels"          : allRules[Context*Rule] ;(I[Rule]-I[PropertyRule]) cRud LINKTO INTERFACE "Rule"
                 , "multipliciteits regels" : allRules[Context*Rule] ;         I[PropertyRule]  cRud LINKTO INTERFACE "Rule"
                 ]
         , "Patterns"   : context [Pattern * Context]~ cRud LINKTO INTERFACE "Pattern"
         ]
  ]


INTERFACE "Concept" FOR Student,Tutor : I[Concept] cRud BOX
  [ "Concept:" : I cRud
  , "Bedoeling:" : purpose[Concept*Purpose];markupText cRud 
  , "Relaties waarin dit concept wordt gebruikt:" : source~ \/ target~ cRud LINKTO INTERFACE "Relation"
  , "Generiekere concepten:" : genspc[Gen*Concept]~;gengen[Gen*Concept] cRud LINKTO INTERFACE "Concept"
  , "Specifiekere concepten:" : gengen[Gen*Concept]~;genspc[Gen*Concept] cRud LINKTO INTERFACE "Concept"
  , conceptualDiagram: conceptualDiagram cRud <ShowConceptualDiagram>
  ]

INTERFACE "Relation" FOR Student,Tutor : I[Relation] cRud BOX
  [ "Relatie:" : I cRud
  , "Source concept:" : source cRud LINKTO INTERFACE "Concept"
  , "Target concept:" : target cRud LINKTO INTERFACE "Concept"
  , "Eigenschappen:" : propertyRule;declaredthrough cRud
  , "Bedoeling:" : purpose[Relation*Purpose];markupText cRud 
  , "Betekenis:" : decmean[Relation*Meaning] cRud 
  ]

INTERFACE "Rule" FOR Student,Tutor : I[Rule] cRud BOX
  [ "Regel:" : I[Rule] cRud
  , "Bedoeling:" : purpose[Rule*Purpose];markupText cRud 
  , "Betekenis:" : meaning[Rule*Meaning] cRud 
  , "Formele expressie:" : formalExpression cRud
  , "origin" : origin cRud
  , conceptualDiagram: (I[Rule]-I[PropertyRule]);conceptualDiagram cRud
  ]

INTERFACE "Pattern" FOR Student,Tutor : I[Pattern] cRud BOX
  [ "Thema's:" : I cRud
  , conceptualDiagram: conceptualDiagram cRud
  ]

VIEW ShowConceptualDiagram: FileObject { imgfile : filePath }
HTML TEMPLATE "ShowConceptualDiagram.html"
ENDVIEW

ENDCONTEXT




--  , "Gens" : context[Concept*Context]~ ;
--             (I[Concept]                                  /\
--                gengen[Gen*Concept]~;gengen[Gen*Concept]  /\
--              -(genspc[Gen*Concept]~;genspc[Gen*Concept])
--             )  
--  , "Roles" : allRoles[Context*Role]
--  , "allConjuncts" : allConjuncts[Context*Conjunct]
