CONTEXT RAP3 IN ENGLISH

PATTERN "Registration of Ampersand source files"
CONCEPT Script "A script is a piece of UTF-8 encoded text that is meant to be compiled by the Ampersand compiler."
CONCEPT Assignment "An assignment is the name of a piece of work that a student should carry out in order to succeed for a course."
REPRESENT Assignment TYPE ALPHANUMERIC
REPRESENT DateTime TYPE DATETIME

  CONCEPT FileObject ""
    IDENT FileObjectName 	: FileObject (filePath)
    VIEW FileObject 		: FileObject DEFAULT {filePath : filePath, fileName : originalFileName} HTML TEMPLATE "View-FILEOBJECT.html" ENDVIEW
    RELATION filePath[FileObject*FilePath] [UNI,TOT]
      REPRESENT FilePath TYPE ALPHANUMERIC
    RELATION originalFileName[FileObject*FileName] [UNI,TOT]
      REPRESENT FileName TYPE ALPHANUMERIC

RELATION submittor[Script*Account] [UNI] --TOT is guaranteed by the ExecEngine
RELATION assignment[Script*Assignment] [UNI]
RELATION submitted[Script*DateTime] [UNI] -- The data/time that the file was uploaded. 
RELATION content[Script*ScriptContent] [UNI]
MEANING "The actual text of the script (as seen in the editor)."

CONCEPT ScriptContent ""
REPRESENT ScriptContent TYPE BIGALPHANUMERIC
VIEW ScriptContent : ScriptContent DEFAULT HTML TEMPLATE "View-ACE.html" ENDVIEW

CONCEPT ScriptVersion ""
RELATION version[Script*ScriptVersion] [INJ,SUR]
RELATION source[ScriptVersion*FileObject]

ROLE ExecEngine MAINTAINS "Submission Timestamping"
RULE "Submission Timestamping": (I[Script]/\content;content~) |- submitted;V
VIOLATION ( TXT "{EX} InsPair;submitted;Script;", SRC I, TXT ";DateTime;{php}date(DATE_ISO8601)"
          )
ROLE ExecEngine MAINTAINS "Submittor"
RULE "Submittor": I[Script]#('_SESSION'[SESSION];sessionAccount) |- submittor;V
VIOLATION ( TXT "{EX} InsPair;submittor;Script;", SRC I, TXT ";Account;", TGT I
          )

--   ROLE ExecEngine MAINTAINS "All accounts must have the role User"
--   RULE "All accounts must have the role User" : newScript~;'_SESSION';sessionAccount |- accAllowedRoles
--   VIOLATION (TXT "{EX}InsPair;accAllowedRoles;Account;", SRC I, TXT ";Role;", TGT I)

ENDPATTERN





PURPOSE RULE "indienen door studenten"
{+ Om scripts te kunnen traceren naar de student, worden alleen scripts van ingeschreven studenten toegestaan.
Daartoe dient de student zijn of haar studentnummer te koppelen aan het account.
-}
ROLE Student MAINTAINS "indienen door studenten"
RULE "indienen door studenten" : submittor |- submittor;(I/\ studentnummer;studentnummer~ )
MEANING "Alleen accounts met een studentnummer mogen een script indienen."
VIOLATION (TXT "Aan uw account is geen studentnummer gekoppeld. Om die reden wordt script ", SRC I, TXT " niet opgeslagen.")
ENDCONTEXT