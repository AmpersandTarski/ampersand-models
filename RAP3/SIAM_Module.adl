CONTEXT SIAM IN ENGLISH -- SIAM: Sessions, Identity- and Access Management.
-- The purpose of this file is to provide an easy way for users of the module to include SIAM functionality.

RULE "This file expects to load SIAM version 2.x": 'SIAM';moduleVsnMajor |- moduleVsnMajor;'2'
VIOLATION (TXT "The SIAM Module files that you have INCLUDEd have major version number ", TGT I)

   INCLUDE "../SIAM/SIAM_Module-versioning.adl"   -- the version definition for this module
   INCLUDE "../SIAM/SIAM_PersonReg.adl"           -- a basic/minimalistic Person registration (just first- and last names).
   INCLUDE "../SIAM/SIAM_OrgReg.adl"              -- a basic/minimalistic Organization registration (just abbreviated and full names).
   INCLUDE "../SIAM/SIAM_PersonOrgs.adl"          -- Extension that defines (and maintains the population of) relation `personOrg`.
-- INCLUDE "../SIAM/SIAM_Persona.adl"             -- Extension that implements Persona (based on Persons and Organizations).
   INCLUDE "../SIAM/SIAM_Roles.adl"               -- Role definitions (allowed roles, default activated roles, 'god'-account property).
   INCLUDE "../SIAM/SIAM_LoginLogging.adl"        -- Adding a timestamp to Logins. 
   INCLUDE "../SIAM/SIAM_Login.adl"               -- This file contains the core functionality of this module (logins, sessions, ...).
   INCLUDE "../SIAM/SIAM_AutoLoginAccount.adl"    -- Extension that implements the `autoLoginAccount` property for accounts.
   INCLUDE "../SIAM/SIAM_GodAccounts.adl"         -- Extension that implements the 'God-accounts' feature (accounts that have all roles).
   INCLUDE "../SIAM/SIAM_SessionSuspension.adl"   -- Extension that allows sessions to temporarily be suspended.
-- INCLUDE "../SIAM/SIAM_LoginISOAuthLevels.adl"  -- Extension that introduces ISO authentication levels in a basic fashion.

-- Only one of the following can be active at any time.
   INCLUDE "../SIAM/SIAM_Login.ifc"                             -- The simplest Login/Logout interface
-- INCLUDE "../SIAM/SIAM_LoginWithAssistance.ifc"               -- Login/Logout interface, only to be used by developers
-- INCLUDE "../SIAM/SIAM_LoginWithAssistanceAndAuthLevels.ifc"  -- Login/Logout interface, only to be used by developers that need LoA's

-- The following VIEW replaces 'dirty' SESSION identifiers with the text "My Session" or "Some other Session"
VIEW Sessions: SESSION DEFAULT 
{ "uid": '_SESSION'[SESSION];V;'My Session'[LoginMessage] 
     \/ (I[SESSION]-'_SESSION');V;'Some other Session'[LoginMessage]
} ENDVIEW
POPULATION LoginMessage CONTAINS [ "My Session", "Some other Session" ]

--[Middle names]--

PURPOSE RELATION personMiddle[Person*Middlepart]
{+In order to write things like "van der" in between the first name and last name,
as is customary in the Netherlands,
we want to be able to register a middle part.
For sorting names, the middle part ensures that sorting can be done on last names.-}
REPRESENT Middlepart TYPE ALPHANUMERIC
RELATION personMiddle[Person*Middlepart] [UNI]
MEANING "The middle part of a person's name."

--[RAP3 specific role management rules]--

CLASSIFY Student ISA Account -- Doing this facilitates the making of rules (and Interfaces) that act on Student-accounts.

ROLE ExecEngine MAINTAINS "Student role assignment"
RULE "Student role assignment": I[Student] |- accAllowedRoles;'Student';accAllowedRoles~
VIOLATION (TXT "{EX} InsPair;accAllowedRoles;Account;", SRC I, TXT ";Role;Student")

ROLE ExecEngine MAINTAINS "Initially, allowed roles are also default roles"
RULE "Initially, allowed roles are also default roles": 
   (I-(accDefaultRoles;accDefaultRoles~));accAllowedRoles |- accDefaultRoles
VIOLATION (TXT "{EX} InsPair;accDefaultRoles;Account;", SRC I, TXT ";Role;", TGT I) --}

ROLE ExecEngine MAINTAINS "None of the accounts may have the role SYSTEM"
RULE "None of the accounts may have the role SYSTEM" : accAllowedRoles;'SYSTEM' |- V
VIOLATION (TXT "{EX}DelPair;accAllowedRoles;Account;", SRC I, TXT ";Role;", TGT I) --}

--[RAP3 specific password management rules]--
RULE "(Re)setting the password for an account can only be done by an Account manager (role) or the account user.":
   accNewPassword |- (sessionAccount~ \/ V;'Account manager';sessionActiveRoles~);'_SESSION';V

accNewPassword :: Account * Password [UNI]
ROLE ExecEngine MAINTAINS "(Re)set the password of an account"
RULE "(Re)set the password of an account": 
   accNewPassword /\ (sessionAccount~ \/ V;'Account manager';sessionActiveRoles~);'_SESSION';V |- -V
MEANING "(Re)setting the password for an account can only be done by an Account manager (role) or the account user."
VIOLATION (TXT "{EX} InsPair;accPassWord;Account;", SRC I, TXT ";Password;", TGT I
          ,TXT "{EX} DelPair;accNewPassword;Account;", SRC I, TXT ";Password;", TGT I
          )

ROLE ExecEngine MAINTAINS "Default password is 'welk0m'"
RULE "Default password is 'welk0m'": I[Account] |- accPassword;accPassword~
VIOLATION (TXT "{EX} InsPair;accPassword;Account;", SRC I, TXT ";Password;welk0m")

--[RAP3 specific account-related interfaces]--

INTERFACE "Deelnemers" FOR "Tutor": '_SESSION';V[SESSION*Student] CRuD INTERFACE "Deelnemer"
INTERFACE "Deelnemer" FOR "Tutor": I[Student] CRuD COLS
   [ "Studentnummer"  : accUserid cRUd --RJ: dit lijkt me niet de goede manier om het te regelen; het moet een andere relatie zijn.
   , "Organization"   : accOrg cRUd
   , "Person"         : accPerson cRUd
   , "Allowed roles"  : accAllowedRoles cRUd
   , "Default roles"  : accDefaultRoles cRUd
   ]

INTERFACE "Mijn Account" FOR "Tutor", "Student": '_SESSION';sessionAccount cRud ROWS
   [ "Userid"         : accUserid cRud
   , "New password"   : accNewPassword cRUd
   , "Organization"   : accOrg cRud
   , "Person"         : accPerson cRud
   , "Allowed roles"  : accAllowedRoles cRud
   , "Default roles"  : accDefaultRoles cRUd
   ]

VIEW Persons : Person(personFirstName, TXT " ", personMiddle, TXT " ", personLastName) -- this syntax is deprecated

--[RAP3 specific account manager interfaces]--
-- An Account manager is capable of CRUDing any kind of account.
POPULATION Role CONTAINS [ "Account manager" ]

INTERFACE "Personen" FOR "Account manager": V[SESSION*Person] CRuD INTERFACE "Persoon"
INTERFACE "Persoon"  FOR "Account manager": I[Person] CRuD COLS
   [ "Voornaam"      : personFirstName cRUd
   , "Tussenvoegsel" : personMiddle cRUd
   , "Achternaam"    : personLastName cRUd
   ]

INTERFACE "Accounts" FOR "Account manager": '_SESSION';V[SESSION*Account] CRuD INTERFACE "Account"
INTERFACE "Account" FOR "Account manager": I[Account] CRuD COLS
   [ "Userid"           : accUserid cRUd
   , "(Re)set password" : accNewPassword cRUd
   , "Organization"     : accOrg cRUd
   , "Person"           : accPerson cRUd
   , "Allowed roles"    : accAllowedRoles cRUd
   , "Default roles"    : accDefaultRoles cRUd
   ]

ENDCONTEXT