CONTEXT RAP3 IN ENGLISH

   INCLUDE "SIAM_importer.adl"
   INCLUDE "SIAMpop.xlsx" -- Initial, minimal set of people and accounts 

   INCLUDE "Scripts.adl"
   INCLUDE "Scripts.docadl"
   INCLUDE "Scripts.ifc"


RELATION personMiddle[Person*Middlepart]
RELATION studentnummer[Account*Studentnummer] [UNI]

RELATION compileRequest[Script*Script] [PROP]
ROLE ExecEngine MAINTAINS "Check script"
RULE "Check script" : compileRequest |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;", SRC content;filePath, TXT ";"
          , TXT "{EX}DelPair;compileRequest;Script;", SRC I, TXT";Script;", SRC I
          , TXT "{EX}InsPair;diagRequest;Script;", SRC I, TXT";Script;", SRC I
          , TXT "{EX}InsPair;funcSpecRequest;Script;", SRC I, TXT";Script;", SRC I
          , TXT "{EX}InsPair;protoRequest;Script;", SRC I, TXT";Script;", SRC I
          )

PATTERN Compilation
RELATION diagnosis[Script*FileObject] [UNI]
RELATION funcSpec[Script*FileObject] [UNI]
RELATION prototype[Script*FileObject] [UNI]
RELATION diagRequest[Script*Script] [PROP]
RELATION funcSpecRequest[Script*Script] [PROP]
RELATION protoRequest[Script*Script] [PROP]
RELATION noErrors[Script*Boolean] [UNI]
REPRESENT Boolean TYPE BOOLEAN
ROLE ExecEngine MAINTAINS trigger_the_diagnosis
RULE trigger_the_diagnosis : diagRequest |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;", SRC content;filePath, TXT ";--diagnosis"
          , TXT "{EX}DelPair;diagRequest;Script;", SRC I, TXT";Script;", SRC I
          )
ROLE ExecEngine MAINTAINS trigger_the_funcSpec
RULE trigger_the_funcSpec : funcSpecRequest |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;", SRC content;filePath, TXT ";-fl"
          , TXT "{EX}DelPair;funcSpecRequest;Script;", SRC I, TXT";Script;", SRC I
          )
ROLE ExecEngine MAINTAINS trigger_the_prototype
RULE trigger_the_prototype : protoRequest |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;", SRC content;filePath, TXT ";-pD:\\htdocs\\rommeltje"
          , TXT "{EX}DelPair;protoRequest;Script;", SRC I, TXT";Script;", SRC I
          )
ENDPATTERN

INTERFACE Compile : '_SESSION'[SESSION];sessionAccount;submittor~ BOX<SCOLS>
   [ script : I
   , compileReq : compileRequest cRUd
   , "diagnose"       : diagRequest cRUd
   , "make functional spec" : funcSpecRequest cRUd
   , "create prototype"       : protoRequest cRUd
   , correct : noErrors
   , diagnosis : diagnosis cRud
   , funcSpec : funcSpec cRud
   , prototype : prototype cRud
   ]
ENDCONTEXT