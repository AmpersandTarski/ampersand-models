CONTEXT RAP3 IN ENGLISH

   INCLUDE "SIAM_importer.adl"
   INCLUDE "../Sequences/SeqOntology.adl"       -- Basic notions

   INCLUDE "Scripts.adl"
   INCLUDE "Scripts.docadl"
   INCLUDE "Scripts.ifc"

   INCLUDE "FARelations.adl"
   INCLUDE "Analyse.ifc"
   
RELATION name[Role*RoleName] [UNI,TOT]
POPULATION name[Role*RoleName] CONTAINS [ ("SystemAdmin","SystemAdmin") ]
VIEW Role : Role(name)
REPRESENT RoleName TYPE ALPHANUMERIC

REPRESENT Language TYPE ALPHANUMERIC
REPRESENT AmpersandVersion,TType,Origin,AtomValue TYPE ALPHANUMERIC
REPRESENT Message,Purpose TYPE BIGALPHANUMERIC

RELATION compileresponse[Script*CompileResponse] [UNI] -- the most recent stdout message of compiling a script
RELATION compileresponse[ScriptVersion*CompileResponse] [UNI]
REPRESENT CompileResponse TYPE BIGALPHANUMERIC

CLASSIFY Script ISA Sequence
CLASSIFY ScriptVersion ISA Item

--[[Manipulating Scripts with Ampersand]]--
PATTERN Compilation
--[Checking scripts]--
RELATION scriptOk[ScriptVersion*ScriptVersion] [PROP] -- A ScriptVersion may have the property that Ampersand can compile it

RELATION compileRequest[Script*Script][PROP]
ROLE ExecEngine MAINTAINS "Check script"
RULE "Check script" : compileRequest |- -V 
VIOLATION ( TXT "{EX}CompileToNewVersion;", SRC I,TXT";", SRC submittor;studentnummer
          , TXT "{EX}DelPair;compileRequest;Script;", SRC I, TXT";Script;", SRC I
          )

--[Functional specifiations]--
RELATION funcSpecRequest[ScriptVersion*ScriptVersion] [PROP] -- User has requested the script to be converted into a functional specification
RELATION funcSpecOk[ScriptVersion*ScriptVersion] [PROP] -- a functional specification has actually been generated (no errors)
RELATION funcSpec[ScriptVersion*FileObject] [UNI] -- the file containing the funcspec of the script, it has the property 'funcSpecOk'

ROLE ExecEngine MAINTAINS "Create fspec" -- reset funcSpecRequest after an attempt to create it has been performed.
RULE "Create fspec" : funcSpecRequest-funcSpec;funcSpec~ |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;fspec;", SRC I, TXT ";",SRC source;filePath
          , TXT "{EX}DelPair;funcSpecRequest;ScriptVersion;", SRC I, TXT";ScriptVersion;", SRC I
          )

--[Diagnosis]--
RELATION diagRequest[ScriptVersion*ScriptVersion] [PROP] -- User has requested the script to be converted into a diagnosis
RELATION diagOk[ScriptVersion*ScriptVersion] [PROP] -- a diagnosis has actually been generated (no errors)
RELATION diag[ScriptVersion*FileObject] [UNI] -- the file containing the diagnosis of the script, it has the property 'diagOk'

ROLE ExecEngine MAINTAINS "Create diagnosis" -- reset diagRequest after an attempt to create it has been performed.
RULE "Create diagnosis" : diagRequest-diag;diag~ |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;diagnosis;", SRC I, TXT ";",SRC source;filePath
          , TXT "{EX}DelPair;diagRequest;ScriptVersion;", SRC I, TXT";ScriptVersion;", SRC I
          )

--[Loading of the script]--
RELATION loadRequest[ScriptVersion*ScriptVersion] [PROP]  -- User has requested the script to be loaded into the FormalAmpersand metatables of RAP3
RELATION loadedInRAP3Ok[ScriptVersion*ScriptVersion] [PROP] -- The exec-engine has loaded the population of the script.
RELATION context[ScriptVersion*Context][UNI] -- The context that is created in RAP when it is loaded into the meta tables

ROLE ExecEngine MAINTAINS "Load population into meta tables" -- reset loadRequest after an attempt to create it has been performed.
RULE "Load population into meta tables" : loadRequest-loadedInRAP3Ok |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;loadPopInRAP3;", SRC I, TXT ";",SRC source;filePath
          , TXT "{EX}DelPair;loadRequest;ScriptVersion;", SRC I, TXT";ScriptVersion;", SRC I
          )

--[Prototypes]--
RELATION protoRequest[ScriptVersion*ScriptVersion] [PROP] -- User has requested the script to be converted into a prototype
RELATION protoOk[ScriptVersion*ScriptVersion] [PROP] -- a prototype has actually been generated (no errors)
RELATION proto[ScriptVersion*FileObject] [UNI] -- the index.php (or whatever) file that must be clicked to start the prototype

ROLE ExecEngine MAINTAINS "Create prototype" -- reset protoRequest after an attempt to create it has been performed.
RULE "Create prototype" : protoRequest-proto;proto~ |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;prototype;", SRC I, TXT ";",SRC source;filePath
          , TXT "{EX}DelPair;protoRequest;ScriptVersion;", SRC I, TXT";ScriptVersion;", SRC I
          )

--[Deletion of Context (Fully cascading!)]--
RELATION requestDestroy[Context*Context] [PROP] -- a request to destroy this context. Beware: Experimental. You might burn all. The idea is that ALL atoms in the context of RAP that are linked with this context atom (transitive closure) will be deleted. ALL relations are taken into account, except for a set, specified in cli.php. Currently, the only relation in that set is context[ScriptVersion*Context]. This is to avoid that atoms are destroyed in the specific RAP part. 
ROLE ExecEngine MAINTAINS "Destroy Context"  -- ExecEngine is een echte pyrotechnicus.... 
RULE "Destroy Context" : requestDestroy |- -V
VIOLATION ( TXT "{EX}Cleanup;", SRC I, TXT ";Context"
          )
ENDPATTERN



ENDCONTEXT