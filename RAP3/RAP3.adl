CONTEXT RAP3 IN ENGLISH

   INCLUDE "SIAM_importer.adl"

   INCLUDE "Scripts.adl"
   INCLUDE "Scripts.docadl"
   INCLUDE "Scripts.ifc"

RELATION compileresponse[Script*CompileResponse] [UNI] -- the most recent stdout message of compiling a script
RELATION exitcode[Script*ExitCode] [UNI]               -- the most recent exitcode of a script 
REPRESENT CompileResponse TYPE BIGALPHANUMERIC
REPRESENT ExitCode TYPE INTEGER


--[[Manipulating Scripts with Ampersand]]--
PATTERN Compilation
--[Checking scripts]--
RELATION scriptHasBeenChecked[Script*Script] [PROP] -- A Script may have the property that Ampersand has checked it for errors, and hence the property 'scriptOk' has a correct value.
RELATION scriptOk[Script*Script] [PROP] -- A Script may have the property that Ampersand can compile it

RELATION compileRequest[Script*Script][PROP]
ROLE ExecEngine MAINTAINS "Check script"
RULE "Check script" : compileRequest |- scriptHasBeenChecked 
VIOLATION ( TXT "{EX}CompileWithAmpersand;compilecheck;", SRC I
          , TXT "{EX}InsPair;scriptHasBeenChecked;Script;", SRC I, TXT";Script;", SRC I
          )

--[Functional specifiations]--
RELATION funcSpecRequest[Script*Script] [PROP] -- User has requested the script to be converted into a functional specification
RELATION funcSpecOk[Script*Script] [PROP] -- a functional specification has actually been generated (no errors)
RELATION funcSpec[Script*FileObject] [UNI] -- the file containing the funcspec of the script, it has the property 'funcSpecOk'

ROLE ExecEngine MAINTAINS "Refuse fspec creation" -- reset funcSpecRequest if script contains errors
RULE "Refuse fspec creation" : funcSpecRequest /\ scriptHasBeenChecked |- scriptOk
VIOLATION ( TXT "{EX}DelPair;funcSpecRequest;Script;", SRC I, TXT";Script;", SRC I)

ROLE ExecEngine MAINTAINS "Create fspec" -- reset funcSpecRequest after an attempt to create it has been performed.
RULE "Create fspec" : funcSpecRequest /\ scriptHasBeenChecked /\ scriptOk |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;fspec;", SRC I
          , TXT "{EX}DelPair;funcSpecRequest;Script;", SRC I, TXT";Script;", SRC I
          )

--[Diagnosis]--
RELATION diagRequest[Script*Script] [PROP] -- User has requested the script to be converted into a diagnosis
RELATION diagOk[Script*Script] [PROP] -- a diagnosis has actually been generated (no errors)
RELATION diag[Script*FileObject] [UNI] -- the file containing the diagnosis of the script, it has the property 'diagOk'

ROLE ExecEngine MAINTAINS "Refuse diagnosis creation" -- reset diagRequest if script contains errors
RULE "Refuse diagnosis creation" : diagRequest /\ scriptHasBeenChecked |- scriptOk
VIOLATION ( TXT "{EX}DelPair;diagRequest;Script;", SRC I, TXT";Script;", SRC I)

ROLE ExecEngine MAINTAINS "Create diagnosis" -- reset diagRequest after an attempt to create it has been performed.
RULE "Create diagnosis" : diagRequest /\ scriptHasBeenChecked /\ scriptOk |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;diagnosis;", SRC I
          , TXT "{EX}DelPair;diagRequest;Script;", SRC I, TXT";Script;", SRC I
          )

--[Loading of the script]--
RELATION loadRequest[Script*Script] [PROP]  -- User has requested the script to be loaded into the FormalAmpersand metatables of RAP3
RELATION loadedInRAP3Ok[Script*Script] [PROP] -- The exec-engine has loaded the population of the script.

ROLE ExecEngine MAINTAINS "Refuse loading population in RAP3" -- reset loadRequest if script contains errors
RULE "Refuse loading population in RAP3" : loadRequest /\ scriptHasBeenChecked |- scriptOk
VIOLATION ( TXT "{EX}DelPair;loadRequest;Script;", SRC I, TXT";Script;", SRC I)

ROLE ExecEngine MAINTAINS "Load population into meta tables" -- reset loadRequest after an attempt to create it has been performed.
RULE "Load population into meta tables" : loadRequest /\ scriptHasBeenChecked /\ scriptOk |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;loadPopInRAP3;", SRC I
          , TXT "{EX}DelPair;loadRequest;Script;", SRC I, TXT";Script;", SRC I
          )



--[Prototypes]--
RELATION protoRequest[Script*Script] [PROP] -- User has requested the script to be converted into a prototype
RELATION protoOk[Script*Script] [PROP] -- a prototype has actually been generated (no errors)
RELATION proto[Script*FileObject] [UNI] -- the index.php (or whatever) file that must be clicked to start the prototype

ROLE ExecEngine MAINTAINS "Refuse prototype creation" -- reset protoRequest if script contains errors
RULE "Refuse prototype creation" : protoRequest /\ scriptHasBeenChecked |- scriptOk
VIOLATION ( TXT "{EX}DelPair;protoRequest;Script;", SRC I, TXT";Script;", SRC I)

ROLE ExecEngine MAINTAINS "Create prototype" -- reset protoRequest after an attempt to create it has been performed.
RULE "Create prototype" : protoRequest /\ scriptHasBeenChecked /\ scriptOk |- -V
VIOLATION ( TXT "{EX}CompileWithAmpersand;prototype;", SRC I
          , TXT "{EX}DelPair;protoRequest;Script;", SRC I, TXT";Script;", SRC I
          )

ENDPATTERN


INTERFACE Compile FOR Tutor, Student, Ampersand : '_SESSION'[SESSION];sessionAccount;submittor~ BOX<SCOLS>
   [ "script" : I
   , "correct" : scriptOk cRud

   , "diagnose" : diagRequest cRUd
   , "diagnosis" : diag cRud

   , "make functional spec" : funcSpecRequest cRUd
   , "funcSpec" : funcSpec cRud

   , "load population" : loadRequest cRUd
   , "view your Atlas" : loadedInRAP3Ok cRud
   
   , "create prototype" : protoRequest cRUd
   , "prototype" : proto cRud
   ]
ENDCONTEXT