CONTEXT RAP3 IN ENGLISH

INTERFACE MyScripts FOR Tutor,Student: '_SESSION';sessionAccount;submittor~ CRuD BOX <SHCOLS>
    [ content  : I cRud
    , opdracht : assignment
    , tijdstip : submitted cRud
    ]

-- 20161111/RJ: Ik begrijp niet waartoe de (twee) expressies (I[Account]/\submittor~;submittor) in deze interface dienen. En (daarom?) ook niet wat de purpose van deze interface is.
INTERFACE StudentScripts FOR Tutor : '_SESSION'[SESSION]#(I[Account]/\submittor~;submittor) cRud BOX <SCOLS>
    [ indiener : I/\submittor~;submittor cRud
    , scripts : submittor~ cRud BOX<SCOLS>
       [ opdracht  : assignment cRud
       , content   : I cRud
       , ingediend : submitted cRud
       ]
    ]

INTERFACE "Nieuw script" FOR Student, User : I[Script] CRUd BOX <ROWSNL>
   [ "Meta-info": I cRud BOX <SCOLS>
      [ indiener  : submittor cRud
      , opdracht  : assignment CRUd
      , ingediend : submitted cRud
      ]
   , "Content":  content cRUd
   , "Actual info": I cRud ROWS
      [ compile : I BOX <PropertyButton> [ property : compileRequest cRUd ]
      , "Compiler message"  : compileresponse cRud
      , controls: seqFirstItem ; I[ScriptVersion] cRud BOX <COLSNL>
         [ "Diagnosis" :  I BOX <PropertyButton>
                           [ disabled : I/\diag;diag~          cRud -- this argument MUST be an expression, not just a [PROP]-relation
                           , property : diagRequest cRUd
                           ]
         , "Func. spec" : I BOX <PropertyButton>
                           [ disabled : I/\funcSpec;funcSpec~  cRud -- this argument MUST be an expression, not just a [PROP]-relation
                           , property : funcSpecRequest cRUd
                           ]
         , "Atlas" :      I BOX <PropertyButton>
                           [ disabled : (I/\context;context~)-(I/\funcSpec;funcSpec~) cRud -- Only active if there are graphics to display.
                           , property : loadRequest cRUd
                           ]
         , "Prototype" :  I BOX <PropertyButton>
                           [ disabled : I/\protoOk;protoOk~        cRud -- this argument MUST be an expression, not just a [PROP]-relation
                           , property : protoRequest cRUd
                           ]
         ]
      , results: seqFirstItem ; I[ScriptVersion] cRud BOX <COLSNL>
         [ "Diagnosis"  : diag cRud
         , "Func. spec" : funcSpec cRud
         , "Atlas" :      context cRud LINKTO INTERFACE "Context"
         , "Prototype" :  protoOk;version~;proto cRud
         ]
      ]
   ]

INTERFACE "ScriptContent" FOR SYSTEM : I[Script];content BOX []

ENDCONTEXT