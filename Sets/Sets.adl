CONTEXT Sets IN ENGLISH
PURPOSE CONTEXT Sets {+The purpose of this context is to show how the subset-relation can be computed automatically.+}

RELATION elem[Element*Set]   
MEANING "elements make up a set"

RELATION isSubsetOf[Set*Set] [RFX,ASY,TRN]
MEANING "a set can be a subset of (the same or) another set" 

--[Automated (de)populating the `isSubsetOf` relation]--

ROLE ExecEngine MAINTAINS "Auto insert into isSubsetOf"
RULE "Auto insert into isSubsetOf": -elem~ ! elem |- isSubsetOf
MEANING "If all elements of set S1 are also elements of set S2, then S1 is a subset of S2"
VIOLATION (TXT "{EX} InsPair;isSubsetOf;Set;", SRC I, TXT ";Set;", TGT I)

ROLE ExecEngine MAINTAINS "Auto delete from isSubsetOf"
RULE "Auto delete from isSubsetOf": isSubsetOf |- -elem~ ! elem 
VIOLATION (TXT "{EX} DelPair;isSubsetOf;Set;", SRC I, TXT ";Set;", TGT I)
MEANING "If set S1 is a subset of set S2, then all elements of S1 are also elements of S2"

--[INTERFACE for testing purposes]--
{- INTERFACE "Sets"(elem): V[SESSION*Set] CRUD COLS
   [ "Set" : I CRud
   , "elements" : elem~ CRUD
   , "subsets" : isSubsetOf~ cRud
   , "is subset of" : isSubsetOf cRud
   ] 

POPULATION elem CONTAINS [ ("A", "Set1") ]
POPULATION elem CONTAINS [ ("B", "Set1") ]
POPULATION elem CONTAINS [ ("C", "Set1") ]
POPULATION elem CONTAINS [ ("D", "Set1") ]
POPULATION elem CONTAINS [ ("E", "Set1") ]

POPULATION elem CONTAINS [ ("A", "Set2") ]
POPULATION elem CONTAINS [ ("B", "Set2") ]
POPULATION elem CONTAINS [ ("C", "Set2") ]

POPULATION elem CONTAINS [ ("B", "Set3") ]
POPULATION elem CONTAINS [ ("C", "Set3") ]

POPULATION elem CONTAINS [ ("C", "Set4") ]
POPULATION elem CONTAINS [ ("D", "Set4") ]
POPULATION elem CONTAINS [ ("E", "Set4") ]

POPULATION elem CONTAINS [ ("C", "Set5") ]
POPULATION elem CONTAINS [ ("D", "Set5") ]

POPULATION isSubsetOf CONTAINS [ ("Set1", "Set1") ]
POPULATION isSubsetOf CONTAINS [ ("Set2", "Set2") ]
POPULATION isSubsetOf CONTAINS [ ("Set3", "Set3") ]
POPULATION isSubsetOf CONTAINS [ ("Set4", "Set4") ]
POPULATION isSubsetOf CONTAINS [ ("Set5", "Set5") ]
-- -}
ENDCONTEXT