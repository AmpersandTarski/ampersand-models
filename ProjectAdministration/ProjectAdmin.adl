CONTEXT ProjectAdministrationOntology IN ENGLISH

--[ Conceptual model (including concepts and relations) ]----------------------
  CONCEPT Project "Planned set of interrelated tasks"
    RELATION projectName[Project*ProjectName] [UNI]  
    RELATION projectStatus[Project*ProjectStatus] [UNI]  	
    RELATION projectDescription[Project*Description] [UNI] 
    RELATION projectStartDate[Project*Date] [UNI] 
    RELATION projectActive[Project*Project] [PROP]
    RELATION pl[Project*Person] [UNI]
    RELATION member[Project*Person]
  
  CONCEPT Person "Person"
    RELATION personFirstName[Person*FirstName] [UNI]
    RELATION personLastName[Person*LastName] [UNI]
    RELATION personStatus[Person*PersonStatus] [UNI]
    RELATION personEmail[Person*Email] [UNI]
    RELATION workswith[Person*Person]
  
  REPRESENT Date TYPE DATE
  REPRESENT ProjectName, ProjectStatus TYPE ALPHANUMERIC
  REPRESENT FirstName, LastName, PersonStatus, Email TYPE ALPHANUMERIC
  REPRESENT Description TYPE BIGALPHANUMERIC

--[ Invariant rules ]----------------------------------------------------------
  RULE "r1" : I[Project] |- projectName;projectName~
  MEANING "Every project MUST have a name"
  MESSAGE "There are projects that don't have a name"
  VIOLATION ( TXT "Project "
            , SRC I
            , TXT " does not have a name"
            )
  ROLE "Planner" MAINTAINS "r1"

  
  RULE "Active projects must have a projectleader" : projectActive |- I /\ pl;pl~
  
  ROLE "Planner" MAINTAINS "Every projectleader is also projectmember"
  RULE "Every projectleader is also projectmember" : pl |- member

-------------------------------------------------------------------------------
ENDCONTEXT