CONTEXT ProjectAdminInterfaces IN ENGLISH

--[ Public interfaces ]--------------------------------------------------------
  INTERFACE "Active projects" : V[SESSION*Project];projectActive COLS
    [ "Projects"      : I[Project]
    , "Name"          : projectName
    , "Description"   : projectDescription
    , "Projectleider" : pl                   
    , "@"             : pl[Project*Person] <PersonEmail>
    , "Start"         : projectStartDate
    , "Active"        : projectActive
    ]
  
  INTERFACE "Project" : I[Project] ROWS
    [ "Name"                : projectName
    , "Description"         : projectDescription
    , "(Planned) start date": projectStartDate
    , "Active"              : projectActive
    , "Current PL"          : pl
    , "Project members"     : member ROWS
      [ "Name"              : personFirstName
      , "Email"             : personEmail
      ]
    ]
    
  INTERFACE "Person"    : I[Person] cRUD ROWS
    [ "First name"      : personFirstName RU
    , "Last name"       : personLastName RU
    , "Status"          : personStatus RU
    , "Email"           : personEmail RU
    , "Projects"        : (member~ \/ pl~);projectActive 
    , "Works with"      : workswith
    , "I"               : I BOX <PANELS>
      [ "First name"    : personFirstName U
      , "I"             : I
      ]
    ]
  
--[ Interfaces for planner ]---------------------------------------------------
  INTERFACE "Inactive projects" FOR "Planner" : V[SESSION*Project];(-(projectActive) /\ I) COLS
    [ "Name"            : I
    , "Description"     : projectDescription
    , "Projectleider"   : pl RU
    , "Active"          : projectActive RU
    ]
  
  INTERFACE "People" FOR "Planner" : V[SESSION*Person]   CRuD ROWS
    [ "Person"          : I[Person]
    , "Email"           : personEmail
    , "Projects"        : member~ 
    ]

--[ Views ]--------------------------------------------------------------------  
  VIEW "Project name"   : Project(projectName)
  VIEW "Person name"    : Person(personFirstName, TXT " ", personLastName)
  VIEW "PersonEmail"    : Person { email : personEmail } HTML TEMPLATE "PersonEmail.html" ENDVIEW

-------------------------------------------------------------------------------  
ENDCONTEXT