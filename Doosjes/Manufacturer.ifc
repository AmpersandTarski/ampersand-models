CONTEXT ComponentManagementInterface IN ENGLISH

{- Bij Manufacturing gaat het om het specificeren van Components (functies, i.e. Scopes en Ports), 
vergelijkbaar met het Computation Independent Model (CIM) uit MDA.

Het lijkt me dat een Manufacturer het volgende moet kunnen:
1) een Scope (component) aanmaken
2) PoortTypes specificeren op een Scope, met bijbehorende kenmerken (input/output poort, default values etc.)
-}

--[ScopeEditingSvc relations]--
  sNwCompFromTemplate :: Scope * Scope [UNI]

  sNwInputPortName :: Scope * Name [UNI]
  sNwOutputPortName :: Scope * Name [UNI]

  sNwConstPort :: Scope * Port [UNI]

  sNwWireSrc :: Scope * Port [UNI]
  sNwWireTgt :: Scope * Port [UNI]
  sDelWire :: Scope * Wire [UNI]

INTERFACE "EditComponent" 
    (scopeName, scopeDoc, docShort, docLong
    ,sNwInputPortName, portIsInput
    ,sNwOutputPortName, portIsOutput
    ,portComponent, portType, portIsConst, portDefValue, portConfigQstn
    ,sNwCompFromTemplate
    ,sNwConstPort, constName, constType, constValue, constPort, constQstn
    ,wScope ,sDelWire, sNwWireSrc, sNwWireTgt
    ) FOR Developer, Manufacturer: scopeIsComponent
TABS[ "External specs" : I
    ROWS[ "Name" : scopeName
        , "Documentation" : scopeDoc
        COLS[ "Short description" : docShort
            , "Long description" : docLong
            ]
        , "Interface" : I -- 2.2.2.7
        ROWS[ "Inputs"    : portComponent~;portIsInput 
            COLS[ "Name" : I LINKTO INTERFACE "EditInputPort"
                , "MinWires" : portMinWires
                , "MaxWires" : portMaxWires
                , "Config Type" : portType
                , "Constant?" : portIsConst <PortProperty>
                , "Default" : portDefValue
                , "Config question" : portConfigQstn
                ]
            , "Add Input" : sNwInputPortName
            , "Outputs"   : portComponent~;portIsOutput
            COLS[ "Name" : I LINKTO INTERFACE "EditOutputPort"
                , "MinWires" : portMinWires
                , "MaxWires" : portMaxWires
                , "Config Type" : portType
                ]
            , "Add Output": sNwOutputPortName
            ]
        ]
    , "Internal specs" : I
    ROWS[ "Subcomponenents" : scopeIPO~
        COLS[ "Component" : I <ScopeInScope>
            , "Is a copy of" : scopeICO <ScopeID>
            ]
        , "Add Subcomponent": sNwCompFromTemplate
        , "Constants": constScope~
        COLS[ "Name" : constName
            , "Type" : constType
            , "Value": constValue
            , "Port" : constPort
            , "Question" : constQstn
            ]
        , "Add Constant (for Port)" : sNwConstPort
        , "Wires" : wScope~;(I /\ wSrc;wSrc~ /\ wTgt;wTgt~)
        , "Add Wire" : I
        COLS[ "Part of" : I
            , "Source" : sNwWireSrc
            , "Srcbox" : sNwWireSrc;portComponent <ScopeInScope>
            , "Target" : sNwWireTgt
            , "Tgtbox" : sNwWireTgt;portComponent <ScopeInScope>
            ]
        , "Delete Wire" : sDelWire
        ]
    , "Configuration" : I
    ROWS[ "Configuration" : I <ScopeIsConfigured>
        , "Please specify" : constScope~;(I /\ -(constValue;constValue~))
        COLS[ " " : I <ConstantQuestion>
            , "Value": constValue
            , "Type" : I <CfgvarType>
            ]
        , "Please check" : constScope~;(I /\ constValue;constValue~)
        COLS[ " " : I <ConstantQuestion>
            , "Value": constValue
            , "Type" : I <CfgvarType>
            ]
        , "Subcomponents" : scopeIPO~;(I /\ constScope~;constScope)
        ROWS[ "Name" : I <ScopeInScope>
            , "Constants" : constScope~
            COLS[ " " : I <ConstantQuestion>
                , "Value": constValue
                , "Type" : I <CfgvarType>
                ]
            ]    
        ]
    ]
    
INTERFACE "EditInputPort"
    (portName, portDoc, docShort, docLong
    ,portMinWires, portMaxWires
    ,portType, portIsConst, portDefValue, portConfigQstn
    ) FOR Developer, Manufacturer: portIsInput
ROWS[ "Input" : I
    COLS[ "Name" : portName
        , "Seq.Nr" : I;portSeqNr -- Do not edit this!
        ]
    , "Documentation" : portDoc
    COLS[ "Short description" : docShort
        , "Long description" : docLong
        ]
    , "MinWires" : portMinWires
    , "MaxWires" : portMaxWires
    , "Config Type" : portType
    , "Constant?" : portIsConst <PortProperty>
    , "Default" : portDefValue
    , "Config question" : portConfigQstn
    ]

INTERFACE "EditOutputPort"
    (portName, portDoc, docShort, docLong
    ,portMinWires, portMaxWires
    ) FOR Developer, Manufacturer: portIsOutput
ROWS[ "Output" : I
    COLS[ "Name" : portName
        , "Seq.Nr" : I;portSeqNr -- Do not edit this!
        ]
    , "Documentation" : portDoc
    COLS[ "Short description" : docShort
        , "Long description" : docLong
        ]
    , "MinWires" : portMinWires
    , "MaxWires" : portMaxWires
    , "Config Type" : portType
    ]

ENDCONTEXT