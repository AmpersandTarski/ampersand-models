CONTEXT ScopeEditingSvc IN ENGLISH

PROCESS "Scope Editing Service" -- Eigenlijk is dit een SERVICE
PURPOSE PROCESS "Scope Editing Service"
{+Editing a scope means that an existing scope is being subjected to modifications, such as the creation/copying of new subcomponents, ports, wires and constants. This service processes user requests for doing so.+}

--[Projects]--

sNwProject :: SESSION * ProjectName [UNI]
ROLE ExecEngine MAINTAINS "Create a new Project"
SERVICE ScopeEditingSvc MAINTAINS "Create a new Project"
RULE "Create a new Project": '_SESSION';sNwProject |- -V
VIOLATION (TXT "{EX} NewStruct;Project"
              ,TXT ";projName;Project;_NEW;ProjectName;", TGT I
--!           ,TXT ";projOrg;Project;_NEW;Organization;", SRC sessionOrg
          ,TXT "{EX} DelPair;sNwProject;Scope;", SRC I, TXT ";ProjectName;", TGT I
          )

pNwNamedApp :: Project * ScopeName [UNI]
ROLE ExecEngine MAINTAINS "In a project, create new (named) application"
SERVICE ScopeEditingSvc MAINTAINS "In a project, create new (named) application"
RULE "In a project, create new (named) application": pNwNamedApp |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeName;Scope;_NEW;ScopeName;", TGT I
--!           ,TXT ";scopeOrg;Scope;_NEW;Organization;", SRC projOrg
          ,TXT "{EX} DelPair;pNwNamedApp;Project;", SRC I, TXT ";ScopeName;", TGT I
          )

pCpAppName :: Project * ScopeName [UNI]
pCpAppFrom :: Project * Scope [UNI] 
ROLE ExecEngine MAINTAINS "In a project, create a new (named) application that is a copy of an existing one"
SERVICE ScopeEditingSvc MAINTAINS "In a project, create a new (named) application that is a copy of an existing one"
RULE "In a project, create a new (named) application that is a copy of an existing one":
  I /\ pCpAppName;V;(I-scopeIsComponent);pCpAppFrom~ |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeName;Scope;_NEW;ScopeName;", SRC pCpAppName
              ,TXT ";scopeICO;Scope;_NEW;Scope;", SRC pCpAppFrom
--!           ,TXT ";scopeOrg;Scope;_NEW;Organization;", SRC projOrg
          ,TXT "{EX} DelPair;pCpAppName;Project;", SRC I, TXT ";ScopeName;", SRC pCpAppName          
          ,TXT "{EX} DelPair;pCpAppFrom;Project;", SRC I, TXT ";Scope;", SRC pCpAppFrom          
          )

--[Scopes: applications (i.e.: non-components)]--

sNwNamedApp :: SESSION * ScopeName [UNI]
ROLE ExecEngine MAINTAINS "Create new (named) application in session"
SERVICE ScopeEditingSvc MAINTAINS "Create new (named) application in session"
RULE "Create new (named) application in session": '_SESSION';sNwNamedApp |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeName;Scope;_NEW;ScopeName;", TGT I
--!           ,TXT ";scopeOrg;Scope;_NEW;Organization;", SRC sessionOrg
          ,TXT "{EX} DelPair;sNwNamedApp;SESSION;", SRC I, TXT ";ScopeName;", TGT I
          )

sCpAppName :: SESSION * ScopeName [UNI]
sCpAppFrom :: SESSION * Scope [UNI] 
ROLE ExecEngine MAINTAINS "Create a new (named) application that is a copy of an existing one"
SERVICE ScopeEditingSvc MAINTAINS "Create a new (named) application that is a copy of an existing one"
RULE "Create a new (named) application that is a copy of an existing one":
  '_SESSION' /\ sCpAppName;V;(I-scopeIsComponent);sCpAppFrom~ |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeName;Scope;_NEW;ScopeName;", SRC sCpAppName
              ,TXT ";scopeICO;Scope;_NEW;Scope;", SRC sCpAppFrom
--!           ,TXT ";scopeOrg;Scope;_NEW;Organization;", SRC sessionOrg
          ,TXT "{EX} DelPair;sCpAppName;SESSION;", SRC I, TXT ";ScopeName;", SRC sCpAppName          
          ,TXT "{EX} DelPair;sCpAppFrom;SESSION;", SRC I, TXT ";Scope;", SRC sCpAppFrom          
          )

--[Scopes: components]--

sNwNamedComp :: SESSION * ScopeName [UNI]
ROLE ExecEngine MAINTAINS "Create new (named) component in session"
SERVICE ScopeEditingSvc MAINTAINS "Create new (named) component in session"
RULE "Create new (named) component in session": '_SESSION';sNwNamedComp |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeName;Scope;_NEW;ScopeName;", TGT I
              ,TXT ";scopeIsComponent;Scope;_NEW;Scope;_NEW"
--!           ,TXT ";scopeOrg;Scope;_NEW;Organization;", SRC sessionOrg
          ,TXT "{EX} DelPair;sNwNamedComp;SESSION;", SRC I, TXT ";ScopeName;", TGT I
          )
          
sCpCompName :: SESSION * ScopeName [UNI]
sCpCompFrom :: SESSION * Scope [UNI] 
ROLE ExecEngine MAINTAINS "Create a new (named) component that is a copy of an existing one"
SERVICE ScopeEditingSvc MAINTAINS "Create a new (named) component that is a copy of an existing one"
RULE "Create a new (named) component that is a copy of an existing one":
  '_SESSION' /\ sCpCompName;V;scopeIsComponent;sCpCompFrom~ |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeName;Scope;_NEW;ScopeName;", SRC sCpCompName
              ,TXT ";scopeICO;Scope;_NEW;Scope;", SRC sCpCompFrom
              ,TXT ";scopeIsComponent;Scope;_NEW;Scope;", SRC sCpCompFrom;scopeIsComponent
--!           ,TXT ";scopeOrg;Scope;_NEW;Organization;", SRC sessionOrg
          ,TXT "{EX} DelPair;sCpCompName;SESSION;", SRC I, TXT ";ScopeName;", SRC sCpCompName          
          ,TXT "{EX} DelPair;sCpCompFrom;SESSION;", SRC I, TXT ";Scope;", SRC sCpCompFrom          
          )

--[Scopes: general]--

sNwCompFromTemplate :: Scope * Scope [UNI]
ROLE ExecEngine MAINTAINS "Within a scope, create a new subscope that is a copy of an existing scope"
SERVICE ScopeEditingSvc MAINTAINS "Within a scope, create a new subscope that is a copy of an existing scope"
RULE  "Within a scope, create a new subscope that is a copy of an existing scope": sNwCompFromTemplate |- -V
VIOLATION (TXT "{EX} NewStruct;Scope"
              ,TXT ";scopeIPO;Scope;_NEW;Scope;", SRC I
              ,TXT ";scopeICO;Scope;_NEW;Scope;", TGT I
          ,TXT "{EX} DelPair;sNwCompFromTemplate;Scope;", SRC I, TXT ";Scope;", TGT I
          ,TXT "{EX} RerunExecEngine;sNwCompFromTemplate" -- Attempt to fix needed re-run to fix transitive closure of scopeIPO
          )

--[Ports]--

sNwInputPortName :: Scope * Name [UNI]
ROLE ExecEngine MAINTAINS "Create a new Input on Component"
SERVICE ScopeEditingSvc MAINTAINS "Create a new Input on Component"
RULE "Create a new Input on Component": sNwInputPortName |- -V
VIOLATION (TXT "{EX} NewStruct;Port"
              ,TXT ";portIsInput;Port;_NEW;Port;_NEW"
              ,TXT ";portComponent;Port;_NEW;Scope;", SRC I
              ,TXT ";portName;Port;_NEW;PortName;", TGT I
          ,TXT "{EX} InsPair;scopeIsComponent;Scope;", SRC I, TXT ";Scope;", SRC I
          ,TXT "{EX} DelPair;sNwInputPortName;Scope;", SRC I, TXT ";Name;", TGT I
          )

sNwOutputPortName :: Scope * Name [UNI]
ROLE ExecEngine MAINTAINS "Create a new Output on Component"
SERVICE ScopeEditingSvc MAINTAINS "Create a new Output on Component"
RULE "Create a new Output on Component": sNwOutputPortName |- -V
VIOLATION (TXT "{EX} NewStruct;Port"
              ,TXT ";portIsOutput;Port;_NEW;Port;_NEW"
              ,TXT ";portComponent;Port;_NEW;Scope;", SRC I
              ,TXT ";portName;Port;_NEW;PortName;", TGT I
          ,TXT "{EX} InsPair;scopeIsComponent;Scope;", SRC I, TXT ";Scope;", SRC I
          ,TXT "{EX} DelPair;sNwOutputPortName;Scope;", SRC I, TXT ";Name;", TGT I
          )

ROLE ExecEngine MAINTAINS "Automatically set the 'scopeIsComponent' property"
SERVICE ScopeEditingSvc MAINTAINS "Automatically set the 'scopeIsComponent' property"
RULE "Automatically set the 'scopeIsComponent' property": portComponent |- portComponent;scopeIsComponent
VIOLATION (TXT "{EX} InsPair;scopeIsComponent;Scope;", TGT I, TXT ";Scope;", TGT I)

--[Constants]--

sNwConstPort :: Scope * Port [UNI]
ROLE ExecEngine MAINTAINS "Create a new Constant for a Port in a Scope"
SERVICE ScopeEditingSvc MAINTAINS "Create a new Constant for a Port in a Scope"
RULE "Create a new Constant for a Port in a Scope": sNwConstPort |- -V
VIOLATION (TXT "{EX} NewStruct;Constant"
              ,TXT ";constScope;Constant;_NEW;Scope;", SRC I
              ,TXT ";constPort;Constant;_NEW;Port;", TGT I
              ,TXT ";constName;Constant;_NEW;ConstName;", TGT portName
              ,TXT ";constType;Constant;_NEW;ConfigType;", TGT portType
              ,TXT ";constQstn;Constant;_NEW;Question;", TGT portConfigQstn
          ,TXT "{EX} DelPair;sNwConstPort;Scope;", SRC I, TXT ";Name;", TGT I
          )

--[Wires]--

sNwWireSrc :: Scope * Port [UNI]
sNwWireTgt :: Scope * Port [UNI]
ROLE ExecEngine MAINTAINS "Create a new Wire in a Scope"
SERVICE ScopeEditingSvc MAINTAINS "Create a new Wire in a Scope"
RULE "Create a new Wire in a Scope": I /\ sNwWireSrc;sNwWireSrc~ /\ sNwWireTgt;sNwWireTgt~ |- -V
VIOLATION (TXT "{EX} NewStruct;Wire"
              ,TXT ";wScope;Wire;_NEW;Scope;", SRC I
              ,TXT ";wSrc;Wire;_NEW;Port;", SRC sNwWireSrc
              ,TXT ";wTgt;Wire;_NEW;Port;", SRC sNwWireTgt
          ,TXT "{EX} DelPair;sNwWireSrc;Scope;", SRC I, TXT ";Port;", SRC sNwWireSrc
          ,TXT "{EX} DelPair;sNwWireTgt;Scope;", SRC I, TXT ";Port;", SRC sNwWireTgt
          )

sDelWire :: Scope * Wire [UNI]
ROLE ExecEngine MAINTAINS "Delete a wire from Scope"
SERVICE ScopeEditingSvc MAINTAINS "Delete a wire from Scope"
RULE "Delete a wire from Scope": sDelWire |- -V
VIOLATION (TXT "{EX} DelAtom;Wire;", TGT I)

ENDPROCESS
-----------------------------------------------------------
ENDCONTEXT