CONTEXT AAS IN ENGLISH

  PATTERN Argument

    RELATION claim [ArgumentC*Claim] [UNI, TOT]
    MEANING "Assertion you hope to prove."
    PURPOSE RELATION claim [ArgumentC*ClaimC]
    {+ Claim is the conclusion that we want to prove.
       Its the source of our effort to build the whole argument.
       Based on - Toulmin the use of argumentation
    +}

    RELATION data[ArgumentC*DataC] [UNI, TOT]
    MEANING "Support/rationale for the claim."
    PURPOSE RELATION data[ArgumentC*DataC]
    {+ To prove some argument we need to star with data, some fact that is well know.
       So we can "appeal to these facts, and present them as the foundation upon which
       our claim is based." - Toulmin the use of argumentation
    +}

    RELATION warrant[ArgumentC*WarrantC] [TOT]
    MEANING "The argument has a justification to support the fact. Connection between Claim and Data."
    PURPOSE RELATION warrant[ArgumentC*WarrantC]
    {+ The purpose of warrant is "to show that, taking these
       data as a starting point, the step to the original claim or conclusion is
       an appropriate and legitimate one. At this point, therefore, what are
       needed are general, hypothetical statements, which can act as bridges,
       and authorise the sort of step to which our particular argument commits
       us." - Toulmin the use of argumentation
    +}

    RELATION backing[WarrantC*BackingC]
    MEANING "Support for the warrant, the justification is supported by backing."
    PURPOSE RELATION backing[WarrantC*BackingC]
    {+ The backing "Standing behind our warrants there will normally be other assurances, without which the
       warrants themselves would posses neither authority nor currency. This
       ‘backing’ of our warrants is something which we shall have to scrutinise
       very carefully: its precise relations to our data, claims, warrants and conditions" - Toulmin the use of argumentation
       We highlight 'precise relations'
    +}

    RELATION rebuttal[ClaimC*RebuttalC]
    MEANING "Potential objection to the claim."
    PURPOSE RELATION rebuttal[ClaimC*RebuttalC]
    {+ Rebuttals are the "conditions indicating circumstances in which the
       general authority of the warrant would have to be set aside" - Toulmin the use of argumentation
    +}

    RELATION qualifier[ArgumentC*QualifierC] [UNI]
    MEANING "Limits put on the claim."
    PURPOSE RELATION qualifier[ArgumentC*QualifierC]
    {+: "We may need to add some explicit reference to the degree of force which our data confer on our claim in virtue of our warrant.
        It is often necessary in the law-courts, not just to appeal to a given statue or common-law doctrine,
        but to discuss explicitly the extent to which this particular law fits the case" - Toulmin the use of argumentation
    +}

    RELATION counter[ArgumentC*ArgumentC] [ASY, IRF]
    MEANING "The Argument can have counter Arguments."
    PURPOSE RELATION counter[ArgumentC*ArgumentC]
    {+ This relation enable us to relate two Arguments (A1,A2), A2 counter A1,
       that A2 is a counter argument of A1
    +}

    RULE "intransitive" : (counter;counter) /\ (counter \/ counter~) |- V-V
    PURPOSE RULE "intransitive"
    {+ If an Argument A1 is related with Argument A2, A2 counter A1,
       and A2 is related with Argument A3, A3 counter A2,
       then A1 can not be related with A3 and A3 can not be related with A1
    +}

    RULE "No Same Warrants in Counter Argument" : warrant /\ counter;warrant |- V-V
    PURPOSE RULE "No Same Warrants in Counter Argument"
    {+ If Argument A1 is related with Warrant W1, W1 warrant A1,
        and Argument A2 is related with Warrant W2, w2 warrant A1,
        and A1 is related with A2, A2 counter A1,
        then W1 needs to be different than W2
    +}

  ENDPATTERN

  INTERFACE "Arguments" : V[SESSION*ArgumentC]
  BOX
  [ "Data" : data
  , "Warrant" : warrant
  , "Qualifier" : qualifier
  , "Claim" : claim
  , "CounterArguments" : counter
  ]

  INTERFACE "Rebuttals" : V[SESSION*ClaimC]
  BOX
  ["Rebuttals" : rebuttal]

  INTERFACE "Backings" : V[SESSION*WarrantC]
  BOX
  ["Backings" : backing]

ENDCONTEXT